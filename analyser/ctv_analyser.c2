/* Copyright 2022 Bas van den Berg
*/

module ctv_analyser;

import ast local;

import string;

type CTV_Analyser struct {
}

func void CTV_Analyser.init(CTV_Analyser* ca) {
    string.memset(ca, 0, sizeof(CTV_Analyser));
}

public type Value struct {
    bool is_signed;
    union {
        u64 uvalue;
        i64 svalue;
    }
}

public func Value get_value(const Expr* e) {
    Value result = { 0 }

    assert(e.isCtv());

    switch (e.getKind()) {
    case ExprKind.IntegerLiteral:
        const IntegerLiteral* i = cast<IntegerLiteral*>(e);
        result.uvalue = i.getValue();
        break;
    case ExprKind.BooleanLiteral:
        const BooleanLiteral* b = cast<BooleanLiteral*>(e);
        result.uvalue = b.getValue();
        break;
    case ExprKind.CharLiteral:
        const CharLiteral* c = cast<CharLiteral*>(e);
        result.uvalue = c.getValue();
        break;
    case ExprKind.StringLiteral:
        assert(0);
        break;
    case ExprKind.Nil:
        break;
    case ExprKind.Identifier:
        const IdentifierExpr* i = cast<IdentifierExpr*>(e);
        return get_decl_value(i.getDecl());
    case ExprKind.Type: fallthrough;
    case ExprKind.Call: fallthrough;
    case ExprKind.InitList: fallthrough;
    case ExprKind.FieldDesignatedInit: fallthrough;
    case ExprKind.ArrayDesignatedInit:
        // should not happen
        break;
    case ExprKind.BinaryOperator:
        return get_binaryop_value(cast<BinaryOperator*>(e));
    case ExprKind.UnaryOperator:
        return get_unaryop_value(cast<UnaryOperator*>(e));
    case ExprKind.ConditionalOperator:
        // should not happen
        break;
    case ExprKind.Builtin:
        const BuiltinExpr* bi = cast<BuiltinExpr*>(e);
        result.uvalue = bi.getValue();
        break;
    case ExprKind.ArraySubscript:
        // TODO can be CTV?
        break;
    case ExprKind.Member:
        const MemberExpr* p = cast<MemberExpr*>(e);
        return get_decl_value(p.getDecl());
    case ExprKind.Paren:
        const ParenExpr* p = cast<ParenExpr*>(e);
        return get_value(p.getInner());
    case ExprKind.BitOffset:
        // TODO
        break;
    case ExprKind.ExplicitCast:
        const ImplicitCastExpr* i = cast<ImplicitCastExpr*>(e);
        // TODO truncateLiteral()
        return get_value(i.getInner());
    case ExprKind.ImplicitCast:
        const ImplicitCastExpr* i = cast<ImplicitCastExpr*>(e);
        return get_value(i.getInner());
    }

    return result;
}

func Value get_decl_value(const Decl* d) {
    assert(d);
    Value result = { 0 }
    switch (d.getKind()) {
    case DeclKind.EnumConstant:
        const EnumConstantDecl* ecd = cast<EnumConstantDecl*>(d);
        result.uvalue = ecd.getValue();
        break;
    case DeclKind.Var:
        const VarDecl* vd = cast<VarDecl*>(d);
        const Expr* initval = vd.getInit();
        assert(initval);
        return get_value(initval);
    default:
        assert(0);
        break;
    }
    return result;
}

func Value get_unaryop_value(const UnaryOperator* e) {
    Value result = { 0 }
    const Expr* inner = e.getInner();
    Value res2 = get_value(inner);

    switch (e.getKind()) {
    case UnaryOpcode.PostInc:  fallthrough;
    case UnaryOpcode.PostDec:  fallthrough;
    case UnaryOpcode.PreInc:   fallthrough;
    case UnaryOpcode.PreDec:
        // should not happen
        break;
    case UnaryOpcode.AddrOf:   fallthrough;
    case UnaryOpcode.Deref:
        // Allowed?
        break;
    case UnaryOpcode.Minus:
        result.is_signed = true;
        result.svalue = res2.is_signed ? - res2.svalue : cast<i64>(- res2.uvalue);
        break;
    case UnaryOpcode.Not:
        // Note: doesn't matter if we check svalue/uvalue
        result.svalue = (result.svalue == 0) ? 1 : 0;
        break;
    case UnaryOpcode.LNot:
        // TODO cast to bool first?
        result.uvalue = res2.is_signed ? cast<u64>(~ res2.svalue) : ~ res2.uvalue;
        break;
    }
    return result;
}

func Value get_binaryop_value(const BinaryOperator* e) {
    Value result = { 0 }
    Value left = get_value(e.getLHS());
    Value right = get_value(e.getRHS());
    result.is_signed = left.is_signed;

    // Note: we take the signed/unsigned rvalue depending on whether the lhs value is sigend.
    // this is not completely correct

    switch (e.getKind()) {
    case BinaryOpcode.Multiply:
        if (left.is_signed) result.svalue = left.svalue * right.svalue;
        else result.uvalue = left.uvalue * right.uvalue;
        break;
    case BinaryOpcode.Divide:
        // Note: should already be checked!
        assert(right.svalue != 0);
        if (left.is_signed) result.svalue = left.svalue / right.svalue;
        else result.uvalue = left.uvalue / right.uvalue;
        break;
    case BinaryOpcode.Reminder:
        // Note: should already be checked!
        assert(right.svalue != 0);
        if (left.is_signed) result.svalue = left.svalue % right.svalue;
        else result.uvalue = left.uvalue % right.uvalue;
        break;
    case BinaryOpcode.Add:
        if (left.is_signed) result.svalue = left.svalue + right.svalue;
        else result.uvalue = left.uvalue + right.uvalue;
        break;
    case BinaryOpcode.Subtract:
        if (left.is_signed) result.svalue = left.svalue - right.svalue;
        else result.uvalue = left.uvalue - right.uvalue;
        break;
    case BinaryOpcode.ShiftLeft:
        // Note: neither may be negative, should already be checked
        // TODO warn about overflow
        result.uvalue = left.uvalue << right.uvalue;
        break;
    case BinaryOpcode.ShiftRight:
        // Note: neither may be negative, should already be checked
        // Note: rhs value should be less than bitwidth of lhs type, should already be checked
        result.uvalue = left.uvalue >> right.uvalue;
        break;
    case BinaryOpcode.LessThan:
        result.is_signed = false;
        if (left.is_signed) result.svalue = left.svalue < right.svalue;
        else result.uvalue = left.uvalue < right.uvalue;
        break;
    case BinaryOpcode.GreaterThan:
        result.is_signed = false;
        if (left.is_signed) result.svalue = left.svalue > right.svalue;
        else result.uvalue = left.uvalue > right.uvalue;
        break;
    case BinaryOpcode.LessEqual:
        result.is_signed = false;
        if (left.is_signed) result.svalue = left.svalue <= right.svalue;
        else result.uvalue = left.uvalue <= right.uvalue;
        break;
    case BinaryOpcode.GreaterEqual:
        result.is_signed = false;
        if (left.is_signed) result.svalue = left.svalue >= right.svalue;
        else result.uvalue = left.uvalue >= right.uvalue;
        break;
    case BinaryOpcode.Equal:
        result.is_signed = false;
        if (left.is_signed) result.svalue = left.svalue == right.svalue;
        else result.uvalue = left.uvalue == right.uvalue;
        break;
    case BinaryOpcode.NotEqual:
        result.is_signed = false;
        if (left.is_signed) result.svalue = left.svalue != right.svalue;
        else result.uvalue = left.uvalue != right.uvalue;
        break;
    case BinaryOpcode.And:
        result.is_signed = false;
        if (left.is_signed) result.svalue = left.svalue & right.svalue;
        else result.uvalue = left.uvalue & right.uvalue;
        break;
    case BinaryOpcode.Xor:
        result.is_signed = false;
        if (left.is_signed) result.svalue = left.svalue ^ right.svalue;
        else result.uvalue = left.uvalue ^ right.uvalue;
        break;
    case BinaryOpcode.Or:
        result.is_signed = false;
        if (left.is_signed) result.svalue = left.svalue | right.svalue;
        else result.uvalue = left.uvalue | right.uvalue;
        break;
    case BinaryOpcode.LAnd:
        result.is_signed = false;
        if (left.is_signed) result.svalue = left.svalue && right.svalue;
        else result.uvalue = left.uvalue && right.uvalue;
        break;
    case BinaryOpcode.LOr:
        result.is_signed = false;
        if (left.is_signed) result.svalue = left.svalue || right.svalue;
        else result.uvalue = left.uvalue || right.uvalue;
        break;
    case BinaryOpcode.Assign:    fallthrough;
    case BinaryOpcode.MulAssign: fallthrough;
    case BinaryOpcode.DivAssign: fallthrough;
    case BinaryOpcode.RemAssign: fallthrough;
    case BinaryOpcode.AddAssign: fallthrough;
    case BinaryOpcode.SubASsign: fallthrough;
    case BinaryOpcode.ShlAssign: fallthrough;
    case BinaryOpcode.ShrASsign: fallthrough;
    case BinaryOpcode.AndAssign: fallthrough;
    case BinaryOpcode.XorAssign: fallthrough;
    case BinaryOpcode.OrAssign:
        assert(0);
        break;
    }
    return result;
}


/* Copyright 2022 Bas van den Berg
*/

module module_analyser;

import ast_context;
import ast_builder;
import ast local;
import ctv_analyser;
import size_analyser local;
import conversion_checker;
import diagnostics;
import module_list;
import src_loc local;
import scope;
import warning_flags;

import ctype;
import stdarg local;
import stdlib;
import string;
import stdio local; // TEMP

const u32 MaxDepth = 8;

// TEMP here until we have templates
type NameVector struct {
    u32* data;
    u32 count;
    u32 capacity;
}

func void NameVector.init(NameVector* v, u32 capacity) {
    v.data = nil;
    v.count = 0;
    v.capacity = capacity / 2; // workaround resize
    v.resize();
}

func void NameVector.free(NameVector* v) {
    stdlib.free(v.data);
    v.count = 0;
    v.capacity = 0;
    v.data = nil;
}

func void NameVector.resize(NameVector* v) {
    v.capacity = v.capacity == 0 ? 4 : v.capacity * 2;
    void* data2 = stdlib.malloc(v.capacity * sizeof(u32));
    if (v.data) {
        string.memcpy(data2, v.data, v.count * sizeof(u32));
        stdlib.free(v.data);
    }
    v.data = data2;
}

func u32 NameVector.add(NameVector* v, u32 name_idx) {
    if (v.count == v.capacity) v.resize();

    u32 index = v.count;
    v.data[index] = name_idx;
    v.count++;
    return index;
}

func u32 NameVector.get(const NameVector* v, u32 idx) {
    return v.data[idx];
}

func bool NameVector.find(NameVector* v, u32 name_idx, u32* index) {
    for (u32 i=0; i<v.count; i++) {
        if (v.data[i] == name_idx) {
            *index = i;
            return true;
        }
    }
    return false;
}

type StackLayer struct {
    Decl* decl;
    scope.Scope* scope;
}

public type Analyser struct {
    diagnostics.Diags* diags;
    conversion_checker.Checker checker;
    ast_context.Context* context;
    ast_builder.Builder* builder;
    module_list.ModList* allmodules;
    const warning_flags.Flags* warnings;

    // below differs per run
    Module* mod;

    // collect struct-functions
    u32 prefix_cache_name;
    u32 prefix_cache_idx;
    // Vector<const char*> prefixes;
    NameVector prefixes;
    StructDecls struct_decls;

    // Type + Vars analysis
    StackLayer[MaxDepth] checkStack;
    u32 checkIndex;
    scope.Scope* scope; // points to top layer in stack

    // for each function-body analysis
    FunctionDecl* curFunction; // set when analysing a function-body
    bool has_error;
} @(opaque)

public func Analyser* create(diagnostics.Diags* diags,
                             ast_context.Context* context,
                             ast_builder.Builder* builder,
                             module_list.ModList* allmodules,
                             const warning_flags.Flags* warnings)
{
    Analyser* ma = stdlib.calloc(1, sizeof(Analyser));
    ma.diags = diags;
    ma.checker.init(diags, builder);
    ma.context = context;
    ma.builder = builder;
    ma.allmodules = allmodules;
    ma.warnings = warnings;
    return ma;
}

public func void Analyser.free(Analyser* ma) {
    ma.prefixes.free();  // Note: safe to be freed twice
    ma.struct_decls.free();
    stdlib.free(ma);
}

public func void Analyser.check(Analyser* ma, Module* mod)
{
    ma.mod = mod;
    ma.prefix_cache_name = 0;
    ma.prefix_cache_idx = 0;
    ma.prefixes.free();  // Note: safe to be freed twice
    ma.struct_decls.free();
    ma.checkIndex = 0;
    ma.scope = nil;
    ma.curFunction = nil;
    ma.has_error = false;

    //stdio.printf("analysing module %s\n", ma.mod.getName());

    mod.visitASTs(Analyser.createGlobalScope, ma);

    // NOTE: C2C Analyser::step1()
    // step 3a: collect incremental arrays
    // TODO use visitIncrArrays()

    // step 3b: collect struct functions
    mod.visitStructFunctions(Analyser.handleStructFunc, ma);

    for (u32 i=0; i<ma.struct_decls.count; i++) {
        const StructDeclInfo* info = &ma.struct_decls.data[i];
        StructTypeDecl* fd = cast<StructTypeDecl*>(info.decl);
        fd.setStructFunctions(ma.context, info.functions.getDecls(), info.functions.size());
    }

    ma.prefixes.free();  // Note: free here already (safe to be freed twice)
    ma.struct_decls.free();

    // NOTE: C2C Analyser::step2()
    // analyse types
    mod.visitTypeDecls(Analyser.handleTypeDecl, ma);

    mod.visitVarDecls(Analyser.handleVarDecl, ma);

    mod.visitStaticAsserts(Analyser.handleStaticAssert, ma);

    mod.visitFunctions(Analyser.analyseFunctionProto, ma);

    // NOTE: C2C Analyser::step3()
    // TODO only for non-interface components
    mod.visitFunctions(Analyser.analyseFunctionBodies, ma);

    mod.visitASTs(Analyser.deleteScope, ma);
}

func void Analyser.error(Analyser* ma, SrcLoc loc, const char* format, ...) {
    Va_list args;
    va_start(args, format);
    ma.diags.error(loc, format, args);
    va_end(args);

    ma.has_error = true;
    // TODO use JmpBuf here? (dont have to check anything then)
}

func void Analyser.errorRange(Analyser* ma, SrcLoc loc, SrcRange range, const char* format, ...) {
    Va_list args;
    va_start(args, format);
    ma.diags.errorRange(loc, range, format, args);
    va_end(args);

    ma.has_error = true;
    // TODO use JmpBuf here? (dont have to check anything then)
}

func void Analyser.createGlobalScope(void* arg, AST* a) {
    Analyser* ma = arg;
    scope.Scope* s = scope.create(ma.allmodules,
                                  ma.diags,
                                  a.getImports(),
                                  ma.mod,
                                  ma.mod.getSymbols(),
                                  !ma.warnings.no_unused_variable);
    a.setPtr(s);
}

func void Analyser.deleteScope(void* /*arg*/, AST* a) {
    scope.Scope* s = a.getPtr();
    a.setPtr(nil);
    s.free();
}

func void Analyser.handleStructFunc(void* arg, FunctionDecl* d) {
    Analyser* ma = arg;
    Ref* prefix = d.getPrefix();
    assert(prefix);
    u32 prefix_name_idx = prefix.name_idx;

    u32 index = 0;
    if (prefix_name_idx == ma.prefix_cache_name) {
        index = ma.prefix_cache_idx;
    } else {
        // search data structure
        bool found = false;
        found = ma.prefixes.find(prefix_name_idx, &index);

        if (!found) {
            Decl* decl = ma.mod.findType(prefix_name_idx);
            if (!decl) {
                // TODO search for all symbols for nice Diag
                ma.diags.report(prefix.loc, "unknown type '%s'", prefix.getName());
                return;
            }
            if (decl.getKind() != DeclKind.StructType) {
                ma.diags.report(prefix.loc, "struct-functions type must be a structs/union");
                return;
            }
            index = ma.prefixes.add(prefix_name_idx);
            ma.struct_decls.addDecl(decl);
        }
        ma.prefix_cache_name = prefix_name_idx;
        ma.prefix_cache_idx = index;
    }

    // TODO check for duplicate names
    ma.struct_decls.addFunc(index, d);
    prefix.decl = ma.struct_decls.getDecl(index);
}

func void Analyser.analyseFunctionProto(void* arg, FunctionDecl* d) {
    Analyser* ma = arg;

    ma.analyseGlobalDecl(cast<Decl*>(d));
}

func void Analyser.analyseFunctionBodies(void* arg, FunctionDecl* d) {
    Analyser* ma = arg;

    ma.analyseFunctionBody(d);
}

func void Analyser.analyseGlobalDecl(Analyser* ma, Decl* d) {
    if (d.isChecked()) return;

    if (!ma.pushCheck(d)) return;

    switch (d.getKind()) {
    case Function:
        ma.analyseFunction(cast<FunctionDecl*>(d));
        break;
    case Import:
        break;
    case StructType:
        ma.analyseStructType(cast<StructTypeDecl*>(d));
        break;
    case EnumType:
        ma.analyseEnumType(cast<EnumTypeDecl*>(d));
        break;
    case EnumConstant:
        assert(0);
        break;
    case FunctionType:
        ma.analyseFunctionType(d);
        break;
    case AliasType:
        ma.analyseAliasType(cast<AliasTypeDecl*>(d));
        break;
    case Var:
        ma.analyseGlobalVarDecl(cast<VarDecl*>(d));
        break;
    case StaticAssert:
        break;
    }

    d.setChecked();
    ma.popCheck();
}

func void Analyser.handleTypeDecl(void* arg, Decl* d) {
    Analyser* ma = arg;
    ma.analyseGlobalDecl(d);
}

func void Analyser.handleStaticAssert(void* arg, Decl* d) {
    Analyser* ma = arg;
    ma.scope = d.getAST().getPtr();

    StaticAssertDecl* sa = cast<StaticAssertDecl*>(d);
    Expr* lhs = sa.getLhs();
    Expr* rhs = sa.getRhs();

    ma.analyseExpr(&lhs, false);
    ma.analyseExpr(&rhs, false);

    bool error = false;
    if (!lhs.isCtv()) {
        ma.diags.reportRange(lhs.getLoc(), lhs.getRange(), "static_assert element is not a compile-time value");
        error = true;
    }
    if (!rhs.isCtv()) {
        ma.diags.reportRange(rhs.getLoc(), rhs.getRange(), "static_assert element is not a compile-time value");
        error = true;
    }

    if (error) return;

    ctv_analyser.Value val1 = ctv_analyser.get_value(lhs);
    ctv_analyser.Value val2 = ctv_analyser.get_value(rhs);

    if (!val1.equals(&val2)) {
        ma.diags.reportRange(rhs.getStartLoc(), rhs.getRange(), "static_assert failed, expected %s, got %s", val1.str(), val2.str());
    }
}

func void Analyser.handleVarDecl(void* arg, VarDecl* v) {
    Analyser* ma = arg;
    ma.analyseGlobalDecl(v.asDecl());
}

func void Analyser.checkName(Analyser* ma, Decl* d, bool is_constant) {
    const char* name = d.getName();
    if (is_constant) {
        if (ctype.islower(name[0])) {
            ma.diags.report(d.getLoc(), "a global constant name must start with an upper case character");
        }
    } else {
        if (ctype.isupper(name[0])) {
            ma.diags.report(d.getLoc(), "a variable name must start with an lower case character");
        }
    }
}

func void Analyser.analyseGlobalVarDecl(Analyser* ma, VarDecl* v) {
    Decl* d = v.asDecl();

    // Note: d.getType() is 0 here! (use refType)
    TypeRef* ref = v.getTypeRef();

    QualType res = ma.analyseTypeRef(ref);

    if (res.isInvalid()) return;
    d.setType(res);

    ma.checkName(d, res.isConstant());

    if (v.hasInit()) {
        ma.analyseInitExpr(v.getInit2(), res);
    }
}

func bool Analyser.analyseInitExpr(Analyser* ma, Expr** e_ptr, QualType expectedType) {
    Expr* e = *e_ptr;

    if (e.getKind() == ExprKind.InitList) {
        return ma.analyseInitListExpr(cast<InitListExpr*>(e), expectedType);
    }

    //if (e.getKind() == ExprKind.ArrayDesignatedInit) ..
    // TODO DesignatedInitExpr

    if (e.getKind() == ExprKind.StringLiteral) {
        // could be (const) char* x = "text" OR (const) char[] x = "text";
        // check array case for overflow
        // when doing char[] a = "abc" -> string is not an LValue
        // when doing char*  a = "abc" -> string is lvalue (so insert ArrayToPointerDecay)
        ArrayType* at = expectedType.getArrayTypeOrNil();
        if (at) {
            ArrayType* at2 = expectedType.getArrayType();
            // check that element type is char/int8
            QualType elem = at.getElemType();
            if (elem.getTypeOrNil() != g_char.getTypeOrNil() && elem.getTypeOrNil() != g_i8.getTypeOrNil()) {
                ma.diags.reportRange(e.getLoc(), e.getRange(), "initializer-string for for non-char/int8 array");
                return false;
            }

            u32 rhs_len = at2.getSize();
            if (at.hasSize()) {
                u32 lhs_len = at.getSize();
                if (rhs_len > lhs_len) {
                    ma.diags.reportRange(e.getLoc(), e.getRange(), "initializer-string for char array is too long");
                    return false;
                }
            } else {
                at.setSize(rhs_len);
            }
            e.setRValue();
        } else {
            // insert ArrayToPointerDecay
            QualType result = getPointerFromArray(ma.builder, e.getType());
            ma.builder.insertImplicitCast(ImplicitCastKind.ArrayToPointerDecay, e_ptr, result);
            e = *e_ptr;
        }
        return true;
    }

    QualType res = ma.analyseExpr(e_ptr, true);
    if (res.isInvalid()) return false;
    e = *e_ptr; // re-read since ImplicitCasts could have been inserted

    // TODO needed?
    //expectedType.clearQuals(); // rhs is not const/volatile
    //e.setType(expectedType);

    if (!ma.curFunction && !e.isCtv()) {
        if (!e.isCtc()) {
            ma.diags.reportRange(e.getLoc(), e.getRange(), "initializer element is not a compile-time constant");
            return false;
        }

        if (expectedType.needsCtvInit()) {
            ma.diags.reportRange(e.getLoc(), e.getRange(), "initializer element is not a compile-time value");
            return false;
        }
    }

    return ma.checker.check(expectedType, res, e_ptr, e.getLoc());
}

func bool Analyser.pushCheck(Analyser* ma, Decl* d) {
    for (u32 i=0; i<ma.checkIndex; i++) {
        if (ma.checkStack[i].decl == d) {
            for (u32 j=i; j<ma.checkIndex; j++) {
                ma.diags.report(d.getLoc(), "circular declaration dependency %s", d.getName());
            }
            // TODO use longjmp here?
            return false;
        }
    }
    // set correct scope for the current Decl (might be in different file than current Decl)
    ma.scope = d.getAST().getPtr();
    ma.checkStack[ma.checkIndex].decl = d;
    ma.checkStack[ma.checkIndex].scope = ma.scope;
    ma.checkIndex++;
    d.setCheckState(DeclCheckState.InProgress);
    return true;
}

func void Analyser.popCheck(Analyser* ma) {
    assert(ma.checkIndex > 0);
    ma.checkIndex--;
    if (ma.checkIndex > 0) ma.scope = ma.checkStack[ma.checkIndex-1].scope;
    else ma.scope = nil;
}

func bool Analyser.analyseInitListExpr(Analyser* ma, InitListExpr* ile, QualType expectedType) {
    const Type* t = expectedType.getTypeOrNil();
    if (t.isArrayType()) {
        return ma.analyseInitListArray(ile, expectedType);
    }

    if (t.isStructType()) {
        return ma.analyseInitListStruct(ile, expectedType);
    }

    Expr* e = cast<Expr*>(ile);
    ma.diags.report(e.getLoc(), "cannot initialize variable of type %s with initializer list", expectedType.diagName());
    return false;
}

func bool Analyser.analyseInitListArray(Analyser* ma, InitListExpr* ile, QualType expectedType) {
    Expr* e = cast<Expr*>(ile);
    u32 numValues = ile.getNumValues();
    Expr** values = ile.getValues();

    expectedType.clearQuals();
    ArrayType* at = expectedType.getArrayType();
    QualType et = at.getElemType();
    et.clearQuals();

    bool ok = true;
    bool ctc = true;
    bool have_designators = false;
    for (u32 i=0; i<numValues; i++) {
        Expr* value = values[i];

        if (value.getKind() == ExprKind.FieldDesignatedInit) {
            ma.diags.reportRange(value.getLoc(), value.getRange(), "field designator cannot initialize an array");
            ok = false;
            continue;
        }
        if (value.getKind() == ExprKind.ArrayDesignatedInit) {
            ok |= ma.analyseArrayDesignatedInit(value, et);
            have_designators = true;
            continue;
        }

        ok |= ma.analyseInitExpr(&values[i], et);
        ctc |= values[i].isCtc();
    }

    if (ctc) e.setCtc();
    if (!ok) return false;

    if (have_designators) {
        // TODO check designators (range, etc)
    }

    if (at.hasSize()) {
        u32 arraySize = at.getSize();
        if (numValues > arraySize) {
            ma.diags.report(values[arraySize].getLoc(), "excess elements in array initializer");
            return false;
        }
    } else {
        at.setSize(numValues);
    }

    e.setType(expectedType);
    return ok;
}

func bool Analyser.analyseInitListStruct(Analyser* ma, InitListExpr* ile, QualType expectedType) {
    // Note: expectedType is StructType
    Expr* e = cast<Expr*>(ile);
    u32 numValues = ile.getNumValues();
    Expr** values = ile.getValues();

    StructType* st = expectedType.getStructType();
    StructTypeDecl* std = st.getDecl();

    if (numValues == 0) {
        e.setType(expectedType);
        return true;
    }

    // only allow none or all fields to use Designators
    const bool haveDesignators = (values[0].getKind() == ExprKind.FieldDesignatedInit);

    if (!haveDesignators && std.isUnion()) {
        ma.diags.report(values[0].getLoc(), "union member initializer needs field designator");
        return false;
    }

    const u32 num_members = std.getNumMembers();
    Decl** members = std.getMembers();

    for (u32 i=0; i<numValues; i++) {
        Expr* value = values[i];
        if (i >= num_members) {
            ma.diags.report(value.getLoc(), "excess initializer elements in struct");
            return false;
        }
        bool is_designator = value.getKind() == ExprKind.FieldDesignatedInit;
        if (haveDesignators != is_designator) {
            ma.diags.report(value.getLoc(), "mixing field designator with non-field designators");
            return false;
        }
        if (is_designator) {
            FieldDesignatedInitExpr* fdi = cast<FieldDesignatedInitExpr*>(value);
            u32 field = fdi.getField();
            Expr** init_ = fdi.getInit2();
            Decl* member = ma.findStructMember(std, fdi.getField(), value.getLoc(), false);
            if (!member) return false;
            // TODO check for duplicates
            member.setUsed();
            // TODO allow substruct init by name?
            bool ok = ma.analyseInitExpr(fdi.getInit2(), member.getType());
            if (!ok) return false;
            value.setType(fdi.getInit().getType());
        } else {
            bool ok = ma.analyseInitExpr(&values[i], members[i].getType());
            if (!ok) return false;
        }
    }

    expectedType.clearQuals();
    e.setType(expectedType);
    return true;
}

func void Analyser.analyseStructType(Analyser* ma, StructTypeDecl* d) {
    NameVector names;
    names.init(d.getNumMembers());

    NameVector locs;
    locs.init(d.getNumMembers());

    ma.analyseStructNames(d, &names, &locs);

    names.free();
    locs.free();

    ma.analyseStructMembers(d);
}

func void Analyser.analyseStructMembers(Analyser* ma, StructTypeDecl* d) {
    u32 count = d.getNumMembers();
    Decl** members = d.getMembers();

    for (u32 i=0; i<count; i++) {
        Decl* member = members[i];
        if (member.getKind() == DeclKind.Var) {
            VarDecl* vd = cast<VarDecl*>(member);
            // Note: dont push to stack, because can self-ref to StructType
            member.setCheckState(DeclCheckState.InProgress);
            ma.analyseStructMember(vd);

            Expr* bitfield = vd.getBitfield();
            if (bitfield) {
                QualType qt = ma.analyseExpr(&bitfield, false);
                if (qt.isInvalid()) return;
                if (!bitfield.isCtv()) {
                    ma.diags.reportRange(bitfield.getLoc(), bitfield.getRange(), "bitfield size is not a compile-time value");
                    return;
                }
            }

            member.setCheckState(DeclCheckState.Checked);
        } else if (member.getKind() == DeclKind.StructType) {
            StructTypeDecl* sub = cast<StructTypeDecl*>(member);
            ma.analyseStructMembers(sub);
            member.setCheckState(DeclCheckState.Checked);
        }
    }

    TypeSize info = sizeOfStruct(d);
    d.setSize(info.size);
    d.setAlignment(info.align);
}

func void Analyser.analyseStructNames(Analyser* ma, StructTypeDecl* d, NameVector* names, NameVector* locs) {
    // note: already checked that struct doesn't have 0 members
    u32 count = d.getNumMembers();
    Decl** members = d.getMembers();

    for (u32 i=0; i<count; i++) {
        // there can more members in anonymous sub-structs
        Decl* member = members[i];
        u32 name_idx = member.getNameIdx();
        u32 old_index;

        StructTypeDecl* sub = nil;
        if (member.getKind() == DeclKind.StructType) sub = cast<StructTypeDecl*>(member);

        if (name_idx == 0) {
            // can be anonymous sub-struct/union or anonymous bit-field
            if (member.getKind() == DeclKind.StructType) {
                ma.analyseStructNames(sub, names, locs);
            }
        } else {
            if (names.find(name_idx, &old_index)) {
                ma.diags.report(member.getLoc(), "duplicate %s member '%s'", d.isStruct() ? "struct" : "union", member.getName());
                ma.diags.note(locs.get(old_index), "previous declaration is here");
                return;
            }
            names.add(name_idx);
            locs.add(member.getLoc());

            if (member.getKind() == DeclKind.StructType) {
                NameVector sub_names;
                sub_names.init(sub.getNumMembers());
                NameVector sub_locs;
                sub_locs.init(sub.getNumMembers());
                ma.analyseStructNames(sub, &sub_names, &sub_locs);
                sub_names.free();
                sub_locs.free();
            }
        }
    }
}

func void Analyser.analyseFunctionType(Analyser* ma, Decl* d) {
    FunctionTypeDecl* ftd = cast<FunctionTypeDecl*>(d);
    FunctionDecl* fd = ftd.getFunction();

    ma.analyseFunction(fd);
}

func void Analyser.analyseFunction(Analyser* ma, FunctionDecl* fd) {
    // return type
    QualType qt = ma.analyseTypeRef(fd.getTypeRef());
    if (qt.isInvalid()) return;

    fd.setRType(qt);

    // analyse params
    u32 num_params = fd.getNumParams();
    VarDecl** params = fd.getParams();
    for (u32 i=0; i<num_params; i++) {
        VarDecl* v = params[i];
        TypeRef* ref = v.getTypeRef();

        QualType res = ma.analyseTypeRef(ref);
        if (res.isInvalid()) continue;

        Decl* d = cast<Decl*>(v);
        d.setType(res);
        d.setChecked();
    }

    if (num_params && fd.hasPrefix()) {
        // check if SF if first arg is (const) Struct*
        // Note: use TypeRef, since it's faster to check
        const Ref* prefix = fd.getPrefix();
        QualType prefixType = prefix.decl.getType();

        TypeRef* ref = params[0].getTypeRef();
        if (ref.isPointerTo(prefixType.getIndex())) {
            fd.setCallKind(CallKind.StructFunc);
        }
    }
}

func void Analyser.analyseAliasType(Analyser* ma, AliasTypeDecl* d) {
    TypeRef* ref = d.getTypeRef();
    QualType res = ma.analyseTypeRef(ref);
    QualType qt = d.asDecl().getType();
    // Note: type is always AliasType
    Type* at = qt.getTypeOrNil();
    QualType canon = res.getCanonicalType();
    canon.copyQuals(res);
    at.setCanonicalType(canon);
}

func void Analyser.analyseEnumType(Analyser* ma, EnumTypeDecl* d) {
    u32 num_constants = d.getNumConstants();
    EnumConstantDecl** constants = d.getConstants();
    u32 value = 0;

    // TODO we need a file-dependent lookup here, to see which imports we can use!
    for (u32 i=0; i<num_constants; i++) {
        EnumConstantDecl* c = constants[i];

        // check for duplicate names
        const char* name = c.asDecl().getName();
        for (u32 j=0; j<i; j++) {
            const Decl* other = constants[j].asDecl();
            if (other.getName() == name) {
                ma.diags.report(c.asDecl().getLoc(), "duplicate enum constant name '%s'", name);
                ma.diags.note(other.getLoc(), "previous declaration is here");
                // TODO ma.diags.info(constants[j].asDecl().getLoc(), "other declaration is here");
                return;
            }
        }

        Expr* initval = c.getInit();
        if (initval) {
            QualType res = ma.analyseExpr(c.getInit2(), true);
            initval = c.getInit();  // re-read in case of ImplicitCast has been inserted
            if (!initval.isCtv()) {
                ma.diags.reportRange(initval.getLoc(), initval.getRange(), "initializer is not a compile-time value");
                return;
            }
            ctv_analyser.Value ctv = ctv_analyser.get_value(initval);
            if (ctv.uvalue < value) {
                ma.diags.report(initval.getLoc(), "enum constants need to increase (value %u, previous %u)", ctv.uvalue, value-1);
                return;
            }
            value = cast<u32>(ctv.uvalue);
        }
        // TODO check if constants fit in impl type
        c.setValue(value);
        c.asDecl().setChecked();
        value++;
    }
}

func void Analyser.analyseStructMember(Analyser* ma, VarDecl* v) {
    TypeRef* ref = v.getTypeRef();

    // TODO pass public
    QualType res = ma.analyseTypeRef(ref);
    if (res.isInvalid()) return;
    v.asDecl().setType(res);

    // TODO check attributes

    // TODO check initValue
}

func QualType Analyser.analyseUserTypeRef(Analyser* ma, TypeRef* ref) {
    const Ref* prefix = ref.getPrefix();
    const Ref* user = ref.getUser();

    Decl* d = nil;
    if (prefix) {
        ImportDecl* i = ma.scope.findModule(prefix.name_idx, prefix.loc);
        if (!i) {
            ma.has_error = true;
            return QualType_Invalid;
        }

        ref.setPrefix(cast<Decl*>(i));
        Module* mod = i.getDest();
        d = ma.scope.findGlobalSymbolInModule(mod, user.name_idx, user.loc);
    } else {
        d = ma.scope.findType(user.name_idx, user.loc);
    }
    if (!d) {
        ma.has_error = true;
        return QualType_Invalid;
    }

    ref.setUser(d);

    if (!d.isTypeDecl()) {
        ma.diags.report(user.loc, "%s is not a type", ast.idx2name(user.name_idx));
        return QualType_Invalid;
    }

    // TODO need used_public?
    bool external = (ma.mod != d.getModule());
    if (!d.isPublic() && external) {
        ma.diags.report(user.loc, "%s is not public", ast.idx2name(user.name_idx));
        return QualType_Invalid;
    }

    bool full = (ref.getNumPointers() == 0);
    DeclCheckState state = d.getCheckState();

    if (full && state == DeclCheckState.InProgress) {
        ma.diags.report(user.loc, "circular declaration");
        return QualType_Invalid;
    }

    if (full && state != DeclCheckState.Checked) {
        ma.analyseGlobalDecl(d);
    }

    d.setUsed();
    if (/* used_public || */ external) d.setUsedPublic();

    return d.getType();
}

func QualType Analyser.analyseTypeRef(Analyser* ma, TypeRef* ref) {
    //ref.dump();
    QualType base = { }
    u32 base_index = 0;
    if (ref.isUser()) {
        base = ma.analyseUserTypeRef(ref);
        if (base.isInvalid()) return base;
        assert(base.hasCanonicalType());
    } else {
        BuiltinKind kind = ref.getBuiltinKind();
        base = ma.builder.actOnBuiltinType(kind);
        assert(base.isValid());
    }

    if (ref.isConst()) base.setConst();
    if (ref.isVolatile()) base.setVolatile();

    QualType resolved = base;
    u32 num_ptrs = ref.getNumPointers();
    for (u32 i=0; i<num_ptrs; i++) {
        resolved = ma.builder.actOnPointerType(resolved) ;
    }

    u32 num_arrays = ref.getNumArrays();
    for (u32 i=0; i<num_arrays; i++) {
        Expr* sizeExpr = ref.getArray(i);  // note: ImplicitCast could have been inserted
        u32 size = 0;
        if (sizeExpr) {
            ma.analyseExpr(ref.getArray2(i), true);
            sizeExpr = ref.getArray(i);  // note: ImplicitCast could have been inserted

            if (!sizeExpr.isCtv()) {
                ma.diags.reportRange(sizeExpr.getLoc(), sizeExpr.getRange(), "array size is not a compile-time value");
            } else {
                ctv_analyser.Value value = ctv_analyser.get_value(sizeExpr);
                if (value.isNegative()) {
                    ma.diags.reportRange(sizeExpr.getLoc(), sizeExpr.getRange(), "array size has negative value %d", value.svalue);
                }
                size = cast<u32>(value.uvalue);
            }
        }
        //NOTE: we dont store ref.isIncrArray() (yet)
        resolved = ma.builder.actOnArrayType(resolved, sizeExpr != nil, size);
    }

    ref.setDest(base.getIndex());
    //resolved.dump();
    //resolved.dump_full();
    return resolved;
}

type MainMarker struct {
    u32 name_idx;
    Decl* main;
}

func void markMainUsed(void* arg, FunctionDecl* fd) {
    MainMarker* m = (arg);
    Decl* d = cast<Decl*>(fd);
    if (d.getNameIdx() == m.name_idx) {
        m.main = d;
    }
}

public func bool Analyser.checkMain(Analyser* ma, Module* top, u32 name_idx) {
    top.setUsed();
    MainMarker marker = { name_idx, nil }
    top.visitFunctions(markMainUsed, &marker);
    if (!marker.main) {
        ma.error(0, "no main function found");
        return false;
    }
    marker.main.setUsed();
    marker.main.setUsedPublic();

    // TODO check return type + args

    return true;
}


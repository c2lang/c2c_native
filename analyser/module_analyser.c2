/* Copyright 2022-2023 Bas van den Berg
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

module module_analyser;

import ast_context;
import ast_builder;
import ast local;
import ctv_analyser;
import size_analyser;
import conversion_checker as conv;
import diagnostics;
import module_list;
import src_loc local;
import scope;
import string_pool;
import warning_flags;
import struct_func_list as sf_list;

import ctype;
import stdarg local;
import stdlib;
import string;

const u32 MaxDepth = 8;
const u32 LHS = 0x01;
const u32 RHS = 0x02;

public type Analyser struct {
    diagnostics.Diags* diags;
    conv.Checker checker;
    ast_context.Context* context;
    string_pool.Pool* astPool;
    ast_builder.Builder* builder;
    module_list.List* allmodules;
    const warning_flags.Flags* warnings;

    // below differs per run
    Module* mod;
    bool usedPublic;    // set to false during function body analysis or during non-public analysis

    // collect struct-functions
    u32 prefix_cache_name;
    u32 prefix_cache_idx;
    NameVector prefixes;
    sf_list.List* struct_decls;   // only set during SF collection

    LabelVector labels;

    // Type + Vars analysis
    StackLayer[MaxDepth] checkStack;
    u32 checkIndex;
    scope.Scope* scope; // points to top layer in stack

    // for each function-body analysis
    FunctionDecl* curFunction; // set when analysing a function-body
    bool has_error;
} @(opaque)

public func Analyser* create(diagnostics.Diags* diags,
                             ast_context.Context* context,
                             string_pool.Pool* astPool,
                             ast_builder.Builder* builder,
                             module_list.List* allmodules,
                             const warning_flags.Flags* warnings)
{
    Analyser* ma = stdlib.calloc(1, sizeof(Analyser));
    ma.diags = diags;
    ma.checker.init(diags, builder);
    ma.context = context;
    ma.astPool = astPool;
    ma.builder = builder;
    ma.allmodules = allmodules;
    ma.warnings = warnings;
    return ma;
}

public func void Analyser.free(Analyser* ma) {
    ma.prefixes.free();  // Note: safe to be freed twice
    ma.labels.free();
    stdlib.free(ma);
}

public func void Analyser.check(Analyser* ma, Module* mod)
{
    ma.mod = mod;
    ma.prefix_cache_name = 0;
    ma.prefix_cache_idx = 0;
    ma.prefixes.free();  // Note: safe to be freed twice
    ma.labels.reset();
    ma.checkIndex = 0;
    ma.scope = nil;
    ma.curFunction = nil;
    ma.has_error = false;
    ma.usedPublic = false;

    //stdio.printf("analysing module %s\n", ma.mod.getName());

    mod.visitASTs(Analyser.createGlobalScope, ma);

    // TODO use mod.visitArrayValues(Analyser.handleEnumValue, ma);

    // collect struct functions
    sf_list.List struct_decls = { }
    ma.struct_decls = &struct_decls;
    mod.visitStructFunctions(Analyser.handleStructFunc, ma);
    if (ma.has_error) return;

    for (u32 i=0; i<struct_decls.count; i++) {
        const sf_list.Info* info = &struct_decls.data[i];
        StructTypeDecl* fd = cast<StructTypeDecl*>(info.decl);
        fd.setStructFunctions(ma.context, info.functions.getDecls(), info.functions.size());
    }

    ma.prefixes.free();  // Note: free here already (safe to be freed twice)
    struct_decls.free();
    ma.struct_decls = nil;

    // analyse types
    mod.visitTypeDecls(Analyser.handleTypeDecl, ma);

    // collect incremental arrays
    mod.visitArrayValues(Analyser.handleArrayValue, ma);

    mod.visitVarDecls(Analyser.handleVarDecl, ma);

    mod.visitStaticAsserts(Analyser.handleStaticAssert, ma);

    mod.visitFunctions(Analyser.analyseFunctionProto, ma);

    if (ma.has_error) return;

    // TODO only for non-interface components
    mod.visitFunctions(Analyser.analyseFunctionBodies, ma);

    // check that exported modules only use other exported-modules publicly
    if (!mod.isExternal() && mod.isExported()) {
        mod.visitImports(Analyser.handleImport, ma);
    }

    mod.visitASTs(Analyser.deleteScope, ma);
}

func void Analyser.handleImport(void* arg, ast.ImportDecl* id) {
    Analyser* ma = arg;
    Decl* d = cast<Decl*>(id);
    if (!d.isUsedPublic()) return;

    Module* dest = id.getDest();
    if (dest.isExternal()) return;
    if (!dest.isExported()) {
        ma.error(d.getLoc(), "exported module '%s' publicly uses non-exported module '%s'", ma.mod.getName(), dest.getName());
    }
}

func void Analyser.setMod(Analyser* ma, Module* mod) {
    ma.mod = mod;
}

// TEMP here until we have templates
type NameVector struct {
    u32* data;
    u32 count;
    u32 capacity;
}

func void NameVector.init(NameVector* v, u32 capacity) {
    v.data = nil;
    v.count = 0;
    v.capacity = capacity / 2; // workaround resize
    v.resize();
}

func void NameVector.free(NameVector* v) {
    stdlib.free(v.data);
    v.count = 0;
    v.capacity = 0;
    v.data = nil;
}

func void NameVector.resize(NameVector* v) {
    v.capacity = v.capacity == 0 ? 4 : v.capacity * 2;
    void* data2 = stdlib.malloc(v.capacity * sizeof(u32));
    if (v.data) {
        string.memcpy(data2, v.data, v.count * sizeof(u32));
        stdlib.free(v.data);
    }
    v.data = data2;
}

func u32 NameVector.add(NameVector* v, u32 name_idx) {
    if (v.count == v.capacity) v.resize();

    u32 index = v.count;
    v.data[index] = name_idx;
    v.count++;
    return index;
}

func u32 NameVector.get(const NameVector* v, u32 idx) {
    return v.data[idx];
}

func bool NameVector.find(NameVector* v, u32 name_idx, u32* index) {
    for (u32 i=0; i<v.count; i++) {
        if (v.data[i] == name_idx) {
            *index = i;
            return true;
        }
    }
    return false;
}

// TEMP here until we have templates

type Label struct {
    u32 name_idx;
    SrcLoc loc;
    bool used;
    bool is_label; // otherwise goto
}

type LabelVector struct {
    Label* labels;
    u32 count;
    u32 capacity;
}

func void LabelVector.init(LabelVector* v, u32 capacity) {
    v.labels = nil;
    v.count = 0;
    v.capacity = capacity / 2; // workaround resize
}

func void LabelVector.free(LabelVector* v) {
    if (v.labels) stdlib.free(v.labels);
    v.count = 0;
    v.capacity = 0;
    v.labels = nil;
}

func void LabelVector.reset(LabelVector* v) {
    v.count = 0;
}

func void LabelVector.resize(LabelVector* v) {
    v.capacity = v.capacity == 0 ? 4 : v.capacity * 2;
    void* data2 = stdlib.malloc(v.capacity * sizeof(Label));
    if (v.labels) {
        string.memcpy(data2, v.labels, v.count * sizeof(Label));
        stdlib.free(v.labels);
    }
    v.labels = data2;
}

func Label* LabelVector.add(LabelVector* v, u32 name_idx, SrcLoc loc, bool is_label) {
    Label* l = v.find(name_idx);
    if (!l) {
        if (v.count == v.capacity) v.resize();

        u32 index = v.count;
        l = &v.labels[index];
        l.name_idx = name_idx;
        l.loc = loc;
        l.is_label = is_label;
        l.used = !is_label;
        v.count++;
    }
    return l;
}

func u32 LabelVector.getCount(const LabelVector* v) {
    return v.count;
}

func const Label* LabelVector.getLabels(const LabelVector* v) {
    return v.labels;
}

func Label* LabelVector.find(LabelVector* v, u32 name_idx) {
    for (u32 i=0; i<v.count; i++) {
        if (v.labels[i].name_idx == name_idx) return &v.labels[i];
    }
    return nil;
}



type StackLayer struct {
    Decl* decl;
    scope.Scope* scope;
    FunctionDecl* function; // only set when analysing function bodies
    bool usedPublic;
}

func void Analyser.note(Analyser* ma, SrcLoc loc, const char* format, ...) {
    Va_list args;
    va_start(args, format);
    ma.diags.note2(loc, format, args);
    va_end(args);
}

func void Analyser.warn(Analyser* ma, SrcLoc loc, const char* format, ...) {
    Va_list args;
    va_start(args, format);
    ma.diags.warn2(loc, format, args);
    va_end(args);
}

func void Analyser.error(Analyser* ma, SrcLoc loc, const char* format, ...) {
    Va_list args;
    va_start(args, format);
    ma.diags.error2(loc, format, args);
    va_end(args);

    ma.has_error = true;
    // TODO use JmpBuf here? (dont have to check anything then)
}

func void Analyser.errorRange(Analyser* ma, SrcLoc loc, SrcRange range, const char* format, ...) {
    Va_list args;
    va_start(args, format);
    ma.diags.errorRange2(loc, range, format, args);
    va_end(args);

    ma.has_error = true;
    // TODO use JmpBuf here? (dont have to check anything then)
}

func void Analyser.createGlobalScope(void* arg, AST* a) {
    Analyser* ma = arg;
    scope.Scope* s = scope.create(ma.allmodules,
                                  ma.diags,
                                  a.getImports(),
                                  ma.mod,
                                  ma.mod.getSymbols(),
                                  !ma.warnings.no_unused_variable);
    a.setPtr(s);
}

func void Analyser.deleteScope(void* /*arg*/, AST* a) {
    scope.Scope* s = a.getPtr();
    a.setPtr(nil);
    s.free();
}

func void Analyser.handleStructFunc(void* arg, FunctionDecl* d) {
    Analyser* ma = arg;
    Ref* prefix = d.getPrefix();
    assert(prefix);
    u32 prefix_name_idx = prefix.name_idx;

    assert(ma.struct_decls);
    u32 index = 0;
    if (prefix_name_idx == ma.prefix_cache_name) {
        index = ma.prefix_cache_idx;
    } else {
        // search data structure
        bool found = false;
        found = ma.prefixes.find(prefix_name_idx, &index);

        if (!found) {
            Decl* decl = ma.mod.findType(prefix_name_idx);
            if (!decl) {
                // TODO search for all symbols for nice Diag
                ma.error(prefix.loc, "unknown type '%s'", prefix.getName());
                return;
            }
            if (decl.getKind() != DeclKind.StructType) {
                ma.error(prefix.loc, "a struct-function type must be a struct/union");
                return;
            }
            if (d.asDecl().isPublic() && !decl.isPublic()) {
                ma.error(prefix.loc, "public struct-functions need a public struct/union");
                return;
            }
            index = ma.prefixes.add(prefix_name_idx);
            ma.struct_decls.addDecl(decl);
        }
        ma.prefix_cache_name = prefix_name_idx;
        ma.prefix_cache_idx = index;
    }

    // TODO check for duplicate names
    ma.struct_decls.addFunc(index, d);
    prefix.decl = ma.struct_decls.getDecl(index);
}

func void Analyser.handleArrayValue(void* arg, ArrayValueDecl* avd) {
    Analyser* ma = arg;
    // TODO
    avd.asDecl().dump();
}

func void Analyser.analyseFunctionProto(void* arg, FunctionDecl* d) {
    Analyser* ma = arg;

    ma.analyseGlobalDecl(cast<Decl*>(d));
}

func void Analyser.analyseFunctionBodies(void* arg, FunctionDecl* d) {
    Analyser* ma = arg;

    ma.analyseFunctionBody(d, d.asDecl().getAST().getPtr());
}

func void Analyser.analyseGlobalDecl(Analyser* ma, Decl* d) {
    if (d.isChecked()) return;

    if (!ma.pushCheck(d, d.getAST().getPtr(), nil)) return;

    switch (d.getKind()) {
    case Function:
        ma.analyseFunction(cast<FunctionDecl*>(d));
        break;
    case Import:
        break;
    case StructType:
        ma.analyseStructType(cast<StructTypeDecl*>(d));
        break;
    case EnumType:
        ma.analyseEnumType(cast<EnumTypeDecl*>(d));
        break;
    case EnumConstant:
        assert(0);
        break;
    case FunctionType:
        ma.analyseFunctionType(d);
        break;
    case AliasType:
        ma.analyseAliasType(cast<AliasTypeDecl*>(d));
        break;
    case Var:
        ma.analyseGlobalVarDecl(cast<VarDecl*>(d));
        break;
    case StaticAssert:
        break;
    case ArrayValue:
        break;
    }

    d.setChecked();
    ma.popCheck();
}

func void Analyser.handleTypeDecl(void* arg, Decl* d) {
    Analyser* ma = arg;
    ma.analyseGlobalDecl(d);
}

func void Analyser.handleStaticAssert(void* arg, Decl* d) {
    Analyser* ma = arg;
    ma.scope = d.getAST().getPtr();

    StaticAssertDecl* sa = cast<StaticAssertDecl*>(d);
    Expr* lhs = sa.getLhs();
    Expr* rhs = sa.getRhs();

    ma.analyseExpr(&lhs, false, RHS);
    ma.analyseExpr(&rhs, false, RHS);

    bool error = false;
    if (!lhs.isCtv()) {
        ma.errorRange(lhs.getLoc(), lhs.getRange(), "static_assert element is not a compile-time value");
        error = true;
    }
    if (!rhs.isCtv()) {
        ma.errorRange(rhs.getLoc(), rhs.getRange(), "static_assert element is not a compile-time value");
        error = true;
    }

    if (error) return;

    Value val1 = ctv_analyser.get_value(lhs);
    Value val2 = ctv_analyser.get_value(rhs);

    if (!val1.equals(&val2)) {
        ma.errorRange(rhs.getStartLoc(), rhs.getRange(), "static_assert failed, expected %s, got %s", val1.str(), val2.str());
    }
}

func void Analyser.handleVarDecl(void* arg, VarDecl* v) {
    Analyser* ma = arg;
    ma.analyseGlobalDecl(v.asDecl());
}

func void Analyser.checkName(Analyser* ma, Decl* d, bool is_constant) {
    const char* name = d.getName();
    if (is_constant) {
        if (ctype.islower(name[0])) {
            ma.error(d.getLoc(), "a global constant name must start with an upper case character");
        }
    } else {
        if (ctype.isupper(name[0])) {
            ma.error(d.getLoc(), "a variable name must start with an lower case character");
        }
    }
}

func void Analyser.analyseGlobalVarDecl(Analyser* ma, VarDecl* v) {
    Decl* d = v.asDecl();

    // Note: d.getType() is 0 here! (use refType)
    TypeRef* ref = v.getTypeRef();

    QualType res = ma.analyseTypeRef(ref);

    if (res.isInvalid()) return;
    d.setType(res);

    ma.checkName(d, res.isConstant());

    if (d.isPublic()) setTypePublicUsed(res);

    if (v.hasInit()) {
        if (!res.isConstant()) {
            ma.checkStack[ma.checkIndex].usedPublic = false;
            ma.usedPublic = false;
        }
        ma.analyseInitExpr(v.getInit2(), res);
    }
}

func bool Analyser.analyseInitExpr(Analyser* ma, Expr** e_ptr, QualType expectedType) {
    Expr* e = *e_ptr;

    if (e.getKind() == ExprKind.InitList) {
        return ma.analyseInitListExpr(cast<InitListExpr*>(e), expectedType);
    }

    //if (e.getKind() == ExprKind.ArrayDesignatedInit) ..
    // TODO DesignatedInitExpr

    if (e.getKind() == ExprKind.StringLiteral) {
        // could be (const) char* x = "text" OR (const) char[] x = "text";
        // check array case for overflow
        // when doing char[] a = "abc" -> string is not an LValue
        // when doing char*  a = "abc" -> string is lvalue (so insert ArrayToPointerDecay)
        ArrayType* at = expectedType.getArrayTypeOrNil();
        if (at) {
            // check that element type is char/int8
            QualType elem = at.getElemType();
            if (elem.getTypeOrNil() != g_char.getTypeOrNil() && elem.getTypeOrNil() != g_i8.getTypeOrNil()) {
                ma.errorRange(e.getLoc(), e.getRange(), "cannot initialize array of '%s' with a string literal", elem.diagName());
                return false;
            }

            QualType st = e.getType();
            ArrayType* at2 = st.getArrayType();
            u32 rhs_len = at2.getSize();
            if (at.hasSize()) {
                u32 lhs_len = at.getSize();
                if (rhs_len > lhs_len) {
                    ma.errorRange(e.getLoc(), e.getRange(), "initializer-string for char array is too long");
                    return false;
                }
            } else {
                at.setSize(rhs_len);
            }
            e.setRValue();
        } else {
            // need to add ArrayToPointerDecay (otherwise checker can get arrays)
            QualType qt = ma.analyseExpr(e_ptr, true, RHS);
            e = *e_ptr;
            if (!ma.checker.check(expectedType, e.getType(), e_ptr, e.getLoc())) return false;
        }
        return true;
    }

    QualType res = ma.analyseExpr(e_ptr, true, RHS);
    if (res.isInvalid()) return false;
    e = *e_ptr; // re-read since ImplicitCasts could have been inserted

    // TODO needed?
    //expectedType.clearQuals(); // rhs is not const/volatile
    //e.setType(expectedType);

    if (e.isCtv()) {
        return ctv_analyser.check(ma.diags, expectedType, e);
    }

    if (!ma.curFunction && !e.isCtv()) {
        if (!e.isCtc()) {
            ma.errorRange(e.getLoc(), e.getRange(), "initializer element is not a compile-time constant");
            return false;
        }

        if (expectedType.needsCtvInit()) {
            ma.errorRange(e.getLoc(), e.getRange(), "initializer element is not a compile-time value");
            return false;
        }
    }

    return ma.checker.check(expectedType, res, e_ptr, e.getLoc());
}

// Note: fd is only set if analysing function bodies
func bool Analyser.pushCheck(Analyser* ma, Decl* d, scope.Scope* s, FunctionDecl* fd) {
    for (u32 i=0; i<ma.checkIndex; i++) {
        if (ma.checkStack[i].decl == d) {
            for (u32 j=i; j<ma.checkIndex; j++) {
                ma.error(d.getLoc(), "circular declaration dependency %s", d.getName());
            }
            // TODO use longjmp here?
            return false;
        }
    }
    // set correct scope for the current Decl (might be in different file than current Decl)
    ma.scope = s;
    StackLayer* top = &ma.checkStack[ma.checkIndex];
    top.decl = d;
    top.scope = ma.scope;
    top.function = fd;
    top.usedPublic = d.isPublic();
    if (fd) ma.curFunction = fd;
    ma.usedPublic = top.usedPublic;
    ma.checkIndex++;
    // for function bodies
    if (!d.isChecked()) d.setCheckState(DeclCheckState.InProgress);
    assert(ma.checkIndex <= MaxDepth);
    return true;
}

func void Analyser.popCheck(Analyser* ma) {
    assert(ma.checkIndex > 0);
    ma.checkIndex--;
    if (ma.checkIndex > 0) {
        StackLayer* top = &ma.checkStack[ma.checkIndex-1];
        ma.scope = top.scope;
        ma.curFunction = top.function;
        ma.usedPublic = top.usedPublic;
    } else {
        ma.scope = nil;
        ma.curFunction = nil;
    }
}

func bool Analyser.analyseInitListExpr(Analyser* ma, InitListExpr* ile, QualType expectedType) {
    const Type* t = expectedType.getTypeOrNil();
    if (t.isArrayType()) {
        return ma.analyseInitListArray(ile, expectedType);
    }

    if (t.isStructType()) {
        return ma.analyseInitListStruct(ile, expectedType);
    }

    Expr* e = cast<Expr*>(ile);
    ma.error(e.getLoc(), "cannot initialize variable of type %s with initializer list", expectedType.diagName());
    return false;
}

func bool Analyser.analyseInitListArray(Analyser* ma, InitListExpr* ile, QualType expectedType) {
    Expr* e = cast<Expr*>(ile);
    u32 numValues = ile.getNumValues();
    Expr** values = ile.getValues();

    expectedType.clearQuals();
    ArrayType* at = expectedType.getArrayType();
    QualType et = at.getElemType();
    et.clearQuals();

    bool ok = true;
    bool ctc = true;
    bool have_designators = false;
    for (u32 i=0; i<numValues; i++) {
        Expr* value = values[i];

        if (value.getKind() == ExprKind.FieldDesignatedInit) {
            ma.errorRange(value.getLoc(), value.getRange(), "field designator cannot initialize an array");
            ok = false;
            continue;
        }
        if (value.getKind() == ExprKind.ArrayDesignatedInit) {
            ok |= ma.analyseArrayDesignatedInit(value, et);
            have_designators = true;
            continue;
        }

        ok |= ma.analyseInitExpr(&values[i], et);
        ctc |= values[i].isCtc();
    }

    if (ctc) e.setCtc();
    if (!ok) return false;

    if (have_designators) {
        // TODO check designators (range, etc)
    }

    if (at.hasSize()) {
        u32 arraySize = at.getSize();
        if (numValues > arraySize) {
            ma.error(values[arraySize].getLoc(), "excess elements in array initializer");
            return false;
        }
    } else {
        at.setSize(numValues);
    }

    e.setType(expectedType);
    return ok;
}

func bool Analyser.analyseInitListStruct(Analyser* ma, InitListExpr* ile, QualType expectedType) {
    // Note: expectedType is StructType
    Expr* e = cast<Expr*>(ile);
    u32 numValues = ile.getNumValues();
    Expr** values = ile.getValues();

    StructType* st = expectedType.getStructType();
    StructTypeDecl* std = st.getDecl();

    if (numValues == 0) {
        e.setType(expectedType);
        return true;
    }

    // only allow none or all fields to use Designators
    const bool haveDesignators = (values[0].getKind() == ExprKind.FieldDesignatedInit);

    if (!haveDesignators && std.isUnion()) {
        ma.error(values[0].getLoc(), "union member initializer needs field designator");
        return false;
    }

    const u32 num_members = std.getNumMembers();
    Decl** members = std.getMembers();

    for (u32 i=0; i<numValues; i++) {
        Expr* value = values[i];
        if (i >= num_members) {
            ma.error(value.getLoc(), "excess initializer elements in struct");
            return false;
        }
        bool is_designator = value.getKind() == ExprKind.FieldDesignatedInit;
        if (haveDesignators != is_designator) {
            ma.error(value.getLoc(), "mixing field designator with non-field designators");
            return false;
        }
        if (is_designator) {
            FieldDesignatedInitExpr* fdi = cast<FieldDesignatedInitExpr*>(value);
            //u32 field = fdi.getField();
            //Expr** init_ = fdi.getInit2();
            Decl* member = ma.findStructMember(std, fdi.getField(), value.getLoc(), false);
            if (!member) return false;
            // TODO check for duplicates
            member.setUsed();
            // TODO allow substruct init by name?
            bool ok = ma.analyseInitExpr(fdi.getInit2(), member.getType());
            if (!ok) return false;
            value.setType(fdi.getInit().getType());
        } else {
            members[i].setUsed();
            bool ok = ma.analyseInitExpr(&values[i], members[i].getType());
            if (!ok) return false;
        }
    }

    expectedType.clearQuals();
    e.setType(expectedType);
    return true;
}

func void Analyser.analyseStructType(Analyser* ma, StructTypeDecl* d) {
    if (d.isOpaque()) {
        ma.checkStack[ma.checkIndex].usedPublic = false;
        ma.usedPublic = false;
    }

    NameVector names;
    names.init(d.getNumMembers());

    NameVector locs;
    locs.init(d.getNumMembers());

    ma.analyseStructNames(d, &names, &locs);

    names.free();
    locs.free();

    ma.analyseStructMembers(d);
}

func void Analyser.analyseStructMembers(Analyser* ma, StructTypeDecl* d) {
    u32 count = d.getNumMembers();
    Decl** members = d.getMembers();
    bool is_public = d.asDecl().isPublic();

    for (u32 i=0; i<count; i++) {
        Decl* member = members[i];
        if (member.getKind() == DeclKind.Var) {
            VarDecl* vd = cast<VarDecl*>(member);
            // Note: dont push to stack, because can self-ref to StructType
            member.setCheckState(DeclCheckState.InProgress);
            ma.analyseStructMember(vd, is_public);

            Expr* bitfield = vd.getBitfield();
            if (bitfield) {
                QualType qt = ma.analyseExpr(&bitfield, false, RHS);
                if (qt.isInvalid()) return;
                if (!bitfield.isCtv()) {
                    ma.errorRange(bitfield.getLoc(), bitfield.getRange(), "bitfield size is not a compile-time value");
                    return;
                }
            }

            member.setCheckState(DeclCheckState.Checked);
        } else if (member.getKind() == DeclKind.StructType) {   // (anonymous) sub-structs
            StructTypeDecl* sub = cast<StructTypeDecl*>(member);
            ma.analyseStructMembers(sub);
            member.setCheckState(DeclCheckState.Checked);
        }
    }

    size_analyser.TypeSize info = size_analyser.sizeOfStruct(d);
    d.setSizeAlignment(info.size, info.align);
}

func void Analyser.analyseStructNames(Analyser* ma, StructTypeDecl* d, NameVector* names, NameVector* locs) {
    // note: already checked that struct doesn't have 0 members
    u32 count = d.getNumMembers();
    Decl** members = d.getMembers();

    for (u32 i=0; i<count; i++) {
        // there can more members in anonymous sub-structs
        Decl* member = members[i];
        u32 name_idx = member.getNameIdx();
        u32 old_index;

        StructTypeDecl* sub = nil;
        if (member.getKind() == DeclKind.StructType) sub = cast<StructTypeDecl*>(member);

        if (name_idx == 0) {
            // can be anonymous sub-struct/union or anonymous bit-field
            if (member.getKind() == DeclKind.StructType) {
                ma.analyseStructNames(sub, names, locs);
            }
        } else {
            if (names.find(name_idx, &old_index)) {
                ma.error(member.getLoc(), "duplicate %s member '%s'", d.isStruct() ? "struct" : "union", member.getName());
                ma.note(locs.get(old_index), "previous declaration is here");
                return;
            }
            names.add(name_idx);
            locs.add(member.getLoc());

            if (member.getKind() == DeclKind.StructType) {
                NameVector sub_names;
                sub_names.init(sub.getNumMembers());
                NameVector sub_locs;
                sub_locs.init(sub.getNumMembers());
                ma.analyseStructNames(sub, &sub_names, &sub_locs);
                sub_names.free();
                sub_locs.free();
            }
        }
    }
}

func void Analyser.analyseFunctionType(Analyser* ma, Decl* d) {
    FunctionTypeDecl* ftd = cast<FunctionTypeDecl*>(d);
    FunctionDecl* fd = ftd.getDecl();

    ma.analyseFunction(fd);
}

// Note: only analyses prototype + args, not the function body
func void Analyser.analyseFunction(Analyser* ma, FunctionDecl* fd) {
    if (fd.isTemplate()) {
        // do analyze template name (eg X) for name clash
        ma.scope.checkGlobalSymbol(fd.getTemplateNameIdx(), fd.getTemplateLoc());
        return;  // only analyse on instantiation
    }

    // return type
    QualType qt = ma.analyseTypeRef(fd.getTypeRef());
    if (qt.isInvalid()) return;

    bool is_public = fd.asDecl().isPublic();
    if (is_public) setTypePublicUsed(qt);

    fd.setRType(qt);

    // analyse params
    u32 num_params = fd.getNumParams();
    VarDecl** params = fd.getParams();
    for (u32 i=0; i<num_params; i++) {
        VarDecl* v = params[i];
        TypeRef* ref = v.getTypeRef();

        QualType res = ma.analyseTypeRef(ref);
        if (res.isInvalid()) continue;

        if (is_public) setTypePublicUsed(res);

        Decl* d = cast<Decl*>(v);
        // Note: we dont check the arg name for clash here, but when checking body
        d.setType(res);
        d.setChecked();
    }

    if (num_params && fd.hasPrefix()) {
        // check if SF if first arg is (const) Struct*
        // Note: use TypeRef, since it's faster to check
        const Ref* prefix = fd.getPrefix();
        QualType prefixType = prefix.decl.getType();

        TypeRef* ref = params[0].getTypeRef();
        if (ref.isPointerTo(prefixType.getIndex())) {
            fd.setCallKind(CallKind.StructFunc);
        }
    }
    // Note: the function body is analysed later
}

func void Analyser.analyseAliasType(Analyser* ma, AliasTypeDecl* d) {
    TypeRef* ref = d.getTypeRef();
    QualType res = ma.analyseTypeRef(ref);
    QualType qt = d.asDecl().getType();
    // Note: type is always AliasType
    Type* at = qt.getTypeOrNil();
    QualType canon = res.getCanonicalType();
    canon.copyQuals(res);
    at.setCanonicalType(canon);
}

func void Analyser.analyseEnumType(Analyser* ma, EnumTypeDecl* d) {
    u32 num_constants = d.getNumConstants();
    EnumConstantDecl** constants = d.getConstants();
    Value value = { }

    // TODO we need a file-dependent lookup here, to see which imports we can use!
    for (u32 i=0; i<num_constants; i++) {
        EnumConstantDecl* c = constants[i];

        // check for duplicate names
        const char* name = c.asDecl().getName();
        for (u32 j=0; j<i; j++) {
            const Decl* other = constants[j].asDecl();
            if (other.getName() == name) {
                ma.error(c.asDecl().getLoc(), "duplicate enum constant name '%s'", name);
                ma.note(other.getLoc(), "previous declaration is here");
                // TODO ma.diags.info(constants[j].asDecl().getLoc(), "other declaration is here");
                return;
            }
        }

        Expr* initval = c.getInit();
        if (initval) {
            QualType res = ma.analyseExpr(c.getInit2(), true, RHS);
            if (res.isInvalid()) return;
            initval = c.getInit();  // re-read in case of ImplicitCast has been inserted
            if (!initval.isCtv()) {
                ma.errorRange(initval.getLoc(), initval.getRange(), "initializer is not a compile-time value");
                return;
            }
            Value ctv = ctv_analyser.get_value(initval);
            if (i > 0 && ctv.less_than(&value)) {
                ma.error(initval.getLoc(), "enum constants need to increase (value %u, previous %u)", ctv.uvalue, value.uvalue);
                return;
            }
            value = ctv;
        }
        // TODO check if constants fit in impl type
        //ctv_analyser.check(...);
        c.setValue(value);
        c.asDecl().setChecked();
        value.incr();
    }
}

func void Analyser.analyseStructMember(Analyser* ma, VarDecl* v, bool is_public) {
    TypeRef* ref = v.getTypeRef();

    QualType res = ma.analyseTypeRef(ref);
    if (res.isInvalid()) return;
    v.asDecl().setType(res);

    if (is_public) setTypePublicUsed(res);

    // TODO check attributes

    // TODO check initValue
}

func QualType Analyser.analyseUserTypeRef(Analyser* ma, TypeRef* ref) {
    assert(ma.mod);
    const Ref* user = ref.getUser();

    // For template types, decl is already set, no need to check/search
    if (user.decl) return user.decl.getType();

    const Ref* prefix = ref.getPrefix();
    Decl* d = nil;
    if (prefix) {
        ImportDecl* i = ma.scope.findModule(prefix.name_idx, prefix.loc);
        if (!i) {
            ma.has_error = true;
            return QualType_Invalid;
        }

        ref.setPrefix(cast<Decl*>(i));
        Module* mod = i.getDest();
        d = ma.scope.findGlobalSymbolInModule(mod, user.name_idx, user.loc);
    } else {
        d = ma.scope.findType(user.name_idx, user.loc);
    }
    if (!d) {
        ma.has_error = true;
        return QualType_Invalid;
    }

    ref.setUser(d);

    if (!d.isTypeDecl()) {
        ma.error(user.loc, "%s is not a type", ast.idx2name(user.name_idx));
        return QualType_Invalid;
    }

    bool external = (ma.mod != d.getModule());
    if (!d.isPublic() && external) {
        ma.error(user.loc, "symbol '%s' is not public", d.getFullName());
        return QualType_Invalid;
    }

    bool full = (ref.getNumPointers() == 0);
    DeclCheckState state = d.getCheckState();

    if (full && state == DeclCheckState.InProgress) {
        ma.error(user.loc, "circular declaration");
        return QualType_Invalid;
    }

    if (full && state != DeclCheckState.Checked) {
        ma.analyseGlobalDecl(d);
    }

    d.setUsed();
    if (external || ma.usedPublic) d.setUsedPublic();

    return d.getType();
}

func QualType Analyser.analyseTypeRef(Analyser* ma, TypeRef* ref) {
    QualType base = { }
    if (ref.isUser()) {
        base = ma.analyseUserTypeRef(ref);
        if (base.isInvalid()) return base;
        assert(base.hasCanonicalType());
    } else {
        BuiltinKind kind = ref.getBuiltinKind();
        base = ma.builder.actOnBuiltinType(kind);
        assert(base.isValid());
    }

    if (ref.isConst()) base.setConst();
    if (ref.isVolatile()) base.setVolatile();

    QualType resolved = base;
    u32 num_ptrs = ref.getNumPointers();
    for (u32 i=0; i<num_ptrs; i++) {
        resolved = ma.builder.actOnPointerType(resolved) ;
    }

    u32 num_arrays = ref.getNumArrays();
    // Note: iterate in reverse, since outer array comes first
    for (u32 i=num_arrays; i>0; i--) {
        Expr* sizeExpr = ref.getArray(i-1);  // note: ImplicitCast could have been inserted
        u32 size = 0;
        if (sizeExpr) {
            ma.analyseExpr(ref.getArray2(i-1), true, RHS);
            sizeExpr = ref.getArray(i-1);  // note: ImplicitCast could have been inserted

            if (!sizeExpr.isCtv()) {
                ma.errorRange(sizeExpr.getLoc(), sizeExpr.getRange(), "array size is not a compile-time value");
            } else {
                Value value = ctv_analyser.get_value(sizeExpr);
                if (value.isNegative()) {
                    ma.errorRange(sizeExpr.getLoc(), sizeExpr.getRange(), "array size has negative value %d", value.svalue);
                }
                size = cast<u32>(value.uvalue);
            }
        }
        //NOTE: we dont store ref.isIncrArray() (yet)
        resolved = ma.builder.actOnArrayType(resolved, sizeExpr != nil, size);
    }

    ref.setDest(base.getIndex());
    //resolved.dump();
    //resolved.dump_full();
    return resolved;
}

func bool Analyser.checkOpaque(Analyser* ma, const StructTypeDecl* std, SrcLoc loc) {
    if (std.isOpaque()) {
        Decl* d = cast<Decl*>(std);
        Module* other = d.getModule();
        if (other != ma.mod) {
            QualType qt = d.getType(); // lazy way to get from decl -> fullname
            ma.error(loc," cannot dereference opaque struct '%s'", qt.diagNameBare());
            return false;
        }
    }
    return true;
}

type MainMarker struct {
    u32 name_idx;
    Decl* main;
}

func void findMainFunction(void* arg, FunctionDecl* fd) {
    MainMarker* m = (arg);
    Decl* d = cast<Decl*>(fd);
    if (d.getNameIdx() == m.name_idx) {
        if (m.main) {
            //ma.error(d.getLoc(), "duplicate main function");
            // TODO prev
            return;
        }
        m.main = d;
    }
}

public func Decl* Analyser.findMain(Analyser* ma, Module* top, u32 name_idx) {
    top.setUsed();
    MainMarker marker = { name_idx, nil }
    top.visitFunctions(findMainFunction, &marker);
    if (marker.main) {
        marker.main.setUsed();
        marker.main.setUsedPublic();
        marker.main.setAttrExport();

        // TODO check return type + args
    }

    return marker.main;
}


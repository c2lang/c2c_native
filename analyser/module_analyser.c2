/* Copyright 2022 Bas van den Berg
*/

module module_analyser;

import ast_context;
import ast local;
import ctv_analyser;
import diagnostics;
import source_mgr;

import string;
import stdlib;

// TEMP here until we have templates
// TODO also use u32 indexes
type StringVector struct {
    const char** data;
    u32 count;
    u32 capacity;
}

func void StringVector.free(StringVector* v) {
    stdlib.free(cast<void*>(v.data));
    v.count = 0;
    v.capacity = 0;
    v.data = nil;
}

func void StringVector.resize(StringVector* v) {
    v.capacity = v.capacity == 0 ? 4 : v.capacity * 2;
    void* data2 = stdlib.malloc(v.capacity * sizeof(char*));
    if (v.data) {
        string.memcpy(data2, cast<void*>(v.data), v.count * sizeof(char*));
        stdlib.free(cast<void*>(v.data));
    }
    v.data = data2;
}

func u32 StringVector.add(StringVector* v, const char* str) {
    if (v.count == v.capacity) v.resize();

    u32 index = v.count;
    v.data[index] = str;
    v.count++;
    return index;
}

func bool StringVector.find(StringVector* v, const char* str, u32* index) {
    for (u32 i=0; i<v.count; i++) {
        if (v.data[i] == str) {
            *index = i;
            return true;
        }
    }
    return false;
}


type ModuleAnalyser struct {
    Module* mod;
    source_mgr.SourceMgr* sm;
    diagnostics.Diags* diags;
    ast_context.Context* context;

    // collect struct-functions
    // TODO use name idx
    const char* prefix_cache;
    u32 prefix_cache_idx;
    // Vector<const char*> prefixes;
    StringVector prefixes;
    StructDecls struct_decls;

    // Type + Vars analysis
    Decl*[8] checkStack;
    u32 checkIndex;
}

public func void check(source_mgr.SourceMgr* sm, diagnostics.Diags* diags, ast_context.Context* context, Module* mod) {
    ModuleAnalyser ma;
    string.memset(&ma, 0, sizeof(ma));
    ma.sm = sm;
    ma.diags = diags;
    ma.context = context;
    ma.mod = mod;

    //printf("analysing module %s\n", ma.mod.getName());
    // NOTE: C2C ModuleAnalyser::step1()
    // step 3a: collect incremental arrays
    // TODO use visitIncrArrays()

    // step 3b: collect struct functions
    mod.visitStructFunctions(ModuleAnalyser.handleStructFunc, &ma);

    for (u32 i=0; i<ma.struct_decls.count; i++) {
        const StructDeclInfo* info = &ma.struct_decls.data[i];
        StructTypeDecl* fd = cast<StructTypeDecl*>(info.decl);
        fd.setStructFunctions(ma.context, info.functions.getDecls(), info.functions.size());
    }

    ma.prefixes.free();  // Note: free here already (safe to be freed twice)
    ma.struct_decls.free();

    // NOTE: C2C ModuleAnalyser::step2()
    // analyse types
    // TODO need stack of Decls, use check_state
    mod.visitTypeDecls(ModuleAnalyser.handleTypeDecl, &ma);


    // analyse vars
    // analyse static asserts
    // analyse function protos

    // NOTE: C2C ModuleAnalyser::step3()
    // analyse functionbodies

    ma.free();
}

func void ModuleAnalyser.free(ModuleAnalyser* ma) {
    ma.prefixes.free();
    ma.struct_decls.free();
}

func void ModuleAnalyser.handleStructFunc(void* arg, FunctionDecl* d) {
    ModuleAnalyser* ma = arg;
    IdentifierExpr* prefix = d.getPrefix();
    const char* prefix_name = prefix.getName();
    // assert(prefix)
    //printf("  %s %s\n", prefix_name, d.asDecl().getName());

    u32 index = 0;
    if (prefix_name == ma.prefix_cache) {
        index = ma.prefix_cache_idx;
    } else {
        // search data structure
        bool found = false;
        found = ma.prefixes.find(prefix_name, &index);

        if (!found) {
            Decl* decl = ma.mod.findType(prefix_name);
            if (!decl) {
                // TODO search for all symbols for nice Diag
                ma.diags.report(prefix.asExpr().getLoc(), "unknown type '%s'", prefix_name);
                return;
            }
            if (decl.getKind() != DeclKind.StructType) {
                ma.diags.report(prefix.asExpr().getLoc(), "struct-functions type must be a structs/union");
                return;
            }
            index = ma.prefixes.add(prefix_name);
            ma.struct_decls.addDecl(decl);
        }
        ma.prefix_cache = prefix_name;
        ma.prefix_cache_idx = index;
    }

    // TODO check for duplicate names
    ma.struct_decls.addFunc(index, d);
    prefix.setDecl(ma.struct_decls.getDecl(index));
}


func void ModuleAnalyser.handleTypeDecl(void* arg, Decl* d) {
    ModuleAnalyser* ma = arg;

    if (d.isChecked()) return;
    // TODO make exception for EnumConstants. Can happen if: B = State.A + 1,

    if (!ma.pushCheck(d)) return;

    switch (d.getKind()) {
    case DeclKind.Function:
        //assert(0);
        return;
    case DeclKind.Import:
        //assert(0);
        return;
    case DeclKind.StructType:
        ma.analyseStructType(cast<StructTypeDecl*>(d));
        break;
    case DeclKind.EnumType:
        ma.analyseEnumType(cast<EnumTypeDecl*>(d));
        break;
    case DeclKind.EnumConstant:
        break;
    case DeclKind.FunctionType:
        break;
    case DeclKind.AliasType:
        break;
    case DeclKind.Var:
        break;
    case DeclKind.StaticAssert:
        //assert(0);
        return;
    }
    d.setChecked();
    ma.popCheck();
}

func bool ModuleAnalyser.pushCheck(ModuleAnalyser* ma, Decl* d) {
    for (u32 i=0; i<ma.checkIndex; i++) {
        if (ma.checkStack[i] == d) {
            for (u32 j=i; j<ma.checkIndex; j++) {
                ma.diags.report(d.getLoc(), "circular declaration dependency %s", d.getName());
            }
            // TODO use longjmp here?
            return false;
        }
    }
    ma.checkStack[ma.checkIndex] = d;
    ma.checkIndex++;
    d.setCheckState(DeclCheckState.InProgress);
    return true;
}

func void ModuleAnalyser.popCheck(ModuleAnalyser* ma) {
    ma.checkIndex--;
}

func void ModuleAnalyser.analyseStructType(ModuleAnalyser* ma, StructTypeDecl* d) {
    // TODO
}

func void ModuleAnalyser.analyseEnumType(ModuleAnalyser* ma, EnumTypeDecl* d) {
    u32 num_constants = d.getNumConstants();
    EnumConstantDecl** constants = d.getConstants();
    u32 value = 0;

    // TODO we need a file-dependent lookup here, to see which imports we can use!
    for (u32 i=0; i<num_constants; i++) {
        EnumConstantDecl* c = constants[i];

        // check for duplicate names
        const char* name = c.asDecl().getName();
        for (u32 j=0; j<i; j++) {
            if (constants[j].asDecl().getName() == name) {
                ma.diags.report(c.asDecl().getLoc(), "duplicate enum constant name '%s'", name);
                // TODO ma.diags.info(constants[j].asDecl().getLoc(), "other declaration is here");
                return;
            }
        }

        Expr* initval = c.getInit();
        if (initval) {
            ma.analyseExpr(initval);
            // TODO check if CTV
            ctv_analyser.Value ctv = ctv_analyser.get_value(initval);
            if (ctv.uvalue < value) {
                ma.diags.report(initval.getLoc(), "enum constants need to increase (value %u, previous %u)", ctv.uvalue, value-1);
                return;
            }
            value = cast<u32>(ctv.uvalue);
        }
        // TODO check if constants fit in impl type
        c.setValue(value);
        c.asDecl().setChecked();
        value++;
    }
}


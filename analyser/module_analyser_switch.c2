/* Copyright 2022-2023 Bas van den Berg
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

module module_analyser;

import ast local;
import src_loc local;
import scope;

func void Analyser.analyseSwitchStmt(Analyser* ma, Stmt* s) {
    SwitchStmt* sw = cast<SwitchStmt*>(s);

    ma.scope.enter(scope.Decl);

    bool is_sswitch = sw.isSSwitch();
    const u32 numCases = sw.getNumCases();
    Stmt** cases = sw.getCases();

    EnumTypeDecl* etd = nil;
    QualType ct = ma.analyseCondition(sw.getCond2());
    if (ct.isInvalid()) return;
    if (is_sswitch) {
        // TODO check for string argument
    } else {
        EnumType* et = ct.getEnumTypeOrNil();
        if (et) etd = et.getDecl();
    }

    //ma.scope.enter(scope.Break);

    if (numCases == 0) {
        ma.error(sw.getLoc(), "%sswitch without cases or default", is_sswitch ? "s" : "");
        return;
    }

    DefaultStmt* defaultStmt = nil;
    for (u32 i=0; i<numCases; i++) {
        Stmt* stmt = cases[i];
        if (stmt.isCase()) {
            u32 flags = scope.Decl | scope.Break;
            if (!is_sswitch) flags |= scope.Fallthrough;
            ma.scope.enter(flags);
            bool ok = ma.analyseCaseStmt(stmt, etd, i+1 == numCases, is_sswitch);
            ma.scope.exit(ma.has_error);
            if (!ok) return;
        } else { // default
            if (defaultStmt) {
                ma.error(defaultStmt.getLoc(), "multiple default labels");
                return;
            }
            defaultStmt = cast<DefaultStmt*>(stmt);
            u32 flags = scope.Decl | scope.Break;
            if (!is_sswitch) flags |= scope.Fallthrough;
            ma.scope.enter(flags);
            bool ok = ma.analyseDefaultStmt(stmt, is_sswitch);
            ma.scope.exit(ma.has_error);
            if (!ok) return;

            if (i+1 != numCases) {
                ma.error(defaultStmt.getLoc(), "default case must be last in %sswitch", is_sswitch ? "s" : "");
                return;
            }
        }
    }

    //ma.scope.exit();
    ma.scope.exit(ma.has_error);

    // TODO for enum: check for uncovered cases
    // TODO otherwise: check for dups

    // TODO Idea: use original EnumConstantDecls to reset/set-bit to check if already used?
    //  only disadvantage is that we dont know the previous location then
}

func bool Analyser.analyseCaseStmt(Analyser* ma, Stmt* s, EnumTypeDecl* etd, bool lastCase, bool is_sswitch) {
    CaseStmt* c = cast<CaseStmt*>(s);

    Expr* cond = c.getCond();
    if (etd) {
        if (!cond.isIdentifier()) {
            if (cond.isMemberExpr()) {
                ma.error(cond.getLoc(), "enum constant may not be prefixed in case statement");
            } else {
                ma.error(cond.getLoc(), "condition must be an enum constant when using enum type in switch");
            }
            return false;
        }

        IdentifierExpr* i = cast<IdentifierExpr*>(cond);
        EnumConstantDecl* ecd = etd.findConstant(i.getNameIdx());
        if (!ecd) {
            ma.error(cond.getLoc(), "enum %s has no constant %s", etd.asDecl().getName(), i.getName());
            return false;
        }
        Decl* d = cast<Decl*>(ecd);
        d.setUsed();
        QualType qt = d.getType();
        cond.setType(qt);
        cond.setCtc();
        cond.setCtv();
        cond.setRValue();
        i.setDecl(d);
        i.setKind(IdentifierKind.EnumConstant);
    } else {
        Expr* orig = c.getCond();
        QualType qt = ma.analyseExpr(c.getCond2(), true, RHS);
        if (qt.isInvalid()) return false;
        cond.setType(qt);

        if (is_sswitch) {
            if (!orig.isNilExpr() && !orig.isStringLiteral()) {
                ma.error(cond.getLoc(), "sswitch case can only have a string literal or nil as condition");
                return false;
            }
        } else {
            if (!cond.isCtv()) {
                ma.error(cond.getLoc(), "case condition is not compile-time constant");
                return false;
            }
        }
    }

    u32 count = c.getNumStmts();
    Stmt** stmts = c.getStmts();
    bool has_decls = false;
    for (u32 i=0; i<count; i++) {
        Stmt* st = stmts[i];
        ma.analyseStmt(st, false);
        if (ma.has_error) return false;

        if (st.isDecl()) has_decls = true;
        if (st.isFallthrough()) {
            FallthroughStmt* f = cast<FallthroughStmt*>(st);
            if (lastCase) {
                ma.error(f.getLoc(), "'fallthrough' statement in last case");
                return false;
            }
            if (i+1 != count) {
                ma.error(f.getLoc(), "'fallthrough' statement must be last statement in case");
                return false;
            }
        }
    }
    if (has_decls) c.setHasDecls();

    Stmt* last = nil;
    if (count) last = stmts[count-1];
    if (!is_sswitch && !ma.checkLastStmt(count, last, c.getLoc(), false, is_sswitch)) return false;

    return true;
}

func bool Analyser.analyseDefaultStmt(Analyser* ma, Stmt* s, bool is_sswitch) {
    DefaultStmt* def = cast<DefaultStmt*>(s);

    u32 count = def.getNumStmts();
    Stmt** stmts = def.getStmts();
    bool has_decls = false;
    for (u32 i=0; i<count; i++) {
        Stmt* st = stmts[i];
        ma.analyseStmt(st, false);
        if (ma.has_error) return false;

        if (st.isDecl()) has_decls = true;  // TODO FIX, can also be CompoundStmt!
            // TODO move to checkLastStmt
        if (st.isFallthrough()) {
            if (i+1 != count) {
                FallthroughStmt* f = cast<FallthroughStmt*>(st);
                ma.error(f.getLoc(), "'fallthrough' statement in last case");
                return false;
            }
        }
    }
    if (has_decls) def.setHasDecls();

    Stmt* last = nil;
    if (count) last = stmts[count-1];
    if (!is_sswitch && !ma.checkLastStmt(count, last, def.getLoc(), true, is_sswitch)) return false;

    return true;
}

func bool Analyser.checkLastStmt(Analyser* ma, u32 count, Stmt* last, SrcLoc loc, bool is_default, bool is_sswitch) {
    bool ok = false;
    if (count) {
        last = get_last_stmt(last); // unwrap CompoundStms
        ok = isTerminatingStmt(last);
        if (!ok) loc = last.getLoc();
    } // else count == 0 -> not ok

    if (!ok) {
        if (is_default) {
            ma.error(loc, "no terminating statement (break|return|continue|goto|noreturn-func) at end of default case");
        } else {
            ma.error(loc, "no terminating statement (break|fallthrough|goto|return|continue|noreturn-func) at end of case");
        }
        return false;
    }

    return true;
}

func Stmt* get_last_stmt(Stmt* s) {
    assert(s);
    while (s.getKind() == StmtKind.Compound) {
        CompoundStmt* c = cast<CompoundStmt*>(s);
        Stmt* last = c.getLastStmt();
        if (!last) break;
        s = last;
    }
    return s;
}

func bool isTerminatingStmt(const Stmt* s) {
    switch (s.getKind()) {
    case Return:
        return true;
    case Expr:
        const Expr* e = cast<Expr*>(s);
        if (!e.isCall()) break;
        // get from CallExpr -> FunctionDecl
        const CallExpr* c = cast<CallExpr*>(e);
        e = c.getFunc();
        QualType qt = e.getType();
        const FunctionType* ft = qt.getFunctionTypeOrNil();
        assert(ft);
        const FunctionDecl* fd = ft.getDecl();
        if (fd.hasAttrNoReturn()) return true;
        break;
    case Break:
        return true;
    case Continue:
        return true;
    case Fallthrough:
        return true;
    case Goto:
        return true;
    default:
        break;
    }
    return false;
}


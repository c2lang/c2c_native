/* Copyright 2022-2023 Bas van den Berg
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

module struct_func_list;

import ast;

import string;
import stdlib;

// this component is used to collect struct functions

public type Info struct {
    ast.Decl* decl;
    ast.FunctionDeclList functions;
}

public type List struct {
    Info* data;
    u32 count;
    u32 capacity;
}

public func void List.free(List* v) {
    for (u32 i=0; i<v.count; i++) {
        v.data[i].functions.free();
    }
    stdlib.free(v.data);
    v.count = 0;
    v.capacity = 0;
    v.data = nil;
}

func void List.resize(List* v) {
    v.capacity = v.capacity == 0 ? 4 : v.capacity * 2;
    Info* data2 = stdlib.malloc(v.capacity * sizeof(Info));
    if (v.data) {
        string.memcpy(data2, v.data, v.count * sizeof(Info));
        stdlib.free(v.data);
    }
    v.data = data2;
}

public func void List.addDecl(List* v, ast.Decl* decl) {
    if (v.count == v.capacity) v.resize();

    Info* info = &v.data[v.count];
    info.decl = decl;
    info.functions.init();
    v.count++;
}

public func ast.Decl* List.getDecl(List* v, u32 index) {
    return v.data[index].decl;
}

public func void List.addFunc(List* v, u32 index, ast.FunctionDecl* fn) {
    assert(index < v.count);
    Info* info = &v.data[index];
    info.functions.add(fn);
}

public func ast.FunctionDecl* List.findFunc(List* v, u32 index, u32 name_idx) {
    assert(index < v.count);
    Info* info = &v.data[index];
    return info.functions.find(name_idx);
}


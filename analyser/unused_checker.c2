/* Copyright 2022 Bas van den Berg
*/

module unused_checker;

import ast local;
import diagnostics;

import stdio;

type Checker struct {
    diagnostics.Diags* diags;
}

func const char* getFullName(const Decl* d) {
    if (d.getKind() != DeclKind.Function) return d.getName();

    const FunctionDecl* fd = cast<FunctionDecl*>(d);
    if (!fd.hasPrefix()) return d.getName();

    local char[64] tmp;
    stdio.sprintf(tmp, "%s.%s", fd.getPrefixName(), d.getName());
    return tmp;
}

func void Checker.check(void* arg, Decl* d) {
    Checker* c = arg;
    bool used = d.isUsed();
    if (!used) {
        c.diags.warnUnusedParam(d.getLoc(), "unused %s: %s", d.getKindName(), getFullName(d));
        return;
    }
    if (d.isPublic() && !d.isUsedPublic()) {
        // TEMTP
        c.diags.warnUnusedParam(d.getLoc(), "unused public %s: %s", d.getKindName(), getFullName(d));
    }

    switch (d.getKind()) {
    case Function:
        // TODO args + vars
        break;
    case Import:
        break;
    case StructType:
        // TODO fields
        break;
    case EnumType:
        if (used) c.checkEnum(cast<EnumTypeDecl*>(d));
        break;
    case EnumConstant:
        break;
    case FunctionType:
        break;
    case AliasType:
        break;
    case Var:
        break;
    case StaticAssert:
        break;
    }
}

func void Checker.checkEnum(Checker* c, EnumTypeDecl* d) {
    u32 num_consts = d.getNumConstants();
    EnumConstantDecl** constants = d.getConstants();
    for (u32 i=0; i<num_consts; i++) {
        EnumConstantDecl* ecd = constants[i];
        Decl* dd = cast<Decl*>(ecd);
        if (!dd.isUsed()) {
            c.diags.warnUnusedParam(dd.getLoc(), "unused %s: %s", dd.getKindName(), dd.getName());
        }
    }
}

public func void check(diagnostics.Diags* diags, Module* mod) {
    Checker c = {
        .diags = diags,
    }
    mod.visitDecls(Checker.check, &c);
}


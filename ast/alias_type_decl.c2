/* Copyright 2022 Bas van den Berg
*/

module ast;

import ast_context;
import string_buffer;
import utils local;

public type AliasTypeDecl struct {
    Decl parent;
    QualType refType;
} @(opaque)

public func AliasTypeDecl* AliasTypeDecl.create(ast_context.Context* c,
                                                const char* name,
                                                SrcLoc loc,
                                                bool is_public,
                                                AST* ast_,
                                                QualType refType) @(inline)
{
    AliasTypeDecl* d = c.alloc(sizeof(AliasTypeDecl));
    AliasType* at = AliasType.create(c, d);
    d.parent.init(DeclKind.AliasType, name, loc, is_public, QualType.init2(cast<Type*>(at)), ast_);
    d.refType = refType;
    return d;
}

public func Decl* AliasTypeDecl.asDecl(AliasTypeDecl* d) @(inline) {
    return &d.parent;
}

public func QualType AliasTypeDecl.getRefType(const AliasTypeDecl* d) { return d.refType; }

func void AliasTypeDecl.print(const AliasTypeDecl* d, string_buffer.Buf* out, u32 indent) {
    d.parent.printKind(out, indent, "AliasTypeDecl");
    d.parent.printBits(out);
    d.parent.printName(out);

    out.add(" ");
    d.refType.print(out);
    out.add("\n");
}


/* Copyright 2022 Bas van den Berg
*/

module ast;

import ast_context;
import string_buffer;

public type BuiltinKind enum u8 {
    Char,
    Int8,
    Int16,
    Int32,
    Int64,
    UInt8,
    UInt16,
    UInt32,
    UInt64,
    Float32,
    Float64,
    ISize,
    USize,
    Bool,
    Void,
}

const char*[] builtinType_names = {
    "char",
    "i8",
    "i16",
    "i32",
    "i64",
    "u8",
    "u16",
    "u32",
    "u64",
    "f32",
    "f64",
    "isize",
    "usize",
    "bool",
    "void",
}

static_assert(elemsof(BuiltinKind), elemsof(builtinType_names));

const bool[] BuiltinType_promotable = {
    true,
    true,
    true,
    false,
    false,
    true,
    true,
    false,
    false,
    false,
    false,
    false,
    false,
    true,
    false,
}

static_assert(elemsof(BuiltinKind), elemsof(BuiltinType_promotable));

// TODO not const because isize/usize
u32[] builtinType_sizes = {
    1,          // Char
    1, 2, 4, 8, // IntX
    1, 2, 4, 8, // UIntX
    4, 8,       // FloatX
    8, 8,       // Isize/Usize <- need to change for ARCH
    1,          // Bool
    0,          // Void
}

// TODO not const because isize/usize, set those from ast.init()
u32[] builtinType_width = {
    7,             // Char
    7, 15, 31, 63, // IntX
    8, 16, 32, 64, // UIntX
    0, 0,          // FloatX
    63, 64,        // Isize/Usize <- need to change for ARCH
    1,             // Bool
    0,             // Void
}

static_assert(elemsof(BuiltinKind), elemsof(builtinType_sizes));

public type BuiltinTypeBits struct {
    u32 : NumTypeBits;
    u32 kind : 4;
}

public type BuiltinType struct {
    Type parent;
} @(opaque)

public func BuiltinType* BuiltinType.create(ast_context.Context* c, BuiltinKind kind) @(inline) {
    BuiltinType* b = c.alloc(sizeof(BuiltinType));
    b.parent.init(TypeKind.Builtin);
    b.parent.builtinTypeBits.kind = kind;
    b.parent.setCanonicalType(QualType.init(&b.parent));
#if AstStatistics
    Stats.addType(TypeKind.Builtin, sizeof(BuiltinType));
#endif
    return b;
}

// TEMP cast needed until Analyser fixed
public func BuiltinKind BuiltinType.getKind(const BuiltinType* b) @(inline) {
    return cast<BuiltinKind>(b.parent.builtinTypeBits.kind);
}

// TODO convert to enum function
func const char* BuiltinType.kind2str(const BuiltinType* b) {
    return builtinType_names[b.getKind()];
}

public func bool BuiltinType.isPromotableIntegerType(const BuiltinType* b) {
    return BuiltinType_promotable[b.getKind()];
}

public func u32 BuiltinType.getAlignment(const BuiltinType* b) {
    return builtinType_sizes[b.getKind()];
}

public func u32 BuiltinType.getWidth(const BuiltinType* b) {
    return builtinType_width[b.getKind()];
}

func void BuiltinType.print(const BuiltinType* b, string_buffer.Buf* out) {
    out.add(builtinType_names[b.getKind()]);
}

public func void BuiltinType.fullPrint(const BuiltinType* t, string_buffer.Buf* out, u32 indent) {
    out.indent(indent);
    out.print("BuiltinType [%p] %s\n", t, t.kind2str());
}

/* Copyright 2022-2023 Bas van den Berg
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

module ast;

import ast_context;
import string_buffer;
import src_loc local;

public type CharLiteralBits struct {
    u32 : NumExprBits;
    u32 value : 8;
}

public type CharLiteral struct {
    Expr parent;
} @(opaque)

public func CharLiteral* CharLiteral.create(ast_context.Context* c, SrcLoc loc, u8 val) @(inline) {
    CharLiteral* e = c.alloc(sizeof(CharLiteral));
    e.parent.init(ExprKind.CharLiteral, loc, 1, 1, 0, ValType.RValue);
    e.parent.parent.charLiteralBits.value = val;
#if AstStatistics
    Stats.addExpr(ExprKind.CharLiteral, sizeof(CharLiteral));
#endif
    e.parent.setType(g_char);
    return e;
}

public func u8 CharLiteral.getValue(const CharLiteral* e) @(inline) {
    return cast<u8>(e.parent.parent.charLiteralBits.value);
}

func void CharLiteral.print(const CharLiteral* e, string_buffer.Buf* out, u32 indent) {
    e.parent.printKind(out, indent);
    e.parent.printTypeBits(out);
    out.add1(' ');
    out.color(col_Value);
    e.printLiteral(out);
    out.add1('\n');
}

public func void CharLiteral.printLiteral(const CharLiteral* e, string_buffer.Buf* out) {
    char c = cast<char>(e.parent.parent.charLiteralBits.value);

    switch (c) {
    case '\a':
        out.add("'\\a'");
        break;
    case '\b':
        out.add("'\\b'");
        break;
    case '\f':
        out.add("'\\f'");
        break;
    case '\n':
        out.add("'\\n'");
        break;
    case '\r':
        out.add("'\\r'");
        break;
    case '\t':
        out.add("'\\t'");
        break;
    case '\v':
        out.add("'\\v'");
        break;
    case '\'':
        out.add("'\\''");
        break;
    case '\\':
        out.add("'\\\\'");
        break;
    default:
        out.print("'%c'", c);
        break;
    }
}


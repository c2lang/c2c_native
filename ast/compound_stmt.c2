/* Copyright 2022 Bas van den Berg
*/

module ast;

import ast_context local;
import utils local;

import string local;
// TEMP
import stdio local;

type CompoundStmtBits struct {
    u32 : NumStmtBits;
    u32 count : 32 - NumStmtBits;
}

public type CompoundStmt struct {
    Stmt stmt;
    Stmt*[0] stmts; // tail-allocated
} @(opaque)

public func CompoundStmt* CompoundStmt.create(Context* c, Stmt** stmts, u32 count) @(inline) {
    // assert(count < 65556);
    u32 size = sizeof(CompoundStmt) + count * sizeof(Stmt**);

    CompoundStmt* s = c.alloc(size);
    s.stmt.init(StmtKind.Compound);
    s.stmt.compoundStmtBits.count = count;

    if (count) {
        memcpy(cast<void*>(s.stmts), cast<void*>(stmts), count * sizeof(Stmt*));
    }
    return s;
}

public func void CompoundStmt.print(const CompoundStmt* s, StringBuffer* out, u32 indent) {
    out.indent(indent);
    out.print("CompoundStmt\n");
    const u32 count = s.stmt.compoundStmtBits.count;
    for (u32 i=0; i<count; i++) {
        s.stmts[i].print(out, indent + 2);
    }
}



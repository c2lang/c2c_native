/* Copyright 2022 Bas van den Berg
*/

module ast;

import utils local;
import string_buffer;
import stdio;

// TODO better order
public type DeclKind enum u8 {
    Function,
    Import,
    StructType,
    EnumType,
    EnumConstant,
    FunctionType,
    AliasType,
    Var,
    // Label
    // ArrayValue
    StaticAssert,
}

public type DeclCheckState enum u8 {
    Unchecked,
    Checking,
    Checked,
}

const char*[] declCheckState_names = {
    "unchecked",
    "checking",
    "checked",
}

static_assert(elemsof(DeclCheckState), elemsof(declCheckState_names));

public type DeclBits struct {
    u32 kind : 8;
    u32 is_public : 1;
    u32 is_used : 1;
    u32 is_used_public : 1;
    u32 check_state : 2;
}
public const u32 NumDeclBits = 13;

public type Decl struct {
    union {
        DeclBits declBits;
        ImportDeclBits importDeclBits;
        FunctionDeclBits functionDeclBits;
        StructTypeDeclBits structTypeDeclBits;
        EnumTypeDeclBits enumTypeDeclBits;
        EnumConstantDeclBits enumConstantDeclBits;
        VarDeclBits varDeclBits;
        u32 bits;
    }
    SrcLoc loc;
#if DeclNamePtr
    const char* name; // Note: may be nil
#else
    u32 name_idx;   // into g_names_start, 0 means nil
#endif

    QualType qt;
    Module* mod;
} @(opaque)

func void Decl.init(Decl* d, DeclKind k, const char* name, SrcLoc loc, bool is_public, QualType qt, Module* mod) @(inline) {
    d.bits = 0;
    d.declBits.kind = k;
    d.declBits.is_public = is_public;
#if DeclNamePtr
    d.name = name;
#else
    if (name) {
//        if (name[0] == 0) {
//            stdio.printf("EMPTY STRING! (kind %u, public %d, loc %u)\n", k, is_public, loc);
//        }
        d.name_idx = cast<u32>(name - g_names_start);
    } else {
        d.name_idx = 0;
    }
#endif
    d.loc = loc;
    d.qt = qt;
    d.mod = mod;
}

// TEMP cast needed until Analyser fixed
public func DeclKind Decl.getKind(const Decl* d) @(inline) { return cast<DeclKind>(d.declBits.kind); }

public func DeclCheckState Decl.getCheckState(const Decl* d) @(inline) { return cast<DeclCheckState>(d.declBits.check_state); }

public func const char* Decl.getName(const Decl* d) @(inline) {
#if DeclNamePtr
    return d.name;
#else
    if (d.name_idx) return &g_names_start[d.name_idx];
    return nil;
#endif
}

public func SrcLoc Decl.getLoc(const Decl* d) @(inline) { return d.loc; }

public func QualType Decl.getType(const Decl* d) @(inline) { return d.qt; }

public func Module* Decl.getModule(const Decl* d) @(inline) { return d.mod; }

public func bool Decl.isPublic(const Decl* d) @(inline) { return d.declBits.is_public; }

public func bool Decl.isUsed(const Decl* d) @(inline) { return d.declBits.is_used; }

public func bool Decl.isUsedPublic(const Decl* d) @(inline) { return d.declBits.is_used_public; }

public func void Decl.dump(const Decl* d) {
    string_buffer.Buf* out = string_buffer.create(10*4096);
    d.print(out, 0);
    stdio.puts(out.data());
    out.free();
}

func void Decl.print(const Decl* d, string_buffer.Buf* out, u32 indent) {
    switch (d.getKind()) {
    case DeclKind.Function:
        FunctionDecl.print(cast<FunctionDecl*>(d), out, indent);
        break;
    case DeclKind.Import:
        ImportDecl.print(cast<ImportDecl*>(d), out, indent);
        break;
    case DeclKind.StructType:
        StructTypeDecl.print(cast<StructTypeDecl*>(d), out, indent);
        break;
    case DeclKind.EnumType:
        EnumTypeDecl.print(cast<EnumTypeDecl*>(d), out, indent);
        break;
    case DeclKind.EnumConstant:
        EnumConstantDecl.print(cast<EnumConstantDecl*>(d), out, indent);
        break;
    case DeclKind.FunctionType:
        FunctionTypeDecl.print(cast<FunctionTypeDecl*>(d), out, indent);
        break;
    case DeclKind.AliasType:
        AliasTypeDecl.print(cast<AliasTypeDecl*>(d), out, indent);
        break;
    case DeclKind.Var:
        VarDecl.print(cast<VarDecl*>(d), out, indent);
        break;
    case DeclKind.StaticAssert:
        StaticAssertDecl.print(cast<StaticAssertDecl*>(d), out, indent);
        break;
    }
}

func void Decl.printKind(const Decl* d, string_buffer.Buf* out, u32 indent, const char* kind_) {
    out.indent(indent);
    out.color(col_Decl);
    out.add(kind_);
    out.add(" ");
    d.qt.printQuoted(out);
}

func void Decl.printName(const Decl* d, string_buffer.Buf* out) {
    out.add(" ");
    out.color(col_Value);
#if DeclNamePtr
    if (d.name) out.add(d.getName());
#else
    if (d.name_idx) {
        stdio.printf("PRINT %5u -> %p\n", d.name_idx, d.getName());
        out.add(d.getName());
    }
#endif
    else out.add("(nil)");
}

func void Decl.printBits(const Decl* d, string_buffer.Buf* out) {
    out.color(col_Attr);
    if (d.isPublic()) out.add(" public");
    out.add(" ");
    out.add(declCheckState_names[d.getCheckState()]);
    // TODO is_external
}

func void Decl.printUsed(const Decl* d, string_buffer.Buf* out) {
    out.color(col_Attributes);
    out.print(" used=%d/%d", d.isUsed(), d.isUsedPublic());
}


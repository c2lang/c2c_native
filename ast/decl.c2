/* Copyright 2022 Bas van den Berg
*/

module ast;

import utils local;
import string_buffer;
import stdio;

public type DeclKind enum u8 {
    Function,
    Import,
    StructType,
    EnumType,
    EnumConstant,
    FunctionType,
    AliasType,
    Var,
    StaticAssert,
}

public type DeclCheckState enum u8 {
    Unchecked,
    Checking,
    Checked,
}

const char*[] declCheckState_names = {
    "unchecked",
    "checking",
    "checked",
}

static_assert(elemsof(DeclCheckState), elemsof(declCheckState_names));

public type DeclBits struct {
    u32 kind : 8;
    u32 is_public : 1;
    u32 is_used : 1;
    u32 is_used_public : 1;
    u32 check_state : 2;
}
public const u32 NumDeclBits = 13;

public type Decl struct {
    union {
        DeclBits declBits;
        ImportDeclBits importDeclBits;
        FunctionDeclBits functionDeclBits;
        StructTypeDeclBits structTypeDeclBits;
        EnumTypeDeclBits enumTypeDeclBits;
        EnumConstantDeclBits enumConstantDeclBits;
        VarDeclBits varDeclBits;
        u32 bits;
    }
    SrcLoc loc;
    const char* name;
    // QualType q;
    // Module* mod;
} @(opaque)

func void Decl.init(Decl* d, DeclKind k, const char* name, SrcLoc loc, bool is_public) @(inline) {
    d.bits = 0;
    d.declBits.kind = k;
    d.declBits.is_public = is_public;
    d.name = name;
    d.loc = loc;
}

// TEMP cast needed until Analyser fixed
public func DeclKind Decl.getKind(const Decl* d) @(inline) { return cast<DeclKind>(d.declBits.kind); }

public func DeclCheckState Decl.getCheckState(const Decl* d) @(inline) { return cast<DeclCheckState>(d.declBits.check_state); }

public func const char* Decl.getName(const Decl* d) @(inline) { return d.name; }

public func SrcLoc Decl.getLoc(const Decl* d) @(inline) { return d.loc; }

public func bool Decl.isPublic(const Decl* d) @(inline) { return d.declBits.is_public; }

public func bool Decl.isUsed(const Decl* d) @(inline) { return d.declBits.is_used; }

public func bool Decl.isUsedPublic(const Decl* d) @(inline) { return d.declBits.is_used_public; }

public func void Decl.dump(const Decl* d) {
    string_buffer.Buf* out = string_buffer.create(10*4096);
    d.print(out, 0);
    stdio.printf("%s\n", out.data());
    out.free();
}

public func void Decl.print(const Decl* d, string_buffer.Buf* out, u32 indent) {
    switch (d.getKind()) {
    case DeclKind.Function:
        FunctionDecl.print(cast<FunctionDecl*>(d), out, indent);
        break;
    case DeclKind.Import:
        ImportDecl.print(cast<ImportDecl*>(d), out, indent);
        break;
    case DeclKind.StructType:
        StructTypeDecl.print(cast<StructTypeDecl*>(d), out, indent);
        break;
    case DeclKind.EnumType:
        EnumTypeDecl.print(cast<EnumTypeDecl*>(d), out, indent);
        break;
    case DeclKind.EnumConstant:
        EnumConstantDecl.print(cast<EnumConstantDecl*>(d), out, indent);
        break;
    case DeclKind.FunctionType:
        FunctionTypeDecl.print(cast<FunctionTypeDecl*>(d), out, indent);
        break;
    case DeclKind.AliasType:
        AliasTypeDecl.print(cast<AliasTypeDecl*>(d), out, indent);
        break;
    case DeclKind.Var:
        VarDecl.print(cast<VarDecl*>(d), out, indent);
        break;
    case DeclKind.StaticAssert:
        StaticAssertDecl.print(cast<StaticAssertDecl*>(d), out, indent);
        break;
    }
}

func void Decl.printKind(const Decl* d, string_buffer.Buf* out, u32 indent, const char* kind_) {
    out.indent(indent);
    out.color(col_Decl);
    out.add(kind_);
}

func void Decl.printName(const Decl* d, string_buffer.Buf* out) {
    out.color(col_Value);
    out.add(" ");
    out.add(d.name);
}

func void Decl.printBits(const Decl* d, string_buffer.Buf* out) {
    out.color(col_Attr);
    if (d.isPublic()) out.add(" public");
    out.add(" ");
    out.add(declCheckState_names[d.getCheckState()]);
    // TODO is_external
}

func void Decl.printUsed(const Decl* d, string_buffer.Buf* out) {
    out.color(col_Attributes);
    out.print(" used=%d/%d", d.isUsed(), d.isUsedPublic());
}


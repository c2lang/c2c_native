/* Copyright 2022 Bas van den Berg
*/

module ast;

import ast_context;
import string_buffer;
import src_loc local;

import string;

type DefaultStmtBits struct {
    u32 : NumStmtBits;
    u32 num_stmts : 15;
    u32 has_decls : 1;
}

public type DefaultStmt struct {
    Stmt parent;
    SrcLoc loc;
    Stmt*[0] stmts; // tail-allocated
} @(opaque)

public func DefaultStmt* DefaultStmt.create(ast_context.Context* c,
                                            SrcLoc loc,
                                            Stmt** stmts,
                                            u32 numStmts) @(inline)
{
    u32 size = sizeof(DefaultStmt) + numStmts * sizeof(Stmt*);
    DefaultStmt* s = c.alloc(size);
    s.parent.init(StmtKind.Default);
    s.parent.defaultStmtBits.num_stmts = numStmts;
    s.loc = loc;
    string.memcpy(cast<void*>(s.stmts), cast<void*>(stmts), numStmts * sizeof(Stmt*));
#if AstStatistics
    Stats.addStmt(StmtKind.Default, size);
#endif
    return s;
}

func Stmt* DefaultStmt.instantiate(DefaultStmt* s, Instantiator* inst) {
    u32 numStmts = s.getNumStmts();
    u32 size = sizeof(DefaultStmt) + numStmts * sizeof(Stmt*);
    DefaultStmt* s2 = inst.c.alloc(size);
    s2.parent = s.parent;
    s2.loc = s.loc;
    for (u32 i=0; i<numStmts; i++) {
    }
#if AstStatistics
    Stats.addStmt(StmtKind.Default, size);
#endif
    return cast<Stmt*>(s2);
}

public func SrcLoc DefaultStmt.getLoc(const DefaultStmt* s) { return s.loc; }

public func u32 DefaultStmt.getNumStmts(const DefaultStmt* s) {
    return s.parent.defaultStmtBits.num_stmts;
}

public func Stmt** DefaultStmt.getStmts(DefaultStmt* s) {
    return s.stmts;
}

public func void DefaultStmt.setHasDecls(DefaultStmt* s) {
    s.parent.defaultStmtBits.has_decls = 1;
}

public func bool DefaultStmt.hasDecls(const DefaultStmt* s) {
    return s.parent.defaultStmtBits.has_decls;
}

func void DefaultStmt.print(const DefaultStmt* s, string_buffer.Buf* out, u32 indent) {
    s.parent.printKind(out, indent);
    if (s.parent.defaultStmtBits.has_decls) {
        out.color(col_Attr);
        out.add(" decls");
    }
    out.add1('\n');

    for (u32 i=0; i<s.parent.defaultStmtBits.num_stmts; i++) {
        s.stmts[i].print(out, indent + 1);
    }
}


/* Copyright 2022-2024 Bas van den Berg
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

module ast;

import ast_context;
import string_buffer;

public type DoStmt struct @(opaque) {
    Stmt parent;
    Stmt* cond;
    Stmt* body;
}

// NOTE: merge with while? (only need single bit to differentiate)
public fn DoStmt* DoStmt.create(ast_context.Context* c,
                                  Stmt* cond,
                                  Stmt* body)
{
    DoStmt* s = c.alloc(sizeof(DoStmt));
    s.parent.init(StmtKind.Do);
    s.cond = cond;
    s.body = body;
#if AstStatistics
    Stats.addStmt(StmtKind.Do, sizeof(DoStmt));
#endif
    return s;
}

fn Stmt* DoStmt.instantiate(DoStmt* s, Instantiator* inst) {
    Stmt* cond2 = s.cond.instantiate(inst);
    Stmt* body2 = s.body.instantiate(inst);
    return cast<Stmt*>(DoStmt.create(inst.c, cond2, body2));
}

fn void DoStmt.print(const DoStmt* s, string_buffer.Buf* out, u32 indent) {
    s.parent.printKind(out, indent);
    out.newline();

    s.cond.print(out, indent + 1);
    s.body.print(out, indent + 1);
}

public fn Stmt* DoStmt.getCond(const DoStmt* s) { return s.cond; }

public fn Stmt* DoStmt.getBody(const DoStmt* s) { return s.body; }


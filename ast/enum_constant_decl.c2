/* Copyright 2022 Bas van den Berg
*/

module ast;

import ast_context;
import string_buffer;
import utils local;

type EnumConstantDeclBits struct {
    u32 : NumDeclBits;
    u32 has_init : 1;
}

public type EnumConstantDecl struct {
    Decl parent;
    u32 value;
    QualType qt;
    Expr*[0] init;  // tail-allocated
} @(opaque)

public func EnumConstantDecl* EnumConstantDecl.create(ast_context.Context* c, const char* name, SrcLoc loc, bool is_public, QualType qt, Expr* initValue) @(inline) {
    u32 size = sizeof(EnumConstantDecl);
    if (initValue) size += sizeof(Expr*);
    EnumConstantDecl* d = c.alloc(size);
    d.parent.init(DeclKind.EnumConstant, name, loc, is_public);
    d.value = 0;
    d.qt = qt;
    if (initValue) {
        d.parent.enumConstantDeclBits.has_init = 1;
        d.init[0] = initValue;
    }
    return d;
}

public func Decl* EnumConstantDecl.asDecl(EnumConstantDecl* d) @(inline) { return &d.parent; }

public func void EnumConstantDecl.print(const EnumConstantDecl* d, string_buffer.Buf* out, u32 indent) {
    d.parent.printKind(out, indent, "EnumConstantDecl");
    out.add(" ");
    d.qt.print(out);
    d.parent.printBits(out);
    d.parent.printName(out);

    out.color(col_Value);
    out.print(" %u", d.value);
    out.print("\n");

    if (d.parent.enumConstantDeclBits.has_init) d.init[0].print(out, indent + 2);
}


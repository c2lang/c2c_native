/* Copyright 2022-2023 Bas van den Berg
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

module ast;

import ast_context;
import string_buffer;
import src_loc local;

public type FieldDesignatedInitExpr struct {
    Expr parent;
    u32 field;
    Expr* initValue;
} @(opaque)

public func FieldDesignatedInitExpr* FieldDesignatedInitExpr.create(ast_context.Context* c,
                                                                    u32 field,
                                                                    SrcLoc loc,
                                                                    Expr* initValue) @(inline)
{
    FieldDesignatedInitExpr* e = c.alloc(sizeof(FieldDesignatedInitExpr));
    e.parent.init(ExprKind.FieldDesignatedInit, loc, 0, 0, 0, ValType.RValue);
    e.field = field;
    e.initValue = initValue;
#if AstStatistics
    Stats.addExpr(ExprKind.FieldDesignatedInit, sizeof(FieldDesignatedInitExpr));
#endif
    return e;
}

func Expr* FieldDesignatedInitExpr.instantiate(FieldDesignatedInitExpr* e, Instantiator* inst) {
    return cast<Expr*>(FieldDesignatedInitExpr.create(inst.c, e.field, e.parent.loc, e.initValue.instantiate(inst)));
}

public func u32 FieldDesignatedInitExpr.getField(const FieldDesignatedInitExpr* e) {
    return e.field;
}

public func const char* FieldDesignatedInitExpr.getFieldName(const FieldDesignatedInitExpr* e) {
    return idx2name(e.field);
}

public func Expr* FieldDesignatedInitExpr.getInit(FieldDesignatedInitExpr* e) {
    return e.initValue;
}

public func Expr** FieldDesignatedInitExpr.getInit2(FieldDesignatedInitExpr* e) {
    return &e.initValue;
}

func void FieldDesignatedInitExpr.print(const FieldDesignatedInitExpr* e, string_buffer.Buf* out, u32 indent) {
    e.parent.printKind(out, indent);
    e.parent.printTypeBits(out);
    out.color(col_Value);
    out.print(" %s\n", idx2name(e.field));
    e.initValue.print(out, indent + 1);
}


/* Copyright 2022 Bas van den Berg
*/

module ast;

import ast_context;
import string_buffer;
import src_loc local;

public type FieldDesignatedInitExpr struct {
    Expr parent;
    u32 field;
    Expr* initValue;
} @(opaque)

public func FieldDesignatedInitExpr* FieldDesignatedInitExpr.create(ast_context.Context* c,
                                                                    u32 field,
                                                                    SrcLoc loc,
                                                                    Expr* initValue) @(inline)
{
    FieldDesignatedInitExpr* e = c.alloc(sizeof(FieldDesignatedInitExpr));
    e.parent.init(ExprKind.FieldDesignatedInit, loc, 0, 0, 0);
    e.field = field;
    e.initValue = initValue;
#if AstStatistics
    Stats.addExpr(ExprKind.FieldDesignatedInit, sizeof(FieldDesignatedInitExpr));
#endif
    return e;
}

func void FieldDesignatedInitExpr.print(const FieldDesignatedInitExpr* e, string_buffer.Buf* out, u32 indent) {
    e.parent.printKind(out, indent);
    e.parent.printTypeBits(out);
    out.color(col_Value);
    out.print(" %s\n", idx2name(e.field));
    e.initValue.print(out, indent + 2);
}


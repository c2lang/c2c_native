/* Copyright 2022 Bas van den Berg
*/

module ast;

import ast_context;
import utils local;
import string_buffer;
import string;

type FunctionDeclBits struct {
    u32 : NumDeclBits;
    u32 is_variadic : 1;
    u32 has_prefix : 1;     // is a (static) struct-function
    u32 num_params : 5;
}

public type FunctionDecl struct {
    Decl parent;
    CompoundStmt* body;
    QualType rt;    // return type after analysis
    TypeRef rtype;  // note: variable size!
    //IdentifierExpr*[0] prefix; // tail-allocated
    //VarDecl*[0] params; // tail-allocated;
} @(opaque)

public func FunctionDecl* FunctionDecl.create(ast_context.Context* c,
                                              u32 name,
                                              SrcLoc loc,
                                              bool is_public,
                                              u32 ast_idx,
                                              const TypeRefHolder* rtype,
                                              IdentifierExpr* prefix,
                                              VarDecl** params,
                                              u32 num_params,
                                              bool is_variadic) @(inline)
{
    u32 size = sizeof(FunctionDecl) + num_params * sizeof(VarDecl*) + rtype.getExtraSize();
    if (prefix) size += sizeof(IdentifierExpr*);
    FunctionDecl* d = c.alloc(size);
    FunctionType* ftype = FunctionType.create(c, d);
    QualType qt = QualType.init(ftype.asType());
    d.parent.init(DeclKind.Function, name, loc, is_public, qt, ast_idx);
    d.parent.functionDeclBits.is_variadic = is_variadic;
    d.parent.functionDeclBits.num_params = num_params;
    d.rt = QualType_Invalid;
    rtype.fill(&d.rtype);
    d.body = nil;
    u32 params_offset = 0;
    Expr** tail = d.rtype.getPointerAfter();
    if (prefix) {
        d.parent.functionDeclBits.has_prefix = 1;
        tail[0] = cast<Expr*>(prefix);
        params_offset = 1;
    }
    if (num_params) {
        string.memcpy(cast<void*>(&tail[params_offset]), cast<void*>(params), num_params * sizeof(VarDecl*));
    }

#if AstStatistics
    Stats.addDecl(DeclKind.Function, size);
#endif
    return d;
}

public func void FunctionDecl.setBody(FunctionDecl* d, CompoundStmt* body) {
    d.body = body;
}

public func CompoundStmt* FunctionDecl.getBody(const FunctionDecl* d) {
    return d.body;
}

public func void FunctionDecl.setRType(FunctionDecl* d, QualType rt) {
    d.rt = rt;
}

public func Decl* FunctionDecl.asDecl(FunctionDecl* d) {
    return &d.parent;
}

public func TypeRef* FunctionDecl.getTypeRef(FunctionDecl* d) {
    return &d.rtype;
}

public func bool FunctionDecl.hasPrefix(const FunctionDecl* d) {
    return d.parent.functionDeclBits.has_prefix;
}

public func IdentifierExpr* FunctionDecl.getPrefix(const FunctionDecl* d) {
    if (d.hasPrefix()) {
        Expr** tail = d.rtype.getPointerAfter();
        return cast<IdentifierExpr*>(tail[0]);
    }
    return  nil;
}

public func bool FunctionDecl.isVariadic(const FunctionDecl* d) {
    return d.parent.functionDeclBits.is_variadic;
}

public func u32 FunctionDecl.getNumParams(const FunctionDecl* d) {
    return d.parent.functionDeclBits.num_params;
}

public func VarDecl* FunctionDecl.getParam(const FunctionDecl* d, u32 idx) {
    if (d.hasPrefix()) idx++;
    VarDecl** tail = d.rtype.getPointerAfter();
    return tail[idx];
}

func void FunctionDecl.print(const FunctionDecl* d, string_buffer.Buf* out, u32 indent) {
    bool valid_type = d.parent.qt.isValid();
    d.parent.printKind(out, indent, valid_type);
    if (!valid_type) {
        out.add(" ");
        d.rtype.print(out, true);
    }

    d.parent.printBits(out);

    if (d.parent.functionDeclBits.has_prefix) {
        out.color(col_Attr);
        // TODO static struct function
        out.add(" SF");
    }
    out.add(" ");
    out.color(col_Value);

    u32 params_offset = 0;
    if (d.parent.functionDeclBits.has_prefix) {
        const IdentifierExpr** tail = d.rtype.getPointerAfter();
        out.add(tail[0].getName());
        out.add(".");
        params_offset = 1;
    }
    out.add(d.parent.getName());
    out.add("\n");

    //d.rtype.print(out, true,);

    const VarDecl** tail = d.rtype.getPointerAfter();
    for (u32 i=0; i<d.parent.functionDeclBits.num_params; i++) {
        tail[params_offset + i].print(out, indent + 2);
    }
    if (d.body) {
        d.body.print(out, indent + 2);
    }
}

func void FunctionDecl.printType(const FunctionDecl* d, string_buffer.Buf* out) {
    if (d.rt.isValid()) {
        d.rt.print(out);
    } else {
        d.rtype.print(out, true);
    }
    out.add(" ");
    out.add("(");
    u32 num_params = d.parent.functionDeclBits.num_params;
    const VarDecl** tail = d.rtype.getPointerAfter();
    i32 params_offset = d.parent.functionDeclBits.has_prefix ? 1 : 0;
    for (u32 i=0; i<num_params; i++) {
        if (i != 0) out.add(", ");
        tail[params_offset + i].printType(out);
    }

    if (d.parent.functionDeclBits.is_variadic) out.add(", ...");

    out.add(")");
}


/* Copyright 2022 Bas van den Berg
*/

module ast;

import ast_context;
import string_buffer local;
import utils local;

public type IdentifierExprBits struct {
    u32 : NumExprBits;
    u32 ref_type : 1;
    // TODO add kind: Type, Var, Module, etc
}

public type IdentifierExpr struct {
    Expr expr;
    union {
        const char* name;
        Decl* decl;     // set during analysis
    }
} @(opaque)

public func IdentifierExpr* IdentifierExpr.create(ast_context.Context* c, SrcLoc loc, const char* name) @(inline) {
    IdentifierExpr* e = c.alloc(sizeof(IdentifierExpr));
    e.expr.init(ExprKind.Identifier, loc, 0, 0, 0);
    e.name = name;
    return e;
}

public func Expr* IdentifierExpr.asExpr(IdentifierExpr* e) @(inline) { return &e.expr; }

public func void IdentifierExpr.print(const IdentifierExpr* e, StringBuffer* out, u32 indent) {
    out.indent(indent);
    out.add("IdentifierExpr");
    if (e.expr.stmt.identifierExprBits.ref_type) {
        out.print(" ref: %s\n", e.decl.getName());
    } else {
        out.print(" name: %s\n", e.name);
    }
}


/* Copyright 2022 Bas van den Berg
*/

module ast;

import ast_context;
import string_buffer;
import src_loc local;

public type IdentifierKind enum u8 {
    Unresolved,
    Module,
    Function,
    Type,
    Var,
    EnumConstant,
    StructMember,
    Label,
}

const char*[] identifierKind_names = {
    "Unresolved",
    "Module",
    "Function",
    "Type",
    "Var",
    "EnumConstant",
    "StructMember",
    "Label",
}

static_assert(elemsof(IdentifierKind), elemsof(identifierKind_names));

public type IdentifierExprBits struct {
    u32 : NumExprBits;
    u32 has_decl : 1;
    u32 kind : 4;   // IdentifierKind
}

public type IdentifierExpr struct {
    Expr parent;
    union {
        u32 name_idx;
        Decl* decl;     // set during analysis
    }
} @(opaque)

public func IdentifierExpr* IdentifierExpr.create(ast_context.Context* c, SrcLoc loc, u32 name) @(inline) {
    IdentifierExpr* e = c.alloc(sizeof(IdentifierExpr));
    e.parent.init(ExprKind.Identifier, loc, 0, 0, 0, ValType.NValue);
    e.name_idx = name;
#if AstStatistics
    Stats.addExpr(ExprKind.Identifier, sizeof(IdentifierExpr));
#endif
    return e;
}

func Expr* IdentifierExpr.instantiate(IdentifierExpr* e, Instantiator* inst) {
    return IdentifierExpr.create(inst.c, e.parent.loc, e.name_idx);
}

public func Expr* IdentifierExpr.asExpr(IdentifierExpr* e) @(inline) { return &e.parent; }

public func Decl* IdentifierExpr.getDecl(const IdentifierExpr* e) @(inline) {
    if (!e.parent.parent.identifierExprBits.has_decl) return nil;
    return e.decl;
}

// for (ref-)generator, convenience
public func Ref IdentifierExpr.getRef(const IdentifierExpr* e) {
    Ref ref = { e.parent.loc, e.getNameIdx(), e.getDecl() }
    return ref;
}

public func void IdentifierExpr.setKind(IdentifierExpr* e, IdentifierKind kind) @(inline) {
    e.parent.parent.identifierExprBits.kind = kind;
}

public func IdentifierKind IdentifierExpr.getKind(const IdentifierExpr* e) @(inline) {
    return cast<IdentifierKind>(e.parent.parent.identifierExprBits.kind);
}

public func void IdentifierExpr.setDecl(IdentifierExpr* e, Decl* decl) @(inline) {
    e.decl = decl;
    e.parent.parent.identifierExprBits.has_decl = true;
}

public func const char* IdentifierExpr.getName(const IdentifierExpr* e) {
    if (e.parent.parent.identifierExprBits.has_decl) return e.decl.getName();
    return idx2name(e.name_idx);
}

public func u32 IdentifierExpr.getNameIdx(const IdentifierExpr* e) @(inline) {
    if (e.parent.parent.identifierExprBits.has_decl) return e.decl.getNameIdx();
    return e.name_idx;
}

func void IdentifierExpr.print(const IdentifierExpr* e, string_buffer.Buf* out, u32 indent) {
    e.parent.printKind(out, indent);
    e.parent.printTypeBits(out);

    out.add1(' ');
    IdentifierKind kind = e.getKind();
    if (kind == IdentifierKind.Unresolved) out.color(col_Error);
    else out.color(col_Attr);
    out.add(identifierKind_names[kind]);
    out.add1(' ');
    if (e.parent.parent.identifierExprBits.has_decl) {
        out.color(col_Value);
        out.add(e.decl.getName());
    } else {
        out.color(col_Value);
        out.add(idx2name(e.name_idx));
    }
    out.add1('\n');
}

func void IdentifierExpr.printLiteral(const IdentifierExpr* e, string_buffer.Buf* out) {
    out.add(e.getName());
}


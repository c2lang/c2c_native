/* Copyright 2022 Bas van den Berg
*/

module ast;

import ast_context;
import string_buffer;
import utils local;

public type IdentifierKind enum u8 {
    Unresolved,
    Module,
    Function,
    Type,
    Var,
    EnumConstant,
    StructMember,
    StructFunction,
    Label,
}

const char*[] identifierKind_names = {
    "Unresolved",
    "Module",
    "Function",
    "Type",
    "Var",
    "EnumConstant",
    "StructMember",
    "StructFunction",
    "Label",
}

static_assert(elemsof(IdentifierKind), elemsof(identifierKind_names));

public type IdentifierExprBits struct {
    u32 : NumExprBits;
    u32 has_decl : 1;
    u32 kind : 4;   // IdentifierKind
    // TODO add Type, Var, Module, etc
}

public type IdentifierExpr struct {
    Expr parent;
    union {
        const char* name;
        Decl* decl;     // set during analysis
    }
} @(opaque)

public func IdentifierExpr* IdentifierExpr.create(ast_context.Context* c, SrcLoc loc, const char* name) @(inline) {
    IdentifierExpr* e = c.alloc(sizeof(IdentifierExpr));
    e.parent.init(ExprKind.Identifier, loc, 0, 0, 0);
    e.name = name;
    return e;
}

public func Expr* IdentifierExpr.asExpr(IdentifierExpr* e) @(inline) { return &e.parent; }

public func Decl* IdentifierExpr.getDecl(const IdentifierExpr* e) @(inline) {
    if (!e.parent.parent.identifierExprBits.has_decl) return nil;
    return e.decl;
}

public func void IdentifierExpr.print(const IdentifierExpr* e, string_buffer.Buf* out, u32 indent) {
    e.parent.printKind(out, indent, "IdentifierExpr");
    e.parent.printTypeBits(out);

    out.add(" ");
    if (e.parent.parent.identifierExprBits.has_decl) {
        out.add(e.decl.getName());
    } else {
        out.color(col_Value);
        out.add(e.name);
        out.color(col_Error);
        out.add(" unresolved");
    }
    out.add("\n");

}

func void IdentifierExpr.printLiteral(const IdentifierExpr* e, string_buffer.Buf* out) {
    if (e.parent.parent.identifierExprBits.has_decl) {
        out.add(e.decl.getName());
    } else {
        out.add(e.name);
    }
}

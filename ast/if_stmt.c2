/* Copyright 2022-2023 Bas van den Berg
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

module ast;

import ast_context;
import string_buffer;

type IfStmtBits struct {
    u32 : NumStmtBits;
    u32 has_else : 1;
}

public type IfStmt struct {
    Stmt parent;
    Stmt* cond;
    Stmt* then;
    Stmt*[0] else_stmt; // tail-allocated
} @(opaque)

public func IfStmt* IfStmt.create(ast_context.Context* c, Stmt* cond, Stmt* then, Stmt* else_stmt) {
    u32 size = sizeof(IfStmt);
    if (else_stmt) size += sizeof(Stmt*);

    IfStmt* s = c.alloc(size);
    s.parent.init(StmtKind.If);
    s.cond = cond;
    s.then = then;

    if (else_stmt) {
        s.parent.ifStmtBits.has_else = 1;
        s.else_stmt[0] = else_stmt;
    }
#if AstStatistics
    Stats.addStmt(StmtKind.If, size);
#endif
    return s;
}

func Stmt* IfStmt.instantiate(IfStmt* s, Instantiator* inst) {
    Stmt* cond2 = s.cond.instantiate(inst);
    Stmt* then2 = s.then.instantiate(inst);
    Stmt* else2 = nil;
    if (s.parent.ifStmtBits.has_else) else2 = s.else_stmt[0].instantiate(inst);

    return cast<Stmt*>(IfStmt.create(inst.c, cond2, then2, else2));
}

public func Stmt* IfStmt.getCond(const IfStmt* s) { return s.cond; }
public func Stmt** IfStmt.getCond2(IfStmt* s) { return &s.cond; }

public func Stmt* IfStmt.getThen(const IfStmt* s) { return s.then; }

public func Stmt* IfStmt.getElse(const IfStmt* s) {
    if (s.parent.ifStmtBits.has_else) return s.else_stmt[0];
    return nil;
}

func void IfStmt.print(const IfStmt* s, string_buffer.Buf* out, u32 indent) {
    s.parent.printKind(out, indent);
    out.newline();

    s.cond.print(out, indent + 1);
    if (s.then) s.then.print(out, indent + 1);
    if (s.parent.ifStmtBits.has_else) s.else_stmt[0].print(out, indent + 1);
}


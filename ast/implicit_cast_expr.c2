/* Copyright 2022-2023 Bas van den Berg
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

module ast;

import ast_context;
import string_buffer;
import src_loc local;

public type ImplicitCastKind enum u8 {
    ArrayToPointerDecay,
    FunctionToPointerDecay,
    LValueToRValue,
    PointerToBoolean,
    PointerToInteger,
    BitCast,    // pointer conversion (char* -> i32*, or void* -> FunctionPtr)
}

public const char*[] implicitCastKind_names = {
    "ArrayToPointerDecay",
    "FunctionToPointerDecay",
    "LValueToRValue",
    "PointerToBoolean",
    "PointerToInteger",
    "BitCast",
}

static_assert(elemsof(ImplicitCastKind), elemsof(implicitCastKind_names));

public type ImplicitCastBits struct {
    u32 : NumExprBits;
    u32 kind : 3;
}

public type ImplicitCastExpr struct {
    Expr parent;
    Expr* inner;
} @(opaque)

public func ImplicitCastExpr* ImplicitCastExpr.create(ast_context.Context* c, SrcLoc loc, ImplicitCastKind kind, Expr* inner) @(inline) {
    ImplicitCastExpr* e = c.alloc(sizeof(ImplicitCastExpr));
    e.parent.init(ExprKind.ImplicitCast, loc, 0, 0, 0, ValType.RValue);
    e.parent.parent.implicitCastBits.kind = kind;
    e.inner = inner;
    e.parent.copyConstantFlags(inner);
    switch (kind) {
    case ArrayToPointerDecay:
        e.parent.copyValType(inner);
        e.parent.parent.exprBits.is_ctv = false;
        break;
    case FunctionToPointerDecay:
        e.parent.copyValType(inner);
        break;
    case LValueToRValue:
        e.parent.parent.exprBits.is_ctc = false;
        break;
    case PointerToBoolean:
        e.parent.copyValType(inner);
        break;
    case PointerToInteger:
        e.parent.copyValType(inner);
        break;
    case BitCast:
        e.parent.parent.exprBits.is_ctc = false;
        break;
    }
#if AstStatistics
    Stats.addExpr(ExprKind.ImplicitCast, sizeof(ImplicitCastExpr));
#endif
    return e;
}

public func ImplicitCastKind ImplicitCastExpr.getKind(const ImplicitCastExpr* e) {
    return cast<ImplicitCastKind>(e.parent.parent.implicitCastBits.kind);
}

public func Expr* ImplicitCastExpr.getInner(const ImplicitCastExpr* e) @(inline) { return e.inner; }

func void ImplicitCastExpr.print(const ImplicitCastExpr* e, string_buffer.Buf* out, u32 indent) {
    e.parent.printKind(out, indent);
    e.parent.printTypeBits(out);
    out.space();
    out.color(col_Calc);
    out.add(implicitCastKind_names[e.getKind()]);
    out.newline();
    e.inner.print(out, indent + 1);
}


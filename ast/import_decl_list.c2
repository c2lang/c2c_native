/* Copyright 2022 Bas van den Berg
*/

module ast;

import string local;
import stdlib local;

public type ImportDeclList struct {
    u32 count;
    u32 capacity;
    ImportDecl** decls;
}

func void ImportDeclList.init(ImportDeclList* l) {
    memset(l, 0, sizeof(ImportDeclList));
}

func void ImportDeclList.free(ImportDeclList* l) {
    if (l.decls) free(cast<void*>(l.decls));
}

func void ImportDeclList.add(ImportDeclList* l, ImportDecl* d) {
    if (l.count >= l.capacity) {
        l.capacity += 4;
        void* decls2 = malloc(l.capacity * sizeof(ImportDecl*));
        void* old = cast<void*>(l.decls);
        if (old) {
            memcpy(decls2, old, l.count * sizeof(ImportDecl*));
            free(old);
        }
        l.decls = decls2;
    }

    l.decls[l.count] = d;
    l.count++;
}

public func u32 ImportDeclList.size(const ImportDeclList* l) {
    return l.count;
}

public func ImportDecl** ImportDeclList.getDecls(const ImportDeclList* l) {
    return l.decls;
}

public func ImportDecl* ImportDeclList.find(const ImportDeclList* l, u32 name_idx) {
    for (u32 i=0; i<l.count; i++) {
        ImportDecl* d = l.decls[i];
        if (d.getImportNameIdx() == name_idx) return d;
    }
    return nil;
}

public func ImportDecl* ImportDeclList.findAny(const ImportDeclList* l, u32 name_idx) {
    for (u32 i=0; i<l.count; i++) {
        ImportDecl* d = l.decls[i];
        if (d.asDecl().getNameIdx() == name_idx) return d;
    }
    return nil;
}

/* Copyright 2022-2023 Bas van den Berg
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

module ast;

import ast_context;
import string_buffer;
import src_loc local;

public type IntegerLiteralBits struct {
    u32 : NumExprBits;
    u32 radix : 5;      // in code
    u32 is_signed : 1;
}

public type IntegerLiteral struct {
    Expr parent;
    u64 val;
} @(opaque)

public func IntegerLiteral* IntegerLiteral.create(ast_context.Context* c, SrcLoc loc, u8 radix, u64 val) {
    IntegerLiteral* i = c.alloc(sizeof(IntegerLiteral));
    i.parent.init(ExprKind.IntegerLiteral, loc, 1, 1, 0, ValType.RValue);
    i.parent.parent.integerLiteralBits.radix = radix;
    i.val = val;
#if AstStatistics
    Stats.addExpr(ExprKind.IntegerLiteral, sizeof(IntegerLiteral));
#endif
    //i.parent.setType(integerVal2Type(val));
    // just set to i32
    i.parent.setType(builtins[BuiltinKind.Int32]);

    return i;
}

public func IntegerLiteral* IntegerLiteral.createUnsignedConstant(ast_context.Context* c, SrcLoc loc, u64 val, QualType qt) {
    IntegerLiteral* i = IntegerLiteral.create(c, loc, 10, val);
    i.parent.setCtv();
    i.parent.setCtc();
    i.parent.setType(qt);
    return i;
}

public func IntegerLiteral* IntegerLiteral.createSignedConstant(ast_context.Context* c, SrcLoc loc, i64 val, QualType qt) {
    IntegerLiteral* i = IntegerLiteral.create(c, loc, 10, cast<u64>(val));
    i.parent.parent.integerLiteralBits.is_signed = 1;
    i.parent.setCtv();
    i.parent.setCtc();
    i.parent.setType(qt);
    return i;
}

public func u64 IntegerLiteral.getValue(const IntegerLiteral* e) { return e.val; }

public func bool IntegerLiteral.isDecimal(const IntegerLiteral* e) {
    return e.parent.parent.integerLiteralBits.radix == 10;
}

public func bool IntegerLiteral.isSigned(const IntegerLiteral* e) {
    return e.parent.parent.integerLiteralBits.is_signed;
}

func void printBinary(string_buffer.Buf* out, u64 value) {
    char[64] tmp;
    tmp[63] = 0;
    char* cp = &tmp[62];
    while (value) {
        *cp = '0' + (value & 0x1);
        cp--;
        value /= 2;
    }
    *cp-- = 'b';
    *cp = '0';
    out.add(cp);
}

func void printOctal(string_buffer.Buf* out, u64 value) {
    char[32] tmp;
    tmp[31] = 0;
    char* cp = &tmp[30];
    while (value) {
        *cp = '0' + (value & 0x7);
        cp--;
        value /= 8;
    }
    *cp = '0';
    out.add(cp);
}

func void IntegerLiteral.print(const IntegerLiteral* e, string_buffer.Buf* out, u32 indent) {
    e.parent.printKind(out, indent);
    e.parent.printTypeBits(out);
    out.color(col_Value);
    out.space();
    e.printLiteral(out);
    out.newline();
}

public func void IntegerLiteral.printLiteral(const IntegerLiteral* e, string_buffer.Buf* out) {
    switch (e.parent.parent.integerLiteralBits.radix) {
    case 2:
        printBinary(out, e.val);
        break;
    case 8:
        printOctal(out, e.val);
        break;
    case 10:
        if (e.parent.parent.integerLiteralBits.is_signed) {
            out.print("%d", cast<i64>(e.val));
        } else {
            out.print("%d", e.val);
        }
        break;
    case 16:
        out.print("0x%x", e.val);
        break;
    }
}

public func void IntegerLiteral.printDecimal(const IntegerLiteral* e, string_buffer.Buf* out) {
    if (e.parent.parent.integerLiteralBits.is_signed) {
        out.print("%d", cast<i64>(e.val));
    } else {
        out.print("%d", e.val);
    }
}

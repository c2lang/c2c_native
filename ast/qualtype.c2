/* Copyright 2022 Bas van den Berg
*/

module ast;

import utils;
import string_buffer;
import stdio local;

public const u32 QualType_Const    = 0x1;
public const u32 QualType_Volatile = 0x2;
public const u32 QualType_Mask     = 0x3;

public type QualType struct {
    usize ptr;
}

public func QualType QualType.init(Type* t, u32 qualifiers) @(inline) {
    QualType qt = { cast<usize>(t) | (qualifiers & QualType_Mask) }
    return qt;
}

public func QualType QualType.init2(Type* t) @(inline) {
    QualType qt = { cast<usize>(t) }
    return qt;
}

public func QualType QualType.init0() @(inline) {
    QualType qt = { 0 }
    return qt;
}

public func void QualType.set(QualType* qt, Type* t) @(inline) {
    qt.ptr = cast<usize>(t);
}

public func void QualType.setConst(QualType* qt) @(inline) {
    qt.ptr |= QualType_Const;
}

public func bool QualType.isConst(QualType* qt) @(inline) {
    return (qt.ptr & QualType_Const) != 0;
}

public func bool QualType.isValid(const QualType* qt) @(inline) { return qt.ptr != 0; }

public func bool QualType.isInvalid(const QualType* qt) @(inline) { return qt.ptr == 0; }

public func Type* QualType.getType(const QualType* qt) @(inline) {
    Type* t = qt.getTypeOrNil();
    assert(t);
    return t;
}

public func Type* QualType.getTypeOrNil(const QualType* qt) @(inline) {
    usize temp = qt.ptr & ~QualType_Mask;
    return cast<Type*>(temp);
    // TODO fix in C2C
    //return cast<Type*>(qt.ptr & ~QualType_Mask);
}

public func bool QualType.hasCanonicalType(const QualType* qt) @(inline) {
    const Type* t = qt.getType();
    return t.canonicalType.ptr != 0;
}

public func QualType QualType.getCanonicalType(const QualType* qt) @(inline) {
    const Type* t = qt.getType();
    return t.canonicalType;
}

public func u32 QualType.getQualifiers(const QualType* qt) @(inline) {
    return qt.ptr & QualType_Mask;
}

public func bool QualType.isBuiltinType(const QualType* qt) {
    return qt.getTypeOrNil().isBuiltinType();
}

public func bool QualType.isArrayType(const QualType* qt) {
    return qt.getTypeOrNil().isArrayType();
}

public func bool QualType.isStructType(const QualType* qt) {
    return qt.getTypeOrNil().isStructType();
}

public func bool QualType.isPointerType(const QualType* qt) {
    return qt.getTypeOrNil().isPointerType();
}

public func bool QualType.isVoidType(const QualType* qt) {
    return qt.getTypeOrNil().isVoidType();
}

// NOTE: doesn't check if kind is ok
public func BuiltinType* QualType.getBuiltinType(const QualType* qt) {
    return cast<BuiltinType*>(qt.getTypeOrNil());
}

// NOTE: doesn't check if kind is ok
public func PointerType* QualType.getPointerType(const QualType* qt) {
    return cast<PointerType*>(qt.getTypeOrNil());
}

// NOTE: doesn't check if kind is ok
public func ArrayType* QualType.getArrayType(const QualType* qt) {
    return cast<ArrayType*>(qt.getTypeOrNil());
}
public func bool QualType.needsCtvInit(const QualType* qt) {
    const Type* t = qt.getTypeOrNil();

    switch (t.getKind()) {
    case Builtin:   return true;
    case Pointer:   return false;
    case Array:     return true;
    case Ref:       return true;
    case Struct:    return true;
    case Enum:      return true;
    case Function:  return false;
    case Module:    return false;
    }
    return false;
}

public func const char* QualType.diagName(const QualType* qt, bool showQualifiers = true) {
    local char[4][128] msgs;
    local u32 msg_id = 0;
    char* msg = msgs[msg_id];
    msg_id = (msg_id + 1) % elemsof(msgs);

    string_buffer.Buf* buf = string_buffer.create_static(128, false, msg);
    // TODO need to create another print?

    buf.add("'");
    qt.print(buf);
    buf.add("'");
    buf.free();

    return msg;
}

public func void QualType.dump(const QualType* qt) {
    Type* t = qt.getTypeOrNil();
    if (t) {
        t.dump();
    } else {
        stdio.printf("QualType(nil)\n");
    }
}

public func void QualType.dump_full(const QualType* qt) {
    string_buffer.Buf* out = string_buffer.create(512, utils.useColor());
    qt.fullPrint(out, 0);
    stdio.puts(out.data());
    out.free();
}

// TODO remove
func void QualType.debugPrint(const QualType* qt, string_buffer.Buf* out) {
    const Type* t = qt.getTypeOrNil();
    if (t) {
        u32 quals = qt.getQualifiers();
        if (!t) out.add(" type=nil");
        if (quals & QualType_Const) out.add(" const");
        if (quals & QualType_Volatile) out.add(" volatile");
        t.debugPrint(out);
    } else {
        out.color(col_Error);
        out.add("NIL");
        out.color(col_Normal);
    }
}

public func void QualType.printQuoted(const QualType* qt, string_buffer.Buf* out) {
    out.color(col_Type);
    out.add("'");
    qt.print(out);
    out.color(col_Type);
    out.add("'");
}

public func void QualType.print(const QualType* qt, string_buffer.Buf* out) {
    const Type* t = qt.getTypeOrNil();
    if (t) {
        u32 quals = qt.getQualifiers();
        if (quals) out.color(col_Type);
        if (quals & QualType_Const) out.add("const ");
        if (quals & QualType_Volatile) out.add("volatile ");
        t.print(out);

        const Type* canon = t.canonicalType.getTypeOrNil();
        if (canon && canon != t) {
            out.add(" => ");
            t.canonicalType.print(out);
        }
    } else {
        out.color(col_Error);
        out.add("??");
    }
}

public func void QualType.fullPrint(const QualType* qt, string_buffer.Buf* out, u32 indent) {
    out.indent(indent);
    out.add("QualType");
    u32 quals = qt.getQualifiers();
    if (quals & QualType_Const) out.add(" const");
    if (quals & QualType_Volatile) out.add(" volatile");
    const Type* t = qt.getTypeOrNil();
    if (t) {
        out.add("\n");
        t.fullPrint(out, indent + 2);
    }
    else out.add(" type=nil\n");
}


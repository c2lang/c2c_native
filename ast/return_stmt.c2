/* Copyright 2022 Bas van den Berg
*/

module ast;

import ast_context;
import string_buffer;
import src_loc local;

type ReturnStmtBits struct {
    u32 : NumStmtBits;
    u32 has_value : 1;
}

public type ReturnStmt struct {
    Stmt parent;
    SrcLoc loc;
    Expr*[0] value; // tail-allocated, dependent on returnStmtBits.has_value
} @(opaque)

public func ReturnStmt* ReturnStmt.create(ast_context.Context* c, SrcLoc loc, Expr* value) @(inline) {
    u32 size = sizeof(ReturnStmt);
    if (value) size += sizeof(Expr*);

    ReturnStmt* s = c.alloc(size);
    s.parent.init(StmtKind.Return);
    s.loc = loc;

    if (value) {
        s.parent.returnStmtBits.has_value = 1;
        s.value[0] = value;
    }
#if AstStatistics
    Stats.addStmt(StmtKind.Return, size);
#endif
    return s;
}

func Stmt* ReturnStmt.instantiate(ReturnStmt* s, Instantiator* inst) {
    if (!s.parent.returnStmtBits.has_value) return cast<Stmt*>(s);

    return cast<Stmt*>(ReturnStmt.create(inst.c, s.loc, s.value[0].instantiate(inst)));
}

public func SrcLoc ReturnStmt.getLoc(const ReturnStmt* s) {
    return s.loc;
}

public func Expr* ReturnStmt.getValue(const ReturnStmt* s) {
    if (s.parent.returnStmtBits.has_value) return s.value[0];
    return nil;
}

public func Expr** ReturnStmt.getValue2(ReturnStmt* s) {
    if (s.parent.returnStmtBits.has_value) return &s.value[0];
    return nil;
}

func void ReturnStmt.print(const ReturnStmt* s, string_buffer.Buf* out, u32 indent) {
    s.parent.printKind(out, indent);
    out.add1('\n');
    if (s.parent.returnStmtBits.has_value) {
        s.value[0].print(out, indent + 1);
    }
}


/* Copyright 2022 Bas van den Berg
*/

module ast;

import ast_context local;
import utils local;

public type StmtKind enum u8 {
    Return,
    Expr,
    If,
    While,
    Do,
    For,
/*
    Switch,
    SSwitch,
    Case,
    Default,
    Break,
    Continue,
    Fallthrough,
    Label,
    Goto,
*/
    Compound,
/*
    Decl,
    Asm,
*/
}

public type StmtBits struct {
    u32 kind : 8;
}
public const u32 NumStmtBits = 8;

public type Stmt struct {
    union {
        StmtBits stmtBits;
        ReturnStmtBits returnStmtBits;
        CompoundStmtBits compoundStmtBits;

        ExprBits exprBits;
        IfStmtBits ifStmtBits;
        BuiltinExprBits builtinExprBits;
        BooleanLiteralBits booleanLiteralBits;
        CharLiteralBits charLiteralBits;
        IdentifierExprBits identifierExprBits;
        u32 bits;
    }
} @(opaque)

func void Stmt.init(Stmt* s, StmtKind k) @(inline) {
    s.bits = 0;
    s.stmtBits.kind = k;
}

// TEMP cast needed until Analyser fixed
public func StmtKind Stmt.getKind(const Stmt* s) @(inline) { return cast<StmtKind>(s.stmtBits.kind); }

public func void Stmt.dump(const Stmt* s) {
    switch (s.getKind()) {
    case StmtKind.Return:
        ReturnStmt.print(cast<ReturnStmt*>(s));
        break;
    case StmtKind.Expr:
        Expr.dump(cast<Expr*>(s));
        break;
    case StmtKind.If:
        IfStmt.print(cast<IfStmt*>(s));
        break;
    case StmtKind.While:
        WhileStmt.print(cast<WhileStmt*>(s));
        break;
    case StmtKind.Do:
        DoStmt.print(cast<DoStmt*>(s));
        break;
    case StmtKind.For:
        ForStmt.print(cast<ForStmt*>(s));
        break;
    case StmtKind.Compound:
        CompoundStmt.print(cast<CompoundStmt*>(s));
        break;
    }
}


/* Copyright 2022 Bas van den Berg
*/

module ast;

import ast_context;
import string_buffer;
import src_loc local;
import string;

type StructTypeDeclBits struct {
    u32 : NumDeclBits;
    u32 is_struct : 1;
    u32 is_global : 1;
    u32 is_generated : 1;      // used by generators
    u32 attr_packed : 1;
    u32 attr_opaque : 1;
    u32 attr_notypedef : 1;
}

public type StructLayout struct {
    u32 size;
    u32 alignment;
    u32 attr_alignment;
    u32 offset; // in parent struct (if non-global)
    // Members are a pointer to a block of members (set during analysis)
    // TODO members: u32 offset, u32 size (combined with align 2-bit flag 1|2|4|8)
    // TODO bitfields? (or not here?)
}

public type StructTypeDecl struct {
    Decl parent;
    StructLayout layout;
    u32 num_members;
    u32 num_struct_functions;
    FunctionDecl** struct_functions;
    Decl*[0] members;   // tail-allocated
} @(opaque)

public func StructTypeDecl* StructTypeDecl.create(ast_context.Context* c,
                                                  u32 name,
                                                  SrcLoc loc,
                                                  bool is_public,
                                                  u32 ast_idx,
                                                  bool is_struct,
                                                  bool is_global,
                                                  VarDecl** members,
                                                  u32 num_members)
@(inline)
{
    u32 size = sizeof(StructTypeDecl) + num_members * sizeof(VarDecl*);
    StructTypeDecl* d = c.alloc(size);
    StructType* stype = StructType.create(c, d);
    QualType qt = QualType.init(stype.asType());
    stype.asType().setCanonicalType(qt);
    d.parent.init(DeclKind.StructType, name, loc, is_public, qt, ast_idx);
    d.parent.structTypeDeclBits.is_struct = is_struct;
    d.parent.structTypeDeclBits.is_global = is_global;
    d.layout.size = 0;
    d.layout.alignment = 0;
    d.layout.attr_alignment = 0;
    d.layout.offset = 0;
    d.num_members = num_members;
    d.num_struct_functions = 0;
    d.struct_functions = nil;
    if (num_members) {
        string.memcpy(cast<void*>(d.members), cast<void*>(members), num_members * sizeof(VarDecl*));
    }
#if AstStatistics
    Stats.addDecl(DeclKind.StructType, size);
#endif
    return d;
}

public func Decl* StructTypeDecl.asDecl(StructTypeDecl* d) @(inline) {
    return &d.parent;
}

public func u32 StructTypeDecl.getNumMembers(const StructTypeDecl* d) @(inline) {
    return d.num_members;
}

public func Decl** StructTypeDecl.getMembers(StructTypeDecl* d) {
    // NOTE: doesn't check if present!
    return d.members;
}

public func bool StructTypeDecl.isStruct(const StructTypeDecl* d) {
    return d.parent.structTypeDeclBits.is_struct;
}

public func bool StructTypeDecl.isUnion(const StructTypeDecl* d) {
    return !d.parent.structTypeDeclBits.is_struct;
}

public func const StructLayout* StructTypeDecl.getLayout(const StructTypeDecl* d) {
    return &d.layout;
}

public func u32 StructTypeDecl.getOffset(const StructTypeDecl* d) {
    return d.layout.offset;
}

public func void StructTypeDecl.setOffset(StructTypeDecl* d, u32 offset) {
    d.layout.offset = offset;
}

public func u32 StructTypeDecl.getSize(const StructTypeDecl* d) {
    return d.layout.size;
}

public func void StructTypeDecl.setSize(StructTypeDecl* d, u32 size) {
    d.layout.size = size;
}

public func u32 StructTypeDecl.getAlignment(const StructTypeDecl* d) {
    return d.layout.alignment;
}

public func void StructTypeDecl.setAlignment(StructTypeDecl* d, u32 alignment) {
    d.layout.alignment = alignment;
}

public func u32 StructTypeDecl.getAttrAlignment(const StructTypeDecl* d) {
    return d.layout.attr_alignment;
}

public func void StructTypeDecl.setAttrAlignment(StructTypeDecl* d, u32 alignment) {
    d.layout.attr_alignment = alignment;
}

public func bool StructTypeDecl.isGenerated(const StructTypeDecl* d) {
    return d.parent.structTypeDeclBits.is_generated;
}

public func void StructTypeDecl.setGenerated(StructTypeDecl* d) {
    d.parent.structTypeDeclBits.is_generated = 1;
}

public func void StructTypeDecl.setPacked(StructTypeDecl* d) {
    d.parent.structTypeDeclBits.attr_packed = 1;
}

public func bool StructTypeDecl.isPacked(const StructTypeDecl* d) {
    return d.parent.structTypeDeclBits.attr_packed;
}

public func void StructTypeDecl.setOpaque(StructTypeDecl* d) {
    d.parent.structTypeDeclBits.attr_opaque = 1;
}

public func bool StructTypeDecl.isOpaque(const StructTypeDecl* d) {
    return d.parent.structTypeDeclBits.attr_opaque;
}

public func void StructTypeDecl.setAttrNoTypeDef(StructTypeDecl* d) {
    d.parent.structTypeDeclBits.attr_notypedef = 1;
}

public func void StructTypeDecl.setStructFunctions(StructTypeDecl* d, ast_context.Context* c, FunctionDecl** funcs, u32 count) {
    const u32 size = count * sizeof(FunctionDecl*);
    void* dest = c.alloc(size);
    string.memcpy(dest, cast<void*>(funcs), size);
    d.struct_functions = dest;
    d.num_struct_functions = count;
}

public func Decl* StructTypeDecl.findAny(const StructTypeDecl* s, u32 name_idx) {
    for (u32 i=0; i<s.getNumMembers(); i++) {
        Decl* d = s.members[i];
        u32 member_name = d.getNameIdx();

        if (member_name == name_idx) return d;

        if (member_name == 0  && d.getKind() == DeclKind.StructType) {
            StructTypeDecl* sub = cast<StructTypeDecl*>(d);
            d = sub.findAny(name_idx);
            if (d) return d;
        }
    }

    if (s.parent.structTypeDeclBits.is_global) {
        for (u32 i=0; i<s.num_struct_functions; i++) {
            Decl* sf = cast<Decl*>(s.struct_functions[i]);
            if (sf.getNameIdx() == name_idx) return sf;
        }
    }
    return nil;
}

public func Decl* StructTypeDecl.findMember(const StructTypeDecl* s, u32 name_idx, u32* offset) {
    for (u32 i=0; i<s.getNumMembers(); i++) {
        Decl* d = s.members[i];
        u32 member_name = d.getNameIdx();

        if (member_name == name_idx) return d;

        // search unnamed sub-structs
        if (member_name == 0 && d.getKind() == DeclKind.StructType) {
            StructTypeDecl* sub = cast<StructTypeDecl*>(d);
            d = sub.findMember(name_idx, offset);
            if (d) {
                *offset += sub.getOffset();
                return d;
            }
        }
    }
    return nil;
}

func void StructTypeDecl.print(const StructTypeDecl* d, string_buffer.Buf* out, u32 indent) {
    d.parent.printKind(out, indent, true);
    d.parent.printBits(out);
    bool is_global = d.parent.structTypeDeclBits.is_global;
    if (is_global) out.add(" global");
    if (d.parent.structTypeDeclBits.is_struct) out.add(" struct");
    else out.add(" union");

    if (d.isPacked()) out.add(" packed");
    if (d.isOpaque()) out.add(" opaque");

    if (is_global) d.parent.printAttrs(out);

    out.color(col_Calc);
    if (!is_global) out.print(" offset=%u", d.layout.offset);
    out.print(" size=%u align=%u", d.layout.size, d.layout.alignment);

    d.parent.printName(out);
    out.add1('\n');

    for (u32 i=0; i<d.num_members; i++) {
        d.members[i].print(out, indent + 2);
    }
}


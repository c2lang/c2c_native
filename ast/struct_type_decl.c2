/* Copyright 2022 Bas van den Berg
*/

module ast;

import ast_context;
import string_buffer;
import utils local;
import string;

type StructTypeDeclBits struct {
    u32 : NumDeclBits;
    u32 is_struct : 1;
    u32 is_global : 1;
    u32 num_members : 10;
}

public type StructTypeDecl struct {
    Decl parent;
    Decl*[0] members;   // tail-allocated
} @(opaque)

public func StructTypeDecl* StructTypeDecl.create(ast_context.Context* c, const char* name, SrcLoc loc, bool is_public,
                                                  bool is_struct, bool is_global, VarDecl** members, u32 num_members) @(inline)
{
    u32 size = sizeof(StructTypeDecl) + num_members * sizeof(VarDecl*);
    StructTypeDecl* d = c.alloc(size);
    StructType* stype = StructType.create(c, d);
    QualType qt = QualType.init2(stype.asType());
    d.parent.init(DeclKind.StructType, name, loc, is_public, qt);
    d.parent.structTypeDeclBits.is_struct = is_struct;
    d.parent.structTypeDeclBits.is_global = is_global;
    d.parent.structTypeDeclBits.num_members = num_members;
    if (num_members) {
        string.memcpy(cast<void*>(d.members), cast<void*>(members), num_members * sizeof(VarDecl*));
    }
    return d;
}

public func Decl* StructTypeDecl.asDecl(StructTypeDecl* d) @(inline) {
    return &d.parent;
}

public func void StructTypeDecl.print(const StructTypeDecl* d, string_buffer.Buf* out, u32 indent) {
    d.parent.printKind(out, indent, "StructTypeDecl");
    d.parent.printBits(out);
    if (d.parent.structTypeDeclBits.is_global) out.add(" global");
    if (d.parent.structTypeDeclBits.is_struct) out.add(" struct");
    else out.add(" union");

    d.parent.printName(out);
    out.add("\n");

    for (u32 i=0; i<d.parent.structTypeDeclBits.num_members; i++) {
        d.members[i].print(out, indent + 2);
    }
}


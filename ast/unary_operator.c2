/* Copyright 2022 Bas van den Berg
*/

module ast;

import ast_context;
import utils local;

public type UnaryOperatorBits struct {
    u32 : NumExprBits;
    u32 kind : 4;
}

public type UnaryOperator struct {
    Expr expr;
    Expr* inner;
} @(opaque)

public type UnaryOpcode enum u8 {
    PostInc,
    PostDec,
    PreInc,
    PreDec,
    AddrOf,
    Deref,
    Minus,
    Not,
    LNot,
}

public func UnaryOperator* UnaryOperator.create(ast_context.Context* c, SrcLoc loc, UnaryOpcode kind, Expr* inner) @(inline) {
    UnaryOperator* e = c.alloc(sizeof(UnaryOperator));
    e.expr.init(ExprKind.UnaryOperator, loc, 0, 0, 0);
    e.expr.stmt.unaryOperatorBits.kind = kind;
    e.inner = inner;
    return e;
}

public func UnaryOpcode UnaryOperator.getKind(const UnaryOperator* e) {
    return cast<UnaryOpcode>(e.expr.stmt.unaryOperatorBits.kind);
}

public func Expr* UnaryOperator.getExpr(UnaryOperator* e) { return e.inner; }
public func Expr** UnaryOperator.getExpr2(UnaryOperator* e) { return &e.inner; }

public func const char* unaryOpcode2Str(UnaryOpcode k) {
    switch (k) {
    case UnaryOpcode.PostInc: return "++";
    case UnaryOpcode.PostDec: return "--";
    case UnaryOpcode.PreInc:  return "++";
    case UnaryOpcode.PreDec:  return "--";
    case UnaryOpcode.AddrOf:  return "&";
    case UnaryOpcode.Deref:   return "*";
    case UnaryOpcode.Minus:   return "-";
    case UnaryOpcode.Not:     return "~";
    case UnaryOpcode.LNot:    return "";
    }
    return "";
}

public func void UnaryOperator.print(const UnaryOperator* e, StringBuffer* out, u32 indent) {
    out.indent(indent);
    out.print("UnaryOperator %s\n", unaryOpcode2Str(e.getKind()));
    e.inner.print(out, indent + 2);
}


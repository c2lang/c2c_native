/* Copyright 2022 Bas van den Berg
*/

module ast;

import ast_context;
import string_buffer;
import utils local;

public type UnaryOperatorBits struct {
    u32 : NumExprBits;
    u32 kind : 4;
}

public type UnaryOperator struct {
    Expr parent;
    Expr* inner;
} @(opaque)

public type UnaryOpcode enum u8 {
    PostInc,
    PostDec,
    PreInc,
    PreDec,
    AddrOf,
    Deref,
    Minus,
    Not,
    LNot,
}

public const char*[] unaryOpcode_names = {
    "++",
    "--",
    "++",
    "--",
    "&",
    "*",
    "-",
    "~",
    "!",
}

static_assert(elemsof(UnaryOpcode), elemsof(unaryOpcode_names));

public func UnaryOperator* UnaryOperator.create(ast_context.Context* c, SrcLoc loc, UnaryOpcode kind, Expr* inner) @(inline) {
    UnaryOperator* e = c.alloc(sizeof(UnaryOperator));
    e.parent.init(ExprKind.UnaryOperator, loc, 0, 0, 0);
    e.parent.parent.unaryOperatorBits.kind = kind;
    e.inner = inner;
    return e;
}

public func UnaryOpcode UnaryOperator.getKind(const UnaryOperator* e) {
    return cast<UnaryOpcode>(e.parent.parent.unaryOperatorBits.kind);
}

public func Expr* UnaryOperator.getInner(const UnaryOperator* e) { return e.inner; }
public func Expr** UnaryOperator.getInner2(UnaryOperator* e) { return &e.inner; }

func bool UnaryOperator.isBefore(const UnaryOperator* e) {
    switch (e.getKind()) {
    case UnaryOpcode.PostInc: return false;
    case UnaryOpcode.PostDec: return false;
    case UnaryOpcode.PreInc:  return true;
    case UnaryOpcode.PreDec:  return true;
    case UnaryOpcode.AddrOf:  return true;
    case UnaryOpcode.Deref:   return true;
    case UnaryOpcode.Minus:   return true;
    case UnaryOpcode.Not:     return true;
    case UnaryOpcode.LNot:    return true;
    }
    return true;
}

func void UnaryOperator.print(const UnaryOperator* e, string_buffer.Buf* out, u32 indent) {
    e.parent.printKind(out, indent, "UnaryOperator");
    e.parent.printTypeBits(out);
    out.add(" ");
    out.color(col_Value);
    out.add(unaryOpcode_names[e.getKind()]);
    out.add("\n");
    e.inner.print(out, indent + 2);
}

func void UnaryOperator.printLiteral(const UnaryOperator* e, string_buffer.Buf* out) {
    const char* opcode = unaryOpcode_names[e.getKind()];
    if (e.isBefore()) {
        out.add(opcode);
        e.inner.printLiteral(out);
    } else {
        e.inner.printLiteral(out);
        out.add(opcode);
    }
}


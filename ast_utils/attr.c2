/* Copyright 2022-2023 Bas van den Berg
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

module attr;

import src_loc local;

// note: keep Attr prefix because Kind already exists a lot
public type AttrKind enum u8 {
    Unknown,
    Export,         // Type | Func | Var
    Packed,         // Struct
    Unused,         // Type | Func | Var
    UnusedParams,   // Type | Func
    Section,        //        Func | Var, needs string arg
    NoReturn,       //        Func
    Inline,         //        Func
    Aligned,        // Type | Func | Var, needs number arg
    Weak,           //        Func | Var
    Opaque,         // Struct
    CName,          // Type | Func | Var, only in interface, needs string arg
    NoTypeDef,      // Type, only interface files
}

const char*[] attrKind_names = {
    "",
    "export",
    "packed",
    "unused",
    "unused_params",
    "section",
    "noreturn",
    "inline",
    "aligned",
    "weak",
    "opaque",
    "cname",
    "no_typedef",
}

static_assert(elemsof(AttrKind), elemsof(attrKind_names));

u32[elemsof(AttrKind)] name_indexes;

public type Value struct {
    union {
        u32 text_idx;
        u32 number;
    }
    SrcLoc loc;
    bool is_number;
}

// Note: only meant for printing (since not allocated in StringPool)
public func const char* kind2name(AttrKind k) {
    return attrKind_names[k];
}

public type ConvertFn func u32 (void* arg, const char* name);

public func void init(ConvertFn convert, void* arg) {
    // skip unused
    for (u32 i=1; i<elemsof(attrKind_names); i++) {
        name_indexes[i] = convert(arg, attrKind_names[i]);
    }
}

public func AttrKind find(u32 name_idx) {
    // skip unused
    for (u32 i=1; i<elemsof(name_indexes); i++) {
        if (name_idx == name_indexes[i]) return cast<AttrKind>(i);
    }
    return AttrKind.Unknown;
}

const AttrReq[elemsof(AttrKind)] Required_arg = {
    [AttrKind.Section] = AttrReq.String,
    [AttrKind.Aligned] = AttrReq.Number,
    [AttrKind.CName] = AttrReq.String,
}

public type AttrReq enum u8 {
    NoArg = 0,
    Arg,
    Number,
    String,
    Power2,
    Ok,
}

func bool isPowerOf2(u32 val) {
    return val && !(val & (val - 1));
}

public func AttrReq check(AttrKind kind, const Value* value) {
    switch (Required_arg[kind]) {
    case NoArg:
        if (value) return AttrReq.NoArg;
        break;
    case Number:
        if (!value) return AttrReq.Arg;
        if (!value.is_number) return AttrReq.Number;
        assert(kind == AttrKind.Aligned);
        if (!isPowerOf2(value.number)) return AttrReq.Power2;
        break;
    case String:
        if (!value) return AttrReq.Arg;
        if (value.is_number) return AttrReq.String;
        break;
    default:
        break;
    }
    return AttrReq.Ok;
}


module c2_tokenizer;

import string local;
import stdio local;
import ctype local;
import stdarg local;

import constants;
import string_list;

type Keyword struct {
    const char* name;
    TokenKind kind;
    u8 len;
}

const Keyword*[] keywords = {
    Keywords_a,
    Keywords_b,
    Keywords_c,
    Keywords_d,
    Keywords_e,
    Keywords_f,
    Keywords_g,
    nil,
    Keywords_i,
    nil,
    nil,
    Keywords_l,
    Keywords_m,
    Keywords_n,
    Keywords_o,
    Keywords_p,
    nil,
    Keywords_r,
    Keywords_s,
    Keywords_t,
    Keywords_u,
    Keywords_v,
    Keywords_w,
    nil,
    nil,
    nil,
}

const Keyword[] Keywords_a = {
    { "as", TokenKind.KW_as, 2 },
    { "asm", TokenKind.KW_asm, 3 },
    { "auto", TokenKind.KW_auto, 4, },
    { nil, TokenKind.None, 0 },
}
const Keyword[] Keywords_b = {
    { "bool", TokenKind.KW_bool, 4 },
    { "break", TokenKind.KW_break, 5 },
    { nil, TokenKind.None, 0 },
}
const Keyword[] Keywords_c = {
    { "case", TokenKind.KW_case, 4 },
    { "cast", TokenKind.KW_cast, 4 },
    { "char", TokenKind.KW_char, 4 },
    { "const", TokenKind.KW_const, 5 },
    { "continue", TokenKind.KW_continue, 8 },
    { nil, TokenKind.None, 0 },
}
const Keyword[] Keywords_d = {
    { "default", TokenKind.KW_default, 8 },
    { "do", TokenKind.KW_do, 2 },
    { nil, TokenKind.None, 0 },
}
const Keyword[] Keywords_e = {
    { "elemsof", TokenKind.KW_elemsof, 7 },
    { "else", TokenKind.KW_else, 4 },
    { "enum_max", TokenKind.KW_enum_max, 8 },
    { "enum_min", TokenKind.KW_enum_min, 8 },
    { "enum", TokenKind.KW_enum, 4 },
    { nil, TokenKind.None, 0 },
}
const Keyword[] Keywords_f = {
    { "f32", TokenKind.KW_f32, 3 },
    { "f64", TokenKind.KW_f64, 3 },
    { "fallthrough", TokenKind.KW_fallthrough, 11 },
    { "false", TokenKind.KW_false, 5 },
    { "for", TokenKind.KW_for, 3 },
    { "func", TokenKind.KW_func, 4 },
    { nil, TokenKind.None, 0 },
}
const Keyword[] Keywords_g = {
    { "goto", TokenKind.KW_goto, 4 },
    { nil, TokenKind.None, 0 },
}
const Keyword[] Keywords_i = {
    { "i16", TokenKind.KW_i16, 3 },
    { "i32", TokenKind.KW_i32, 3 },
    { "i64", TokenKind.KW_i64, 3 },
    { "i8", TokenKind.KW_i8, 2 },
    { "if", TokenKind.KW_if, 2 },
    { "import", TokenKind.KW_import, 6 },
    { "isize", TokenKind.KW_isize, 5 },
    { nil, TokenKind.None, 0 },
}
const Keyword[] Keywords_l = {
    { "local", TokenKind.KW_local, 5 },
    { nil, TokenKind.None, 0 },
}
const Keyword[] Keywords_m = {
    { "module", TokenKind.KW_module, 6 },
    { nil, TokenKind.None, 0 },
}
const Keyword[] Keywords_n = {
    { "nil", TokenKind.KW_nil, 3 },
    { nil, TokenKind.None, 0 },
}
const Keyword[] Keywords_o = {
    { "offsetof", TokenKind.KW_offsetof, 8 },
    { nil, TokenKind.None, 0 },
}
const Keyword[] Keywords_p = {
    { "public", TokenKind.KW_public, 6 },
    { nil, TokenKind.None, 0 },
}
const Keyword[] Keywords_r = {
    { "reg16", TokenKind.KW_reg16, 5 },
    { "reg32", TokenKind.KW_reg32, 5 },
    { "reg64", TokenKind.KW_reg64, 5 },
    { "reg8", TokenKind.KW_reg8, 4 },
    { "return", TokenKind.KW_return, 6 },
    { nil, TokenKind.None, 0 },
}
const Keyword[] Keywords_s = {
    { "sizeof", TokenKind.KW_sizeof, 6 },
    { "sswitch", TokenKind.KW_sswitch, 7 },
    { "static_assert", TokenKind.KW_static_assert, 13 },
    { "struct", TokenKind.KW_struct, 6 },
    { "switch", TokenKind.KW_switch, 6 },
    { nil, TokenKind.None, 0 },
}
const Keyword[] Keywords_t = {
    { "to_container", TokenKind.KW_to_container, 12 },
    { "true", TokenKind.KW_true, 4 },
    { "type", TokenKind.KW_type, 4 },
    { nil, TokenKind.None, 0 },
}
const Keyword[] Keywords_u = {
    { "u16", TokenKind.KW_u16, 3 },
    { "u32", TokenKind.KW_u32, 3 },
    { "u64", TokenKind.KW_u64, 3 },
    { "u8", TokenKind.KW_u8, 2 },
    { "union", TokenKind.KW_union, 5 },
    { "usize", TokenKind.KW_usize, 5 },
    { nil, TokenKind.None, 0 },
}
const Keyword[] Keywords_v = {
    { "void", TokenKind.KW_void, 4 },
    { "volatile", TokenKind.KW_volatile, 8 },
    { nil, TokenKind.None, 0 },
}
const Keyword[] Keywords_w = {
    { "while", TokenKind.KW_while, 5 },
    { nil, TokenKind.None, 0 },
}

public type TokenKind enum u8 {
    None,
    Identifier,
    NumberLiteral,
    CharLiteral,
    StringLiteral,
    LParen,
    RParen,
    LSquare,
    RSquare,
    LBrace,
    RBrace,
    Exclaim,
    ExclaimEqual,
    Star,
    StarEqual,
    Amp,
    AmpAmp,
    AmpEqual,
    Pipe,
    PipePipe,
    PipeEqual,
    Equal,
    EqualEqual,
    Semicolon,
    Colon,
    At,
    Caret,
    CaretEqual,
    Question,
    Dot,
    Ellipsis,
    Comma,
    Plus,
    PlusPlus,
    PlusEqual,
    Minus,
    MinusMinus,
    MinusEqual,
    Tilde,
    Slash,
    SlashEqual,
    Percent,
    PercentEqual,
    Less,
    LessLess,
    LessEqual,
    LessLessEqual,
    Greater,
    GreaterGreater,
    GreaterEqual,
    GreaterGreaterEqual,
    KW_as,
    KW_asm,
    KW_auto,
    KW_bool,
    KW_break,
    KW_case,
    KW_cast,
    KW_char,
    KW_const,
    KW_continue,
    KW_default,
    KW_do,
    KW_elemsof,
    KW_else,
    KW_enum_max,
    KW_enum_min,
    KW_enum,
    KW_f32,
    KW_f64,
    KW_fallthrough,
    KW_false,
    KW_for,
    KW_func,
    KW_goto,
    KW_i16,
    KW_i32,
    KW_i64,
    KW_i8,
    KW_if,
    KW_import,
    KW_isize,
    KW_local,
    KW_module,
    KW_nil,
    KW_offsetof,
    KW_public,
    KW_reg16,
    KW_reg32,
    KW_reg64,
    KW_reg8,
    KW_return,
    KW_sizeof,
    KW_sswitch,
    KW_static_assert,
    KW_struct,
    KW_switch,
    KW_to_container,
    KW_true,
    KW_type,
    KW_u16,
    KW_u32,
    KW_u64,
    KW_u8,
    KW_union,
    KW_usize,
    KW_void,
    KW_volatile,
    KW_while,
    Eof,
    Warning,
    Error,
}

// NOTE: keep in sync with TokenKind
const char*[] token_names = {
    "none",
    "identifier",
    "number",
    "char",
    "string",
    "(",
    ")",
    "[",
    "]",
    "{",
    "}",
    "!",
    "!=",
    "*",
    "*=",
    "&",
    "&&",
    "&=",
    "|",
    "||",
    "|=",
    "=",
    "==",
    ";",
    ":",
    "@",
    "^",
    "^=",
    "?",
    ".",
    "...",
    ",",
    "+",
    "++",
    "+=",
    "-",
    "--",
    "-=",
    "~",
    "/",
    "/=",
    "%",
    "%=",
    "<",
    "<<",
    "<=",
    "<<=",
    ">",
    ">>",
    ">=",
    ">>=",
    "as",
    "asm",
    "auto",
    "bool",
    "break",
    "case",
    "cast",
    "char",
    "const",
    "continue",
    "default",
    "do",
    "elemsof",
    "else",
    "enum_max",
    "enum_min",
    "enum",
    "f32",
    "f64",
    "falltrhough",
    "false",
    "for",
    "func",
    "goto",
    "i16",
    "i32",
    "i64",
    "i8",
    "if",
    "import",
    "isize",
    "local",
    "module",
    "nil",
    "offsetof",
    "public",
    "reg16",
    "reg32",
    "reg64",
    "reg8",
    "return",
    "sizeof",
    "sswitch",
    "static_assert",
    "struct",
    "switch",
    "to_container",
    "true",
    "type",
    "u16",
    "u32",
    "u64",
    "u8",
    "union",
    "usize",
    "void",
    "volatile",
    "while",
    "eof",
    "warning",
    "error",
}

//static_assert(elemsof(TokenKind) == elemsof(token_names), "should be the same");

// TODO order by frequency
type Action enum u8 {
    INVALID = 0,
    TABSPACE,
    IDENT_OR_KEYWORD,
    IDENT,
    DIGIT,
    LPAREN,
    RPAREN,
    LSQUARE,
    RSQUARE,
    NEWLINE,
    EXCLAIM,
    DQUOTE,
    SQUOTE,
    POUND,
    STAR,
    PLUS,
    MINUS,
    COMMA,
    DOT,
    PERCENT,
    SLASH,
    COLON,
    SEMI_COLON,
    LESS,
    EQUAL,
    GREATER,
    QUESTION,
    AT,
    AMP,
    CARET,
    LBRACE,
    RBRACE,
    PIPE,
    TILDE,
    CR,
    EOF,
}

u8[128] char_lookup = {
// 0 - 15
   [  0]  = Action.EOF,
   ['\t'] = Action.TABSPACE,
   ['\n'] = Action.NEWLINE,
   ['\r'] = Action.CR,
// 16 - 31
// 32 - 47
    [' '] = Action.TABSPACE,
    ['!'] = Action.EXCLAIM,
    ['"'] = Action.DQUOTE,
    ['#'] = Action.POUND,
    ['%'] = Action.PERCENT,
    ['&'] = Action.AMP,
   ['\''] = Action.SQUOTE,
    ['('] = Action.LPAREN,
    [')'] = Action.RPAREN,
    ['*'] = Action.STAR,
    ['+'] = Action.PLUS,
    [','] = Action.COMMA,
    ['-'] = Action.MINUS,
    ['.'] = Action.DOT,
    ['/'] = Action.SLASH,
// 48 - 63
    ['0'] = Action.DIGIT,
    ['1'] = Action.DIGIT,
    ['2'] = Action.DIGIT,
    ['3'] = Action.DIGIT,
    ['4'] = Action.DIGIT,
    ['5'] = Action.DIGIT,
    ['6'] = Action.DIGIT,
    ['7'] = Action.DIGIT,
    ['8'] = Action.DIGIT,
    ['9'] = Action.DIGIT,
    [':'] = Action.COLON,
    [';'] = Action.SEMI_COLON,
    ['<'] = Action.LESS,
    ['='] = Action.EQUAL,
    ['>'] = Action.GREATER,
    ['?'] = Action.QUESTION,
// 64 - 79
    ['@'] = Action.AT,
    ['A'] = Action.IDENT,
    ['B'] = Action.IDENT,
    ['C'] = Action.IDENT,
    ['D'] = Action.IDENT,
    ['E'] = Action.IDENT,
    ['F'] = Action.IDENT,
    ['G'] = Action.IDENT,
    ['H'] = Action.IDENT,
    ['I'] = Action.IDENT,
    ['J'] = Action.IDENT,
    ['K'] = Action.IDENT,
    ['L'] = Action.IDENT,
    ['M'] = Action.IDENT,
    ['N'] = Action.IDENT,
    ['O'] = Action.IDENT,
// 80 - 95
    ['P'] = Action.IDENT,
    ['Q'] = Action.IDENT,
    ['R'] = Action.IDENT,
    ['S'] = Action.IDENT,
    ['T'] = Action.IDENT,
    ['U'] = Action.IDENT,
    ['V'] = Action.IDENT,
    ['W'] = Action.IDENT,
    ['X'] = Action.IDENT,
    ['Y'] = Action.IDENT,
    ['Z'] = Action.IDENT,
    ['['] = Action.LSQUARE,
    // '\'
    [']'] = Action.RSQUARE,
    ['^'] = Action.CARET,
    //'_'
// 96 - 111
    ['a'] = Action.IDENT_OR_KEYWORD,
    ['b'] = Action.IDENT_OR_KEYWORD,
    ['c'] = Action.IDENT_OR_KEYWORD,
    ['d'] = Action.IDENT_OR_KEYWORD,
    ['e'] = Action.IDENT_OR_KEYWORD,
    ['f'] = Action.IDENT_OR_KEYWORD,
    ['g'] = Action.IDENT_OR_KEYWORD,
    ['h'] = Action.IDENT,
    ['i'] = Action.IDENT_OR_KEYWORD,
    ['j'] = Action.IDENT,
    ['k'] = Action.IDENT,
    ['l'] = Action.IDENT_OR_KEYWORD,
    ['m'] = Action.IDENT_OR_KEYWORD,
    ['n'] = Action.IDENT_OR_KEYWORD,
    ['o'] = Action.IDENT_OR_KEYWORD,
// 112 -] 127
    ['p'] = Action.IDENT_OR_KEYWORD,
    ['q'] = Action.IDENT,
    ['r'] = Action.IDENT_OR_KEYWORD,
    ['s'] = Action.IDENT_OR_KEYWORD,
    ['t'] = Action.IDENT_OR_KEYWORD,
    ['u'] = Action.IDENT_OR_KEYWORD,
    ['v'] = Action.IDENT_OR_KEYWORD,
    ['w'] = Action.IDENT_OR_KEYWORD,
    ['x'] = Action.IDENT,
    ['y'] = Action.IDENT,
    ['z'] = Action.IDENT,
    ['{'] = Action.LBRACE,
    ['|'] = Action.PIPE,
    ['}'] = Action.RBRACE,
    ['~'] = Action.TILDE,
}

public func const char* Token.str(const Token* tok) @(inline) {
    return token_names[tok.kind];
}

public func const char* kind2str(TokenKind kind) @(inline) {
    return token_names[kind];
}

func bool identifier_char(char c) @(inline) {
    // TODO use lookup table (same as for initial?)
    if (c >= 'a' && c <= 'z') return true;
    if (c >= 'A' && c <= 'Z') return true;
    if (c >= '0' && c <= '9') return true;
    if (c == '_') return true;
    return false;
}

func const Keyword* check_keyword(const char* cp) {
    const Keyword* table = keywords[*cp - 'a'];
    u32 i = 0;
    while (table[i].name) {
        const char* word = cp;
        const char* kw = table[i].name;
        u32 idx = 0;
        while (1) {
            char a = kw[idx];
            char b = word[idx];
            if (a == 0) {
                if (!identifier_char(b)) return &table[i];
                break;
            }
            if (a != b) {
                if (b < a) return nil;
                break;
            }
            idx++;
        }
        i++;
    }

    return nil;
}

public type Token struct {
    u32 loc;
    TokenKind kind;
    union {
        const char* error_msg;  // ERROR
        const char* text_value; // Identifier, NumberLiteral
        char char_value; // CharLiteral
    }
    bool more;
}

public func void Token.init(Token* tok) {
    tok.kind = TokenKind.None;
    tok.more = true;
}

public type Alloc_fn func const char* (void* arg, const char* value, u32 len);

public type Feature struct {
    bool is_if;
    bool enabled;
}

public type Tokenizer struct {
    const char* cur;
    u32 loc_start;
    const char* input_start;

    Token next;
    const char* line_start;

    Alloc_fn alloc;
    void* alloc_arg;

    // Feature handling
    Feature[constants.MaxFeatureDepth] feature_stack;
    u32 feature_count;
    const string_list.List* features;

    char[256] error_msg;
}

public func void Tokenizer.init(Tokenizer* t,
                                const char* input,
                                u32 loc_start,
                                Alloc_fn alloc,
                                void* arg,
                                const string_list.List* features)
{
    memset(t, 0, sizeof(Tokenizer));
    t.cur = input;
    t.input_start = input;
    t.loc_start = loc_start;
    t.alloc = alloc;
    t.alloc_arg = arg;
    t.line_start = input;
    t.next.kind = TokenKind.None;
    t.features = features;
}

public func void Tokenizer.lex(Tokenizer* t, Token* result) {
    if (t.next.kind != TokenKind.None) {
        memcpy(result, &t.next, sizeof(Token));
        t.next.kind = TokenKind.None;
        return;
    }

    while (1) {
        Action act = char_lookup[*t.cur];
        switch (act) {
        case Action.INVALID:
            t.error(result, "invalid char '%c'", *t.cur);
            return;
        case Action.TABSPACE:
            t.cur++;
            continue;
        case Action.IDENT_OR_KEYWORD:
            result.loc = t.loc_start + cast<u32>(t.cur - t.input_start);
            const Keyword* kw = check_keyword(t.cur);
            if (kw) {
                result.kind = kw.kind;
                t.cur += kw.len;
            } else {
                t.lex_identifier(result);
            }
            return;
        case Action.IDENT:
            t.lex_identifier(result);
            return;
        case Action.DIGIT:
            t.lex_number(result);
            return;
        case Action.LPAREN:
            result.loc = t.loc_start + cast<u32>(t.cur - t.input_start);
            result.kind = TokenKind.LParen;
            t.cur++;
            return;
        case Action.RPAREN:
            result.loc = t.loc_start + cast<u32>(t.cur - t.input_start);
            result.kind = TokenKind.RParen;
            t.cur++;
            return;
        case Action.LSQUARE:
            result.loc = t.loc_start + cast<u32>(t.cur - t.input_start);
            result.kind = TokenKind.LSquare;
            t.cur++;
            return;
        case Action.RSQUARE:
            result.loc = t.loc_start + cast<u32>(t.cur - t.input_start);
            result.kind = TokenKind.RSquare;
            t.cur++;
            return;
        case Action.NEWLINE:
            t.cur++;
            t.line_start = t.cur;
            continue;
        case Action.EXCLAIM:
            result.loc = t.loc_start + cast<u32>(t.cur - t.input_start);
            t.cur++;
            if (*t.cur == '=') {
                result.kind = TokenKind.ExclaimEqual;
                t.cur++;
            } else {
                result.kind = TokenKind.Exclaim;
            }
            return;
        case Action.DQUOTE:
            t.lex_string_literal(result);
            return;
        case Action.SQUOTE:
            t.lex_char_literal(result);
            return;
        case Action.POUND:
            if (t.lex_feature_cmd(result)) return;
            if (!t.is_enabled()) {
                if (t.skip_feature(result)) return;
            }
            continue;
        case Action.STAR:
            result.loc = t.loc_start + cast<u32>(t.cur - t.input_start);
            t.cur++;
            if (*t.cur == '=') {
                result.kind = TokenKind.StarEqual;
                t.cur++;
            } else {
                result.kind = TokenKind.Star;
            }
            return;
        case Action.PLUS:
            result.loc = t.loc_start + cast<u32>(t.cur - t.input_start);
            t.cur++;
            if (*t.cur == '+') {
                t.cur++;
                result.kind = TokenKind.PlusPlus;
                return;
            }
            if (*t.cur == '=') {
                t.cur++;
                result.kind = TokenKind.PlusEqual;
                return;
            }
            result.kind = TokenKind.Plus;
            return;
        case Action.MINUS:
            result.loc = t.loc_start + cast<u32>(t.cur - t.input_start);
            t.cur++;
            if (*t.cur == '-') {
                t.cur++;
                result.kind = TokenKind.MinusMinus;
                return;
            }
            if (*t.cur == '=') {
                t.cur++;
                result.kind = TokenKind.MinusEqual;
                return;
            }
            result.kind = TokenKind.Minus;
            return;
        case Action.COMMA:
            result.loc = t.loc_start + cast<u32>(t.cur - t.input_start);
            result.kind = TokenKind.Comma;
            t.cur++;
            return;
        case Action.DOT:
            result.loc = t.loc_start + cast<u32>(t.cur - t.input_start);
            t.cur++;
            if (t.cur[0] == '.' && t.cur[1] == '.') {
                t.cur += 2;
                result.kind = TokenKind.Ellipsis;
            } else {
                result.kind = TokenKind.Dot;
            }
            return;
        case Action.PERCENT:
            result.loc = t.loc_start + cast<u32>(t.cur - t.input_start);
            t.cur++;
            if (*t.cur == '=') {
                result.kind = TokenKind.PercentEqual;
                t.cur++;
                return;
            }
            result.kind = TokenKind.Percent;
            return;
        case Action.SLASH:
            result.loc = t.loc_start + cast<u32>(t.cur - t.input_start);
            t.cur++;
            if (*t.cur == '=') {
                result.kind = TokenKind.SlashEqual;
                t.cur++;
                return;
            }
            if (*t.cur == '/') {
                t.skip_line_comment();
                // TODO sometimes have TokenKind.Comment
                continue;
            }
            if (*t.cur == '*') {
                t.skip_block_comment();
                continue;
            }
            result.kind = TokenKind.Slash;
            return;
        case Action.COLON:
            result.loc = t.loc_start + cast<u32>(t.cur - t.input_start);
            result.kind = TokenKind.Colon;
            t.cur++;
            return;
        case Action.SEMI_COLON:
            result.loc = t.loc_start + cast<u32>(t.cur - t.input_start);
            result.kind = TokenKind.Semicolon;
            t.cur++;
            return;
        case Action.LESS:
            result.loc = t.loc_start + cast<u32>(t.cur - t.input_start);
            t.cur++;
            if (*t.cur == '=') {
                t.cur++;
                result.kind = TokenKind.LessEqual;
                return;
            }
            if (*t.cur == '<') {
                t.cur++;
                if (*t.cur == '=') {
                    t.cur++;
                    result.kind = TokenKind.LessLessEqual;
                }
                result.kind = TokenKind.LessLess;
                return;
            }
            result.kind = TokenKind.Less;
            return;
        case Action.EQUAL:
            result.loc = t.loc_start + cast<u32>(t.cur - t.input_start);
            t.cur++;
            if (*t.cur == '=') {
                result.kind = TokenKind.EqualEqual;
                t.cur++;
            } else {
                result.kind = TokenKind.Equal;
            }
            return;
        case Action.GREATER:
            result.loc = t.loc_start + cast<u32>(t.cur - t.input_start);
            t.cur++;
            if (*t.cur == '=') {
                t.cur++;
                result.kind = TokenKind.GreaterEqual;
                return;
            }
            if (*t.cur == '>') {
                t.cur++;
                if (*t.cur == '=') {
                    t.cur++;
                    result.kind = TokenKind.GreaterGreaterEqual;
                }
                result.kind = TokenKind.GreaterGreater;
                return;
            }
            result.kind = TokenKind.Greater;
            return;
        case Action.QUESTION:
            result.loc = t.loc_start + cast<u32>(t.cur - t.input_start);
            result.kind = TokenKind.Question;
            t.cur++;
            return;
        case Action.AT:
            result.loc = t.loc_start + cast<u32>(t.cur - t.input_start);
            result.kind = TokenKind.At;
            t.cur++;
            return;
        case Action.AMP:
            result.loc = t.loc_start + cast<u32>(t.cur - t.input_start);
            t.cur++;
            if (*t.cur == '&') {
                result.kind = TokenKind.AmpAmp;
                t.cur++;
                return;
            }
            if (*t.cur == '=') {
                result.kind = TokenKind.AmpEqual;
                t.cur++;
                return;
            }
            result.kind = TokenKind.Amp;
            return;
        case Action.CARET:
            result.loc = t.loc_start + cast<u32>(t.cur - t.input_start);
            t.cur++;
            if (*t.cur == '=') {
                t.cur++;
                result.kind = TokenKind.CaretEqual;
                return;
            }
            result.kind = TokenKind.Caret;
            return;
        case Action.LBRACE:
            result.loc = t.loc_start + cast<u32>(t.cur - t.input_start);
            result.kind = TokenKind.LBrace;
            t.cur++;
            return;
        case Action.RBRACE:
            result.loc = t.loc_start + cast<u32>(t.cur - t.input_start);
            result.kind = TokenKind.RBrace;
            t.cur++;
            return;
        case Action.PIPE:
            result.loc = t.loc_start + cast<u32>(t.cur - t.input_start);
            t.cur++;
            if (*t.cur == '|') {
                result.kind = TokenKind.PipePipe;
                t.cur++;
                return;
            }
            if (*t.cur == '=') {
                result.kind = TokenKind.PipeEqual;
                t.cur++;
                return;
            }
            result.kind = TokenKind.Pipe;
            return;
        case Action.TILDE:
            result.loc = t.loc_start + cast<u32>(t.cur - t.input_start);
            result.kind = TokenKind.Tilde;
            t.cur++;
            return;
        case Action.CR:
            t.cur++;
            if (*t.cur != '\n') {
                t.error(result, "unexpected char 0x%02X", *t.cur);
                return;
            }
            t.cur++;
            return;
        case Action.EOF:
            result.loc = 0; //t.loc_start + cast<u32>(t.cur - t.input_start);
            result.kind = TokenKind.Eof;
            result.more = false;
            return;
        }
    }
}

public func Token* Tokenizer.lex_next(Tokenizer* t) {
    if (t.next.kind == TokenKind.None) t.lex(&t.next);

    return &t.next;
}

func void Tokenizer.error(Tokenizer* t, Token* result, const char* format, ...) {
    Va_list args;
    va_start(args, format);
    vsnprintf(t.error_msg, sizeof(t.error_msg)-1, format, args);
    va_end(args);

    result.loc = t.loc_start + cast<u32>(t.cur - t.input_start);
    result.kind = TokenKind.Error;
    result.error_msg = t.error_msg;
    result.more = false;
}

func void Tokenizer.lex_identifier(Tokenizer* t, Token* result) {
    // NOTE: result.loc are already set
    result.kind = TokenKind.Identifier;
    result.loc = t.loc_start + cast<u32>(t.cur - t.input_start);
    const char* start = t.cur;
    const char* end = t.cur + 1;
    while (identifier_char(*end)) end++;

    u32 len = cast<u32>(end - start);
    if (len > constants.MaxIdentifierLen) {
        t.error(result, "identifier too long (max %u)", constants.MaxIdentifierLen);
        return;
    }
    t.cur += len;
    result.text_value = t.alloc(t.alloc_arg, start, len);
}

func void Tokenizer.lex_number(Tokenizer* t, Token* result) {
    result.kind = TokenKind.NumberLiteral;
    result.loc = t.loc_start + cast<u32>(t.cur - t.input_start);
    const char* start = t.cur;
    const char* end = t.cur + 1;
    while (isdigit(*end)) end++;

    u32 len = cast<u32>(end - start);
    t.cur += len;
    result.text_value = t.alloc(t.alloc_arg, start, len);
}

func void Tokenizer.lex_char_literal(Tokenizer* t, Token* result) {
    result.kind = TokenKind.CharLiteral;
    result.loc = t.loc_start + cast<u32>(t.cur - t.input_start);

    if (t.cur[1] == '\\') {
        t.cur++;
        switch (t.cur[1]) {
        case 'n':
            result.char_value = '\n';
            break;
        case 'r':
            result.char_value = '\r';
            break;
        case 't':
            result.char_value = '\t';
            break;
        case '\\':
            result.char_value = '\\';
            break;
        case '\'':
            result.char_value = '\'';
            break;
        default:
            t.error(result, "unknown escape sequence \\%c", t.cur[1]);
            return;
        }
    } else {
        result.char_value = t.cur[1];
    }

    if (t.cur[2] != '\'') {
        t.error(result, "missing terminating ' character");
        return;
    }

    t.cur += 3;
}

func void Tokenizer.lex_string_literal(Tokenizer* t, Token* result) {
    result.kind = TokenKind.StringLiteral;
    result.loc = t.loc_start + cast<u32>(t.cur - t.input_start);
    t.cur++;
    const char* start = t.cur;
    u32 len;

    while (1) {
        switch (*t.cur) {
        case 0: fallthrough;
        case '\r': fallthrough;
        case '\n':
            t.cur--;
            t.error(result, "unterminated string");
            return;
        case '\\':
            t.cur++;
            switch (*t.cur) {
            case '0':
                break;
            case 'r':
                break;
            case 'n':
                break;
            case 't':
                break;
            case '"':
                break;
            case '\\':
                break;
            default:
                t.cur--;
                t.error(result, "unknown escape sequence \\%c", *t.cur);
                return;
            }
            t.cur++;
            break;
        case '"':
            goto out;
            break;
        default:
            t.cur++;
            break;
        }
    }
out:
    len = cast<u32>(t.cur - start);
    t.cur++;    // skip end delimiter
    result.text_value = t.alloc(t.alloc_arg, start, len);
}

func void Tokenizer.skip_line_comment(Tokenizer* t) {
    t.cur += 2;
    const char* start = t.cur;
    const char* end = start;

    while (*end) {
        if (*end == '\r' || *end == '\n') break;
        end++;
    }

    u32 len = cast<u32>(end - start);
    t.cur += len;
}

func void Tokenizer.skip_block_comment(Tokenizer* t) {
    t.cur += 2;
    const char* start = t.cur;
    const char* end = start;

    while (*end) {
        if (end[0] == '*' || end[1] == '/') break;
        end++;
    }

    u32 len = cast<u32>(end - start);
    t.cur += len;
}

func bool Tokenizer.skip_line(Tokenizer* t, Token* result) {
    // only expect whitespace + newline
    while (*t.cur != '\n') {
        if (!isspace(*t.cur)) {
            t.error(result, "unexpected character '%c'", *t.cur);
            return true;
        }
        t.cur++;
    }
    return false;
}

func bool compare_word(const char* cur, const char* expect) {
    while (*expect) {
        if (*cur != *expect) return false;
        cur++;
        expect++;
    }
    return !identifier_char(*cur);
}

// return true if we pass result
func bool Tokenizer.lex_feature_cmd(Tokenizer* t, Token* result) {
    if (t.cur != t.line_start) {
        t.error(result, "#if/#else/#endif must be at start of line");
        return true;
    }
/*
    Syntax:
        #if <selection>
        #else
        #endif
        #error "msg"

    later:
        AND OR NOT
        #warn "msg"
*/
    t.cur++;    // skip #

    if (compare_word(t.cur, "if")) {
        t.cur += 2;
        if (t.handle_if(result)) return true;
    } else if (compare_word(t.cur, "else")) {
        t.cur += 4;
        if (t.skip_line(result)) return true;
        if (t.handle_else(result)) return true;
    } else if (compare_word(t.cur, "endif")) {
        t.cur += 5;
        if (t.skip_line(result)) return true;
        if (t.handle_endif(result)) return true;
    } else if (compare_word(t.cur, "error")) {
        t.cur += 5;
        return t.parse_error_warn(result, true);
    } else if (compare_word(t.cur, "warn")) {
        t.cur += 4;
        return t.parse_error_warn(result, false);
    } else {
        t.error(result, "unknown feature-selection command");
        return true;
    }
    return false;
}

func bool Tokenizer.parse_error_warn(Tokenizer* t, Token* result, bool is_error) {
    // NOTE: #warn/#error must always have good syntax, even in dis-abled feature!

    t.cur++;    // skip SPACE
    if (*t.cur != '"') {
        t.error(result, "expect '\"'");
        return true;
    }
    t.cur++;
    const char* start = t.cur;
    while (*t.cur != '\"') {
        switch (*t.cur) {
        case 0: fallthrough;
        case '\r': fallthrough;
        case '\n':
            t.error(result, "unterminated string");
            return true;
        case '\"':
            break;
        default:
            t.cur++;
            break;
        }
    }
    u32 len = cast<u32>(t.cur - start);
    t.cur++;
    if (len > constants.MaxIdentifierLen) {
        t.error(result, "error msg too long (max %u bytes)", constants.MaxErrorMsgLen);
        return true;
    }
    char[constants.MaxErrorMsgLen+1] msg;
    memcpy(msg, start, len);
    msg[len] = 0;

    if (t.is_enabled()) {
        if (is_error) {
            t.cur = t.line_start;
            t.error(result, "%s", msg);
        } else {
            strcpy(t.error_msg, msg);
            result.loc = t.loc_start + cast<u32>(t.line_start - t.input_start);
            result.kind = TokenKind.Warning;
            result.error_msg = t.error_msg;
        }
        return true;
    }
    return false;
}

func bool Tokenizer.is_enabled(const Tokenizer* t) @(inline) {
    for (u32 i=0; i<t.feature_count; i++) {
        if (!t.feature_stack[i].enabled) return false;
    }
    return true;
}

func bool Tokenizer.handle_if(Tokenizer* t, Token* result) {
    // Syntax: 0 | 1 | FEATURE | FEATURE=<value>

    if (t.feature_count >= constants.MaxFeatureDepth) {
        t.error(result, "feature nesting too much");
        return true;
    }

    //t.skip_whitespace();
    t.cur++;    // just skip space

    bool enabled = false;
    Action act = char_lookup[*t.cur];
    switch (act) {
    case Action.INVALID:
        t.error(result, "invalid char '%c'", *t.cur);
        return true;
    case Action.IDENT_OR_KEYWORD: fallthrough;
    case Action.IDENT:
        if (t.parse_feature(result, &enabled)) return true;
        break;
    case Action.DIGIT:
        if (*t.cur != '0') enabled = true;
        t.cur++;
        break;
    case Action.EOF:
        t.cur--;
        t.error(result, "expected feature");
        return true;
    default:
        t.error(result, "invalid feature value");
        return true;
    }

    Feature* next = &t.feature_stack[t.feature_count];
    next.is_if = true;
    next.enabled = enabled;
    t.feature_count++;
    return false;
}

func bool Tokenizer.parse_feature(Tokenizer* t, Token* result, bool* enabled) {
    const char* start = t.cur;
    while (identifier_char(*t.cur)) t.cur++;

    u32 len = cast<u32>(t.cur - start);
    if (len > constants.MaxFeatureName) {
        t.error(result, "feature name too long (max %u)", constants.MaxFeatureName);
        return true;
    }

    char[constants.MaxFeatureName+1] name;
    memcpy(name, start, len);
    name[len] = 0;

    if (t.features.contains(name)) *enabled = true;
    return false;
}

func bool Tokenizer.handle_else(Tokenizer* t, Token* result) {
    if (t.feature_count == 0) {
        t.error(result, "#else without #if");
        return true;
    }
    Feature* top = &t.feature_stack[t.feature_count-1];
    if (!top.is_if) {
        t.error(result, "#else in #else");
        return true;
    }
    top.is_if = false;
    top.enabled = !top.enabled;
    return false;
}

func bool Tokenizer.handle_endif(Tokenizer* t, Token* result) {
    if (t.feature_count == 0) {
        t.error(result, "#endif without #if/#else");
        return true;
    }
    Feature* top = &t.feature_stack[t.feature_count-1];
    t.feature_count--;
    return false;
}

func bool Tokenizer.skip_feature(Tokenizer* t, Token* result) {
    while (1) {
        Action act = char_lookup[*t.cur];
        switch (act) {
        case Action.INVALID:
            t.error(result, "invalid char '%c'", *t.cur);
            return true;
        case Action.NEWLINE:
            t.cur++;
            t.line_start = t.cur;
            break;
        case Action.DQUOTE:
            t.skip_string_literal();
            break;
        case Action.SQUOTE:
            t.skip_char_literal();
            break;
        case Action.POUND:
            if (t.lex_feature_cmd(result)) return true;
            if (t.is_enabled()) return false;
            break;
        default:
            t.cur++;
            break;
        }
    }
    return false;
}

func void Tokenizer.skip_string_literal(Tokenizer* t) {
    t.cur++;

    while (1) {
        switch (*t.cur) {
        case 0:
            return;
        case '\r':
            t.cur++;
            return;
        case '\n':
            return;
        case '"':
            t.cur++;
            return;
        default:
            t.cur++;
            break;
        }
    }
}

func void Tokenizer.skip_char_literal(Tokenizer* t) {
    t.cur++;

    while (1) {
        switch (*t.cur) {
        case 0:
            return;
        case '\'':
            t.cur++;
            return;
        default:
            t.cur++;
            break;
        }
    }
}


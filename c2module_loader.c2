module c2module_loader;

import ast local;
import ast_context;
import string_pool;

import string;

type CType struct {
    const char* name;
    BuiltinTypeKind kind;
    //QualType qt;
}

CType[] ctypes = {
    { "c_char",      BuiltinTypeKind.Int8 },
    { "c_uchar",     BuiltinTypeKind.UInt8 },
    { "c_short",     BuiltinTypeKind.Int16 },
    { "c_ushort",    BuiltinTypeKind.UInt16 },
    { "c_int",       BuiltinTypeKind.Int32 },
    { "c_uint",      BuiltinTypeKind.UInt32 },
    // TODO dynamic 32-bit target types
    { "c_long",      BuiltinTypeKind.Int64 },
    { "c_ulong",     BuiltinTypeKind.UInt64 },
    { "c_size",      BuiltinTypeKind.UInt64 },
    { "c_ssize",     BuiltinTypeKind.Int64 },
    { "c_longlong",  BuiltinTypeKind.Int64 },
    { "c_ulonglong", BuiltinTypeKind.UInt64 },
    { "c_float",     BuiltinTypeKind.Float32 },
    { "c_double",    BuiltinTypeKind.Float64 },
}

public func Module* load(ast_context.Context* context, string_pool.Pool* pool) {
    // TODO if 32 bit change some kinds

    const char* name = pool.add("c2", 2, true);
    Module* m = Module.create(name);
    m.setUsed();

    // TODO AST name must be in pool?
    // Note: add AST, since Decls have AST*
    AST* a = m.add("<generated>");

    for (u32 i=0; i<elemsof(ctypes); i++) {
        const char* type_name = ctypes[i].name;
        const char* type_name2 = pool.add(type_name, string.strlen(type_name), true);
        // TODO use table inside ast.utils
        QualType qt;
        switch (ctypes[i].kind) {
        case BuiltinTypeKind.Int8:
            qt = g_i8;
            break;
        case BuiltinTypeKind.Int16:
            qt = g_i16;
            break;
        case BuiltinTypeKind.Int32:
            qt = g_i32;
            break;
        case BuiltinTypeKind.Int64:
            qt = g_i64;
            break;
        case BuiltinTypeKind.UInt8:
            qt = g_u8;
            break;
        case BuiltinTypeKind.UInt16:
            qt = g_u16;
            break;
        case BuiltinTypeKind.UInt32:
            qt = g_u32;
            break;
        case BuiltinTypeKind.UInt64:
            qt = g_u64;
            break;
        case BuiltinTypeKind.Float32:
            qt = g_f32;
            break;
        case BuiltinTypeKind.Float64:
            qt = g_f64;
            break;
        default:
            break;
        }
        AliasTypeDecl* t = AliasTypeDecl.create(context, type_name2, 0, true, a, qt);
        Decl* d = t.asDecl();
        d.setChecked();
        a.addTypeDecl(d);
        m.addSymbol(ast.name2idx(type_name2), d);
    }

    // TODO add other symbols
    // i8 min_i8
    // i8 max_i8
    // u8 min_u8
    // u8 max_u8
    // i16 min_i16
    // i16 max_i16
    // u16 min_u16
    // u16 max_u16
    // i32 min_i32
    // i32 max_i32
    // u32 min_u32
    // u32 max_u32
    // i64 min_i64
    // i64 max_i64
    // u64 min_u64
    // u64 max_u64
    // i32/i64 min_isize
    // u32/u64 max_usize
    // u32/u64 mun_usize
    // i32/i64 max_isize

    return m;
}


/* Copyright 2022-2023 Bas van den Berg
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

module build_target;

import library_list;
import src_loc local;
import string_list;
import string_pool;
import warning_flags;

import stdlib;
import string;

public type Kind enum u8 {
    Image,
    Executable,
    StaticLibrary,
    DynamicLibrary,
}

public type Plugin struct {
    u32 name; // into pool
    u32 options; // into pool
    SrcLoc loc;
}

public type PluginList struct {
    Plugin* plugins;
    u32 count;
    u32 capacity;
}

fn void PluginList.init(PluginList* l) {
    string.memset(l, 0, sizeof(PluginList));
}

public fn u32 PluginList.size(const PluginList* l) { return l.count; }

public fn const Plugin* PluginList.get(const PluginList* l, u32 idx) {
    return &l.plugins[idx];
}

public fn void PluginList.add(PluginList* l, u32 name, u32 options, SrcLoc loc) {
    if (l.count == l.capacity) {
        l.capacity = l.capacity ? l.capacity * 2 : 4;
        Plugin* plugins2 = stdlib.malloc(l.capacity * sizeof(Plugin));
        if (l.count) {
            string.memcpy(plugins2, l.plugins, l.count * sizeof(Plugin));
            stdlib.free(l.plugins);
        }
        l.plugins = plugins2;
    }
    Plugin* p = &l.plugins[l.count];
    p.name = name;
    p.options = options;
    p.loc = loc;
    l.count++;
}

public type File struct {
    u32 name;       // index into auxPool
    SrcLoc loc;
}

public type Target struct {
    u32 name_idx;       // into aux pool
    SrcLoc loc;
    warning_flags.Flags warnings;
    Kind kind;

    bool disable_asserts;
    bool no_libc;
    // cgen flags
    bool backend_c;
    bool cgen_no_build;
    bool cgen_fast;
    bool backend_llvm;
    bool backend_qbe;

    string_list.List features;
    library_list.List libs;
    string_list.List exports;
    PluginList plugins;

    File* files;
    u32 num_files;
    u32 max_files;

    File* asm_files;
    u32 asm_file_count;
    u32 asm_file_max;
} @(opaque)

public fn Target* create(u32 name_idx, SrcLoc loc, Kind kind, string_pool.Pool* pool) {
    Target* t = stdlib.calloc(1, sizeof(Target));
    t.name_idx = name_idx;
    t.loc = loc;
    t.kind = kind;
    t.max_files = 8;
    t.features.init(pool);
    t.libs.init();
    t.exports.init(pool);
    t.files = stdlib.malloc(t.max_files * sizeof(File));
    t.asm_file_max = 4;
    t.asm_files = stdlib.malloc(t.asm_file_max * sizeof(File));
    return t;
}

public fn void Target.free(Target* t) {
    t.exports.free();
    t.libs.free();
    t.features.free();
    stdlib.free(t.files);
    stdlib.free(t.asm_files);
    stdlib.free(t);
}

public fn bool Target.hasBackend(const Target* t) {
    return t.backend_c || t.backend_llvm || t.backend_qbe;
}

public fn u32 Target.getNameIdx(const Target* t) { return t.name_idx; }

public fn u32 Target.numFiles(const Target* t) { return t.num_files; }

public fn u32 Target.numAsmFiles(const Target* t) { return t.asm_file_count; }

public fn const string_list.List* Target.getFeatures(const Target* t) { return &t.features; }

public fn void Target.addFeature(Target* t, u32 feature) {
    t.features.add(feature);
}

public fn void Target.addPlugin(Target* t, u32 name, u32 options, SrcLoc loc) {
    t.plugins.add(name, options, loc);
}

public fn const PluginList* Target.getPlugins(const Target* t) { return &t.plugins; }


public fn void Target.disableAsserts(Target* t) { t.disable_asserts = true; }

public fn bool Target.hasAsserts(const Target* t) { return !t.disable_asserts; }

public fn void Target.visitLibs(const Target* t, library_list.Visitor visitor, void* arg) {
    t.libs.visit(visitor, arg);
}

public fn void Target.addLib(Target* t, u32 lib, bool is_static) {
    t.libs.add(lib, is_static);
}

public fn const warning_flags.Flags* Target.getWarnings(const Target* t) {
    return &t.warnings;
}

public fn warning_flags.Flags* Target.getWarnings2(Target* t) {
    return &t.warnings;
}

public fn void Target.addExport(Target* t, u32 export) {
    t.exports.add(export);
}

public fn const string_list.List* Target.getExports(const Target* t) { return &t.exports; }

public fn Kind Target.getKind(const Target* t) { return t.kind; }

public fn bool Target.needsMain(const Target* t) {
    switch (t.kind) {
    case Image: fallthrough;
    case Executable:
        return true;
    case StaticLibrary: fallthrough;
    case DynamicLibrary:
        break;
    }
    return false;
}

public fn bool Target.addFile(Target* t, u32 filename, SrcLoc loc) {
    // check for duplicates
    for (u32 i=0; i<t.num_files; i++) {
        if (t.files[i].name == filename) return false;
    }

    if (t.num_files == t.max_files) {
        t.max_files *= 2;
        File* files2 = stdlib.malloc(t.max_files * sizeof(File));
        string.memcpy(files2, t.files, t.num_files * sizeof(File));
        stdlib.free(t.files);
        t.files = files2;
    }

    t.files[t.num_files].name = filename;
    t.files[t.num_files].loc = loc;
    t.num_files++;
    return true;
}

public fn const File* Target.getFile(const Target* t, u32 idx) {
    return &t.files[idx];
}

public fn bool Target.addAsmFile(Target* t, u32 filename, SrcLoc loc) {
    // check for duplicates
    for (u32 i=0; i<t.asm_file_count; i++) {
        if (t.asm_files[i].name == filename) return false;
    }

    if (t.asm_file_count == t.asm_file_max) {
        t.asm_file_max *= 2;
        File* files2 = stdlib.malloc(t.asm_file_max * sizeof(File));
        string.memcpy(files2, t.files, t.asm_file_count * sizeof(File));
        stdlib.free(t.asm_files);
        t.asm_files = files2;
    }

    t.asm_files[t.asm_file_count].name = filename;
    t.asm_files[t.asm_file_count].loc = loc;
    t.asm_file_count++;
    return true;
}

public fn const File* Target.getAsmFile(const Target* t, u32 idx) {
    return &t.asm_files[idx];
}

public fn void Target.setNoLibC(Target* t) {
    t.no_libc = true;
}

public fn bool Target.getNoLibC(const Target* t) {
    return t.no_libc;
}

public fn void Target.setBackendC(Target* t) {
    t.backend_c = true;
}

public fn bool Target.getBackendC(const Target* t) {
    return t.backend_c;
}

public fn void Target.setBackendLLVM(Target* t) {
    t.backend_llvm = true;
}

public fn bool Target.getBackendLLVM(const Target* t) {
    return t.backend_llvm;
}

public fn void Target.setBackendQBE(Target* t) {
    t.backend_qbe = true;
}

public fn bool Target.getBackendQBE(const Target* t) {
    return t.backend_qbe;
}

public fn void Target.setCGenNoBuild(Target* t) {
    t.cgen_no_build = true;
}

public fn bool Target.getCGenNoBuild(const Target* t) {
    return t.cgen_no_build;
}

public fn void Target.setCGenFastBuild(Target* t) {
    t.cgen_fast = true;
}

public fn bool Target.getCGenFastBuild(const Target* t) {
    return t.cgen_fast;
}


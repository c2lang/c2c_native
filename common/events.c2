module events;

import stdlib local;
import unistd local;
import sys_time;
import stdio local;
import string local;
import libc_poll local;
import libc_fcntl local;

public func u64 current_time() {
    sys_time.Timeval tv;
    sys_time.gettimeofday(&tv, nil);
    u64 now64 = cast<u64>(tv.tv_sec);
    now64 *= 1000000;
    now64 += tv.tv_usec;
    return now64;
}

const u32 MaxEvents = 64;

type CmdKind enum u8 {
   LoopBreak = 0,
   Callback,
   TimerAdd,
   TimerDel,
}

type Cmd struct {
    CmdKind kind;
    union {
        struct callback {
            Function fn;
            void* arg1;
            void* arg2;
        }
        struct timer {
            Timer* timer;
            u64 delay_us;
        }
    }
}

public type Base struct {
    u32 running_loop;
    u32 loop_break;
    u8 return_value;
    Event* event_head;
    Event* event_tail;
    Timer* timers;
    Pollfd[MaxEvents] fds;
    Event*[MaxEvents] events;
    Event selfpipe;
    i32 read_pipe;
    i32 write_pipe;
    u32 numfds;
    bool eventsDirty;
} @(opaque)

public func Base* Base.create() {
    i32[2] pipefd;
    if (pipe2(pipefd, O_CLOEXEC | O_NONBLOCK) != 0) {
        return nil;
    }

    Base* base = stdlib.calloc(1, sizeof(Base));
    base.read_pipe = pipefd[0];
    base.write_pipe = pipefd[1];
    base.selfpipe.assign(base, base.read_pipe, READ, handlePipe, base);
    base.selfpipe.add();
    base.updateEvents();
    return base;
}

public func void Base.destroy(Base* base) {
    stdlib.free(base);
}

public func u8 Base.mainloop(Base* base) {
    base.return_value = 0;
    base.running_loop = 1;
    while (!base.loop_break) {
        base.internal_loop();
    }
    base.running_loop = 0;
    return base.return_value;
}

public func void Base.loopbreak(Base* base, u8 return_value) {
    base.return_value = return_value;
    base.loop_break = 1;
    if (base.running_loop) {
        Cmd cmd;
        memset(&cmd, 0, sizeof(Cmd));
        cmd.kind = CmdKind.LoopBreak;
        base.writeCmd(&cmd);
    }
}

func void Base.writeCmd(Base* base, Cmd* cmd) {
    i64 written = write(base.write_pipe, cmd, sizeof(Cmd));
    if (written != sizeof(Cmd)) {
        fprintf(stderr, "Error writing to pipe\n");
        exit(EXIT_FAILURE);
    }
}

func void handlePipe(i32 fd, void* arg, u16 flags) {
    Base* base = cast<Base*>(arg);
    Cmd cmd;
    i64 numread = read(base.read_pipe, &cmd, sizeof(Cmd));
    // TODO assert numread
    switch (cmd.kind) {
    case CmdKind.LoopBreak:
        break;
    case CmdKind.Callback:
        cmd.callback.fn(cmd.callback.arg1, cmd.callback.arg2);
        break;
    case CmdKind.TimerAdd:
        cmd.timer.timer.add(cmd.timer.delay_us);
        break;
    case CmdKind.TimerDel:
        cmd.timer.timer.del();
        break;
    }
}

func void Base.fire_timer(Base* base) {
    Timer* timer = base.timers;
    base.timers = timer.next;
    timer.next = nil;
    if (timer.flags &= PERSIST) {
        timer.timeout += timer.interval_usec;
        timer.add_internal();
    } else {
        timer.flags &= ~TIMER_PENDING;
    }
    timer.handler(timer.handler_arg);
}

func void Base.internal_loop(Base* base) {
    i32 timeout_ms = -1;    // infinity
    if (base.timers) {
        u64 now = current_time();
        Timer* timer = base.timers;
        if (now >= timer.timeout) {
            base.fire_timer();
            return;
        }
        u64 delay = (timer.timeout - now);
        // Round delay to ms
        delay += 500;
        delay /= 1000;
        timeout_ms = cast<i32>(delay);
        //timeout_ms = (delay + 500) / 1000;     // causes assert in c2c
    }
    if (base.eventsDirty) {
        Event* ev = base.event_head;
        u32 i = 0;
        while (ev) {
            base.fds[i].fd = ev.fd;
            // TEMP only read, should check events.READ/WRITE
            base.fds[i].events = POLLIN;
            base.events[i] = ev;
            ev = ev.next;
            i++;
        }
        base.numfds = i;
        base.eventsDirty = false;
    }
    i32 ret = poll(base.fds, base.numfds, timeout_ms);
    if (ret == 0) { // timeout
        if (base.timers) base.fire_timer();
    } else if (ret < 0) {   // error
        // interrupted (by signal, etc)
    } else { // events
        for (u32 i=0; i<base.numfds; i++) {
            Pollfd* current = &base.fds[i];
            if (current.revents) {
                // TODO check POLLIN/POLLOUT
                Event* ev = base.events[i];
                ev.handler(current.fd, ev.handler_arg, READ);
                return;
            }
        }
        printf("no event found.. error\n");
        stdlib.exit(stdlib.EXIT_FAILURE);
    }
}

func void Base.updateEvents(Base* base)
{
    base.eventsDirty = true;
}

public type Function func void (void* arg1, void* arg2);

public func void Base.schedule_safe(Base* base, Function fn, void* arg1, void* arg2) {
    Cmd cmd;
    memset(&cmd, 0, sizeof(Cmd));
    cmd.kind = CmdKind.Callback;
    cmd.callback.fn = fn;
    cmd.callback.arg1 = arg1;
    cmd.callback.arg2 = arg2;
    base.writeCmd(&cmd);
}



public type EventFunc func void(i32 fd, void* arg, u16 flags);

public type Event struct {
    i32 fd;
    Event* next;
    Base* base;
    EventFunc handler;
    void* handler_arg;
}

public const u8 READ  = 0x02;
public const u8 WRITE = 0x04;

public func void Event.assign(Event* ev, Base* base, i32 fd, u16 flags, EventFunc fn, void* arg)
{
    ev.fd = fd;
    ev.next = nil;
    ev.base = base;
    ev.handler = fn;
    ev.handler_arg = arg;
}

public func void Event.add(Event* ev)
{
    Base* base = ev.base;
    if (base.event_tail) base.event_tail.next = ev;
    else base.event_head = ev;
    base.event_tail = ev;

    base.updateEvents();
}

public func void Event.del(Event* ev)
{
    Base* base = ev.base;

    bool wasActive = false;
    if (base.event_head == ev) {
        // is first event
        base.event_head = ev.next;
        if (base.event_head == nil) base.event_tail = nil;
        wasActive = true;
    } else {
        Event* cur = base.event_head;
        while (cur) {
            if (cur.next == ev) {
                cur.next = ev.next;
                if (base.event_tail == ev) base.event_tail = cur;
                wasActive = true;
                break;
            }
            cur = cur.next;
        }
    }
    if (wasActive) {
        ev.next = nil;
        base.updateEvents();
    }
}


public type TimerHandler func void(void* arg);

// struct fields for internal use only. Users should consider this struct opaque
public type Timer struct {
    Timer* next;
    u64 timeout;
    Base* base;
    TimerHandler handler;
    void* handler_arg;
    u64 interval_usec;
    u32 flags;
}

public const u32 PERSIST = 0x1;
const u32 TIMER_PENDING  = 0x2;

public func void Timer.assign(Timer* timer, Base* base, u32 flags, TimerHandler fn, void* arg)
{
    timer.next = nil;
    timer.timeout = 0;
    timer.base = base;
    timer.handler = fn;
    timer.handler_arg = arg;
    timer.interval_usec = 0;
    timer.flags = flags;
}

func void Timer.add_internal(Timer* timer)
{
    Base* base = timer.base;
    u64 timeout = timer.timeout;
    if (base.timers) {
        if (timeout < base.timers.timeout) {
            // add before first
            timer.next = base.timers;
            base.timers = timer;
        } else {
            // add sorted
            Timer* cur = base.timers;
            while (cur.next) {
                if (cur.next.timeout > timeout) break;
                cur = cur.next;
            }
            timer.next = cur.next;
            cur.next = timer;
        }
    } else {
        // add as first
        base.timers = timer;
    }
}

public func void Timer.add(Timer* timer, u64 delay_usec)
{
    u64 now = current_time();
    timer.interval_usec = delay_usec;
    timer.timeout = now + delay_usec;
    timer.flags |= TIMER_PENDING;
    timer.next = nil;
    timer.add_internal();
}

public func bool Timer.isActive(const Timer* timer)
{
    return (timer.flags & TIMER_PENDING) != 0;
}

public func void Timer.del(Timer* timer)
{
    if (!timer.isActive()) return;
    Base* base = timer.base;

    if (timer == base.timers) {
        base.timers = timer.next;
    } else {
        Timer* cur = base.timers;
        while (cur.next) {
            if (cur.next == timer) {
                cur.next = timer.next;
                break;
            }
            cur = cur.next;
        }
    }
    timer.next = nil;
    timer.flags &= ~TIMER_PENDING;

}

public func void Timer.add_sync(Timer* timer, u64 delay_us)
{
    Cmd cmd;
    memset(&cmd, 0, sizeof(Cmd));
    cmd.kind = CmdKind.TimerAdd;
    cmd.timer.timer = timer;
    cmd.timer.delay_us = delay_us;
    timer.base.writeCmd(&cmd);
}

public func void Timer.del_sync(Timer* timer)
{
    Cmd cmd;
    memset(&cmd, 0, sizeof(Cmd));
    cmd.kind = CmdKind.TimerDel;
    cmd.timer.timer = timer;
    timer.base.writeCmd(&cmd);
}


/* Copyright 2022 Bas van den Berg
*/

module source_mgr;

import color;
import file_utils;
import string_pool;
import utils;

import stdio;
import stdlib;
import string;

const u32 InitialMaxFiles = 8;

type File struct {
    u32 filename;
    u32 offset;
    file_utils.Reader file;
    bool needed; // they are closed by user, but may still be open

    // to find line-number
    u32 last_offset;
    Location last_loc;
    // IDEA: put in some list: open-list, open-but-unneeded list or closed-list
    /*
        Initial: create, open, put in open
        not-needed -> open -> open-but-unneeded
        really closing: ->closed_list (tail)
        when needed an fd, close first from open-but-unneeded list
    */
}

func void File.close(File* f) {
    f.file.close();
}

public type SourceMgr struct {
    const string_pool.Pool* pool;

    File* files;
    u32 num_files;
    u32 max_files;
    u32 num_open;   // actually open
    u32 max_open;

    // for report
    u32 other_count;
    u32 other_size;
    u32 sources_count;
    u32 sources_size;
} @(opaque)

public func SourceMgr* create(const string_pool.Pool* pool, u32 max_open) {
    SourceMgr* sm = stdlib.calloc(1, sizeof(SourceMgr));
    sm.pool = pool;
    sm.max_files = InitialMaxFiles;
    sm.files = stdlib.malloc(sizeof(File) * sm.max_files);
    sm.max_open = max_open;
    return sm;
}

public func void SourceMgr.free(SourceMgr* sm) {
    for (u32 i=0; i<sm.num_files; i++) {
        sm.files[i].close();
    }
    stdlib.free(sm.files);
    stdlib.free(sm);
}

// Note closes all files above the handle (excluding the handle)
public func void SourceMgr.clear(SourceMgr* sm, u32 handle) {
    handle++;
    for (u32 i=handle; i<sm.num_files; i++) {
        File* f = &sm.files[i];
        if (f.needed) {
            stdio.printf("WARN %s still not closed\n", sm.pool.idx2str(f.filename));
        }
        f.file.close();
    }
    sm.num_files = handle;
    sm.num_open = 0; // recalculate below
    sm.other_count = handle;   // 'assumes' handle is only the recipe
    sm.other_size = 0;  // recalculate below
    sm.sources_count = 0;
    sm.sources_size = 0;
    for (u32 i=0; i<sm.num_files; i++) {
        const File* f = &sm.files[i];
        if (f.file.isOpen()) sm.num_open++;
        sm.other_size += f.file.size;
    }
}

func file_utils.Reader SourceMgr.openInternal(SourceMgr* sm, const char* filename, utils.SrcLoc loc) {
    file_utils.Reader file;
    if (file.open(filename)) {
        sm.num_open++;
    } else {
        char[256] error_msg;
        if (file.errno == file_utils.Err_not_a_file) {
            stdio.sprintf(error_msg, "cannot open %s: %s\n", filename, "not a regular file");
        } else {
            stdio.sprintf(error_msg, "cannot open %s: %s\n", filename, string.strerror(file.errno));
        }
        if (loc) {
            stdio.fprintf(stdio.stderr, "%s: %serror:%s %s\n", sm.loc2str(loc), color.Red, color.Normal, error_msg);
        } else {
            stdio.fprintf(stdio.stderr, "%serror%s: %s\n", color.Red, color.Normal, error_msg);
        }
    }
    return file;
}

// Note: return true if a file could be closed
func bool SourceMgr.close_oldest(SourceMgr* sm) {
    for (u32 i=0; i<sm.num_files; i++) {
        File* f = &sm.files[i];
        if (f.file.isOpen() && !f.needed) {
            f.file.close();
            sm.num_open--;
            return true;
        }
    }
    return false;
}

// Note: filename must be allocated in pool passed to SourceMgr
public func i32 SourceMgr.open(SourceMgr* sm, u32 filename, utils.SrcLoc loc, bool is_source) {
    if (sm.num_open == sm.max_open) {
        if (!sm.close_oldest()) {
            // TODO use diags (color)
            stdio.fprintf(stdio.stderr, "%serror%s: too many files open\n", color.Red, color.Normal);
            return -1;
        }
    }

    file_utils.Reader file = sm.openInternal(sm.pool.idx2str(filename), loc);
    if (!file.isOpen()) return -1;

    if (sm.num_files == sm.max_files) {
        sm.max_files *= 2;
        File* files2 = stdlib.malloc(sizeof(File) * sm.max_files);
        string.memcpy(files2, sm.files, sm.num_files * sizeof(File));
        stdlib.free(sm.files);
        sm.files = files2;
    }

    i32 file_id = cast<i32>(sm.num_files);
    File* f = &sm.files[sm.num_files];
    string.memset(f, 0, sizeof(File));

    u32 offset = 1;
    if (sm.num_files) offset = sm.files[sm.num_files-1].offset + sm.files[sm.num_files-1].file.size;
    f.filename = filename;
    f.offset = offset;
    f.file = file;
    f.needed = true;

    if (is_source) {
        sm.sources_count++;
        sm.sources_size += file.size;
    } else {
        sm.other_count++;
        sm.other_size += file.size;
    }

    sm.num_files++;
    //sm.dump();
    return file_id;
}

public func void SourceMgr.close(SourceMgr* sm, i32 file_id) {
    sm.files[file_id].needed = false;   // close lazily when needed
}

func void SourceMgr.checkOpen(SourceMgr* sm, i32 handle) {
    File* f = &sm.files[handle];
    if (f.file.isOpen()) return;

    if (sm.num_open == sm.max_open) {
        if (!sm.close_oldest()) {
            // TODO use diags (color)
            stdio.fprintf(stdio.stderr, "%serror%s: too many files open\n", color.Red, color.Normal);
            stdlib.exit(-1);
        }
    }

    f.file = sm.openInternal(sm.pool.idx2str(f.filename), 0);
    if (!f.file.isOpen()) stdlib.exit(-1);   // if we cannot re-open file, just exit
}

public func const char* SourceMgr.get_content(SourceMgr* sm, i32 handle) {
    sm.checkOpen(handle);
    return cast<char*>(sm.files[handle].file.data());
}

public func const char* SourceMgr.get_token_source(SourceMgr* sm, utils.SrcLoc loc) {
    File* f = sm.find_file(loc);
    if (!f) return "";

    u32 offset = loc - f.offset;
    // TODO check if open, otherwise re-open
    char* data = f.file.region;
    data += offset;

    return data;
}

public func u32 SourceMgr.get_offset(SourceMgr* sm, i32 handle) {
    return sm.files[handle].offset;
}

public func const char* SourceMgr.getFileName(SourceMgr* sm, i32 handle) {
    u32 idx = sm.files[handle].filename;
    return sm.pool.idx2str(idx);
}

#if 0
func void SourceMgr.dump(const SourceMgr* sm) {
    stdio.printf("SourceMgr  files %u  (open %u/%u)\n", sm.num_files, sm.num_open, sm.max_open);
    u32 total_size = 0;
    for (u32 i=0; i<sm.num_files; i++) {
        File* f = &sm.files[i];
        total_size += f.file.size;
        stdio.printf("  [%2u]  %7u  %7u %s\n", i, f.offset, f.file.size, f.filename);
    }
    stdio.printf("Total size %u\n", total_size);
}
#endif

public type Location struct {
    u32 line;
    u32 column;
    const char* filename;
    const char* line_start;
}

func File* SourceMgr.find_file(SourceMgr* sm, u32 loc) {
    for (u32 i=0; i<sm.num_files; i++) {
        File* f = &sm.files[i];
        if (loc >= f.offset && loc < f.offset + f.file.size) {
            sm.checkOpen(cast<i32>(i)); // map will be needed, so check if file is really open
            return f;
        }
    }
    return nil;
}

func void find_line_col(const char* data, u32 offset, Location* loc, u32 last_offset) {
    const char* line;
    u32 line_nr = loc.line;
    if (last_offset) {
        line = data + last_offset - loc.column + 1;
    } else {
        line = data;
    }
    for (u32 i=last_offset; i<offset; i++) {
        if (data[i] == '\n') {
            line_nr++;
            line = data + i + 1;
        }
    }
    loc.line = line_nr;
    loc.column = cast<u32>(&data[offset] - line) + 1;
    loc.line_start = line;
}

func Location SourceMgr.locate(SourceMgr* sm, u32 loc) {
    Location l = { 1, 1, nil }
    File* f = sm.find_file(loc);
    if (f) {
        l.filename = sm.pool.idx2str(f.filename);
        u32 offset = loc - f.offset;
        u32 last_offset = 0;
        if (f.last_offset != 0 && offset > f.last_offset) {
            l = f.last_loc;
            last_offset = f.last_offset;
        }
        find_line_col(cast<char*>(f.file.data()), offset, &l, last_offset);

        f.last_offset = offset;
        f.last_loc = l;
    }
    return l;
}

public func Location SourceMgr.getLocation(SourceMgr* sm, u32 src_loc) {
    Location loc = { 0 }
    if (src_loc == 0) {
        loc.filename = "-";
    } else {
        loc = sm.locate(src_loc);
    }
    return loc;
}

public func const char* SourceMgr.loc2str(SourceMgr* sm, u32 src_loc) {
    local char[256] tmp;
    if (src_loc == 0) {
        string.strcpy(tmp, "-");
    } else {
        Location loc = sm.locate(src_loc);
        stdio.sprintf(tmp, "%s:%u:%u", loc.filename, loc.line, loc.column);
    }
    return tmp;
}

public func void SourceMgr.report(const SourceMgr* sm) {
    stdio.printf("files: %u sources (%u bytes), %u other (%u bytes)\n",
        sm.sources_count, sm.sources_size, sm.other_count, sm.other_size);
}


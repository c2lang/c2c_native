/* Copyright 2022 Bas van den Berg
*/

module string_buffer;

import stdarg local;
import stdlib local;
import string local;

public type Buf struct {
    u32 capacity;
    u32 size_;
    char* data_;
}

static_assert(16, sizeof(Buf));

public func Buf* create(u32 capacity)
{
    Buf* buf = malloc(sizeof(Buf));
    buf.capacity = capacity;
    buf.size_ = 0;
    buf.data_ = malloc(capacity);
    return buf;
}

public func void Buf.free(Buf* buf)
{
    free(buf.data_);
    free(buf);
}

public func u32 Buf.size(const Buf* buf)
{
    return buf.size_;
}

public func const char* Buf.data(const Buf* buf)
{
    return buf.data_;
}

public func void Buf.clear(Buf* buf)
{
    buf.size_ = 0;
}

public func void Buf.add(Buf* buf, const char* text)
{
    u32 len = cast<u32>(strlen(text));
    buf.add2(text, len);
}

public func void Buf.add2(Buf* buf, const char* text, u32 len)
{
    if ((len + 1) >= (buf.capacity - buf.size_)) {
        u32 newcap = buf.capacity * 2;
        if ((len + 1) >= (newcap - buf.size_)) newcap += buf.size_ + len + 1;
        buf.capacity = newcap;
        char* data2 = malloc(newcap);
        memcpy(data2, buf.data_, buf.size_ + 1);
        free(buf.data_);
        buf.data_ = data2;
    }
    strcpy(&buf.data_[buf.size_], text);
    buf.size_ += len;
    buf.data_[buf.size_] = 0;
}

public func void Buf.print(Buf* buf, const char* format, ...) {
    // NOTE: no growing
    Va_list args;
    va_start(args, format);
    i32 len = vsprintf(&buf.data_[buf.size_], format, args);
    // TODO check size
    buf.size_ += len;
    va_end(args);
}

public func void Buf.indent(Buf* buf, u32 indent) {
    // NOTE: no growing/checking
    for (u32 i=0; i<indent; i++) {
        buf.data_[buf.size_++] = ' ';
    }
    buf.data_[buf.size_] = 0;
}


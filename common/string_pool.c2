/* Copyright 2022 Bas van den Berg
*/

module string_pool;

import string local;
import stdlib local;
// TEMP
import stdio local;

#if ContextDebug
u32 num_allocs;
#endif

type Block struct {
    u8* data;
    u32 size;
    Block* next;
}

func Block* Block.create(u32 blk_size) {
    Block* b = malloc(sizeof(Block));
    b.data = malloc(blk_size);
    b.size = 0;
    b.next = nil;
    return b;
}

func Block* Block.free(Block* b) {
    Block* next = b.next;
    free(b.data);
    free(b);
    return next;
}

func u32 Block.count(const Block* b) {
    u32 total = 0;
    const Block* blk = b;
    while (blk) {
        total ++;
        blk = blk.next;
    }
    return total;
}

func u32 Block.get_total(const Block* b) {
    u32 total = 0;
    const Block* blk = b;
    while (blk) {
        total += blk.size;
        blk = blk.next;
    }
    return total;
}

public type Pool struct {
    Block* blk_head;
    Block* blk_tail;
    u32 blk_size;
} @(opaque)

public func Pool* Pool.create(u32 blk_size) {
    Pool* c = calloc(1, sizeof(Pool));
    c.blk_size = blk_size;
    c.init();
    return c;
}

func void Pool.freeBlocks(Pool* c) {
    Block* blk = c.blk_head;
    while (blk) blk = blk.free();
}

public func void Pool.free(Pool* c) {
    c.freeBlocks();
    free(c);
}

func void Pool.init(Pool* c) {
    c.blk_head = Block.create(c.blk_size);
    c.blk_tail = c.blk_head;
}

public func void Pool.clear(Pool* c) {
    c.freeBlocks();
    c.init();
}

public func const char* Pool.alloc(Pool* c, const char* text, usize len, bool filter) @(inline) {
    // assert(c.str_blk_head);
    // assert(len < strblk_size)
    len++;  // add 0-terminator
#if ContextDebug
    num_allocs++;
#endif

    Block* last = c.blk_tail;
    if (last.size + len >= c.blk_size) {
        c.blk_tail = Block.create(c.blk_size);
        last.next = c.blk_tail;
        last = last.next;
    }

    char* buf = cast<char*>(last.data + last.size);
    last.size += len;

    len--;
    memcpy(buf, text, len);
    buf[len] = 0;
    return buf;
}

public func void Pool.dump(const Pool* c) {
    u32 num = c.blk_head.count();
    u32 total = c.blk_head.get_total();

    printf("Pool:\n");
    printf("    blk size %u, %u blocks, total %u/%u\n", c.blk_size, num, total, num * c.blk_size);
#if ContextDebug
    printf("    %u allocs (avg %u bytes)\n", num_allocs, total / num_allocs);
#endif
}

public func u32 Pool.get_total(const Pool* c) {
    return c.blk_head.get_total() + c.blk_head.get_total();
}


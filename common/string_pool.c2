/* Copyright 2022 Bas van den Berg
*/

module string_pool;

import string;
import stdlib;
import stdio;

/*
    Make Nodes resizable (start at 2000)
    Make data resizable?
        . can we put blocks underneath (block vector, each 16Kb)
*/

type Color enum u8 { Black, Red }
type Dir enum u8 { Left, Right }

type Node struct {
    u16[2] child;
    u32 word_idx;
    u16 parent;
    Color color;
}

public type Pool struct {
    u32 size; // rename dataSize
    u32 data_capacity;
    char* data;
    Node* nodes;
    u16 node_capacity;
    u16 node_count;
    Node* root;
#if StringPoolDebug
    u32 num_adds;
    u32 total_size;
#endif

} @(opaque)

public func Pool* Pool.create(u32 data_capacity, u16 node_capacity) {
    Pool* p = stdlib.calloc(1, sizeof(Pool));
    p.resize_data(data_capacity);
    p.node_capacity = node_capacity;
    p.nodes = stdlib.malloc(p.node_capacity * sizeof(Node));
    p.node_count = 1; // skip node 1
    return p;
}

public func void Pool.free(Pool* p) {
    stdlib.free(p.data);
    stdlib.free(p.nodes);
    stdlib.free(p);
}

func Node* Pool.getChild(Pool* pool, Node* x, Dir dir) @(inline) {
    // assert(x);
    u16 idx = x.child[dir];
    if (idx) return pool.nodes + idx;
    return nil;
}

func u16 Pool.toIndex(Pool* pool, Node* x) @(inline) {
    return cast<u16>(x - pool.nodes);
}

func void Pool.rotate(Pool* pool, Node* p, Dir dir) {
    Dir rdir = 1-dir;
    u16 g = p.parent;
    u16 s = p.child[rdir];
    u16 c = pool.nodes[s].child[dir];
    p.child[rdir] = c;
    u16 p_idx = pool.toIndex(p);
    if (c) pool.nodes[c].parent = p_idx;
    pool.nodes[s].child[dir] = p_idx;
    p.parent = s;
    pool.nodes[s].parent = g;
    if (g) {
        pool.nodes[g].child[p_idx == pool.nodes[g].child[Dir.Right] ? Dir.Right : Dir.Left] = s;
    } else {
        pool.root = &pool.nodes[s];
    }
}

func void Pool.insert(Pool* pool, Node* n, Node* p) {
    n.color = Color.Red;
    // Note left+right+ parent already set

    if (p == nil) {
        pool.root = n;
        return;
    }
    //p.child[dir] = n; . already done

    Node* g;
    Dir dir;
    Dir rdir;
    do {
        if (p.color == Color.Black) return;  // case I1 (P black);

        // From now on P is red
        if (p.parent == 0) goto Case_I4;

        g = pool.nodes + p.parent;
        dir = (p == (pool.nodes + g.child[Dir.Right])) ? Dir.Right : Dir.Left; // the side of parent G on which node P is located
        rdir = 1-dir;
        u16 u_idx = g.child[rdir]; // uncle
        if (u_idx == 0) goto Case_I56;
        Node* u = pool.nodes + u_idx;
        if (u.color == Color.Black) goto Case_I56;

        // Case_I2 (P+U red):
        p.color = Color.Black;
        u.color = Color.Black;
        g.color = Color.Red;
        n = g;  // new current node;
        // iterate 1 black level higher (= 2 tree levels)

        if (n.parent == 0) break;
        p = pool.nodes + n.parent;
    } while (1);

    // Case_I3: N is the root and red
    return;
Case_I4:
    p.color = Color.Black;
    return;
Case_I56:
    if (n == pool.getChild(p, rdir)) {
        // Case-I5 (P red && U black && N inner grandchild of G):
        pool.rotate(p, dir);
        n = p;   // new current node
        p = pool.nodes + g.child[dir]; // new parent of N
        //fall through to Case_I6
    }
    // Case_I6 (P red && U black && N outer grandchild of G):
    pool.rotate(g, rdir); // G may be the root
    p.color = Color.Black;
    g.color = Color.Red;
    return;
}

// NOTE: right is NOT 0-terminated!
// 0 left
// 1 right
// 2 =
func u32 compare(const char* left, const char* right, usize rlen) @(inline) {
    u32 i = 0;
    while (i < rlen) {
        char l = left[i];
        char r = right[i];
        char c = l - r;
        if (c < 0) return 1;
        if (c > 0) return 0;
        i++;
    }
    if (left[rlen] == 0) return 2;
    return 0;
}


public func const char* Pool.add(Pool* pool, const char* text, usize len, bool filter) {
    // NOTE: text is not 0-terminated!! len is strlen(text)
#if StringPoolDebug
    pool.num_adds++;
    pool.total_size += len;
#endif
    if (filter) {
        Node* parent;
        Node* n = pool.root;
        while (n) {
            const char* word = pool.data + n.word_idx;
            switch (compare(word, text, len)) {
            case 0: // left
                if (n.child[Dir.Left]) {
                    n = pool.nodes + n.child[Dir.Left];
                    continue;
                } else {
                    n.child[Dir.Left] = pool.node_count;
                    goto after_loop;
                }
                break;
            case 1: // right
                if (n.child[Dir.Right]) {
                    n = pool.nodes + n.child[Dir.Right];
                    continue;
                } else {
                    n.child[Dir.Right] = pool.node_count;
                    goto after_loop;
                }
                break;
            case 2: // ==
                return word;
            }
        }
after_loop:
        parent = n;
        u16 parent_idx = cast<u16>(n ? cast<u16>(n - pool.nodes) : 0);
        // fill new node
        //assert(pool.node_count < pool.node_capacity);
        n = pool.nodes + pool.node_count;
        n.parent = parent_idx;
        pool.node_count++;
        n.word_idx = pool.size;
        n.child[Dir.Left] = 0;
        n.child[Dir.Right] = 0;

        pool.insert(n, parent);
    }

    if (pool.size + len + 1 > pool.data_capacity) {
        pool.resize_data(pool.data_capacity * 2);
    }
    char* dest = pool.data + pool.size;
    string.memcpy(dest, text, len);
    dest[len] = 0;
    pool.size += len + 1;
    //assert(pool.size < pool.data_capacity);
    return dest;
}

public func void Pool.resize_data(Pool* p, u32 capacity) {
    p.data_capacity = capacity;
    char* buf = stdlib.malloc(p.data_capacity);
    if (p.data) {
        string.memcpy(buf, p.data, p.size);
        stdlib.free(p.data);
    }
    p.data = buf;
}

public func void Pool.dump(const Pool* pool) {
    u32 index_used = pool.node_count * sizeof(Node);
    u32 index_size = pool.node_capacity * sizeof(Node);
    index_used = (index_used + 1023) / 1024;
    index_size = (index_size + 1023) / 1024;
#if StringPoolDebug
    stdio.printf("StringPool: %u/%u words, data %u/%u bytes, index %u/%u Kb\n", pool.node_count - 1, pool.num_adds, pool.size, pool.total_size, index_used, index_size);
#else
    stdio.printf("StringPool: %u words, data %u bytes, index %u/%u Kb\n", pool.node_count - 1, pool.size, index_used, index_size);
#endif
}


/* Copyright 2022-2023 Bas van den Berg
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

module c2recipe;

import build_target;
import color;
import ctype;
import source_mgr;
import src_loc local;
import string_list;
import string_pool;
import warning_flags;

import csetjmp local;
import stdarg local;
import stdio local;
import string;

type Kind enum u8 {
    // global
    Plugin,
    PluginOptions,
    Text,           // can also be a text number (eg '10')
    Executable,
    Lib,
    Image,
    File,
    End,
    Warnings,
    Backend,
    DisableAsserts,
    NoLibc,
    Config,
    Export,
    Use,
    AsmFile,
    Eof,
}

const char*[] kind_names = {
    "plugin",
    "[plugin_options]",
    "text",
    "executable",
    "lib",
    "image",
    "file",
    "end",
    "$warnings",
    "$backend",
    "$disable-asserts",
    "$nolibc",
    "$config",
    "$export",
    "$use",
    "$asm",
    "eof",
}

static_assert(elemsof(Kind), elemsof(kind_names));

type Token struct {
    Kind kind;
    SrcLoc loc;
    bool more;
    u32 value;  // allocated in StringPool
}

fn void Token.init(Token* t) {
    string.memset(t, 0, sizeof(Token));
    t.more = true;
}

type Parser struct {
    Recipe* recipe;
    string_pool.Pool* pool;
    source_mgr.SourceMgr* sm;
    string_list.List global_configs;

    const char* input_start;
    const char* cur;
    SrcLoc loc_start;
    JmpBufTag jmpbuf;
    Token token;
    bool new_line;
    bool targets_started;
    build_target.Target* target;
}

fn bool Parser.parse(Recipe* recipe, string_pool.Pool* pool, source_mgr.SourceMgr* sm, i32 file_id) {
    const char* data = sm.get_content(file_id);
    Parser p = { }
    p.recipe = recipe;
    p.pool = pool;
    p.sm = sm;
    p.global_configs.init(pool);
    p.input_start = data;
    p.cur = data;
    p.loc_start = sm.get_offset(file_id);
    p.new_line = true;

    p.token.init();

#if 0
    while (p.token.more) {
        p.consumeToken();
    }
#else
    i32 res = setjmp(&p.jmpbuf);
    if (res == 0) {
        p.consumeToken();
        p.parseTop();
    }
#endif
    p.global_configs.free();
    return res == 0;
}

fn bool Parser.addGlobalFeature(Parser* p, u32 feature) {
    if (p.global_configs.contains_idx(feature)) return false;

    p.global_configs.add(feature);
    return true;
}

fn void Parser.error(Parser* p, const char* format, ...) @(noreturn, printf_format=2) {
    char[128] msg;
    Va_list args;
    va_start(args, format);
    vsnprintf(msg, sizeof(msg)-1, format, args);
    va_end(args);

    if (color.useColor()) {
        fprintf(stderr, "%s: %serror:%s %s\n", p.sm.loc2str(p.token.loc), color.Red, color.Normal, msg);
    } else {
        fprintf(stderr, "%s: error: %s\n", p.sm.loc2str(p.token.loc), msg);
    }
    longjmp(&p.jmpbuf, 1);
}

fn void Parser.consumeToken(Parser* p) {
    p.lex(&p.token);
#if 0
    printf("  %s   %s", p.sm.loc2str(p.token.loc), kind_names[p.token.kind]);
    switch (p.token.kind) {
    case PluginOptions: fallthrough;
    case Text: fallthrough;
    case File:
        printf("  %s", p.pool.idx2str(p.token.value));
        break;
    default:
        break;
    }
    printf("\n");
#endif
}

fn void Parser.expect(Parser* p, Kind kind, const char* msg) {
    if (p.token.kind != kind) p.error("%s", msg);
}

fn bool Parser.is(const Parser* p, Kind kind) {
    return p.token.kind == kind;
}

fn void Parser.lex(Parser* p, Token* result) {
    while (1) {
        //printf("%c\n", *p.cur);
        switch (*p.cur) {
        case 0:
            p.cur--;
            result.loc = p.loc_start + cast<SrcLoc>(p.cur - p.input_start);
            result.kind = Kind.Eof;
            result.more = false;
            return;
        case ' ':   fallthrough;
        case '\t':  fallthrough;
        case '\r':
            p.cur++;
            break;
        case '\n':
            p.cur++;
            p.new_line = true;
            break;
        case '#':
            p.skip_comments();
            break;
        case '[':
            p.lex_plugin_options(result);
            p.new_line = false;
            return;
        case '$':
            p.lex_option(result);
            p.new_line = false;
            return;
        case '/':
            // filename
            // lex until whitespace
            const char* start = p.cur;
            while (*p.cur && !ctype.isspace(*p.cur)) p.cur++;
            result.kind = p.new_line ? Kind.File : Kind.Text;
            if (*p.cur == 0) result.kind = Kind.Eof;
            p.new_line = false;
            u32 len = cast<u32>(p.cur - start);
            result.value = p.pool.add(start, len, true);
            return;
        default:
            if (ctype.isalnum(*p.cur)) {
                result.loc = p.loc_start + cast<SrcLoc>(p.cur - p.input_start);
                if (equals(p.cur, "plugin ", 7)) {
                    result.kind = Kind.Plugin;
                    p.cur += 7;
                    p.new_line = false;
                    return;
                }
                if (equals(p.cur, "executable ", 11)) {
                    result.kind = Kind.Executable;
                    p.cur += 11;
                    p.new_line = false;
                    return;
                }
                if (equals(p.cur, "lib ", 4)) {
                    result.kind = Kind.Lib;
                    p.cur += 4;
                    p.new_line = false;
                    return;
                }
                if (equals(p.cur, "end", 3)) {
                    result.kind = Kind.End;
                    p.cur += 3;
                    p.new_line = false;
                    return;
                }
                if (equals(p.cur, "image ", 6)) {
                    result.kind = Kind.Image;
                    p.cur += 6;
                    p.new_line = false;
                    return;
                }
                if (equals(p.cur, "config ", 7)) {
                    result.kind = Kind.Config;
                    p.cur += 7;
                    p.new_line = false;
                    return;
                }

                // lex until whitespace
                const char* start = p.cur;
                while (*p.cur && !ctype.isspace(*p.cur)) p.cur++;
                // TODO check EOF
                result.kind = p.new_line ? Kind.File : Kind.Text;
                p.new_line = false;
                u32 len = cast<u32>(p.cur - start);
                result.value = p.pool.add(start, len, true);
                return;
            }
            result.loc = p.loc_start + cast<SrcLoc>(p.cur - p.input_start);
            p.error("unexpected input '%c'", *p.cur);
            return;
        }
    }
}


fn void Parser.lex_plugin_options(Parser* p, Token* result) {
    p.cur++;
    const char* start = p.cur;
    while (1) {
        if (*p.cur == 0) {
            // TODO give error
            return;
        }
        if (*p.cur == ']') {
            u32 len = cast<u32>(p.cur - start);
            result.loc = p.loc_start + cast<SrcLoc>(start - p.input_start);
            result.kind = Kind.PluginOptions;
            result.value = p.pool.add(start, len, true);
            p.cur++;
            return;
        }
        p.cur++;
    }
}

fn void Parser.lex_option(Parser* p, Token* result) {
    p.cur++;

    result.loc = p.loc_start + cast<SrcLoc>(p.cur - p.input_start);

    const char* end = p.cur;
    while (*end && !ctype.isspace(*end)) end++;

    u32 len = cast<u32>(end - p.cur);
    if (len >= 20) p.error("unknown option");

    char[24] option;
    string.memcpy(option, p.cur, len);
    option[len] = 0;

    sswitch (option) {
    case "warnings":
        result.kind = Kind.Warnings;
    case  "backend":
        result.kind = Kind.Backend;
    case "disable-asserts":
        result.kind = Kind.DisableAsserts;
    case "nolibc":
        result.kind = Kind.NoLibc;
    case "config":
        result.kind = Kind.Config;
    case "export":
        result.kind = Kind.Export;
    case "plugin":
        result.kind = Kind.Plugin;
    case "use":
        result.kind = Kind.Use;
    case "asm":
        result.kind = Kind.AsmFile;
    default:
        p.error("unknown option '%s'", option);
    }
    p.cur += string.strlen(option);
}

fn void Parser.skip_comments(Parser* p) {
    while (*p.cur) {
        if (*p.cur == '\n') return;
        p.cur++;
    }
}

fn void Parser.parseTop(Parser* p) {
    while (1) {
        switch (p.token.kind) {
        case Plugin:
            p.parsePlugin(true);
            break;
        case PluginOptions:
            break;
        case Text:
            break;
        case Executable:
            p.parseExecutable();
            break;
        case Lib:
            p.parseLibrary();
            break;
        case Image:
            p.parseImage();
            break;
        case File:
            p.error("syntax error");
            break;
        case End:
            break;
        case Warnings:     fallthrough;
        case Backend:      fallthrough;
        case DisableAsserts: fallthrough;
        case NoLibc:
            p.error("must be inside target");
            break;
        case Config:
            if (p.targets_started) p.error("global configs must come before targets");
            p.consumeToken();
            p.expect(Kind.Text, "expect config");
            if (!p.addGlobalFeature(p.token.value)) {
                p.error("duplicate config '%s'", p.pool.idx2str(p.token.value));
            }
            p.consumeToken();
            break;
        case Export:       fallthrough;
        case Use:          fallthrough;
        case AsmFile:
            p.error("must be inside target");
            break;
        case Eof:
            return;
        }
    }
}

fn void Parser.parsePlugin(Parser* p, bool is_global) {
    p.consumeToken();
    p.expect(Kind.Text, "expect plugin name");
    u32 name = p.token.value;
    SrcLoc loc = p.token.loc;
    p.consumeToken();

    u32 options = 0;
    if (p.token.kind == Kind.PluginOptions) {
        options = p.token.value;
        p.consumeToken();
    }
    if (is_global) {
        p.recipe.addPlugin(name, options, loc);
    } else {
        p.target.addPlugin(name, options, loc);
    }
}

fn void Parser.parseWarnings(Parser* p) {
    p.consumeToken();
    p.expect(Kind.Text, "expect options");
    warning_flags.Flags* warnings = p.target.getWarnings2();
    // TODO no need to store in string pool
    while (p.is(Kind.Text)) {
        const char* option = p.pool.idx2str(p.token.value);
        if (string.strcmp(option, "no-unused") == 0) {
            warnings.no_unused = true;
            warnings.no_unused_variable = true;
            warnings.no_unused_function = true;
            warnings.no_unused_parameter = true;
            warnings.no_unused_type = true;
            warnings.no_unused_module = true;
            warnings.no_unused_import = true;
            warnings.no_unused_public = true;
            warnings.no_unused_label = true;
            warnings.no_unused_enum_constant = true;
        } else if (string.strcmp(option, "no-unused-variable") == 0) {
            warnings.no_unused_variable = true;
        } else if (string.strcmp(option, "no-unused-function") == 0) {
            warnings.no_unused_function = true;
        } else if (string.strcmp(option, "no-unused-parameter") == 0) {
            warnings.no_unused_parameter = true;
        } else if (string.strcmp(option, "no-unused-type") == 0) {
            warnings.no_unused_type = true;
        } else if (string.strcmp(option, "no-unused-module") == 0) {
            warnings.no_unused_module = true;
        } else if (string.strcmp(option, "no-unused-import") == 0) {
            warnings.no_unused_import = true;
        } else if (string.strcmp(option, "no-unused-public") == 0) {
            warnings.no_unused_public = true;
        } else if (string.strcmp(option, "no-unused-label") == 0) {
            warnings.no_unused_label = true;
        } else if (string.strcmp(option, "no-unused-enum-constant") == 0) {
            warnings.no_unused_enum_constant = true;
        } else if (string.strcmp(option, "promote-to-error") == 0) {
            warnings.are_errors = true;
        } else {
            p.error("unknown warning '%s'", option);
        }
        p.consumeToken();
    }
}

fn void Parser.parseExecutable(Parser* p) {
    p.consumeToken();
    p.expect(Kind.Text, "expect target name");
    p.target = p.recipe.addTarget(p.token.value, p.token.loc, build_target.Kind.Executable);
    p.consumeToken();

    p.parseTarget();
}

fn void Parser.parseImage(Parser* p) {
    p.consumeToken();
    p.expect(Kind.Text, "expect image name");
    p.target = p.recipe.addTarget(p.token.value, p.token.loc, build_target.Kind.Image);
    p.consumeToken();

    p.parseTarget();
}

fn void Parser.parseLibrary(Parser* p) {
    p.consumeToken();
    p.expect(Kind.Text, "expect target name");
    u32 name = p.token.value;
    SrcLoc loc = p.token.loc;
    p.consumeToken();
    p.expect(Kind.Text, "expect lib type");
    u32 kind_name = p.token.value;
    build_target.Kind kind = build_target.Kind.StaticLibrary;
    sswitch (p.pool.idx2str(kind_name)) {
    case "static":
        kind = build_target.Kind.StaticLibrary;
    case "shared":
        kind = build_target.Kind.DynamicLibrary;
    default:
        p.error("invalid library type (allowed: static|shared)");
    }

    p.consumeToken();

    p.target = p.recipe.addTarget(name, loc, kind);
    p.parseTarget();
}

fn void Parser.parseTarget(Parser* p) {
    p.targets_started = true;
    bool files_started = false;

    // add global configs
    for (u32 i=0; i<p.global_configs.length(); i++) {
        p.target.addFeature(p.global_configs.get_idx(i));
    }

    while (1) {
        switch (p.token.kind) {
        case Plugin:
            p.parsePlugin(false);
            break;
        case PluginOptions: fallthrough;
        case Text: fallthrough;
        case Executable: fallthrough;
        case Lib: fallthrough;
        case Image:
            p.error("syntax error");
            break;
        case File:
            files_started = true;
            if (!p.target.addFile(p.token.value, p.token.loc)) {
                p.error("duplicate file '%s'", p.pool.idx2str(p.token.value));
            }
            p.consumeToken();
            break;
        case End:
            p.consumeToken();
            p.target = nil;
            return;
        case Warnings:
            if (files_started) p.error("$warnings must come before files");
            p.parseWarnings();
            break;
        case Backend:
            if (files_started) p.error("$backend must come before files");
            p.parseBackend();
            break;
        case DisableAsserts:
            if (files_started) p.error("$disable-asserts must come before files");
            p.consumeToken();
            p.target.disableAsserts();
            break;
        case NoLibc:
            if (files_started) p.error("$nolibc must come before files");
            p.consumeToken();
            p.target.setNoLibC();
            break;
        case Config:
            if (files_started) p.error("$config must come before files");
            p.consumeToken();
            p.expect(Kind.Text, "expect config");
            p.target.addFeature(p.token.value);
            // TODO add value
            p.consumeToken();
            break;
        case Export:
            if (files_started) p.error("$export must come before files");
            p.consumeToken();
            p.expect(Kind.Text, "expect export");

            while (p.token.kind == Kind.Text) {
                p.target.addExport(p.token.value);
                p.consumeToken();
            }
            break;
        case Use:
            if (files_started) p.error("$use must come before files");
            p.consumeToken();

            p.expect(Kind.Text, "expect library name");
            // TODO check duplicates
            u32 libname = p.token.value;
            p.consumeToken();

            p.expect(Kind.Text, "expect library type");
            bool is_static = false;
            const char* libtype = p.pool.idx2str(p.token.value);
            sswitch (libtype) {
            case "static":
                is_static = true;
            case "dynamic":
            default:
                p.error("unknown library kind '%s'", libtype);
            }
            p.target.addLib(libname, is_static);
            p.consumeToken();

            while (p.token.kind == Kind.Text) p.consumeToken();
            break;
        case AsmFile:
            if (files_started) p.error("$asm must come before files");
            p.consumeToken();

            p.expect(Kind.Text, "expect filename");
            if (!p.target.addAsmFile(p.token.value, p.token.loc)) {
                p.error("duplicate asm file '%s'", p.pool.idx2str(p.token.value));
            }
            p.consumeToken();
            break;
        case Eof:
            p.error("un-terminated target");
            return;
        }
    }
}

fn void Parser.parseBackend(Parser* p) {
    // syntax: $backend [c/qbe/llvm] <options>
    p.consumeToken();
    // - check valid (c/qbe/llvm)
    p.expect(Kind.Text, "expect backend type");
    const char* backend_kind = p.pool.idx2str(p.token.value);
    p.consumeToken();

    sswitch (backend_kind) {
    // - check duplicates
    case "c":
        if (p.target.getCGenGenerate()) p.error("duplicate c backend");
        p.target.setCGenGenerate();
        p.parseCGenOptions();
    case "llvm":
        // TODO check duplicate
        while (p.token.kind == Kind.Text) p.consumeToken();
        // TODO
    case "qbe":
        // TODO check duplicate
        while (p.token.kind == Kind.Text) p.consumeToken();
        // TODO
    default:
        p.error("unknown backend type (supported: c,llvm,qbe)");
    }
}

fn void Parser.parseCGenOptions(Parser* p) {
    while (p.token.kind == Kind.Text) {
        const char* option = p.pool.idx2str(p.token.value);
        sswitch (option) {
        case "no-build":
            p.target.setCGenNoBuild();
        case "fast":
            p.target.setCGenFastBuild();
        case "skip":   // legacy, just ignore
        default:
            p.error("invalid c backend option '%s'", option);
        }
        p.consumeToken();
    }
}


fn bool equals(const char* str, const char* expect, u32 len) {
    for (u32 i=0; i<len; i++) {
        if (str[i] != expect[i]) return false;
    }
    return true;
}


module diagnostics;

import color;
import source_mgr;
import string_buffer;
import utils;

import stdarg local;
import stdio local;
import stdlib;

public type Diags struct {
    source_mgr.SourceMgr* sm;
    bool use_color;
    u32 num_errors;
    u32 num_warnings;
} @(opaque)

public func Diags* create(source_mgr.SourceMgr* sm, bool use_color) {
    Diags* diags = stdlib.calloc(1, sizeof(Diags));
    diags.sm = sm;
    diags.use_color = use_color;
    return diags;
}

public func void Diags.free(Diags* diags) {
    stdlib.free(diags);
}

type Category enum u8 {
    Info,
    Warning,
    Error,
}

const char*[] category_names = {
    "info",
    "warning",
    "error",
}

const char*[] category_colors = {
    color.Grey,
    color.Yellow,
    color.Bred,
}

// return true if error, false if warning
public func bool Diags.report(Diags* diags, utils.SrcLoc src_loc, const char* format, ...) {
    Category category = Category.Error;

    if (category == Category.Error) {
        diags.num_errors++;
    } else {
        diags.num_warnings++;
    }

    Va_list args;
    va_start(args, format);
    diags.internal(category, src_loc, format, args);
    va_end(args);
    return category == Category.Error;
}

public func void Diags.note(Diags* diags, utils.SrcLoc src_loc, const char* format, ...) {
    Va_list args;
    va_start(args, format);
    diags.internal(Category.Info, src_loc, format, args);
    va_end(args);
}

func void Diags.internal(Diags* diags, Category category, utils.SrcLoc src_loc, const char* format, Va_list args) {
    string_buffer.Buf* out = string_buffer.create(512, diags.use_color);

    source_mgr.Location loc = diags.sm.getLocation(src_loc);
    if (src_loc) {
        out.print("%s:%u:%u: ", loc.filename, loc.line, loc.column);
    }

    out.color(category_colors[category]);
    out.add(category_names[category]);
    out.add(": ");
    out.color(color.Normal);

    char[256] tmp;
    vsprintf(tmp, format, args);
    out.add(tmp);
    out.add("\n");

    if (src_loc) {
        // TODO if it's too long, just show relevant part
        out.add_line(loc.line_start);
        out.add("\n");

        out.indent(loc.column - 1);
        out.color(color.Bgreen);
        out.add("^");
        out.color(color.Normal);
    }

    stdio.FILE* stream = (category == Category.Info) ? stdout : stderr;
    fprintf(stream, "%s\n", out.data());
}

public func u32 Diags.getNumErrors(const Diags* diags) { return diags.num_errors; }

public func u32 Diags.getNumWarnings(const Diags* diags) { return diags.num_warnings; }


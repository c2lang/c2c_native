module diagnostics;

import color;
import source_mgr;
import string_buffer;
import src_loc local;
import c2_tokenizer; // to find end of token

import stdarg local;
import stdio local;
import stdlib;

public type Diags struct {
    source_mgr.SourceMgr* sm;
    string_buffer.Buf* out;
    u32 num_errors;
    u32 num_warnings;
    bool promote_warnings;
} @(opaque)

public func Diags* create(source_mgr.SourceMgr* sm, bool use_color) {
    Diags* diags = stdlib.calloc(1, sizeof(Diags));
    diags.sm = sm;
    diags.out = string_buffer.create(512, use_color);
    return diags;
}

public func void Diags.free(Diags* diags) {
    diags.out.free();
    stdlib.free(diags);
}

public func void Diags.clear(Diags* diags) {
    diags.num_errors = 0;
    diags.num_warnings = 0;
}

public func void Diags.setWarningAsError(Diags* diags, bool are_errors) {
    diags.promote_warnings = are_errors;
}

type Category enum u8 {
    Info,
    Warning,
    Error,
}

const char*[] category_names = {
    "info",
    "warning",
    "error",
}

const char*[] category_colors = {
    color.Grey,
    color.Magenta,
    color.Bred,
}

public func void Diags.reportRange(Diags* diags, SrcLoc loc, SrcRange range, const char* format, ...) {
    Category category = Category.Error;

    Va_list args;
    va_start(args, format);
    diags.internal(category, loc, range, format, args);
    va_end(args);
}

public func void Diags.report(Diags* diags, SrcLoc loc, const char* format, ...) {
    Category category = Category.Error;

    Va_list args;
    va_start(args, format);
    SrcRange range = { 0, 0 }
    diags.internal(category, loc, range, format, args);
    va_end(args);
}

public func void Diags.note(Diags* diags, SrcLoc loc, const char* format, ...) {
    Va_list args;
    va_start(args, format);
    SrcRange range = { 0, 0 }
    diags.internal(Category.Info, loc, range, format, args);
    va_end(args);
}

public func void Diags.warn(Diags* diags, SrcLoc loc, const char* format, ...) {
    Va_list args;
    va_start(args, format);
    SrcRange range = { 0, 0 }
    Category category = Category.Warning;
    if (diags.promote_warnings) category = Category.Error;
    diags.internal(category, loc, range, format, args);
    va_end(args);
}

public func void Diags.error(Diags* diags, SrcLoc loc, const char* format, Va_list args) {
    SrcRange range = { 0, 0 }
    diags.internal(Category.Error, loc, range, format, args);
}

public func void Diags.errorRange(Diags* diags, SrcLoc loc, SrcRange range, const char* format, Va_list args) {
    diags.internal(Category.Error, loc, range, format, args);
}

func void Diags.internal(Diags* diags, Category category, SrcLoc sloc, SrcRange range, const char* format, Va_list args) {
    if (category == Category.Error) {
        diags.num_errors++;
    } else {
        diags.num_warnings++;
    }

    string_buffer.Buf* out = diags.out;
    out.clear();

    source_mgr.Location startLoc = diags.sm.getLocation(range.start);
    source_mgr.Location loc = diags.sm.getLocation(sloc);
    // get end of last token
    if (range.end) {
        const char* src = diags.sm.get_token_source(range.end);
        range.end = c2_tokenizer.getTokenEnd(src, range.end);
    }
    source_mgr.Location endLoc = diags.sm.getLocation(range.end);

    if (sloc) {
        out.print("%s:%u:%u: ", loc.filename, loc.line, loc.column);
    }

    out.color(category_colors[category]);
    out.add(category_names[category]);
    out.add(": ");
    out.color(color.Normal);

    char[256] tmp;
    vsprintf(tmp, format, args);
    out.add(tmp);
    out.add("\n");

    if (sloc) {
        // TODO if it's too long, just show relevant part
        out.add_line(loc.line_start);
        out.add("\n");

        if (range.start && range.end) {
            // TODO handle case where a Range is spread over multiple lines
            assert(endLoc.column >= startLoc.column);
            out.indent(startLoc.column - 1);
            out.color(color.Bgreen);
            for (u32 i=startLoc.column; i<=endLoc.column; i++) {
                if (i == loc.column) out.add("^");
                else out.add("~");
            }
            out.color(color.Normal);
        } else {
            out.indent(loc.column - 1);
            out.color(color.Bgreen);
            out.add("^");
            out.color(color.Normal);
        }
    }

    stdio.FILE* stream = (category == Category.Info) ? stdout : stderr;
    fprintf(stream, "%s\n", out.data());
}

public func bool Diags.isOk(const Diags* diags) { return diags.num_errors == 0; }
public func bool Diags.hasErrors(const Diags* diags) { return diags.num_errors != 0; }

public func u32 Diags.getNumErrors(const Diags* diags) { return diags.num_errors; }

public func u32 Diags.getNumWarnings(const Diags* diags) { return diags.num_warnings; }

public func void Diags.printStatus(const Diags* diags) {
    string_buffer.Buf* out = diags.out;
    out.clear();

    if (diags.num_warnings) {
        out.print("%u warning%s", diags.num_warnings, diags.num_warnings > 1 ? "s" : "");
    }
    if (diags.num_errors) {
        if (diags.num_warnings) out.add(" and ");
        out.print("%u error%s", diags.num_errors, diags.num_errors > 1 ? "s" : "");
    }
    if (out.size()) {
        out.add(" generated.\n");
        fputs(out.data(), stderr);
    }
}


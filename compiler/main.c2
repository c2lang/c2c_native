/* Copyright 2022 Bas van den Berg
*/

module c2c_main;

import c2recipe;
import compiler;
import constants;
import diagnostics;
import source_mgr;
import string_pool;
import utils;

import stdlib;
import unistd;
import string local;
import c_errno local;
import stdio local;

type Options struct {
    bool print_ast;
    bool check_only;
    bool print_lib_ast;
    bool print_modules;
    bool print_symbols;
    bool print_ast_stats;
    bool print_qbe;
    bool print_reports;
    const char* target;
    const char* single_file;
    const char* other_dir;
}

func void usage(const char* me) {
    printf("Usage: %s <options> <filename>\n", me);
    printf("Options\n");
    printf("\t-a        print ASTs\n");
    printf("\t-A        print Library ASTs\n");
    printf("\t-c        check only (dont generate)\n");
    printf("\t-d [dir]  go to dir first\n");
    printf("\t-f        only parse single file\n");
    printf("\t-m        print modules\n");
    printf("\t-q        print QBE\n");
    printf("\t-r        print reports\n");
    printf("\t-s        print symbols\n");
    printf("\t-t        print AST statistics\n");
    stdlib.exit(-1);
}

func void parse_opts(i32 argc, char** argv, Options* opts) {
    for (i32 i=1; i<argc; i++) {
        const char* arg = argv[i];
        if (arg[0] == '-') {
            switch (arg[1]) {
            case '-':
                // for now ignore all '--' options
                break;
            case 'A':
                opts.print_lib_ast = true;
                break;
            case 'a':
                opts.print_ast = true;
                break;
            case 'c':
                opts.check_only = true;
                break;
            case 'd':
                if (i==argc-1) usage(argv[0]);
                i++;
                opts.other_dir = argv[i];
                break;
            case 'f':
                if (i==argc-1) usage(argv[0]);
                i++;
                opts.single_file = argv[i];
                break;
            case 'm':
                opts.print_modules = true;
                break;
            case 'q':
                opts.print_qbe = true;
                break;
            case 'r':
                opts.print_reports = true;
                break;
            case 's':
                opts.print_symbols = true;
                break;
            case 't':
                opts. print_ast_stats = true;
                break;
            default:
                printf("unknown option\n");
                usage(argv[0]);
                break;
            }
        } else {
            if (opts.target) usage(argv[0]);
            opts.target = arg;
        }
    }
    if (opts.target && opts.single_file) usage(argv[0]);
}

public func i32 main(i32 argc, char** argv) {
    Options opts = { }

    parse_opts(argc, argv, &opts);

    if (opts.other_dir) {
        if (unistd.chdir(opts.other_dir)) {
            fprintf(stderr, "cannot chdir to %s: %s\n", opts.other_dir, strerror(*errno2()));
            stdlib.exit(-1);
        }
    }

    // note: auxPool is used by recipe, build-file and manifests
    string_pool.Pool* auxPool = string_pool.create(32*1024, 0);

    source_mgr.SourceMgr* sm = source_mgr.create(auxPool, constants.Max_open_files);
    diagnostics.Diags* diags = diagnostics.create(sm, utils.useColor());

    c2recipe.Recipe* recipe = c2recipe.create(sm, auxPool);

    i32 recipe_id = -1;

    bool hasError = false;

    if (opts.single_file) {
        recipe.addDummyTarget(opts.single_file);
    } else {
        if (!utils.findProjectDir()) {
            fprintf(stderr, "c2c: error: cannot find C2 root dir\n");
            fprintf(stderr, "c2c requires a %s file in the project root\n", constants.recipe_name);
            fprintf(stderr, "Use argument -h for a list of available opts and usage of c2c\n");
            return -1;
        }
        u32 recipe_idx = auxPool.addStr(constants.recipe_name, false);

        i32 yaml_id = sm.open(recipe_idx, 0, false);
        if (yaml_id == -1) return -1;
        if (!recipe.parse(yaml_id)) return -1;

        // TEMP also parse yaml version
        recipe_id = yaml_id;
        //u32 recipe2_idx = auxPool.addStr("c2recipe.yaml", false);
        //recipe_id = sm.open(recipe2_idx, 0, false);
        //if (recipe_id == -1) return -1;
        //if (!recipe.parseYaml(recipe_id)) return -1;
    }

    //recipe.dump();

    compiler.Options options = { }
    options.check_only = opts.check_only;
    options.print_ast = opts.print_ast;
    options.print_lib_ast = opts.print_lib_ast;
    options.print_modules = opts.print_modules;
    options.print_symbols = opts.print_symbols;
    options.print_ast_stats = opts.print_ast_stats;
    options.print_qbe = opts.print_qbe;
    options.print_reports = opts.print_reports;

    u32 num_build = 0;
    for (u32 i=0; i<recipe.numTargets(); i++) {
        c2recipe.Target* target = recipe.getTarget(i);
        const char* target_name = auxPool.idx2str(target.getNameIdx());
        if (opts.target && strcmp(opts.target, target_name) != 0) continue;
        printf("building %s\n", target_name);
        num_build++;

        compiler.build(auxPool, sm, diags, target, &options);
        hasError |= (diags.getNumErrors() != 0);

        if (recipe_id != -1) sm.clear(cast<u32>(recipe_id));
    }

    if (opts.target && num_build == 0)  {
        fprintf(stderr, "No such target\n");
    }

    recipe.free();
    auxPool.free();
    diags.free();
    sm.free();

	return hasError ? -1 : 0;
}


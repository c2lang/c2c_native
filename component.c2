/* Copyright 2022 Bas van den Berg
*/

module component;

import ast;
import module_list;
import string_buffer;
import utils;

import stdlib local;
import string;

import color;// TEMP
import stdio local; // TEMP

/*
    Represents a library or the main-component

    Used to:
    - create + store Modules
*/
public type Component struct {
    const char* name;   // no ownership
    bool is_external;

    ast.Module** mods;
    u32 num_mods;
    u32 max_mods;
    module_list.ModList* globalList;
} @(opaque)

public func Component* create(module_list.ModList* globalList, const char* name, bool is_external) {
    Component* c = calloc(1, sizeof(Component));
    c.name = name;
    c.is_external = is_external;

    c.resize(4);
    c.globalList = globalList;
    return c;
}

public func void Component.free(Component* c) {
    for (u32 i=0; i<c.num_mods; i++) {
        c.mods[i].free();
    }
    free(cast<void*>(c.mods));
    free(c);
}

public func const char* Component.getName(const Component* c) { return c.name; }

public func bool Component.isExternal(const Component* c) { return c.is_external; }

public type ModuleVisitor func void (void* arg, ast.Module* m);

public func void Component.visitModules(const Component* c, ModuleVisitor visitor, void* arg) {
    for (u32 i=0; i<c.num_mods; i++) {
        visitor(arg, c.mods[i]);
    }
}

public func ast.Module* Component.getModule(const Component* c, u32 idx) {
    return c.mods[idx];
}

public func ast.Module** Component.getModules(Component* c) {
    return c.mods;
}

public func u32 Component.getNumModules(const Component* c) {
    return c.num_mods;
}

func void Component.resize(Component* c, u32 cap) {
    c.max_mods = cap;
    void* buf = malloc(c.max_mods * sizeof(ast.Module*));
    if (c.mods) {
        void* old = cast<void*>(c.mods);
        string.memcpy(buf, old, c.num_mods * sizeof(ast.Module*));
        free(old);
    }
    c.mods = buf;
}

public func ast.Module* Component.getOrAddModule(Component* c, const char* modname) {
    u32 name_idx = ast.name2idx(modname);
    for (u32 i=0; i<c.num_mods; i++) {
        ast.Module* m = c.mods[i];
        if (m.getNameIdx() == name_idx) return m;
    }

    ast.Module* m = c.globalList.find(ast.name2idx(modname));
    if (m) return nil;

    m = ast.Module.create(modname);

    if (c.num_mods == c.max_mods) c.resize(c.max_mods * 2);

    c.globalList.add(m);
    c.mods[c.num_mods] = m;
    c.num_mods++;
    return m;
}

public func ast.Module* Component.findModule(const Component* c, const char* name) {
    for (u32 i=0; i<c.num_mods; i++) {
        // Note: we can search by pointer compares
        if (c.mods[i].getName() == name) return c.mods[i];
    }
    return nil;
}

public func bool Component.hasModule(const Component* c, const ast.Module* mod) {
    for (u32 i=0; i<c.num_mods; i++) {
        if (c.mods[i] == mod) return true;
    }
    return false;
}

public func void Component.markTopModuleUsed(Component* c) {
    if (c.num_mods == 0) return;
    ast.Module* top = c.mods[c.num_mods-1];
    top.setUsed();
    // TODO check that top contains main (if binary, exported if lib)

    //TODO also mark main function as used
}

public func void Component.info(const Component* c) {
    string_buffer.Buf* out = string_buffer.create(64*1024, utils.useColor());
    out.print("component %s\n", c.name);
    for (u32 i=0; i<c.num_mods; i++) {
        c.mods[i].info(out);
    }
    stdio.printf("%s\n", out.data());
    out.free();
}

public func void Component.print(const Component* c) {
    string_buffer.Buf* out = string_buffer.create(128*1024, utils.useColor());
    for (u32 i=0; i<c.num_mods; i++) {
        c.mods[i].print(out);
    }

    stdio.puts(out.data());
    out.free();
}

public func void Component.printModules(const Component* c) {
    string_buffer.Buf* out = string_buffer.create(4096, utils.useColor());
    out.print("--- %s ---\n", c.name);
    for (u32 i=0; i<c.num_mods; i++) {
        const ast.Module* m = c.mods[i];
        const char* col = m.isUsed() ? color.Normal : color.Grey;
        out.color(col);
        out.print("   %s\n", m.getName());
    }
    out.color(color.Normal);
    stdio.puts(out.data());
    out.free();
}

public func void Component.printSymbols(const Component* c) {
    string_buffer.Buf* out = string_buffer.create(4096, utils.useColor());
    out.print("--- %s ---\n", c.name);
    for (u32 i=0; i<c.num_mods; i++) {
        out.print("  %s\n", c.mods[i].getName());
        const ast.SymbolTable* table = c.mods[i].getSymbols();
        table.print(out);
    }
    stdio.puts(out.data());
    out.free();
}

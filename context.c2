/* Copyright 2022 Bas van den Berg
*/

module ast_context;

import stdlib local;
import stdio local; // TEMP

type Block struct {
    u8* data;
    u32 size;
    Block* next;
}

func Block* Block.create(u32 blk_size) {
    Block* b = malloc(sizeof(Block));
    b.data = malloc(blk_size);
    b.size = 0;
    b.next = nil;
    return b;
}

func Block* Block.free(Block* b) {
    Block* next = b.next;
    free(b.data);
    free(b);
    return next;
}

func u32 Block.count(const Block* b) {
    u32 total = 0;
    const Block* blk = b;
    while (blk) {
        total ++;
        blk = blk.next;
    }
    return total;
}

func u32 Block.get_total(const Block* b) {
    u32 total = 0;
    const Block* blk = b;
    while (blk) {
        total += blk.size;
        blk = blk.next;
    }
    return total;
}

public type Context struct {
    Block* blk_head;
    Block* blk_tail;
    u32 blk_size;
    u32 num_allocs;
} @(opaque)

public func Context* Context.create(u32 blk_size) {
    Context* c = calloc(1, sizeof(Context));
    c.blk_size = blk_size;
    c.init();
    return c;
}

func void Context.freeBlocks(Context* c) {
    Block* blk = c.blk_head;
    while (blk) blk = blk.free();
}

public func void Context.free(Context* c) {
    c.freeBlocks();
    free(c);
}

func void Context.init(Context* c) {
    c.blk_head = Block.create(c.blk_size);
    c.blk_tail = c.blk_head;
}

public func void Context.clear(Context* c) {
    c.freeBlocks();
    c.init();
}

public func void* Context.alloc(Context* c, u32 len) {
    c.num_allocs++;

    Block* last = c.blk_tail;
    if (last.size + len >= c.blk_size) {
        c.blk_tail = Block.create(c.blk_size);
        last.next = c.blk_tail;
        last = last.next;
    }

    void* cur = last.data + last.size;
    last.size += len;
    return cur;
}

public func void Context.dump(const Context* c) {
    u32 num = c.blk_head.count();
    u32 total = c.blk_head.get_total();

    u32 avg = 0;
    if (c.num_allocs) avg = total / c.num_allocs;
    printf("context: %u allocs (avg %u bytes), %u blocks (%u), total %u Kb\n",
        c.num_allocs, avg, num, c.blk_size, (total / 1024) + 1);
}


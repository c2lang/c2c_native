/* Copyright 2022-2023 Bas van den Berg
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

module ast_visitor;

import ast local;

import stdlib;

public type OnRef fn void (void* arg, const Ref* ref);

public type Visitor struct @(opaque) {
    void* arg;
    OnRef on_ref;
}

public fn Visitor* create(void* arg, OnRef on_ref) {
    Visitor* v = stdlib.calloc(1, sizeof(Visitor));
    v.arg = arg;
    v.on_ref = on_ref;
    return v;
}

public fn void Visitor.free(Visitor* v) {
    stdlib.free(v);
}

public fn void Visitor.handleAssert(Visitor* v, StaticAssert* a) {
    v.handleExpr(a.getLhs());
    v.handleExpr(a.getRhs());
}

public fn void Visitor.handle(Visitor* v, Decl* d) {
    switch (d.getKind()) {
    case Function:
        v.handleFunction(cast<FunctionDecl*>(d));
        break;
    case Import:
        // TODO
        break;
    case StructType:
        StructTypeDecl* s = cast<StructTypeDecl*>(d);
        u32 num_members = s.getNumMembers();
        Decl** members = s.getMembers();
        for (u32 i=0; i<num_members; i++) {
            v.handle(members[i]);
        }
        break;
    case EnumType:
        // TODO
        break;
    case EnumConstant:
        // TODO
        break;
    case FunctionType:
        FunctionTypeDecl* ftd = cast<FunctionTypeDecl*>(d);
        v.handleFunction(ftd.getDecl());
        break;
    case AliasType:
        AliasTypeDecl* atd = cast<AliasTypeDecl*>(d);
        v.handleTypeRef(atd.getTypeRef());
        break;
    case Variable:
        v.handleVarDecl(cast<VarDecl*>(d));
        break;
    }
}

fn void Visitor.handleFunction(Visitor* v, FunctionDecl* d) {
    if (d.isTemplate()) return; // TODO skip template functions for now

    // return type
    v.handleTypeRef(d.getReturnTypeRef());

    // struct prefix
    Ref* prefix = d.getPrefix();
    if (prefix) v.on_ref(v.arg, prefix);

    // args
    u32 num_params = d.getNumParams();
    VarDecl** args = d.getParams();
    for (u32 i=0; i<num_params; i++) v.handleVarDecl(args[i]);

    // body
    CompoundStmt* body = d.getBody();
    if (body) {
        v.handleCompoundStmt(body);
    }
}

fn void Visitor.handleVarDecl(Visitor* v, VarDecl* d) {
    v.handleTypeRef(d.getTypeRef());

    Expr* init_expr = d.getInit();
    if (init_expr) v.handleExpr(init_expr);
}

fn void Visitor.handleTypeRef(Visitor* v, const TypeRef* r) {
    if (r.isUser()) {
        const Ref* prefix = r.getPrefix();
        if (prefix) v.on_ref(v.arg, prefix);

        const Ref* user = r.getUser();
        v.on_ref(v.arg, user);
    }

    u32 num_arrays = r.getNumArrays();
    for (u32 i=0; i<num_arrays; i++) {
        Expr* e = r.getArray(i);
        if (e) v.handleExpr(e); // could be foo[]
    }

}

fn void Visitor.handleType(Visitor* v, QualType qt) {
    // TODO use TypeRef or QualType?
}

fn void Visitor.handleStmt(Visitor* v, Stmt* s) {
    switch (s.getKind()) {
    case Return:
        ReturnStmt* r = cast<ReturnStmt*>(s);
        Expr* e = r.getValue();
        if (e) v.handleExpr(e);
        break;
    case Expr:
        v.handleExpr(cast<Expr*>(s));
        break;
    case If:
        IfStmt* i = cast<IfStmt*>(s);
        v.handleStmt(i.getCond());
        v.handleStmt(i.getThen());
        Stmt* e = i.getElse();
        if (e) v.handleStmt(e);
        break;
    case While:
        WhileStmt* w = cast<WhileStmt*>(s);
        v.handleStmt(w.getCond());
        v.handleStmt(w.getBody());
        break;
    case Do:
        DoStmt* d = cast<DoStmt*>(s);
        v.handleStmt(d.getCond());
        v.handleStmt(d.getBody());
        break;
    case For:
        ForStmt* f = cast<ForStmt*>(s);
        Stmt* in = f.getInit();
        if (in) v.handleStmt(in);
        Expr* cond = f.getCond();
        if (cond) v.handleExpr(cond);
        Expr* incr = f.getIncr();
        if (incr) v.handleExpr(incr);
        Stmt* body = f.getBody();
        if (body) v.handleStmt(body);
        break;
    case Switch:
        SwitchStmt* sw = cast<SwitchStmt*>(s);
        v.handleExpr(sw.getCond());
        const u32 numcases = sw.getNumCases();
        SwitchCase** cases = sw.getCases();
        for (u32 i=0; i<numcases; i++) {
            SwitchCase* c = cases[i];
            if (c.numMulti()) {
                IdentifierExpr** multi = c.getMultiCond();
                for (u32 j=0; j<c.numMulti(); j++) {
                    IdentifierExpr* id = multi[j];
                    Ref ref = id.getRef();
                    v.on_ref(v.arg, &ref);
                }
            } else {
                if (c.getCond()) v.handleExpr(c.getCond());
            }

            const u32 numstmts = c.getNumStmts();
            Stmt** stmts = c.getStmts();
            for (u32 j=0; j<numstmts; j++) v.handleStmt(stmts[j]);
        }
        break;
    case Break:
        break;
    case Continue:
        break;
    case Fallthrough:
        break;
    case Label:
        break;
    case Goto:
        // TODO need dest in GotoStmt
        break;
    case Compound:
        v.handleCompoundStmt(cast<CompoundStmt*>(s));
        break;
    case Decl:
        DeclStmt* d = cast<DeclStmt*>(s);
        v.handleVarDecl(d.getDecl());
        break;
    case Asm:
        AsmStmt* a = cast<AsmStmt*>(s);
        u32 num_exprs = a.getNumExprs();
        Expr** exprs = a.getExprs();
        for (u32 i=0; i<num_exprs; i++) {
            v.handleExpr(exprs[i]);
        }
        break;
    case Assert:
        AssertStmt* a = cast<AssertStmt*>(s);
        v.handleExpr(a.getInner());
        break;
    }
}

fn void Visitor.handleCompoundStmt(Visitor* v, CompoundStmt* s) {
    u32 count = s.getCount();
    Stmt** stmts = s.getStmts();
    for (u32 i=0; i<count; i++) v.handleStmt(stmts[i]);
}


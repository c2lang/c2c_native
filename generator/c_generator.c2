module c_generator;

import ast local;
import component;
import string_buffer;
import ctv_analyser;
import file_utils;
import linked_list;

import string;
import stdlib;
import stdio local;

type Fragment struct {
    string_buffer.Buf* buf;
    linked_list.Element list;
}

func Fragment* Fragment.create() {
    Fragment* f = stdlib.malloc(sizeof(Fragment));
    f.buf = string_buffer.create(128, false);
    return f;
}

func void Fragment.clear(Fragment* f) {
    f.buf.clear();
}

func void Fragment.free(Fragment* f) {
    f.buf.free();
    stdlib.free(f);
}


type Generator struct {
    string_buffer.Buf* out;
    const char* target;
    const char* output_dir;
    bool cur_external;  // whether current component is external
    u32 func_idx;   // also body idx
    u32 string_idx; // for string literals
    u32 substruct_idx;  // for unnamed substructs/unions

    const char* mod_name; // set per module
    Module* mod;

    // list of fragments
    linked_list.Element free_list;
    linked_list.Element used_list;
}


const char[] Dir = "cgen";
const char[] Filename = "main.c";

// TODO IDEA for less memory: have 2 lists: small/big fragments, request one (small = 128)
func Fragment* Generator.getFragment(Generator* gen) {
    if (gen.free_list.isEmpty()) {
        return Fragment.create();
    }

    linked_list.Element* e = gen.free_list.popFront();
    Fragment* f = to_container(Fragment, list, e);
    f.clear();
    return f;
}

func void Generator.addFragment(Generator* gen, Fragment* f) {
    gen.used_list.addTail(&f.list);
}

func void Generator.freeFragment(Generator* gen, Fragment* f) {
    gen.free_list.addTail(&f.list);
}


func void Generator.emitCName(Generator* gen, string_buffer.Buf* out, const Decl* d) {
    gen.emitCNameMod(out, d, gen.mod);
}

func void Generator.emitCNameMod(Generator* gen, string_buffer.Buf* out, const Decl* d, Module* mod) {
    if (d.isExternal()) {
        const char* cname = d.getCName();
        if (cname) {
            out.add(cname);
            return;
        }
    }
    out.add(mod.getName());
    out.add1('_');
    if (d.getKind() == DeclKind.EnumConstant) {
        QualType qt = d.getType();
        EnumType* et = cast<EnumType*>(qt.getType());
        out.add(et.getName());
        out.add1('_');
    }
    out.add(d.getName());
}

func void Generator.emitDeclName(Generator* gen, string_buffer.Buf* out, const Decl* d) {
    if (d.getKind() == DeclKind.Var) {
        VarDecl* vd = cast<VarDecl*>(d);
        if (!vd.isGlobal()) {
            out.add(d.getName());
            return;
        }
    }
    gen.emitCNameMod(out, d, d.getModule());
}

/*
func void Generator.emitCtv(Generator* gen, string_buffer.Buf* out, const Expr* e) {
    ctv_analyser.Value val = ctv_analyser.get_value(e);
    if (val.is_signed) out.print("%ld", val.svalue);
    else out.print("%lu", val.uvalue);
}
*/

func void Generator.handleFunction(Generator* gen, Decl* d) {
    Fragment* f = gen.getFragment();
    FunctionDecl* fd = cast<FunctionDecl*>(d);
    string_buffer.Buf* out = f.buf;

    // TODO
}

func void Generator.emitEnum(Generator* gen, string_buffer.Buf* out, Decl* d) {
    EnumTypeDecl* etd = cast<EnumTypeDecl*>(d);
    out.add("typedef enum {\n");
    u32 num_constants = etd.getNumConstants();
    EnumConstantDecl** constants = etd.getConstants();
    for (u32 i=0; i<num_constants; i++) {
        EnumConstantDecl* ecd = constants[i];
        out.indent(4);
        out.add(gen.mod_name);
        out.add1('_');
        out.add(d.getName());
        out.add1('_');
        out.add(ecd.asDecl().getName());
        Expr* ie = ecd.getInit();
        if (ie) {
            out.add(" = ");
            gen.emitExpr(out, ie);
        }
        out.add(",\n");
    }
    // TODO add max to ensure size is ok

    out.add("} __attribute__((packed)) ");
    gen.emitCName(out, d);
    out.add(";\n\n");
}

func void Generator.onEnum(Generator* gen, Decl* d) {
    if (d.isGenerated()) return;
    Fragment* f = gen.getFragment();
    gen.emitEnum(f.buf, d);
    gen.addFragment(f);
}

const char*[] builtinType_cnames = {
    "char",
    "int8_t",
    "int16_t",
    "int32_t",
    "int64_t",
    "uint8_t",
    "uint16_t",
    "uint32_t",
    "uint64_t",
    "float",
    "double",
    "ssize_t",
    "size_t",
    "bool",
    "void",
}

static_assert(elemsof(BuiltinKind), elemsof(builtinType_cnames));
func void Generator.emitTypePre(Generator* gen, string_buffer.Buf* out, QualType qt) {
    Decl* decl = nil;

    if (qt.isConst()) out.add("const ");

    switch (qt.getKind()) {
    case Builtin:
        BuiltinType* bt = cast<BuiltinType*>(qt.getType());
        out.add(builtinType_cnames[bt.getKind()]);
        return;
    case Pointer:
        PointerType* pt = cast<PointerType*>(qt.getType());
        gen.emitTypePre(out, pt.getInner());
        out.add1('*');
        return;
    case Array:
        ArrayType* at = cast<ArrayType*>(qt.getType());
        gen.emitTypePre(out, at.getElemType());
        // rest is done in emitTypePost
        return;
    case Struct:
        StructType* st = cast<StructType*>(qt.getType());
        decl = cast<Decl*>(st.getDecl());
        break;
    case Enum:
        EnumType* et = cast<EnumType*>(qt.getType());
        decl = cast<Decl*>(et.getDecl());
        break;
    case Function:
        FunctionType* et = cast<FunctionType*>(qt.getType());
        decl = cast<Decl*>(et.getDecl());
        break;
    case Alias:
        AliasType* at = cast<AliasType*>(qt.getType());
        decl = cast<Decl*>(at.getDecl());
        break;
    case Module:
        assert(0);
        return;
    }

    gen.emitCNameMod(out, decl, decl.getModule());
}

func void Generator.emitTypePost(Generator* gen, string_buffer.Buf* out, QualType qt) {
    if (qt.getKind() != TypeKind.Array) return;
    ArrayType* at = cast<ArrayType*>(qt.getType());
    out.add1('[');
    if (at.hasSize()) out.print("%u", at.getSize());
    out.add1(']');
}

func void Generator.genDeclIfNeeded(Generator* gen, Decl* d) {
    if (d.isGenerated()) return;

    if (d.getKind() == DeclKind.Var) {
        VarDecl* vd = cast<VarDecl*>(d);
        if (!vd.isGlobal()) return;
    }

    gen.emitGlobalDecl(d);
}

func void Generator.genTypeIfNeeded(Generator* gen, QualType qt, bool full) {
    Decl* d = nil;

    switch (qt.getKind()) {
    case Builtin:
        return;
    case Pointer:
        PointerType* pt = cast<PointerType*>(qt.getType());
        gen.genTypeIfNeeded(pt.getInner(), false);
        return;
    case Array:
        ArrayType* at = cast<ArrayType*>(qt.getType());
        gen.genTypeIfNeeded(at.getElemType(), true);
        return;
    case Struct:
        StructType* st = cast<StructType*>(qt.getType());
        if (!full) return;
        d = cast<Decl*>(st.getDecl());
        break;
    case Enum:
        EnumType* et = cast<EnumType*>(qt.getType());
        d = cast<Decl*>(et.getDecl());
        break;
    case Function:
        FunctionType* et = cast<FunctionType*>(qt.getType());
        d = cast<Decl*>(et.getDecl());
        break;
    case Alias:
        AliasType* at = cast<AliasType*>(qt.getType());
        d = cast<Decl*>(at.getDecl());
        break;
    case Module:
        assert(0);
        return;
    }

    if (!d.isGenerated()) gen.emitGlobalDecl(d);
}

func void Generator.emitStructMember(Generator* gen, string_buffer.Buf* out, Decl* d, u32 indent) {
    if (d.getKind() == DeclKind.Var) {
        // check if full/pointer dep
        QualType qt = d.getType();
        // TODO canon for AliasType
        gen.genTypeIfNeeded(d.getType(), true);

        out.indent(indent*4);
        gen.emitTypePre(out, d.getType());
        out.add1(' ');
        if (d.getNameIdx()) out.add(d.getName());
        gen.emitTypePost(out, d.getType());

        VarDecl* vd = cast<VarDecl*>(d);
        Expr* bitfield = vd.getBitfield();
        if (bitfield) {
            out.add(" : ");
            gen.emitExpr(out, bitfield);
        }
        out.add(";\n");
    } else {
        assert(d.getKind() == DeclKind.StructType);
        gen.emitStruct(out, d, indent);
    }
}

func void Generator.emitStruct(Generator* gen, string_buffer.Buf* out, Decl* d, u32 indent) {
    StructTypeDecl* std = cast<StructTypeDecl*>(d);
    if (std.isGlobal()) {
        if (std.isStruct()) out.add("struct ");
        else out.add("union ");
        gen.emitCName(out, d);
        out.add("_ {\n");
    } else {
        out.indent(indent*4);
        if (std.isStruct()) out.add("struct");
        else out.add("union");
        out.add(" {\n");
    }

    u32 num_members = std.getNumMembers();
    Decl** members = std.getMembers();
    for (u32 i=0; i<num_members; i++) {
        gen.emitStructMember(out, members[i], indent+1);
    }

    if (std.isGlobal()) {
        out.add("};\n\n");
    } else {
        out.indent(indent*4);
        out.add1('}');
        if (d.getNameIdx()) {
            out.add1(' ');
            out.add(d.getName());
        }
        out.add(";\n");
    }
}

func void Generator.onStruct(Generator* gen, Decl* d) {
    //TODO do check + Fragment generation in caller
    if (d.isGenerated()) return;

    Fragment* f = gen.getFragment();
    gen.emitStruct(f.buf, d, 0);
    gen.addFragment(f);
}

func void Generator.emitFunctionType(Generator* gen, string_buffer.Buf* out, Decl* d) {
    FunctionTypeDecl* ftd = cast<FunctionTypeDecl*>(d);
    FunctionDecl* fd = ftd.getDecl();

    out.add("typedef ");
    gen.emitTypePre(out, fd.getRType());
    out.add(" (*");
    // TODO might have prefix: mod_prefix_name
    gen.emitCName(out, d);
    out.add(")(");
    u32 num_params = fd.getNumParams();
    VarDecl** params = fd.getParams();
    for (u32 i=0; i<num_params; i++) {
        Decl* arg = cast<Decl*>(params[i]);
        if (i != 0) out.add(", ");
        gen.emitTypePre(out, arg.getType());
        // dont emit names (not needed)
    }
    if (fd.isVariadic()) {
        if (num_params) out.add(", ");
        out.add("...");
    }
    out.add(");\n\n");
}

func void Generator.onFunctionType(Generator* gen, Decl* d) {
    if (d.isGenerated()) return;

    Fragment* f = gen.getFragment();
    gen.emitFunctionType(f.buf, d);
    gen.addFragment(f);
}

func void Generator.emitAliasType(Generator* gen, string_buffer.Buf* out, Decl* d) {
    AliasTypeDecl* atd = cast<AliasTypeDecl*>(d);
    // For now, just generate canonicalType as RHS
    QualType qt = d.getType();
    qt = qt.getCanonicalType();
    out.add("typedef ");
    gen.emitTypePre(out, qt);
    gen.emitTypePost(out, qt);
    out.add1(' ');
    gen.emitCName(out, d);
    out.add(";\n\n");
}

func void Generator.onAliasType(Generator* gen, Decl* d) {
    if (d.isGenerated()) return;

    Fragment* f = gen.getFragment();
    gen.emitAliasType(f.buf, d);
    gen.addFragment(f);
}

func void Generator.emitGlobalVarDecl(Generator* gen, string_buffer.Buf* out, Decl* d) {
    VarDecl* vd = cast<VarDecl*>(d);

    out.add("static ");

    gen.emitTypePre(out, d.getType());
    out.add1(' ');
    gen.emitCName(out, d);
    gen.emitTypePost(out, d.getType());
    Expr* ie = vd.getInit();
    if (ie) {
        out.add(" = ");
        gen.emitExpr(out, ie);
    }
    out.add(";\n\n");
}

func void Generator.onVarDecl(Generator* gen, Decl* d) {
    if (d.isGenerated()) return;

    Fragment* f = gen.getFragment();
    gen.emitGlobalVarDecl(f.buf, d);
    gen.addFragment(f);
}

func void Generator.on_forward_stucts(void* arg, Decl* d) {
    if (d.getKind() != DeclKind.StructType) return;

    Generator* gen = arg;
    string_buffer.Buf* out = gen.out;
    StructTypeDecl* std = cast<StructTypeDecl*>(d);

    out.add("typedef ");
    if (std.isStruct()) out.add("struct");
    else out.add("union");
    out.add1(' ');
    gen.emitCName(out, d);
    out.add("_ ");
    gen.emitCName(out, d);
    out.add(";\n");
}

func void Generator.emitGlobalDecl(Generator* gen, Decl* d) {

    switch (d.getKind()) {
    case Function:
        //if (!gen.cur_external) gen.handleFunction(d);
        break;
    case Import:
        assert(0); // cannot happen
        return;
    case StructType:
        gen.onStruct(d);
        break;
    case EnumType:
        gen.onEnum(d);
        break;
    case EnumConstant:
        // Can happen, we need to generate the containing enum then
        // TODO
        printf("TODO gen enum\n");
        break;
    case FunctionType:
        gen.onFunctionType(d);
        break;
    case AliasType:
        gen.onAliasType(d);
        break;
    case Var:
        gen.onVarDecl(d);
        break;
    case StaticAssert:
        assert(0); // cannot happen
        break;
    }

    d.setGenerated();

    while (!gen.used_list.isEmpty()) {
        linked_list.Element* e = gen.used_list.popFront();
        Fragment* f = to_container(Fragment, list, e);
        gen.out.add2(f.buf.data(), f.buf.size());
        gen.freeFragment(f);
    }
}

func void Generator.on_decl(void* arg, Decl* d) {
    Generator* gen = arg;
    gen.emitGlobalDecl(d);
}

func void Generator.on_vardecl(void* arg, VarDecl* vd) {
    Generator* gen = arg;
    gen.emitGlobalDecl(cast<Decl*>(vd));
}

func void Generator.on_ast_types(void* arg, AST* a) {
    Generator* gen = arg;
    a.visitTypeDecls(Generator.on_decl, arg);
}

func void Generator.on_ast_vars(void* arg, AST* a) {
    Generator* gen = arg;
    a.visitVarDecls(Generator.on_vardecl, arg);
}

func void Generator.on_ast_structs(void* arg, AST* a) {
    Generator* gen = arg;
    a.visitTypeDecls(Generator.on_forward_stucts, arg);
}

func void Generator.on_module(void* arg, Module* m) {
    if (m.isUsed()) {
        Generator* gen = arg;
        gen.out.print("\n// --- module %s ---\n\n", m.getName());
        gen.mod_name = m.getName();
        gen.mod = m;

        // generate forward decls of structs
        m.visitASTs(Generator.on_ast_structs, arg);

        m.visitASTs(Generator.on_ast_types, arg);
        // TODO generate function protos first
        m.visitASTs(Generator.on_ast_vars, arg);
    }
}

func void Generator.init(Generator* gen, const char* target, const char* output_dir) {
    string.memset(gen, 0, sizeof(Generator));
    gen.out = string_buffer.create(256*1024, false);
    gen.target = target;
    gen.output_dir = output_dir;
    // starting at 1 looks nicer
    gen.func_idx = 1;
    gen.string_idx = 1;
    gen.free_list.init();
    gen.used_list.init();
}

func void Generator.free(Generator* gen) {
    while (!gen.free_list.isEmpty()) {
        linked_list.Element* e = gen.free_list.popFront();
        Fragment* f = to_container(Fragment, list, e);
        f.free();
    }
    gen.out.free();
}

func void Generator.write(Generator* gen, const char* output_dir, const char* filename) {
    //printf("%s\n", gen.out.data());

    char[256] fullname;
    sprintf(fullname, "%s/%s", output_dir, filename);

    file_utils.Writer writer;
    bool ok = writer.write(fullname, cast<u8*>(gen.out.data()), gen.out.size());
    // TODO check ok
}

func void Generator.createMakefile(Generator* gen, const char* output_dir) {
    string_buffer.Buf* out = gen.out;
    out.clear();
    out.add("# This makefile is auto-generated, any modifications will be lost\n\n");
    out.add("CC=gcc\n");
    out.add("CFLAGS=\n");
    out.add1('\n');
    out.print("../%s: main.c\n", gen.target);
    out.print("\t\t$(CC) $(CFLAGS) main.c -o ../%s\n\n", gen.target);

    out.add("clean:\n");
    out.print("\t\trm -f ../%s\n\n", gen.target);

    gen.write(output_dir, "Makefile");
}

public func void generate(const char* target,
                          const char* output_dir,
                          Module* c2mod,
                          component.Component** comps,
                          u32 count,
                          bool print)
{
    // TODO put in aux-pool? (lowers stack-size)
    char[256] dir;
    sprintf(dir, "%s/%s", output_dir, Dir);
    i32 err = file_utils.create_directory(dir);
    if (err) {
        fprintf(stderr, "Error creating directory %s: %s\n", dir, string.strerror(err));
        return;
    }

    Generator gen;
    gen.init(target, dir);
    string_buffer.Buf* out = gen.out;

    out.add("// --- internally added ---\n");
    out.add("#define bool char\n");
    out.add("#define int8_t char\n");
    out.add("#define uint8_t unsigned char\n");
    out.add("#define int16_t short\n");
    out.add("#define uint16_t unsigned short\n");
    out.add("#define int32_t int\n");
    out.add("#define uint32_t unsigned int\n");
    out.add("#define int64_t long\n");
    out.add("#define uint64_t unsigned long\n");
    out.add("#define ssize_t long\n");
    out.add("#define size_t unsigned long\n");

    out.add("#define NULL ((void*)0)\n");
    out.add("#define ARRAY_SIZE(x) (sizeof(x)/sizeof(x[0]))\n");

    gen.cur_external = false;
    Generator.on_module(&gen, c2mod);

    for (u32 i=0; i<count; i++) {
        component.Component* c = comps[i];
        gen.cur_external = c.isExternal();
        c.visitModules(Generator.on_module, &gen);
    }

    if (print) puts(gen.out.data());
    gen.write(dir, Filename);

    gen.createMakefile(dir);

    gen.free();
}


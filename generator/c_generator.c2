/* Copyright 2022-2023 Bas van den Berg
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

module c_generator;

import ast local;
import build_file;
import build_target;
import component;
import console;
import ctv_analyser;
import decl_sorter;
import file_utils;
import linked_list;
import module_list;
import process_utils;
import source_mgr;
import string_buffer;
import string_list;
import string_utils;

import string;
import stdlib;
import stdio;

type Fragment struct {
    string_buffer.Buf* buf;
    linked_list.Element list;
}

func Fragment* Fragment.create() {
    Fragment* f = stdlib.malloc(sizeof(Fragment));
    f.buf = string_buffer.create(128, false, 3);
    return f;
}

func void Fragment.clear(Fragment* f) {
    f.buf.clear();
}

func void Fragment.free(Fragment* f) {
    f.buf.free();
    stdlib.free(f);
}


type Generator struct {
    string_buffer.Buf* out;
    const char* target;
    build_target.Kind target_kind;
    const char* results_dir;
    const char* output_dir;
    source_mgr.SourceMgr* sm;
    const build_file.Info* build_info;
    bool enable_asserts;

    // set during generation
    bool cur_external;  // whether current component is external
    FunctionDecl* cur_function; // needed for assert
    const char* mod_name; // set per module
    string_list.List imports;  // used to filter out duplicate imports (from different ASTs)
    DeclList decls;

    Decl* mainFunc;
    u32 stdargName;


    Module* mod;

    string_buffer.Buf* c2i_file;
    string_buffer.Buf* header_file;

    // list of fragments
    linked_list.Element free_list;
    linked_list.Element used_list;
}


const char[] Dir = "cgen";
const char[] LogFile = "build.log";

func Fragment* Generator.getFragment(Generator* gen) {
    if (gen.free_list.isEmpty()) {
        return Fragment.create();
    }

    linked_list.Element* e = gen.free_list.popFront();
    Fragment* f = to_container(Fragment, list, e);
    f.clear();
    return f;
}

func void Generator.addFragment(Generator* gen, Fragment* f) {
    gen.used_list.addTail(&f.list);
}

func void Generator.freeFragment(Generator* gen, Fragment* f) {
    gen.free_list.addTail(&f.list);
}


func void Generator.emitCtv(Generator* /*gen*/, string_buffer.Buf* out, const Expr* e) {
    Value val = ctv_analyser.get_value(e);
    if (val.is_signed) out.print("%ld", val.svalue);
    else out.print("%lu", val.uvalue);
}

func void Generator.emitCName(Generator* gen, string_buffer.Buf* out, const Decl* d) {
    gen.emitCNameMod(out, d, gen.mod);
}

func void Generator.emitCNameMod(Generator* /*gen*/, string_buffer.Buf* out, const Decl* d, Module* mod) {
    if (d.isExternal()) {
        const char* cname = d.getCName();
        if (cname) {
            out.add(cname);
        } else {
            out.add(d.getName());
        }
        return;
    }
    out.add(mod.getName());
    out.add1('_');
    if (d.isFunction()) {
        FunctionDecl* fd = cast<FunctionDecl*>(d);
        Ref* prefix = fd.getPrefix();
        if (prefix) {
            out.add(ast.idx2name(prefix.name_idx));
            out.add1('_');
        }
    }
    if (d.isEnumConstant()) {
        QualType qt = d.getType();
        EnumType* et = cast<EnumType*>(qt.getType());
        out.add(et.getName());
        out.add1('_');
    }
    out.add(d.getName());
}

func void Generator.emitDeclName(Generator* gen, string_buffer.Buf* out, const Decl* d) {
    if (d.isVariable()) {
        VarDecl* vd = cast<VarDecl*>(d);
        if (!vd.isGlobal()) {
            out.add(d.getName());
            return;
        }
    }
    gen.emitCNameMod(out, d, d.getModule());
}

func void Generator.emitEnum(Generator* gen, string_buffer.Buf* out, Decl* d) {
    EnumTypeDecl* etd = cast<EnumTypeDecl*>(d);
    out.add("typedef enum {\n");
    u32 num_constants = etd.getNumConstants();
    EnumConstantDecl** constants = etd.getConstants();
    for (u32 i=0; i<num_constants; i++) {
        EnumConstantDecl* ecd = constants[i];
        out.indent(1);
        out.add(gen.mod_name);
        out.add1('_');
        out.add(d.getName());
        out.add1('_');
        out.add(ecd.asDecl().getName());
        Expr* ie = ecd.getInit();
        ecd.asDecl().setGenerated();
        if (ie) {
            out.add(" = ");
            gen.emitExpr(out, ie);
        }
        out.add(",\n");
    }
    // add max to ensure size is ok
    QualType implType = etd.getImplType();
    const BuiltinType* bi = implType.getBuiltin();
    out.indent(1);
    out.print("_%s_%s_max = ", gen.mod_name, d.getName());
    switch (bi.getAlignment()) {
    case 1:
        out.add("255");
        break;
    case 2:
        out.add("65535");
        break;
    case 4:
        out.add("4294967295");
        break;
    case 8:
        out.add("18446744073709551615");
        break;
    }
    out.newline();

    out.add("} __attribute__((packed)) ");
    gen.emitCName(out, d);
    out.add(";\n\n");
}

const char*[] builtinType_cnames = {
    "char",
    "int8_t",
    "int16_t",
    "int32_t",
    "int64_t",
    "uint8_t",
    "uint16_t",
    "uint32_t",
    "uint64_t",
    "float",
    "double",
    "ssize_t",
    "size_t",
    "bool",
    "void",
}

static_assert(elemsof(BuiltinKind), elemsof(builtinType_cnames));

func void Generator.emitGlobalVarTypePre(Generator* gen, string_buffer.Buf* out, QualType qt) {
    if (gen.cur_external && qt.isArray()) {
        // convert to pointer
        ArrayType* at = cast<ArrayType*>(qt.getType());
        gen.emitGlobalVarTypePre(out, at.getElemType());
        out.add1('*');
    } else {
        gen.emitTypePre(out, qt);
    }
}

func void Generator.emitTypePre(Generator* gen, string_buffer.Buf* out, QualType qt) {
    Decl* decl = nil;

    if (qt.isConst()) out.add("const ");
    if (qt.isVolatile()) out.add("volatile ");

    switch (qt.getKind()) {
    case Builtin:
        BuiltinType* bt = cast<BuiltinType*>(qt.getType());
        out.add(builtinType_cnames[bt.getKind()]);
        return;
    case Pointer:
        PointerType* pt = cast<PointerType*>(qt.getType());
        gen.emitTypePre(out, pt.getInner());
        out.add1('*');
        return;
    case Array:
        ArrayType* at = cast<ArrayType*>(qt.getType());
        gen.emitTypePre(out, at.getElemType());
        return;
    case Struct:
        StructType* st = cast<StructType*>(qt.getType());
        StructTypeDecl* std = st.getDecl();
        if (std.hasAttrNoTypeDef()) {
            out.add(std.isStruct() ? "struct " : "union ");
        }

        decl = cast<Decl*>(st.getDecl());
        break;
    case Enum:
        EnumType* et = cast<EnumType*>(qt.getType());
        decl = cast<Decl*>(et.getDecl());
        break;
    case Function:
        FunctionType* ft = cast<FunctionType*>(qt.getType());
        decl = cast<Decl*>(ft.getDecl());
        break;
    case Alias:
        AliasType* at = cast<AliasType*>(qt.getType());
        decl = cast<Decl*>(at.getDecl());
        break;
    case Module:
        assert(0);
        return;
    }

    gen.emitCNameMod(out, decl, decl.getModule());
}

func void Generator.emitGlobalVarTypePost(Generator* gen, string_buffer.Buf* out, QualType qt) {
    if (!qt.isArray()) return;
    if (gen.cur_external) return;
    gen.emitTypePost(out, qt);
}

func void Generator.emitTypePost(Generator* gen, string_buffer.Buf* out, QualType qt) {
    if (!qt.isArray()) return;
    ArrayType* at = cast<ArrayType*>(qt.getType());

    out.add1('[');
    if (at.hasSize()) out.print("%u", at.getSize());
    out.add1(']');
    gen.emitTypePost(out, at.getElemType());
}

func void Generator.genDeclIfNeeded(Generator* gen, Decl* d) {
    if (d.isGenerated()) return;

    if (d.isVariable()) {
        VarDecl* vd = cast<VarDecl*>(d);
        if (!vd.isGlobal()) return;
    }

    gen.emitGlobalDecl(d);
}

func void Generator.genTypeIfNeeded(Generator* gen, QualType qt, bool full) {
    Decl* d = nil;

    switch (qt.getKind()) {
    case Builtin:
        return;
    case Pointer:
        PointerType* pt = cast<PointerType*>(qt.getType());
        gen.genTypeIfNeeded(pt.getInner(), false);
        return;
    case Array:
        ArrayType* at = cast<ArrayType*>(qt.getType());
        gen.genTypeIfNeeded(at.getElemType(), true);
        return;
    case Struct:
        StructType* st = cast<StructType*>(qt.getType());
        if (!full) return;
        d = cast<Decl*>(st.getDecl());
        break;
    case Enum:
        EnumType* et = cast<EnumType*>(qt.getType());
        d = cast<Decl*>(et.getDecl());
        break;
    case Function:
        FunctionType* et = cast<FunctionType*>(qt.getType());
        d = cast<Decl*>(et.getDecl());
        break;
    case Alias:
        AliasType* at = cast<AliasType*>(qt.getType());
        d = cast<Decl*>(at.getDecl());
        break;
    case Module:
        assert(0);
        return;
    }

    if (!d.isGenerated()) gen.emitGlobalDecl(d);
}

func void Generator.emitStructMember(Generator* gen, string_buffer.Buf* out, Decl* d, u32 indent) {
    if (d.isVariable()) {
        // check if full/pointer dep
        //QualType qt = d.getType();
        // TODO canon for AliasType
        gen.genTypeIfNeeded(d.getType(), true);

        out.indent(indent);
        gen.emitTypePre(out, d.getType());
        out.space();
        if (d.getNameIdx()) out.add(d.getName());
        gen.emitTypePost(out, d.getType());

        VarDecl* vd = cast<VarDecl*>(d);
        Expr* bitfield = vd.getBitfield();
        if (bitfield) {
            out.add(" : ");
            // C does not allow using constants here, so generate CTV Value
            gen.emitCtv(out, bitfield);
        }
        out.add(";\n");
    } else {
        assert(d.isStructType());
        gen.emitStruct(out, d, indent);
    }
}

func void Generator.emitStruct(Generator* gen, string_buffer.Buf* out, Decl* d, u32 indent) {
    StructTypeDecl* std = cast<StructTypeDecl*>(d);
    if (std.isGlobal()) {
        if (std.isStruct()) out.add("struct ");
        else out.add("union ");
        gen.emitCName(out, d);
        if (!std.hasAttrNoTypeDef()) out.add1('_'); // either generate 'struct stat_' or 'struct stat'
        out.add(" {\n");
    } else {
        out.indent(indent);
        if (std.isStruct()) out.add("struct");
        else out.add("union");
        out.add(" {\n");
    }

    u32 num_members = std.getNumMembers();
    Decl** members = std.getMembers();
    for (u32 i=0; i<num_members; i++) {
        gen.emitStructMember(out, members[i], indent+1);
    }

    if (std.isGlobal()) {
        out.add("}");
        if (std.isPacked()) out.add(" __attribute__((packed))");
        out.add(";\n\n");
    } else {
        out.indent(indent);
        out.add1('}');
        if (d.getNameIdx()) {
            out.space();
            out.add(d.getName());
        }
        out.add(";\n");
    }
}

func void Generator.emitFunctionType(Generator* gen, string_buffer.Buf* out, Decl* d) {
    FunctionTypeDecl* ftd = cast<FunctionTypeDecl*>(d);
    FunctionDecl* fd = ftd.getDecl();
    fd.asDecl().setGenerated();

    out.add("typedef ");
    gen.emitTypePre(out, fd.getRType());
    out.add(" (*");
    gen.emitCName(out, d);
    out.add(")(");
    u32 num_params = fd.getNumParams();
    VarDecl** params = fd.getParams();
    for (u32 i=0; i<num_params; i++) {
        Decl* arg = cast<Decl*>(params[i]);
        if (i != 0) out.add(", ");
        gen.emitTypePre(out, arg.getType());
        const char* name = arg.getName();
        out.space();
        if (name) out.add(name);
        else out.print("_arg%u", i);
    }
    if (fd.isVariadic()) {
        if (num_params) out.add(", ");
        out.add("...");
    }
    if (num_params == 0 && !fd.isVariadic()) out.add("void");
    out.add(")");
    if (d.hasAttrUnused()) out.add(" __attribute__((unused))");
    out.add(";\n\n");
}

func void Generator.emitAliasType(Generator* gen, string_buffer.Buf* out, Decl* d) {
    // For now, just generate canonicalType as RHS
    QualType qt = d.getType();
    qt = qt.getCanonicalType(); // just generate final type
    out.add("typedef ");
    gen.emitTypePre(out, qt);
    gen.emitTypePost(out, qt);
    out.space();
    gen.emitCName(out, d);
    out.add(";\n\n");
}

func bool emitAsDefine(const VarDecl* vd) {
    const Decl* d = cast<Decl*>(vd);

    if (!vd.getInit()) return false;
    QualType qt = d.getType();
    QualType canon = qt.getCanonicalType();
    if (!qt.isConst()) return false;
    if (qt.isVolatile()) return false;
    return canon.isBuiltin();
}

func void Generator.emitGlobalVarDecl(Generator* gen, string_buffer.Buf* out, Decl* d) {
    VarDecl* vd = cast<VarDecl*>(d);

    QualType qt = d.getType();

    // emit 'simple'-constants as a defines
    if (emitAsDefine(vd)) {
        out.add("#define ");
        gen.emitCName(out, d);
        out.add(" (");
        gen.emitExpr(out, vd.getInit());
        out.add(")\n\n");
        return;
    }

    if (gen.cur_external) {
        out.add("extern ");
    } else {
        if (!d.isExported()) out.add("static ");
    }

    gen.emitGlobalVarTypePre(out, qt);
    out.space();
    gen.emitCName(out, d);
    gen.emitGlobalVarTypePost(out, qt);

    if (vd.hasAttrWeak()) out.add(" __attribute__((weak))");

    const char* section = d.getSection();
    if (section) {
        out.space();
        gen.emitSectionAttr(out, section);
    }

    Expr* ie = vd.getInit();
    if (!gen.cur_external) {
        out.add(" = ");
        if (ie) {
            gen.emitExpr(out, ie);
        } else {
            // auto-initialize
            gen.emitAutoInit(out, qt);
        }
    }
    out.add(";\n\n");
}

func void Generator.emitSectionAttr(Generator* gen, string_buffer.Buf* out, const char* name) {
    out.print("__attribute__((section(\"%s\")))", name);
}

func void Generator.emitAutoInit(Generator* gen, string_buffer.Buf* out, QualType qt) {
    qt = qt.getCanonicalType();
    if (qt.isArray() || qt.isStruct()) {
        out.add("{ }");
    } else if (qt.isPointer()) {
        out.add("NULL");
    } else {
        out.add1('0');
    }
}

func void Generator.on_forward_structs(void* arg, Decl* d) {
    Generator* gen = arg;
    string_buffer.Buf* out = gen.out;

    if (!d.isStructType()) return;
    if (gen.cur_external && !d.isUsed()) return;

    gen.emitForwardStructDecl(d, out);
}

func void Generator.emitForwardStructDecl(Generator* gen, Decl* d, string_buffer.Buf* out) {
    StructTypeDecl* std = cast<StructTypeDecl*>(d);
    if (std.hasAttrNoTypeDef()) return;

    out.add("typedef ");
    if (std.isStruct()) out.add("struct");
    else out.add("union");
    out.space();
    gen.emitCName(out, d);
    out.add("_ ");
    gen.emitCName(out, d);
    out.add(";\n");
}

func void Generator.emitGlobalDecl(Generator* gen, Decl* d) {
    if (d.isGenerated()) return;

    // dont generate unused external decls
    if (gen.cur_external && !d.isUsed()) return;

    // TODO refactor getFragment to here, need EnumConstant case done

    switch (d.getKind()) {
    case Function:
        FunctionDecl* fd = cast<FunctionDecl*>(d);
        if (fd.isTemplate()) break;
        gen.gen_func_proto(fd, gen.out);
        gen.out.add(";\n");
        break;
    case Import:
        assert(0); // cannot happen
        return;
    case StructType:
        Fragment* f = gen.getFragment();
        gen.emitStruct(f.buf, d, 0);
        gen.addFragment(f);
        break;
    case EnumType:
        Fragment* f = gen.getFragment();
        gen.emitEnum(f.buf, d);
        gen.addFragment(f);
        break;
    case EnumConstant:
        // Can happen, we need to generate the containing enum then
        // TODO
        console.warn("TODO gen enum %d", d.isGenerated());
/*
        // Q: move this part to enum_constant_decl.c2? or ast_utils?
        QualType qt = d.getType();
        EnumType* et = qt.getEnumTypeOrNil();
        EnumTypeDecl* etd = et.getDecl();
*/
        break;
    case FunctionType:
        Fragment* f = gen.getFragment();
        gen.emitFunctionType(f.buf, d);
        gen.addFragment(f);
        break;
    case AliasType:
        Fragment* f = gen.getFragment();
        gen.emitAliasType(f.buf, d);
        gen.addFragment(f);
        break;
    case Var:
        Fragment* f = gen.getFragment();
        gen.emitGlobalVarDecl(f.buf, d);
        gen.addFragment(f);
        break;
    }

    d.setGenerated();
    gen.flattenFragments();
}

func void Generator.flattenFragments(Generator* gen) {
    while (!gen.used_list.isEmpty()) {
        linked_list.Element* e = gen.used_list.popFront();
        Fragment* f = to_container(Fragment, list, e);
        gen.out.add2(f.buf.data(), f.buf.size());
        gen.freeFragment(f);
    }
}

func void Generator.on_decl(void* arg, Decl* d) {
    Generator* gen = arg;
    gen.emitGlobalDecl(d);
}

func void Generator.on_vardecl(void* arg, VarDecl* vd) {
    Generator* gen = arg;
    gen.emitGlobalDecl(cast<Decl*>(vd));
}

func void Generator.on_ast_types(void* arg, AST* a) {
    a.visitTypeDecls(Generator.on_decl, arg);
}

func void Generator.gen_func_proto(Generator* gen, FunctionDecl* fd, string_buffer.Buf* out) {
    Decl* d = cast<Decl*>(fd);

    // template are generated as late as possible, since the Type might not be known yet
    if (fd.isTemplate()) return;

    if (d == gen.mainFunc) {
        out.add("int32_t main");
    } else {
        if (!gen.cur_external && !d.isExported()) out.add("static ");
        gen.emitTypePre(out, fd.getRType());
        out.space();
        gen.emitCName(out, d);
    }
    out.lparen();
    u32 num_params = fd.getNumParams();
    VarDecl** params = fd.getParams();
    for (u32 i=0; i<num_params; i++) {
        Decl* argx = cast<Decl*>(params[i]);
        if (i != 0) out.add(", ");
        gen.emitTypePre(out, argx.getType());
        out.space();
        const char* name = argx.getName();
        if (name) out.add(name);
        else out.print("_arg%u", i);
    }
    if (fd.isVariadic()) {
        if (num_params) out.add(", ");
        out.add("...");
    } else {
        if (num_params == 0) out.add("void");
    }
    out.rparen();
}


func void Generator.gen_func_forward_decl(void* arg, FunctionDecl* fd) {
    Generator* gen = arg;
    gen.emitGlobalDecl(cast<Decl*>(fd));
}

func void Generator.gen_func_protos(void* arg, AST* a) {
    a.visitFunctions(Generator.gen_func_forward_decl, arg);
}

func void Generator.emitFunction(Generator* gen, FunctionDecl* fd) {
    Fragment* f = gen.getFragment();
    string_buffer.Buf* out = f.buf;

    if (fd.hasAttrInline()) out.add("__inline__ ");
    if (fd.hasAttrWeak()) out.add("__attribute__((weak)) ");

    const char* section = fd.asDecl().getSection();
    if (section) {
        gen.emitSectionAttr(out, section);
        out.space();
    }

    gen.gen_func_proto(fd, out);
    out.newline();

    // work-around Stmts using gen.out
    string_buffer.Buf* saved = gen.out;
    gen.out = out;

    // TODO always emit indent/newline before Compound? (can have 'if (x) {' on single line)
    gen.emitStmt(cast<ast.Stmt*>(fd.getBody()), 0, true);
    out.newline();

    gen.out = saved;    // restore saved gen.out
    gen.addFragment(f);
}

func void Generator.gen_full_func(void* arg, FunctionDecl* fd) {
    Generator* gen = arg;
    // Note: there could be template functions that need to be generated, so generate full function in fragment

    // template are generated as late as possible, since the Type might not be known yet
    if (fd.isTemplate()) return;

    gen.cur_function = fd;

    gen.emitFunction(fd);

    gen.cur_function = nil;

    gen.flattenFragments();
}

func void Generator.gen_full_funcs(void* arg, AST* a) {
    a.visitFunctions(Generator.gen_full_func, arg);
}

func void Generator.on_interface_import(void* arg, ImportDecl* i) {
    Generator* gen = arg;
    Decl* d = cast<Decl*>(i);

    if (!d.isUsedPublic()) return;

    // filter duplicate imports between different ASTs
    u32 name_idx = d.getNameIdx();
    if (gen.imports.contains_idx(name_idx)) return;
    gen.imports.add(name_idx);

    // TODO also do Header part
    gen.emitC2Decl(d);

    gen.header_file.print("#include \"%s.h\"\n", d.getName());
}

func void Generator.create_interface_imports(void* arg, AST* a) {
    a.visitImports(Generator.on_interface_import, arg);
}

func void Generator.on_interface_decl(void* arg, Decl* d) {
    Generator* gen = arg;

    if (!d.isExported()) return;
    if (d.isImport()) return;

    gen.emitC2Decl(d);

    // create forward decl for structs
    if (d.isStructType()) {
        gen.emitForwardStructDecl(d, gen.header_file);
        gen.header_file.newline();
    }
    // For C header we need to sort the decls, just collect all decls first
    gen.decls.add(d);
}

func void Generator.create_interface_decls(void* arg, AST* a) {
    a.visitDecls(Generator.on_interface_decl, arg);
}

func void Generator.on_ast_vars(void* arg, AST* a) {
    a.visitVarDecls(Generator.on_vardecl, arg);
}

func void Generator.on_ast_structs(void* arg, AST* a) {
    Generator* gen = arg;
    a.visitTypeDecls(Generator.on_forward_structs, arg);
    gen.out.newline();
}

func void Generator.decl_mark_generated(void* /*arg*/, Decl* d) {
    d.setGenerated();
}

func void Generator.ast_mark_generated(void* arg, AST* a) {
    a.visitDecls(Generator.decl_mark_generated, arg);
}

func void Generator.on_header_decl(void* arg, Decl* d) {
    Generator* gen = arg;
    string_buffer.Buf* out = gen.header_file;

    // TODO refactor to common code with normal C generation (currently uses fragments)
    switch (d.getKind()) {
    case Function:
        FunctionDecl* fd = cast<FunctionDecl*>(d);
        if (fd.isTemplate()) break;
        gen.gen_func_proto(fd, out);
        out.add(";\n");
        out.newline();
        break;
    case Import:
        assert(0); // cannot happen
        return;
    case StructType:
        gen.emitStruct(out, d, 0);
        break;
    case EnumType:
        gen.emitEnum(out, d);
        break;
    case EnumConstant:
        assert(0);
        return;
    case FunctionType:
        gen.emitFunctionType(out, d);
        break;
    case AliasType:
        gen.emitAliasType(out, d);
        break;
    case Var:
        gen.emitGlobalVarDecl(out, d);
        break;
    }
}

func void add_gen_warning(string_buffer.Buf* out) {
    out.add("// WARNING: this file is auto-generated by the C2 compiler.\n");
    out.add("// Any changes you make might be lost!\n\n");
}

func void Generator.generateInterfaceFiles(Generator* gen, Module* m) {
    gen.imports.clear();
    gen.c2i_file.clear();
    gen.header_file.clear();
    gen.decls.clear();

    string_buffer.Buf* c2i = gen.c2i_file;
    add_gen_warning(c2i);
    c2i.print("module %s;\n\n", m.getName());

    string_buffer.Buf* hdr = gen.header_file;
    add_gen_warning(hdr);
    char[32] upper_name;
    string_utils.toUpper(m.getName(), upper_name);
    hdr.print("#ifndef %s_H\n", upper_name);
    hdr.print("#define %s_H\n\n", upper_name);
    hdr.add("#include \"c2types.h\"\n\n");
    hdr.add("#ifdef __cplusplus\nextern \"C\" {\n#endif\n");

    gen.cur_external = true;
    m.visitASTs(Generator.create_interface_imports, gen);
    if (gen.imports.length()) c2i.newline();
    hdr.newline();
    m.visitASTs(Generator.create_interface_decls, gen);

    // sort decls for C header
    // TODO dont re-create decl_sorter every time
    decl_sorter.Sorter* sorter = decl_sorter.create(m, &gen.decls, gen, Generator.on_header_decl);
    sorter.sort();

    hdr.add("#ifdef __cplusplus\n}\n#endif\n\n");
    hdr.add("#endif\n");

    char[256] fullname;
    // TODO use stringbuf for this
    stdio.sprintf(fullname, "%s/%s.c2i", gen.results_dir, m.getName());
    file_utils.Writer writer;
    if (!writer.write(fullname, cast<u8*>(c2i.data()), c2i.size())) {
        console.error(writer.getError());
    }

    // TODO use stringbuf for this
    stdio.sprintf(fullname, "%s/%s.h", gen.results_dir, m.getName());
    if (!writer.write(fullname, cast<u8*>(hdr.data()), hdr.size())) {
        console.error(writer.getError());
    }
}

func void Generator.on_module(void* arg, Module* m) {
    if (!m.isUsed()) return;

    Generator* gen = arg;
    string_buffer.Buf* out = gen.out;
    out.print("\n// --- module %s ---\n", m.getName());
    gen.mod_name = m.getName();
    gen.mod = m;

    // Note: special case for stdarg.h va_list
    if (m.getNameIdx() == gen.stdargName) {
        out.add("// Note: this module is a special case and is custom generated\n\n");
        out.add("#define va_list __builtin_va_list\n");
        out.add("#define va_start __builtin_va_start\n");
        out.add("#define va_end __builtin_va_end\n");
        out.newline();
        out.add("int32_t vdprintf(int32_t __fd, const char* __fmt, va_list __arg);\n");
        out.add("int32_t vsprintf(char* str, const char* format, va_list __ap);\n");
        out.add("int32_t vsnprintf(char* str, uint64_t size, const char* format, va_list __ap);\n");
        out.newline();

        // set all decls to generated
        m.visitASTs(Generator.ast_mark_generated, arg);
        return;
    }

    // TODO use dsm, like generateInterfaceFiles
    // generate forward decls of structs
    m.visitASTs(Generator.on_ast_structs, arg);

    m.visitASTs(Generator.on_ast_types, arg);
    m.visitASTs(Generator.gen_func_protos, arg);
    out.newline();
    m.visitASTs(Generator.on_ast_vars, arg);
    if (!gen.cur_external) {
        m.visitASTs(Generator.gen_full_funcs, arg);
    }

    if (m.isExported()) {
        gen.generateInterfaceFiles(m);
    }
}

func void Generator.init(Generator* gen,
                         const char* target,
                         build_target.Kind kind,
                         const char* results_dir,       // output/<target>/
                         const char* output_dir,        // output/<target>/cgen/
                         source_mgr.SourceMgr* sm,
                         const build_file.Info* build_info,
                         Decl* mainFunc,
                         u32 stdargName) {
    string.memset(gen, 0, sizeof(Generator));
    gen.out = string_buffer.create(256*1024, false, 3);
    gen.target = target;
    gen.target_kind = kind;
    gen.results_dir = results_dir;
    gen.output_dir = output_dir;
    gen.sm = sm;
    gen.build_info = build_info;
    gen.mainFunc = mainFunc;
    gen.stdargName = stdargName;
    gen.free_list.init();
    gen.used_list.init();
    gen.c2i_file = string_buffer.create(8192, false, 2);
    gen.header_file = string_buffer.create(8192, false, 2);
    gen.imports.init(nil);  // note: cannot get string values! (not needed)
    gen.decls.init(16);
}

func void Generator.free(Generator* gen) {
    while (!gen.free_list.isEmpty()) {
        linked_list.Element* e = gen.free_list.popFront();
        Fragment* f = to_container(Fragment, list, e);
        f.free();
    }
    gen.out.free();
    gen.c2i_file.free();
    gen.header_file.free();
    gen.imports.free();
    gen.decls.free();
}

func void Generator.write(Generator* gen, const char* output_dir, const char* filename) {
    //printf("%s\n", gen.out.data());

    char[256] fullname;
    stdio.sprintf(fullname, "%s/%s", output_dir, filename);

    file_utils.Writer writer;
    bool ok = writer.write(fullname, cast<u8*>(gen.out.data()), gen.out.size());
    if (!ok) {
        console.error(writer.getError());
    }
}

func void Generator.createMakefile(Generator* gen, const char* output_dir, component.List* comps, bool enable_asserts, bool fast_build) {
    string_buffer.Buf* out = gen.out;
    out.clear();
    out.add("# This makefile is auto-generated, any modifications will be lost\n\n");

    const char* cc = "gcc";
    const char* cflags = nil;
    const char* ldflags = "";
    const char* ldflags2 = "";

    if (gen.build_info) {
        const build_file.Info* info = gen.build_info;
        if (info.getCC()) cc = info.getCC();
        if (info.getCFlags()) cflags = info.getCFlags();
        if (info.getLdFlags()) ldflags = info.getLdFlags();
        if (info.getLdFlags2()) ldflags2 = info.getLdFlags2();
    }

    out.print("CC=%s\n", cc);

    out.add("CFLAGS=-Wall -Wextra -Wno-unused -Wno-switch -Wno-char-subscripts -Wno-zero-length-bounds -Wno-format-overflow -Wno-stringop-overflow\n");
    out.add("CFLAGS+=-pipe -std=c99 -Wno-missing-field-initializers\n");
    if (fast_build) out.add("CFLAGS+=-O0 -g\n");
    else out.add("CFLAGS+=-O2 -g\n");
    if (!enable_asserts) out.add("CFLAGS+=-DNDEBUG\n");
    if (cflags) out.print("CFLAGS+=%s\n", cflags);
    out.newline();

    out.print("LDFLAGS=%s\n", ldflags ? ldflags : "");
    out.print("LDFLAGS2=%s\n", ldflags2 ? ldflags2 : "");
    out.newline();

    char[128] target_name;

    switch (gen.target_kind) {
    case Executable:
        string.strcpy(target_name, gen.target);
        out.print("all: ../%s\n\n", target_name);
        out.print("../%s: build.c\n", target_name);
        out.print("\t\t$(CC) $(CFLAGS) -c build.c -o build.o\n");
        out.print("\t\t$(CC) $(LDFLAGS) -o ../%s build.o", target_name);
        for (u32 i=0; i<comps.size(); i++) {
            component.Component* c = comps.get(i);
            if (c.isExternal()) {
                const char* linkname = c.getLinkName();
                if (linkname) out.print(" -l%s", linkname);
            }
        }
        out.add(" $(LDFLAGS2)\n");
        out.newline();
        break;
    case StaticLibrary:
        stdio.sprintf(target_name, "lib%s.a", gen.target);
        out.print("all: ../%s\n\n", target_name);
        out.print("../%s: build.c\n", target_name);
	    out.print("\t\t$(CC) $(CFLAGS) -c build.c -o %s.o\n", gen.target);
	    out.print("\t\tar rcs ../%s %s.o\n", target_name, gen.target);
        break;
    case DynamicLibrary:
        stdio.sprintf(target_name, "lib%s.so", gen.target);
        out.print("all: ../%s\n\n", target_name);
        out.print("../%s: build.c\n", target_name);
	    out.print("\t\t$(CC) $(CFLAGS) -c build.c -o %s.o\n", gen.target);
	    out.print("\t\t$(CC) $(LDFLAGS) %s.o -shared -o ../%s -Wl,-soname,%s.1 -Wl,--version-script=exports.version $(LDFLAGS2)\n",
            gen.target, target_name, target_name);
        break;
    }

    out.newline();
    out.add("symbols:\n");
    out.print("\t\tnm -g -D -C --defined-only ../%s\n\n", target_name);

    out.add("clean:\n");
    out.print("\t\trm -f ../%s\n\n", target_name);

    gen.write(output_dir, "Makefile");
}

func void Generator.createExportsFile(Generator* gen, const char* output_dir, component.Component* mainComp) {
    if (gen.target_kind != build_target.Kind.DynamicLibrary) return;

    string_buffer.Buf* out = gen.out;
    out.clear();

    out.add("LIB_1.0 {\n");
    out.add("\tglobal:\n");

    // iterate all mods in main Component
    module_list.List* mods = mainComp.getModules();
    u32 count = mods.length();

    for (u32 i=0; i<count; i++) {
        const SymbolTable* symbols = mods.at(i).getSymbols();
        Decl** decls = symbols.getDecls();
        u32 num_symbols = symbols.size();
        for (u32 j=0; j<num_symbols; j++) {
            Decl* d = decls[j];
            if (!d.isExported()) continue;
            if (!d.isFunction() && !d.isVariable()) continue;
            out.add("\t\t");
            gen.emitCNameMod(out, d, d.getModule());
            out.add(";\n");
        }
    }

    out.add("\tlocal:\n\t\t*;\n");
    out.add("};\n");

    gen.write(output_dir, "exports.version");
}

func void Generator.generateC2TypesHeader(Generator* gen) {
    string_buffer.Buf* out = gen.c2i_file;
    out.clear();
    out.add("// WARNING: this file is auto-generated by the C2 compiler.\n");
    out.add("// Any changes you make might be lost!\n\n");

    out.add("#ifndef C2TYPES_H\n");
    out.add("#define C2TYPES_H\n");

    out.newline();
    out.add("#ifdef __cplusplus\nextern \"C\" {\n#endif\n\n");
    out.add("#define NULL ((void*)0)\n");
    // NOTE: might change for 32 bit?
    out.add("#define offsetof(TYPE, MEMBER) ((unsigned long) &((TYPE *)0)->MEMBER)\n");
    out.add("#define to_container(type, member, ptr) ((type *)((char *)(ptr)-(unsigned long)(&((type *)0)->member)))\n");

    out.newline();
    // NOTE: 64-bit only for now
    out.add("typedef signed char int8_t;\n");
    out.add("typedef unsigned char uint8_t;\n");
    out.add("typedef signed short int int16_t;\n");
    out.add("typedef unsigned short int uint16_t;\n");
    out.add("typedef signed int int32_t;\n");
    out.add("typedef unsigned int uint32_t;\n");
    out.add("typedef signed long int64_t;\n");
    out.add("typedef unsigned long uint64_t;\n");
    out.newline();
    out.add("#ifdef __cplusplus\n}\n#endif\n\n");

    out.add("#endif\n");

    char[256] fullname;
    // TODO use stringbuf for this
    stdio.sprintf(fullname, "%s/c2types.h", gen.results_dir);
    file_utils.Writer writer;
    if (!writer.write(fullname, cast<u8*>(out.data()), out.size())) {
        console.error(writer.getError());
    }
}

public func void generate(const char* target,
                          build_target.Kind kind,
                          const char* output_dir,
                          source_mgr.SourceMgr* sm,
                          build_file.Info* build_info,
                          Module* c2mod,
                          component.List* comps,
                          Decl* mainFunc,
                          u32 stdargName,
                          bool enable_asserts,
                          bool fast_build,
                          bool print_code)
{
    char[256] dir;
    stdio.sprintf(dir, "%s/%s", output_dir, Dir);
    i32 err = file_utils.create_directory(dir);
    if (err) {
        console.error("cannot create directory %s: %s", dir, string.strerror(err));
        return;
    }

    Generator gen;
    gen.init(target, kind, output_dir, dir, sm, build_info, mainFunc, stdargName);
    gen.enable_asserts = enable_asserts;
    string_buffer.Buf* out = gen.out;

    // generate C2-internal and external lib stuff in external.h, the main component in build.c

    out.add("#ifndef EXTERNAL_H\n");
    out.add("#define EXTERNAL_H\n\n");
    out.add("// --- internally added ---\n");
    out.add("#include <assert.h>\n\n"); // TEMP until we generate own assert (correct line/file)
    out.add("typedef unsigned char bool;\n");
    out.add("typedef signed char int8_t;\n");
    out.add("typedef unsigned char uint8_t;\n");
    out.add("typedef signed short int16_t;\n");
    out.add("typedef unsigned short uint16_t;\n");
    out.add("typedef signed int int32_t;\n");
    out.add("typedef unsigned int uint32_t;\n");
    out.add("typedef signed long int64_t;\n");
    out.add("typedef unsigned long uint64_t;\n");
    out.add("typedef long ssize_t;\n");
    out.add("typedef unsigned long size_t;\n");
    out.add("#define true 1\n");
    out.add("#define false 0\n");

    out.add("#define NULL ((void*)0)\n");
    out.add("#define ARRAY_SIZE(x) (sizeof(x)/sizeof(x[0]))\n");
    out.add("#define offsetof(TYPE, MEMBER) ((unsigned long) &((TYPE *)0)->MEMBER)\n");
    out.add("#define to_container(type, member, ptr) ((type *)((char *)(ptr)-(unsigned long)(&((type *)0)->member)))\n\n");

    if (enable_asserts) {
        // TODO or also wrap in NDEBUG?
        out.add("int dprintf(int fd, const char *format, ...);\n");
        out.add("void abort(void);\n\n");
        out.add("static void c2_assert(bool condition, const char* location, const char* condstr) {\n");
        out.add("  if (condition) return;\n");
        out.print("  static const char me[] = \"%s\";\n", target);
        out.add("  dprintf(2, \"%s: %s: Assertion '%s' failed\\n\", me, location, condstr);\n");
        out.add("  abort();\n");
        out.add("}\n\n");
    }

    gen.cur_external = false;
    Generator.on_module(&gen, c2mod);

    if (gen.target_kind != build_target.Kind.Executable) {
        gen.generateC2TypesHeader();
    }

    // generate external components
    for (u32 i=0; i<comps.size()-1; i++) {
        component.Component* c = comps.get(i);
        gen.cur_external = c.isExternal();
        c.visitModules(Generator.on_module, &gen);
    }

    out.add("#endif\n");
    gen.write(dir, "external.h");

    // generate main module
    out.clear();
    add_gen_warning(out);
    out.add("#include \"external.h\"\n\n");
    gen.cur_external = false;
    component.Component* c = comps.get(comps.size()-1);
    c.visitModules(Generator.on_module, &gen);

    if (print_code) stdio.puts(gen.out.data());
    gen.write(dir, "build.c");

    gen.createMakefile(dir, comps, enable_asserts, fast_build);
    gen.createExportsFile(dir, comps.get(comps.size()-1));

    gen.free();
}

public func void build(const char* output_dir)
{
    // TODO put in aux-pool? (lowers stack-size)
    char[256] dir;
    stdio.sprintf(dir, "%s/%s/", output_dir, Dir);

    i32 retval = process_utils.run(dir, "/usr/bin/make", LogFile);
    if (retval != 0) {
        console.error("error during external C compilation");
        console.log("see %s%s for defails", dir, LogFile);
    }
}


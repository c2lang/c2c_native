/* Copyright 2022-2023 Bas van den Berg
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

module deps_generator;

import ast local;
import component;
import string_buffer;
import string_pool;
import file_utils;
import ast_visitor;

import string local;
import stdlib local;
import stdio local;

type Dir struct {
    u32 name_idx;
    u32 indent; // of children
    string_buffer.Buf* buf;

    Dir* subdirs;
    u32 count;
    u32 capacity;
}

func void Dir.init(Dir* d, u32 name_idx, u32 indent) {
    d.name_idx = name_idx;
    d.indent = indent;
    d.buf = string_buffer.create(16*1024, false, 2);
}

func Dir* Dir.get(Dir* d, u32 name_idx) {
    // search for subdir, create if it doesn't exist
    for (u32 i=0; i<d.count; i++) {
        if (d.subdirs[i].name_idx == name_idx) return &d.subdirs[i];
    }

    if (d.count == d.capacity) {
        d.capacity += 2;
        Dir* subdirs = calloc(d.capacity, sizeof(Dir));
        if (d.subdirs) {
            memcpy(subdirs, d.subdirs, d.count * sizeof(Dir));
            free(d.subdirs);
        }
        d.subdirs = subdirs;
    }
    //stdio.printf("CREATE %u %u\n", d.indent + 1, name_idx);
    Dir* subdir = &d.subdirs[d.count];
    subdir.init(name_idx, d.indent + 1);
    d.count++;

    return subdir;
}

func void Dir.free(Dir* d) {
    for (u32 i=0; i<d.count; i++) {
        d.subdirs[i].free();
    }
    free(d.subdirs);
    d.buf.free();
}

func void Dir.flatten(const Dir* d, string_buffer.Buf* out, string_pool.Pool* pool) {
    // flattens subdirs into passed buf
    for (u32 i=0; i<d.count; i++) {
        const Dir* subdir = &d.subdirs[i];
        const char* dirname = pool.idx2str(subdir.name_idx);

        out.indent(d.indent);
        //printf("DIR %u  %s\n", d.indent, dirname);
        out.print("<group name='%s' full='dir:%s' collapsed='1'>\n", dirname, dirname);

        out.add2(subdir.buf.data(), subdir.buf.size());
        subdir.flatten(out, pool);

        out.indent(d.indent);
        out.add("</group>\n");
    }
}

type Generator struct {
    const char* curfile;
    bool is_external;  // current component is external

    ast_visitor.Visitor* visitor;

    // TODO use vector
    Decl** deps;
    u32 deps_count;
    u32 deps_capacity;

    Dir root;
    string_pool.Pool* pool;

    string_buffer.Buf* out; // no owership, points to Buf inside Dir
    u32 indent;

    // options
    bool show_dirs;
    bool show_files;
    bool show_externals;
    //bool show_private;
}

func void Generator.on_decl(void* arg, const Decl* d, bool global) {
    Generator* gen = arg;

    switch (d.getKind()) {
    case Function:
        FunctionDecl* fd = cast<FunctionDecl*>(d);
        gen.handleTypeRef(fd.getTypeRef());
        break;
    case Import:
        return;
    case StructType:
        StructTypeDecl* s = cast<StructTypeDecl*>(d);
        u32 num_members = s.getNumMembers();
        Decl** members = s.getMembers();
        for (u32 i=0; i<num_members; i++) {
            Generator.on_decl(arg, members[i], false);
        }
        break;
    case EnumType:
        EnumTypeDecl* etd = cast<EnumTypeDecl*>(d);
        EnumConstantDecl** constants = etd.getConstants();
        for (u32 i=0; i<etd.getNumConstants(); i++) {
            Decl* ecd = cast<Decl*>(constants[i]);
            // NOTE: we add the symbol here, since we have the EnumDecl here
            char[64] fullname;
            sprintf(fullname, "%s.%s", d.getName(), ecd.getName());
            //gen.refs.add_symbol(fullname, &dest);

            Generator.on_decl(arg, ecd, false);
        }
        break;
    case EnumConstant:
        break;
    case FunctionType:
        break;
    case AliasType:
        break;
    case Var:
        const VarDecl* v = cast<VarDecl*>(d);
        gen.handleTypeRef(v.getTypeRef());
        break;
    case StaticAssert:
        return;
    case ArrayValue:
        return;
    }

    gen.visitor.handle(d);
}

func void Generator.handleTypeRef(Generator* gen, const TypeRef* ref) {
    const Ref* prefix = ref.getPrefix();
    if (prefix) gen.handleRef(prefix);

    const Ref* user = ref.getUser();
    if (user) gen.handleRef(user);
}

func void Generator.handleRef(Generator* gen, const Ref* ref) {
    if (!ref.decl) {
        fprintf(stderr, "refs: Decl not set!\n");
        return;
    }

    if (!ast.isGlobal(ref.decl)) return;
    if (ref.decl.getKind() == DeclKind.EnumConstant) return;

    gen.deps_add(ref.decl);
}

func void Generator.on_global_decl(void* arg, Decl* d) {
    Generator* gen = arg;

    if (!d.isUsed() && gen.is_external) return;
    if (d.getKind() == DeclKind.Import) return;

    gen.out.indent(gen.indent);
    gen.out.print("<atom name='%s' full='%s'>\n", ast.getPrefixedName(d), d.getFullName());

    gen.indent++;
    gen.deps_count = 0;
    Generator.on_decl(arg, d, true);

    for (u32 i=0; i<gen.deps_count; i++) {
        Decl* dep = gen.deps[i];
        // TODO check options (show-private, show-external, show-files, show-dirs, etc)
        gen.out.indent(gen.indent);
        gen.out.print("<dep dest='%s' str='1'/>\n", dep.getFullName());
    }

    gen.indent--;

    gen.out.indent(gen.indent);
    gen.out.add("</atom>\n");
}

func void Generator.on_ast(void* arg, AST* a) {
    Generator* gen = arg;
    gen.curfile = a.getFilename();

    gen.out.indent(gen.indent);
    gen.out.print("<group name='%s' full='file:%s' collapsed='1'>\n", gen.curfile, gen.curfile);

    gen.indent++;
    a.visitDecls(Generator.on_global_decl, arg);
    gen.indent--;

    gen.out.indent(gen.indent);
    gen.out.add("</group>\n");
}

func void Generator.on_module(void* arg, Module* m) {
    Generator* gen = arg;

    if (gen.is_external && !m.isUsed()) return;

    u32 saved_indent = gen.indent;
    gen.out = gen.root.buf;
    gen.indent = gen.root.indent;

    if (!gen.is_external && gen.show_dirs) {
        // find correct (sub)dir group
        const char* filename = m.getFirstFilename();
        const char* cp = filename;
        Dir* d = &gen.root;
        const char* start = cp;
        while (*cp) {
            if (*cp == '/') {
                u32 len = cast<u32>(cp - start);
                u32 name_idx = gen.pool.add(start, len, true);
                start = cp+1;
                d =  d.get(name_idx);
            }
            cp++;
        }
        gen.out = d.buf;
        gen.indent = d.indent;
    }


    gen.out.indent(gen.indent);
    gen.out.print("<group name='%s' full='module:%s' collapsed='1'>\n", m.getName(), m.getName());

    gen.indent++;
    m.visitASTs(Generator.on_ast, arg);
    gen.indent--;
    gen.indent = saved_indent;

    gen.out.indent(gen.indent);
    gen.out.add("</group>\n");
}

func void Generator.on_ref(void* arg, const Ref* ref) {
    Generator* gen = arg;
    gen.handleRef(ref);
}

func void Generator.deps_resize(Generator* gen, u32 capacity) {
    gen.deps_capacity = capacity;

    Decl** deps = malloc(capacity * sizeof(Decl*));
    if (gen.deps_count) {
        memcpy(cast<void*>(deps), cast<void*>(gen.deps), gen.deps_count * sizeof(Decl*));
        free(cast<void*>(gen.deps));
    }

    gen.deps = deps;
}

func void Generator.deps_add(Generator* gen, Decl* d) {
    // check if already in list
    for (u32 i=0; i<gen.deps_count; i++) {
        if (gen.deps[i] == d) {
            return;
        }
    }

    if (gen.deps_count == gen.deps_capacity) gen.deps_resize(gen.deps_capacity * 2);

    // add
    gen.deps[gen.deps_count] = d;
    gen.deps_count++;
}

public func void generate(const char* title, const char* output_dir, component.Component** c, u32 count) {
    Generator gen = { nil }

    // TODO options to be passed later
    gen.show_dirs = true;
    gen.show_files = true;
    gen.show_externals = true;

    gen.visitor = ast_visitor.create(&gen, Generator.on_ref);
    gen.root.init(0, 3);
    gen.deps_resize(64);
    gen.pool = string_pool.create(4096, 64);

    gen.out = gen.root.buf;
    string_buffer.Buf* out = gen.out;


    out.add("<?xml version=\"1.0\" encoding=\"UTF-8\"?>\n");
    out.print("<dsm name='%s'>\n", title);
    gen.indent++;
    out.indent(gen.indent);
    out.add("<model>\n");

    gen.indent++;

    for (u32 i=0; i<count; i++) {
        const component.Component* comp = c[i];
        gen.is_external = comp.isExternal();
        out.indent(gen.indent);
        out.print("<group name='%s' full='component:%s' collapsed='0'>\n", comp.getName(), comp.getName());
        gen.indent++;
        comp.visitModules(Generator.on_module, &gen);

        // TODO dir in multiple writes, dont copy (measure speed)
        gen.root.flatten(gen.root.buf, gen.pool);

        gen.indent--;
        out.indent(gen.indent);
        out.add("</group>\n");
    }

    gen.indent--;

    out.indent(gen.indent);
    out.add("</model>\n");
    gen.indent--;
    out.add("</dsm>\n");

    char[128] outfile;
    sprintf(outfile, "%s/%s", output_dir, "deps2.xml");
    file_utils.Writer file;
    file.write(outfile, cast<u8*>(out.data()), out.size());

    gen.visitor.free();

    free(cast<void*>(gen.deps));
    gen.root.free();
    gen.pool.free();
}


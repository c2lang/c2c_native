module c2c_main;

import file_utils;
import c2_tokenizer local;

import stdio local;
import string local;
import c_errno local;
import sys_time;

func u64 now() {
    sys_time.Timeval tv;
    sys_time.gettimeofday(&tv, nil);
    u64 now64 = cast<u64>(tv.tv_sec);
    now64 *= 1000000;
    now64 += tv.tv_usec;
    return now64;
}

char[1024*1024] alloc_buf;
u32 alloc_idx;

func const char* alloc(void* arg, const char* value, u32 len) {
    char* start = alloc_buf + alloc_idx;
    memcpy(start, value, len);
    alloc_idx += len;
    alloc_buf[alloc_idx] = 0;
    alloc_idx++;
    return start;
}

func void parse_file(const char* data) {
    Tokenizer tokenizer;
    char[256] error_msg;
    error_msg[0] = 0;
    alloc_idx = 0;
    tokenizer.init(data, 0, error_msg, alloc, nil);
    Token tok;
    tok.init();

    u32[enum_max(TokenKind)] count = { 0 }

    u64 t1 = now();
    u32 token_count = 0;
    while (tok.more) {
        tokenizer.lex(&tok);
        //tok.dump();
        //count[tok.kind]++;
        token_count++;
    }
    u64 t2 = now();

    printf("--- %u tokens (%u alloc)  (%llu usec) ---\n", token_count, alloc_idx, t2 - t1);
    for (u8 i=0; i<elemsof(count); i++) {
        if (count[i] == 0) continue;
        printf("%5u  %10s  %4.1f %%\n", count[i], kind2str(i), count[i] * 100.0 / token_count);
    }
}

public func i32 main(i32 argc, char** argv) {
    if (argc < 2) {
        printf("usage: %s [file]\n", argv[0]);
        return -1;
    }

    for (i32 i=1; i<argc; i++) {
        const char* filename = argv[i];
        printf("------ %s ------\n", filename);

        file_utils.Reader file;
        if (!file.open(filename)) {
            fprintf(stderr, "Error opening file: %s\n", strerror(*errno2()));
            return -1;
        }

        for (i32 j=0; j<100; j++) {
            parse_file(file.data());
        }

        file.close();
    }

	return 0;
}

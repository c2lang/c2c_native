/* Copyright 2022 Bas van den Berg
*/

module c2c_main;

import c2recipe;
import compiler;
import constants;
import diagnostics;
import source_mgr;
import string_pool;
import utils;

import stdlib;
import stdio local;

type Options struct {
    bool print_ast;
    bool print_modules;
    bool print_symbols;
    const char* single_file;
}

func void usage(const char* me) {
    printf("Usage: %s <options> <filename>\n", me);
    printf("Options\n");
    printf("\t-a    print ASTs\n");
    printf("\t-m    print modules\n");
    printf("\t-s    print symbols\n");
    stdlib.exit(-1);
}

func void parse_opts(i32 argc, char** argv, Options* opts) {
    for (i32 i=1; i<argc; i++) {
        const char* arg = argv[i];
        if (arg[0] == '-') {
            switch (arg[1]) {
            case 'a':
                opts.print_ast = true;
                break;
            case 'm':
                opts.print_modules = true;
                break;
            case 's':
                opts.print_symbols = true;
                break;
            default:
                printf("unknown option\n");
                usage(argv[0]);
                break;
            }
        } else {
            if (opts.single_file) usage(argv[0]);
            opts.single_file = arg;
        }
    }
}

public func i32 main(i32 argc, char** argv) {
    Options opts = { 0 }

    parse_opts(argc, argv, &opts);

    // note: auxPool is used by recipe, build-file and manifests
    string_pool.Pool* auxPool = string_pool.create(64*1024, 0);

    source_mgr.SourceMgr* sm = source_mgr.create();
    diagnostics.Diags* diags = diagnostics.create(sm, utils.useColor());

    c2recipe.Recipe* recipe = c2recipe.create(sm, auxPool);

    i32 recipe_id = -1;

    if (opts.single_file) {
        recipe.addDummyTarget(opts.single_file);
    } else {
        if (!utils.findProjectDir()) {
            fprintf(stderr, "c2c: error: cannot find C2 root dir\n");
            fprintf(stderr, "c2c requires a %s file in the project root\n", constants.recipe_name);
            fprintf(stderr, "Use argument -h for a list of available opts and usage of c2c\n");
            return -1;
        }

        i32 yaml_id = sm.open(constants.recipe_name, 0);
        if (yaml_id == -1) return -1;
        if (!recipe.parse(yaml_id)) return -1;

        // TEMP also parse yaml version
        recipe_id = sm.open("c2recipe.yml", 0);
        if (recipe_id == -1) return -1;
        if (!recipe.parseYaml(recipe_id)) return -1;
    }

    //recipe.dump();

    compiler.Options options = { 0 }
    options.print_ast = opts.print_ast;
    options.print_modules = opts.print_modules;
    options.print_symbols = opts.print_symbols;

    for (u32 i=0; i<recipe.numTargets(); i++) {
        c2recipe.Target* target = recipe.getTarget(i);
        printf("building %s\n", target.getName());

        compiler.build(auxPool, sm, diags, target, &options);

        if (recipe_id != -1) sm.clear(cast<u32>(recipe_id));
    }

    recipe.free();
    auxPool.free();
    diags.free();
    sm.free();

	return 0;
}


/* Copyright 2022-2023 Bas van den Berg
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

module c2_parser;

//import ast_builder local;   // is used!!
import ast local;
import token local;
import src_loc local;
import ctype;

/// PrecedenceLevels - These have been altered from C99 to C2
/// In particular, addition now comes after bitwise and shifts
/// Bitwise is directly after shift and equality and relational have
/// the same precedence.

type Prec enum u8 {
    Unknown         = 0,    // Not binary operator.
    Comma           = 1,    // ,
    Assignment      = 2,    // =, *=, /=, %=, +=, -=, <<=, >>=, &=, ^=, |=
    Conditional     = 3,    // ?
    LogicalAndOr    = 4,    // &&, ||
    Relational      = 5,    // ==, !=, >=, <=, >, <
    Additive        = 6,    // -, +
    Bitwise         = 7,    // ^, |, &
    Shift           = 8,    // <<, >>
    Multiplicative  = 9,    // *, /, %
}

func Expr* Parser.parseExpr(Parser* p) {
    Expr* lhs = p.parseAssignmentExpression();
    return p.parseRHSOfBinaryExpression(lhs, Prec.Comma);
}

func Expr* Parser.parseAssignmentExpression(Parser* p) {
    Expr* lhs = p.parseCastExpr(false, false);
    return p.parseRHSOfBinaryExpression(lhs, Prec.Assignment);
}

const Prec[128] BinOpPrecLookup = {
    [Kind.Comma] = Prec.Comma,

    [Kind.Equal] = Prec.Assignment,
    [Kind.StarEqual] = Prec.Assignment,
    [Kind.SlashEqual] = Prec.Assignment,
    [Kind.PercentEqual] = Prec.Assignment,
    [Kind.PlusEqual] = Prec.Assignment,
    [Kind.MinusEqual] = Prec.Assignment,
    [Kind.LessLessEqual] = Prec.Assignment,
    [Kind.GreaterGreaterEqual] = Prec.Assignment,
    [Kind.AmpEqual] = Prec.Assignment,
    [Kind.CaretEqual] = Prec.Assignment,
    [Kind.PipeEqual] = Prec.Assignment,

    [Kind.Question] = Prec.Conditional,

    [Kind.PipePipe] = Prec.LogicalAndOr,
    [Kind.AmpAmp] = Prec.LogicalAndOr,

    [Kind.ExclaimEqual] = Prec.Relational,
    [Kind.EqualEqual] = Prec.Relational,
    [Kind.LessEqual] = Prec.Relational,
    [Kind.Less] = Prec.Relational,
    [Kind.Greater] = Prec.Relational,
    [Kind.GreaterEqual] = Prec.Relational,

    [Kind.Plus] = Prec.Additive,
    [Kind.Minus] = Prec.Additive,

    [Kind.Pipe] = Prec.Bitwise,
    [Kind.Caret] = Prec.Bitwise,
    [Kind.Amp] = Prec.Bitwise,

    [Kind.LessLess] = Prec.Shift,
    [Kind.GreaterGreater] = Prec.Shift,

    [Kind.Percent] = Prec.Multiplicative,
    [Kind.Slash] = Prec.Multiplicative,
    [Kind.Star] = Prec.Multiplicative,
}


// Note: all empty entries are also 0 (= BinaryOpcode.Multiply)
const BinaryOpcode[128] BinOpTokenLookup = {
    [Kind.Star]           = BinaryOpcode.Multiply,
    [Kind.Slash]          = BinaryOpcode.Divide,
    [Kind.Percent]        = BinaryOpcode.Reminder,
    [Kind.Plus]           = BinaryOpcode.Add,
    [Kind.Minus]          = BinaryOpcode.Subtract,
    [Kind.LessLess]       = BinaryOpcode.ShiftLeft,
    [Kind.GreaterGreater] = BinaryOpcode.ShiftRight,
    [Kind.Less]           = BinaryOpcode.LessThan,
    [Kind.Greater]        = BinaryOpcode.GreaterThan,
    [Kind.LessEqual]      = BinaryOpcode.LessEqual,
    [Kind.GreaterEqual]   = BinaryOpcode.GreaterEqual,
    [Kind.EqualEqual]     = BinaryOpcode.Equal,
    [Kind.ExclaimEqual]   = BinaryOpcode.NotEqual,
    [Kind.Amp]            = BinaryOpcode.And,
    [Kind.Caret]          = BinaryOpcode.Xor,
    [Kind.Pipe]           = BinaryOpcode.Or,
    [Kind.AmpAmp]         = BinaryOpcode.LAnd,
    [Kind.PipePipe]       = BinaryOpcode.LOr,
    [Kind.Equal]          = BinaryOpcode.Assign,
    [Kind.StarEqual]      = BinaryOpcode.MulAssign,
    [Kind.SlashEqual]     = BinaryOpcode.DivAssign,
    [Kind.PercentEqual]   = BinaryOpcode.RemAssign,
    [Kind.PlusEqual]      = BinaryOpcode.AddAssign,
    [Kind.MinusEqual]     = BinaryOpcode.SubAssign,
    [Kind.LessLessEqual]  = BinaryOpcode.ShlAssign,
    [Kind.GreaterGreaterEqual] = BinaryOpcode.ShrASsign,
    [Kind.AmpEqual]       = BinaryOpcode.AndAssign,
    [Kind.CaretEqual]     = BinaryOpcode.XorAssign,
    [Kind.PipeEqual]      = BinaryOpcode.OrAssign,
}

func Expr* Parser.parseRHSOfBinaryExpression(Parser* p, Expr* lhs, Prec minPrec) {
    Prec nextTokPrec = BinOpPrecLookup[p.tok.kind];

    SrcLoc colonLoc = 0;
    while (1) {
        if (nextTokPrec < minPrec) return lhs;

        if (p.tok.kind == Kind.Comma) return lhs;

        Token opToken = p.tok;
        p.consumeToken();

        Expr* ternaryMiddle = nil;
        if (nextTokPrec == Prec.Conditional) {
            if (p.tok.kind == Kind.Colon) {
                p.error("TODO conditional expr"); // or warning?
            } else {
                ternaryMiddle = p.parseExpr();
            }

            if (p.tok.kind == Kind.Colon) {
                colonLoc = p.tok.loc;
                p.consumeToken();
            }
        }

        Expr* rhs = p.parseCastExpr(false, false);

        Prec thisPrec = nextTokPrec;
        nextTokPrec = BinOpPrecLookup[p.tok.kind];

        bool isRightAssoc = (thisPrec == Prec.Conditional || thisPrec == Prec.Assignment);

        if (thisPrec < nextTokPrec || (thisPrec == nextTokPrec && isRightAssoc)) {
            rhs = p.parseRHSOfBinaryExpression(rhs, thisPrec + !isRightAssoc);

            nextTokPrec = BinOpPrecLookup[p.tok.kind];
        }

        if (ternaryMiddle) {
            lhs = p.builder.actOnConditionalOperator(opToken.loc, colonLoc, lhs, ternaryMiddle, rhs);
        } else {
            BinaryOpcode opcode = BinOpTokenLookup[opToken.kind];
            lhs = p.builder.actOnBinaryOperator(opToken.loc, opcode, lhs, rhs);
        }
    }
    return nil;
}

const u8[128] CastExprTokenLookup = {
    // TODO order actions by frequency (less jump)
    //[Kind.Comma] = Prec.Comma,
    [Kind.Identifier] = 1,
    [Kind.NumberLiteral] = 2,
    [Kind.CharLiteral] = 3,
    [Kind.StringLiteral] = 4,
    [Kind.LParen] = 5,
    [Kind.Star] = 6,
    [Kind.Tilde] = 6,
    [Kind.Minus] = 6,
    [Kind.Exclaim] = 6,
    [Kind.Amp] = 7,
    [Kind.KW_cast] = 8,
    [Kind.Plus] = 9,
    [Kind.PlusPlus] = 10,
    [Kind.MinusMinus] = 10,
    [Kind.KW_elemsof] = 11,
    [Kind.KW_enum_min] = 12,
    [Kind.KW_enum_max] = 12,
    [Kind.KW_false] = 13,
    [Kind.KW_true] = 13,
    [Kind.KW_nil] = 14,
    [Kind.KW_offsetof] = 15,
    [Kind.KW_sizeof] = 16,
    [Kind.KW_to_container] = 17,
}

// Note: tried lookup table, was slower..
func UnaryOpcode convertTokenToUnaryOpcode(Kind kind) {
    switch (kind) {
    case Exclaim: return UnaryOpcode.LNot;
    case Star: return UnaryOpcode.Deref;
    case Amp: return UnaryOpcode.AddrOf;
    case PlusPlus:   return UnaryOpcode.PreInc;
    case Minus: return UnaryOpcode.Minus;
    case MinusMinus: return UnaryOpcode.PreDec;
    case Tilde: return UnaryOpcode.Not;
    default:
        assert(0);
        break;
    }
    return UnaryOpcode.PreInc;
}

func Expr* Parser.parseCastExpr(Parser* p, bool /*isUnaryExpr*/, bool /*isAddrOfOp*/) {
    // This handles all of cast-expression, unary-expression, postfix-expression,
    // and primary-expression.  We handle them together like this for efficiency
    // and to simplify handling of an expression starting with a '(' token: which
    // may be one of a parenthesized expression, cast-expression, compound literal
    // expression, or statement expression.
    //
    // If the parsed tokens consist of a primary-expression, the cases below
    // break out of the switch;  at the end we call ParsePostfixExpressionSuffix
    // to handle the postfix expression suffixes.  Cases that cannot be followed
    // by postfix exprs should return without invoking
    // ParsePostfixExpressionSuffix.

    Kind savedKind = p.tok.kind;
    Expr* res = nil;
    bool couldBeTemplateCall = false;

    switch (CastExprTokenLookup[savedKind]) {
    case 0:
        p.error("syntax error");
        break;
    case 1:
        // Fast-path, keep checking multiple identifier.identifier. etc
        // parse them all in a single MemberExpr
        //TODO res = p.parseFullIdentifier(); (is a bit slower, since it checks again)
        Token t2 = p.tokenizer.lookahead(1);
        if (t2.kind == Kind.Dot) {
            res = p.parsePureMemberExpr();
        } else {
            res = p.parseIdentifier().asExpr();
        }
        couldBeTemplateCall = true;
/*
        // Make sure to pass down the right value for isAddressOfOperand.
        if (isAddressOfOperand && isPostfixExpressionSuffixStart())
            isAddressOfOperand = false;
*/
        break;
    case 2:
        res = p.builder.actOnIntegerLiteral(p.tok.loc, p.tok.radix, p.tok.number_value);
        p.consumeToken();
        break;
    case 3:
        res = p.builder.actOnCharLiteral(p.tok.loc, p.tok.char_value);
        p.consumeToken();
        break;
    case 4:
        res = p.parseStringLiteral();
        break;
    case 5:
        res = p.parseParenExpr();
        break;
    case 6:
        SrcLoc loc = p.tok.loc;
        p.consumeToken();
        res = p.parseCastExpr(false, false);
        UnaryOpcode opcode = convertTokenToUnaryOpcode(savedKind);
        return p.builder.actOnUnaryOperator(loc, opcode, res);
    case 7:
        SrcLoc loc = p.tok.loc;
        p.consumeToken();
        res = p.parseCastExpr(false, true);
        UnaryOpcode opcode = convertTokenToUnaryOpcode(savedKind);
        return p.builder.actOnUnaryOperator(loc, opcode, res);
    case 8:
        return p.parseExplicitCastExpr();
    case 9:
        // ignore unary +
        p.consumeToken();
        return p.parseCastExpr(false, false);
    case 10:
        SrcLoc loc = p.tok.loc;
        p.consumeToken();
        res = p.parseCastExpr(false, false);
        UnaryOpcode opcode = convertTokenToUnaryOpcode(savedKind);
        return p.builder.actOnUnaryOperator(loc, opcode, res);
    case 11:
        res = p.parseElemsof();
        break; // TODO not return?
    case 12:
        return p.parseEnumMinMax(savedKind == Kind.KW_enum_min);
    case 13:
        res = p.builder.actOnBooleanConstant(p.tok.loc, savedKind == Kind.KW_true);
        p.consumeToken();
        break;
    case 14:
        res = p.builder.actOnNilExpr(p.tok.loc);
        p.consumeToken();
        break;
    case 15:
        return p.parseOffsetOfExpr();
    case 16:
        return p.parseSizeof();
    case 17:
        return p.parseToContainerExpr();
    }
    return p.parsePostfixExprSuffix(res, couldBeTemplateCall);
}

func Expr* Parser.parsePostfixExprSuffix(Parser* p, Expr* lhs, bool couldBeTemplateCall) {
    while (1) {
        switch (p.tok.kind) {
        case Identifier:
            return lhs;
        case LParen:
            // C2: can only be call expr?
            lhs = p.parseCallExpr(lhs);
            break;
        case LSquare:
            p.consumeToken();
            Expr* idx = p.parseExpr();
            if (p.tok.kind  == Kind.Colon) {
                // BitOffset <expr> : <expr>
                SrcLoc colLoc = p.tok.loc;
                p.consumeToken();
                Expr* rhs = p.parseExpr();
                // TODO need colLoc?
                idx = p.builder.actOnBitOffsetExpr(colLoc, idx, rhs);
            }
            SrcLoc rloc = p.tok.loc;
            p.expectAndConsume(Kind.RSquare);
            lhs = p.builder.actOnArraySubscriptExpr(rloc, lhs, idx);
            break;
        case Dot:
            lhs = p.parseImpureMemberExpr(lhs);
            break;
        case PlusPlus: fallthrough;
        case MinusMinus:
            lhs = p.builder.actOnPostFixUnaryOperator(p.tok.loc, p.tok.kind, lhs);
            p.consumeToken();
            break;
        case Less:
            if (couldBeTemplateCall && p.isTemplateFunctionCall()) {
                p.consumeToken();

                TypeRefHolder ref;
                ref.init();
                p.parseTypeSpecifier(&ref, false, false);

                p.expectAndConsume(Kind.Greater);
                lhs = p.parseTemplateCallExpr(lhs, &ref);
            }
            return lhs;
        default:
            return lhs;
        }
    }
    assert(0);
    // TODO should not come here
    return nil;
}

func Expr* Parser.parseCallExpr(Parser* p, Expr* fn) {
    p.consumeToken(); // '('

    ExprList args;
    args.init(4);

    while (p.tok.kind != Kind.RParen) {

        args.add(p.parseExpr());

        if (p.tok.kind == Kind.RParen) break;
        p.expectAndConsume(Kind.Comma);
    }
    SrcLoc endLoc = p.tok.loc;
    p.expectAndConsume(Kind.RParen);
    Expr* res = p.builder.actOnCallExpr(endLoc, fn, args.getExprs(), args.size());
    args.free();
    return res;
}

func Expr* Parser.parseTemplateCallExpr(Parser* p, Expr* fn, const TypeRefHolder* ref) {
    p.consumeToken(); // '('

    ExprList args;
    args.init(4);

    while (p.tok.kind != Kind.RParen) {

        args.add(p.parseExpr());

        if (p.tok.kind == Kind.RParen) break;
        p.expectAndConsume(Kind.Comma);
    }
    SrcLoc endLoc = p.tok.loc;
    p.expectAndConsume(Kind.RParen);
    Expr* res = p.builder.actOnTemplateCallExpr(endLoc, fn, args.getExprs(), args.size(), ref);
    args.free();
    return res;
}

func Expr* Parser.parseImpureMemberExpr(Parser* p, Expr* base) {
    // a[x].b.c, a[x] is base
    p.consumeToken();   // dot
    p.expectIdentifier();

    // TODO dont use Ref (since we dont need Decl*)
    Ref[MemberExprMaxDepth] ref;

    ref[0].loc = p.tok.loc;
    ref[0].name_idx = p.tok.text_idx;
    u32 refcount = 1;
    p.consumeToken();

    while (p.tok.kind == Kind.Dot) {
        p.consumeToken();
        p.expectIdentifier();

        if (refcount == elemsof(ref)) p.error("max member depth is %u", MemberExprMaxDepth);

        ref[refcount].loc = p.tok.loc;
        ref[refcount].name_idx = p.tok.text_idx;
        refcount++;
        p.consumeToken();
    }

    return p.builder.actOnMemberExpr(base, ref, refcount);
}

func Expr* Parser.parsePureMemberExpr(Parser* p) {
    // parse as many <id>.<id>.<id> and store them in a single member-expr
    Ref[MemberExprMaxDepth] ref;

    ref[0].loc = p.tok.loc;
    ref[0].name_idx = p.tok.text_idx;
    u32 refcount = 1;
    p.consumeToken();

    while (p.tok.kind == Kind.Dot) {
        p.consumeToken();
        p.expectIdentifier();

        if (refcount == elemsof(ref)) p.error("max member depth is %u", MemberExprMaxDepth);

        ref[refcount].loc = p.tok.loc;
        ref[refcount].name_idx = p.tok.text_idx;
        refcount++;
        p.consumeToken();
    }

    return p.builder.actOnMemberExpr(nil, ref, refcount);
}

func IdentifierExpr* Parser.parseIdentifier(Parser* p) {
    IdentifierExpr* e = p.builder.actOnIdentifier(p.tok.loc, p.tok.text_idx);
    p.consumeToken();
    return e;
}

func Expr* Parser.parseStringLiteral(Parser* p) {
    Expr* e = p.builder.actOnStringLiteral(p.tok.loc, p.tok.text_idx, p.tok.text_len);
    p.consumeToken();
    return e;
}

func Expr* Parser.parseParenExpr(Parser* p) {
    SrcLoc loc = p.tok.loc;
    p.consumeToken();

    Expr* res = p.parseExpr();
    p.expectAndConsume(Kind.RParen);

    return p.builder.actOnParenExpr(loc, res);
}

func bool Parser.isTemplateFunctionCall(Parser* p) {
    assert(p.tok.kind == Kind.Less);
    // check if tokens after < could be type, followed by >(
    // longest token line could be mod.type**
    u32 ahead = 1;

    // fast path, just for builtintype
    Token t = p.tokenizer.lookahead(ahead);
    //p.dump_token(&t);
    if (t.kind >= Kind.KW_bool && t.kind <= Kind.KW_void) return true;

    // TODO keep mini state machine: 0 initial, 1-> seen id (dot changes nothing), 3-> * seen. if id in 3, return false (a*b)
    while (ahead < 8) {
        t = p.tokenizer.lookahead(ahead);
        switch (t.kind) {
        case Identifier: fallthrough;
        case Star: fallthrough;
        case Dot:
            break;
        case Greater:
            t = p.tokenizer.lookahead(ahead+1);
            return t.kind == Kind.LParen;
        case KW_const:
            return true;
        default:
            return false;
        }
        ahead++;
    }
    return false;
}

func bool couldBeType(Token tok) {
    if (tok.kind != Kind.Identifier) return false;

    // Note: might still be Type, since we dont know
    // Even Upper case might be a Constant/Type
    // TODO use lookahead (looking for [ or . or *)
    const char* name = idx2name(tok.text_idx);
    return ctype.isupper(name[0]) != 0;
}

func Expr* Parser.parseSizeof(Parser* p) {
    SrcLoc loc = p.tok.loc;
    p.consumeToken();

    p.expectAndConsume(Kind.LParen);

    Expr* res = nil;
    if (p.builtinArgIsType() || couldBeType(p.tok)) { // parse as type
        TypeRefHolder ref;
        ref.init();
        p.parseTypeSpecifier(&ref, false, true);
        res = p.builder.actOnTypeExpr(p.tok.loc, &ref);
    } else { // parse as variable
        if (p.tok.kind != Kind.Identifier) {
            p.error("expect a type or variable name");
        }
        res = p.parseFullIdentifier();

        // parse optional array indexes (like array[0])
        while (p.tok.kind == Kind.LSquare) {
            p.consumeToken();
            Expr* idx = p.parseExpr();
            SrcLoc rloc = p.tok.loc;
            p.expectAndConsume(Kind.RSquare);
            res = p.builder.actOnArraySubscriptExpr(rloc, res, idx);
        }
    }

    p.expectAndConsume(Kind.RParen);

    return p.builder.actOnBuiltinExpr(loc, res, BuiltinExprKind.Sizeof);
}

func Expr* Parser.parseElemsof(Parser* p) {
    SrcLoc loc = p.tok.loc;
    p.consumeToken();

    p.expectAndConsume(Kind.LParen);

    p.expectIdentifier();

    // TODO parse as TypeRefholder (can have module.prefix and array index
    Expr* res = p.parseFullIdentifier();
    res = p.parsePostfixExprSuffix(res, false);

    p.expectAndConsume(Kind.RParen);
    return p.builder.actOnBuiltinExpr(loc, res, BuiltinExprKind.Elemsof);
}

func Expr* Parser.parseInitValue(Parser* p, bool* need_semi, bool allow_designators) {
    switch (p.tok.kind) {
    case LBrace:
        *need_semi = false;
        return p.parseInitList();
    case Dot:
        if (!allow_designators) p.error("designator not allowed here");
        return p.parseFieldDesignator(need_semi);
    case LSquare:
        if (!allow_designators) p.error("designator not allowed here");
        return p.parseArrayDesignator(need_semi);
    default:
        break;
    }
    *need_semi = true;
    return p.parseAssignmentExpression();
}

func Expr* Parser.parseInitList(Parser* p) {
    SrcLoc left = p.tok.loc;
    p.expectAndConsume(Kind.LBrace);

    ExprList values;
    values.init(8);

    while (p.tok.kind != Kind.RBrace) {
        bool unused;
        Expr* e = p.parseInitValue(&unused, true);
        values.add(e);
        if (p.tok.kind == Kind.Comma) {
            p.consumeToken();
        } else {
            break;
        }
    }

    SrcLoc right = p.tok.loc;
    p.expectAndConsume(Kind.RBrace);
    Expr* e = p.builder.actOnInitList(left, right, values.getExprs(), values.size());
    values.free();
    return e;
}

func Expr* Parser.parseFieldDesignator(Parser* p, bool* need_semi) {
    p.consumeToken();   // .
    p.expectIdentifier();
    u32 field = p.tok.text_idx;
    SrcLoc loc = p.tok.loc;
    p.consumeToken();

    p.expectAndConsume(Kind.Equal);
    Expr* value = p.parseInitValue(need_semi, false);

    return p.builder.actOnFieldDesignatedInit(field, loc, value);
}

func Expr* Parser.parseArrayDesignator(Parser* p, bool* need_semi) {
    SrcLoc loc = p.tok.loc;
    p.consumeToken();   // '['

    Expr* designator = p.parseAssignmentExpression();
    p.expectAndConsume(Kind.RSquare);
    p.expectAndConsume(Kind.Equal);

    Expr* initValue = p.parseInitValue(need_semi, false);
    return p.builder.actOnArrayDesignatedInit(loc, designator, initValue);
}

func Expr* Parser.parseExplicitCastExpr(Parser* p) {
    SrcLoc loc = p.tok.loc;
    p.consumeToken();

    p.expectAndConsume(Kind.Less);
    TypeRefHolder ref;
    ref.init();
    p.parseTypeSpecifier(&ref, true, false);
    p.expectAndConsume(Kind.Greater);

    p.expectAndConsume(Kind.LParen);
    Expr* expr = p.parseExpr();
    p.expectAndConsume(Kind.RParen);

    return p.builder.actOnExplicitCast(loc, &ref, expr);
}

// Syntax:
//   'enum_min' '(' type-name ')'
//   'enum_max' '(' type-name ')'
func Expr* Parser.parseEnumMinMax(Parser* p, bool is_min) {
    SrcLoc loc = p.tok.loc;
    p.consumeToken();
    p.expectAndConsume(Kind.LParen);

    p.expectIdentifier();
    // TODO parse as TypeRefHolder
    Expr* expr = p.parseExpr();
    p.expectAndConsume(Kind.RParen);

    return p.builder.actOnBuiltinExpr(loc, expr, is_min ? BuiltinExprKind.EnumMin : BuiltinExprKind.EnumMax);
}

func Expr* Parser.parseOffsetOfExpr(Parser* p) {
    SrcLoc loc = p.tok.loc;
    p.consumeToken();
    p.expectAndConsume(Kind.LParen);

    // TODO use TypeRefHolder (modified version to only allow identifiers)
    Expr* structExpr = p.parseFullIdentifier();

    p.expectAndConsume(Kind.Comma);

    Expr* member = p.parseFullIdentifier();

    p.expectAndConsume(Kind.RParen);

    return p.builder.actOnOffsetOfExpr(loc, structExpr, member);
}

func Expr* Parser.parseToContainerExpr(Parser* p) {
    SrcLoc loc = p.tok.loc;
    p.consumeToken();
    p.expectAndConsume(Kind.LParen);

    Expr* structExpr = p.parseFullIdentifier();

    p.expectAndConsume(Kind.Comma);

    Expr* member = p.parseFullIdentifier();

    p.expectAndConsume(Kind.Comma);

    Expr* pointer = p.parseExpr();

    p.expectAndConsume(Kind.RParen);

    return p.builder.actOnToContainerExpr(loc, structExpr, member, pointer);
}

// Syntax:
//   identifier
//   identifier.identifier
//   identifier.identifier.identifier etc
func Expr* Parser.parseFullIdentifier(Parser* p) {
    p.expectIdentifier();

    Token t2 = p.tokenizer.lookahead(1);
    if (t2.kind == Kind.Dot) {
        return p.parsePureMemberExpr();
    }
    return p.parseIdentifier().asExpr();
}


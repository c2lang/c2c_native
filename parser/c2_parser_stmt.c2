/* Copyright 2022-2023 Bas van den Berg
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

module c2_parser;

import stmt_list;
import ast local;
import token local;
import src_loc local;

func Stmt* Parser.parseStmt(Parser* p) {
    // TODO use Jump Table (combined one for multiple purposes?)
    // TODO also jump to Expr part, since most entries use parseExprStmt (dont lookup again in Expr)
    switch (p.tok.kind) {
    case Identifier:
        return p.parseDeclOrStmt();
    case LBrace:
        return cast<Stmt*>(p.parseCompoundStmt());
    case RBrace:
        p.error("expected stmt");
        break;
    case KW_asm:
        return p.parseAsmStmt();
    case KW_assert:
        return p.parseAssertStmt();
    case KW_break:
        return p.parseBreakStmt();
    case KW_continue:
        return p.parseContinueStmt();
    case KW_do:
        return p.parseDoStmt();
    case KW_fallthrough:
        return p.parseFallthroughStmt();
    case KW_for:
        return p.parseForStmt();
    case KW_goto:
        return p.parseGotoStmt();
    case KW_if:
        return p.parseIfStmt();
    case KW_return:
        return p.parseReturnStmt();
    case KW_switch:
        return p.parseSwitchStmt(false);
    case KW_sswitch:
        return p.parseSwitchStmt(true);
    case KW_bool:   fallthrough;
    case KW_char:   fallthrough;
    case KW_const:   fallthrough;
    case KW_i8:    fallthrough;
    case KW_i16:   fallthrough;
    case KW_i32:   fallthrough;
    case KW_i64:   fallthrough;
    case KW_isize:   fallthrough;
    case KW_f32:   fallthrough;
    case KW_f64:   fallthrough;
    case KW_local:   fallthrough;
    case KW_reg8:    fallthrough;
    case KW_reg16:   fallthrough;
    case KW_reg32:   fallthrough;
    case KW_reg64:   fallthrough;
    case KW_u8:    fallthrough;
    case KW_u16:   fallthrough;
    case KW_u32:   fallthrough;
    case KW_u64:   fallthrough;
    case KW_usize:   fallthrough;
    case KW_volatile:   fallthrough;
    case KW_void:
        return p.parseDeclStmt(true, true);
    case KW_while:
        return p.parseWhileStmt();
    default:
        return p.parseExprStmt();
    }
    return nil;
}

/*
    Declarations (type + name)
        a* b <init> -> yes
        a[] b <init> -> yes
        a[10] b -> yes
        a*[] b <init> -> yes
        a b <init> -> yes
        a.b c -> yes
    Assignments/Function calls
        a = ..     -> no
        a *= .. etc -> no
        a() -> no
        a[10] = .. -> no
        a.b.c .. -> no
        a.b->.. -> no
*/
func bool Parser.isTypeSpec(Parser* p) {
    assert(p.tok.kind == Kind.Identifier);
    Token t;

    // State: 0 = ID1, 1 = ID2, 2 = pointers, 3 = arrays
    u32 state = 0;
    u32 lookahead = 1;
    while (1) {
        Token t2 = p.tokenizer.lookahead(lookahead);
        switch (t2.kind) {
        case Identifier:
            goto type_done;
        case LSquare:
            lookahead = p.skipArray(lookahead);
            state = 3;
            break;
        case Star:
            if (state == 3) return false; // a[1] * ..
            state = 2;
            lookahead++;
            break;
        case Dot:
            if (state == 0) {
                Token t3 = p.tokenizer.lookahead(lookahead+1);
                if (t3.kind != Kind.Identifier) {
                    // syntax error
                    return false;
                }
                state = 2;
                lookahead += 2;
            } else {
                return false; // a.b.c
            }
            break;
        default:
            goto type_done;
        }
    }
type_done:
    // if token after type is identifier, it's a decl, otherwise it's not
    t = p.tokenizer.lookahead(lookahead);
    return t.kind == Kind.Identifier;
}

func u32 Parser.skipArray(Parser* p, u32 lookahead) {
    lookahead++;

    u32 depth =1;
    while (depth) {
        Token next = p.tokenizer.lookahead(lookahead);
        switch (next.kind) {
        case LSquare:
            depth++;
            break;
        case RSquare:
            depth--;
            break;
        case Eof:
            p.error("unexpected end-of-file");
            break;
        default:
            break;
        }
        lookahead++;
    }

    return lookahead;
}

/*
  Syntax:
    Number num = .     // id = type
    Utils.Type t = .  // id = module.type
    myfunc()        // id = func
    Mod.func()     // id = module.func
    count =         // id = var
    Mod.var =      // id = module.var
    id:             // id = label
*/
func Stmt* Parser.parseDeclOrStmt(Parser* p) {
    assert(p.tok.kind == Kind.Identifier);
    bool isDecl = p.isTypeSpec();

    if (isDecl) return p.parseDeclStmt(true, true);

    Token next = p.tokenizer.lookahead(1);
    if (next.kind == Kind.Colon) return p.parseLabelStmt();

    return p.parseExprStmt();
}

func CompoundStmt* Parser.parseCompoundStmt(Parser* p) {
    p.expectAndConsume(Kind.LBrace);

    stmt_list.List stmts;
    stmts.init();   // TODO register somewhere to free memory on error (register handler?)

    while (p.tok.kind != Kind.RBrace) {
        stmts.add(p.parseStmt());
    }

    SrcLoc endLoc = p.tok.loc;
    p.expectAndConsume(Kind.RBrace);

    CompoundStmt* s = p.builder.actOnCompoundStmt(endLoc, stmts.getData(), stmts.size());
    stmts.free();

    return s;
}

func Stmt* Parser.parseAsmStmt(Parser* p) {
    p.consumeToken();
    u32 quals = p.parseOptionalTypeQualifier();

    p.expectAndConsume(Kind.LParen);

    // TEMP skip until matching paren
    u32 depth = 1;
    while (1) {
        if (p.tok.kind == Kind.Eof) break;
        if (p.tok.kind == Kind.LParen) depth++;
        if (p.tok.kind == Kind.RParen) depth--;
        if (depth == 0) break;
        p.consumeToken();
    }

    p.expectAndConsume(Kind.RParen);
    p.expectAndConsume(Kind.Semicolon);

    return nil;
}

func Stmt* Parser.parseAssertStmt(Parser* p) {
    p.consumeToken();
    p.expectAndConsume(Kind.LParen);
    Expr* inner = p.parseExpr();
    p.expectAndConsume(Kind.RParen);
    p.expectAndConsume(Kind.Semicolon);
    return p.builder.actOnAssertStmt(inner);
}

func Stmt* Parser.parseBreakStmt(Parser* p) {
    SrcLoc loc = p.tok.loc;
    p.consumeToken();

    p.expectAndConsume(Kind.Semicolon);
    return p.builder.actOnBreakStmt(loc);
}

func Stmt* Parser.parseContinueStmt(Parser* p) {
    SrcLoc loc = p.tok.loc;
    p.consumeToken();

    p.expectAndConsume(Kind.Semicolon);
    return p.builder.actOnContinueStmt(loc);
}

func Stmt* Parser.parseFallthroughStmt(Parser* p) {
    SrcLoc loc = p.tok.loc;
    p.consumeToken();

    p.expectAndConsume(Kind.Semicolon);
    return p.builder.actOnFallthroughStmt(loc);
}

func Stmt* Parser.parseCondition(Parser* p) {
    p.expectAndConsume(Kind.LParen);

    Stmt* s;
    if (p.isDeclaration()) {
        s = p.parseDeclStmt(false, false);
    } else {
        Expr* cond = p.parseExpr();
        s = cond.asStmt();
    }
    p.expectAndConsume(Kind.RParen);
    return s;
}

func Stmt* Parser.parseIfStmt(Parser* p) {
    p.consumeToken();

    Stmt* cond = p.parseCondition();
    Stmt* then = p.parseStmt();

    Stmt* else_stmt = nil;
    if (p.tok.kind == Kind.KW_else) {
        p.consumeToken();
        else_stmt = p.parseStmt();
    }

    return p.builder.actOnIfStmt(cond, then, else_stmt);
}

func Stmt* Parser.parseReturnStmt(Parser* p) {
    SrcLoc loc = p.tok.loc;
    p.consumeToken();

    Expr* ret = nil;
    if (p.tok.kind != Kind.Semicolon) {
        ret = p.parseExpr();
    }

    p.expectAndConsume(Kind.Semicolon);
    return p.builder.actOnReturnStmt(loc, ret);
}

func Stmt* Parser.parseSwitchStmt(Parser* p, bool is_sswitch) {
    SrcLoc loc = p.tok.loc;
    p.consumeToken();
    Stmt* cond = p.parseCondition();

    p.expectAndConsume(Kind.LBrace);

    stmt_list.List cases;
    cases.init();

    while (p.tok.kind != Kind.RBrace) {
        Stmt* c = nil;
        switch (p.tok.kind) {
        case KW_case:
            c = p.parseCaseStmt();
            break;
        case KW_default:
            c = p.parseDefaultStmt();
            break;
        default:
            p.error("expected 'case' or 'default' keyword");
            break;
        }
        cases.add(c);
    }

    p.expectAndConsume(Kind.RBrace);

    Stmt* s = p.builder.actOnSwitchStmt(loc, cond, cases.getData(), cases.size(), is_sswitch);
    cases.free();
    return s;
}

func Stmt* Parser.parseCaseStmt(Parser* p) {
    SrcLoc loc = p.tok.loc;
    p.consumeToken();

    Expr* cond = p.parseExpr();

    p.expectAndConsume(Kind.Colon);

    stmt_list.List stmts;
    stmts.init();

    bool more = true;
    while (more) {
        switch (p.tok.kind) {
        case RBrace: fallthrough;
        case KW_case:  fallthrough;
        case KW_default:
            more = false;
            break;
        default:
            stmts.add(p.parseStmt());
            break;
        }
    }

    Stmt* s = p.builder.actOnCaseStmt(loc, cond, stmts.getData(), stmts.size());
    stmts.free();
    return s;
}

func Stmt* Parser.parseDefaultStmt(Parser* p) {
    SrcLoc loc = p.tok.loc;
    p.consumeToken();

    p.expectAndConsume(Kind.Colon);

    stmt_list.List stmts;
    stmts.init();

    bool more = true;
    while (more) {
        switch (p.tok.kind) {
        case RBrace: fallthrough;
        case KW_case:  fallthrough;
        case KW_default:
            more = false;
            break;
        default:
            stmts.add(p.parseStmt());
            break;
        }
    }

    Stmt* s = p.builder.actOnDefaultStmt(loc, stmts.getData(), stmts.size());
    stmts.free();
    return s;
}

func Stmt* Parser.parseDoStmt(Parser* p) {
    p.consumeToken();

    Stmt* then = p.parseStmt();
    p.expectAndConsume(Kind.KW_while);
    Stmt* cond = p.parseCondition();

    p.expectAndConsume(Kind.Semicolon);
    return p.builder.actOnDoStmt(cond, then);
}

func Stmt* Parser.parseForStmt(Parser* p) {
    p.consumeToken();

    p.expectAndConsume(Kind.LParen);

    // init
    Stmt* init_ = nil;
    if (p.tok.kind != Kind.Semicolon) {
        // parseCondition
        if (p.isDeclaration()) {
            init_ = p.parseDeclStmt(false, false);
        } else {
            init_ = p.parseExpr().asStmt();
        }
    }
    p.expectAndConsume(Kind.Semicolon);

    // cond
    Expr* cond = nil;
    if (p.tok.kind != Kind.Semicolon) {
        cond = p.parseExpr();
    }
    p.expectAndConsume(Kind.Semicolon);

    // incr
    Expr* incr = nil;
    if (p.tok.kind != Kind.RParen) {
        incr = p.parseExpr();
    }
    p.expectAndConsume(Kind.RParen);

    Stmt* body = p.parseStmt();

    return p.builder.actOnForStmt(init_, cond, incr, body);
}

func Stmt* Parser.parseWhileStmt(Parser* p) {
    p.consumeToken();

    Stmt* cond = p.parseCondition();
    Stmt* then = p.parseStmt();

    return p.builder.actOnWhileStmt(cond, then);
}

func Stmt* Parser.parseDeclStmt(Parser* p, bool checkSemi, bool allowLocal) {
    bool has_local = false;
    if (p.tok.kind == Kind.KW_local) {
        has_local = true;
        if (!allowLocal) p.error("local is not allowed here");
        p.consumeToken();
    }

    TypeRefHolder ref;
    ref.init();
    p.parseTypeSpecifier(&ref, true, true);

    p.expectIdentifier();

    u32 name = p.tok.text_idx;
    SrcLoc loc = p.tok.loc;
    p.consumeToken();

    // TODO same as parseVarDecl()
    bool need_semi = true;
    Expr* initValue = nil;
    if (p.tok.kind == Kind.Equal) {
        p.consumeToken();
        initValue = p.parseInitValue(&need_semi, false);
    }

    Stmt* s = p.builder.actOnVarDeclStmt(name, loc, &ref, initValue, has_local);
    if (checkSemi && need_semi) {
        p.expectAndConsume(Kind.Semicolon);
    }

    return s;
}

func Stmt* Parser.parseExprStmt(Parser* p) {
    Expr* e = p.parseExpr();
    p.expectAndConsume(Kind.Semicolon);
    return e.asStmt();
}

func Stmt* Parser.parseLabelStmt(Parser* p) {
    u32 name =  p.tok.text_idx;
    SrcLoc loc = p.tok.loc;
    p.consumeToken(); // identifier
    p.expectAndConsume(Kind.Colon);
    return p.builder.actOnLabelStmt(name, loc);
}

func Stmt* Parser.parseGotoStmt(Parser* p) {
    p.consumeToken();
    p.expectIdentifier();
    u32 name =  p.tok.text_idx;
    SrcLoc loc = p.tok.loc;
    p.consumeToken();
    p.expectAndConsume(Kind.Semicolon);
    return p.builder.actOnGotoStmt(name, loc);
}

// TODO rename: detect whether we have a Type
func bool Parser.isDeclaration(Parser* p) {
    const Kind kind = p.tok.kind;
    if (kind == Kind.Identifier) return p.isTypeSpec();
    if (kind >= Kind.KW_bool && kind <= Kind.KW_void) return true;
    return false;
}

/*
    Type:
        <builtins>
        A*
        m.A*
    Variable:
        A       -> could be Type, check later
        m.A     -> could be Type, check later
        a.b
        a[0]
*/
func bool Parser.parseAsType(Parser* p) {
    const Kind kind = p.tok.kind;
    if (kind >= Kind.KW_bool && kind <= Kind.KW_void) return true;

    u32 lookahead = 1;
    while (1) {
        Token t2 = p.tokenizer.lookahead(lookahead);
        switch (t2.kind) {
        case Identifier:
            break;
        case Star:
            return true;
        case Dot:
            break;
        case LSquare:
            return false;
        default:
            return false;
        }
        lookahead++;
    }

    return false;
}


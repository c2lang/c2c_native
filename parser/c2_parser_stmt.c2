/* Copyright 2022 Bas van den Berg
*/

module c2_parser;

import ast_builder local;
import ast_utils local;
import ast local;
import tokens local;
import utils local;

import stdio local; // TEMP

func Stmt* Parser.parseStmt(Parser* p) {
    // NOTE: keep in order
    switch (p.tok.kind) {
    case Kind.LBrace:
        return p.parseCompoundStmt();
    case Kind.KW_do:
        return p.parseDoStmt();
    case Kind.KW_for:
        return p.parseForStmt();
    case Kind.KW_if:
        return p.parseIfStmt();
    case Kind.KW_return:
        return p.parseReturnStmt();
    case Kind.KW_while:
        return p.parseWhileStmt();
    default:
        return nil; // TEMP
        break;
    }
    return nil;
}

func Stmt* Parser.parseCompoundStmt(Parser* p) {
    p.expectAndConsume(Kind.LBrace);

    StmtList stmts;
    stmts.init();   // TODO register somewhere to free memory on error (register handler?)

    while (1) {
        if (p.tok.kind == Kind.RBrace) break;

        Stmt* s = p.parseStmt();
        stmts.add(s);
    }

    p.expectAndConsume(Kind.RBrace);

    //p.builder.actOnCompoundStmt(&stmts);
    Stmt* s = p.builder.actOnCompoundStmt();
    stmts.free();

    return s;
}

func Expr* Parser.parseCondition(Parser* p) {
    p.expectAndConsume(Kind.LParen);
    Expr* cond = p.parseExpr();
    p.expectAndConsume(Kind.RParen);
    return cond;
}

func Stmt* Parser.parseIfStmt(Parser* p) {
    p.consumeToken();

    Expr* cond = p.parseCondition();
    Stmt* then = p.parseStmt();

    Stmt* else_stmt = nil;
    if (p.tok.kind == Kind.KW_else) {
        p.consumeToken();
        else_stmt = p.parseStmt();
    }

    return p.builder.actOnIfStmt(cond, then, else_stmt);
}

func Stmt* Parser.parseReturnStmt(Parser* p) {
    SrcLoc loc = p.tok.loc;
    p.consumeToken();

    Expr* ret = nil;
    if (p.tok.kind != Kind.Semicolon) {
        ret = p.parseExpr();
    }

    p.expectAndConsume(Kind.Semicolon);
    return p.builder.actOnReturnStmt(loc, ret);
}

func Stmt* Parser.parseDoStmt(Parser* p) {
    p.consumeToken();

    Stmt* then = p.parseStmt();
    p.expectAndConsume(Kind.KW_while);
    Expr* cond = p.parseCondition();

    p.expectAndConsume(Kind.Semicolon);
    return p.builder.actOnDoStmt(cond, then);
}

func Stmt* Parser.parseForStmt(Parser* p) {
    p.consumeToken();

    p.expectAndConsume(Kind.LParen);

    // init
    Stmt* init_ = nil;
    if (p.tok.kind != Kind.Semicolon) {
        // TODO check if declaration
        init_ = p.parseExpr();
    }
    p.expectAndConsume(Kind.Semicolon);

    // cond
    Expr* cond = nil;
    if (p.tok.kind != Kind.Semicolon) {
        cond = p.parseExpr();
    }
    p.expectAndConsume(Kind.Semicolon);

    // incr
    Expr* incr = nil;
    if (p.tok.kind != Kind.RParen) {
        incr = p.parseExpr();
    }
    p.expectAndConsume(Kind.RParen);

    Stmt* body = p.parseStmt();

    return p.builder.actOnForStmt(init_, cond, incr, body);
}

func Stmt* Parser.parseWhileStmt(Parser* p) {
    p.consumeToken();

    Expr* cond = p.parseCondition();
    Stmt* then = p.parseStmt();

    return p.builder.actOnWhileStmt(cond, then);
}


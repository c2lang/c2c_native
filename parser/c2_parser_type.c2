/* Copyright 2022 Bas van den Berg
*/

module c2_parser;

//import ast_builder local;
import ast local;
import token local;
import utils local;

import ctype local;

func void Parser.parseTypeDef(Parser* p, bool is_public) {
    p.consumeToken();
    p.expectIdentifier();
    u32 type_name = p.tok.text_idx;
    u32 type_loc = p.tok.loc;
    const char* name = p.info.pool.idx2str(type_name);
    if (!isupper(name[0])) p.error("type name must start with upper-case character");

    p.consumeToken();


    switch (p.tok.kind) {
    case KW_func:
        p.parseFunctionType(type_name, type_loc, is_public);
        break;
    case KW_struct:
        p.parseStructType(true, type_name, type_loc, is_public);
        break;
    case KW_union:
        p.parseStructType(false, type_name, type_loc, is_public);
        break;
    case KW_enum:
        p.parseEnumType(type_name, type_loc, is_public);
        break;
    default:
        p.parseAliasType(type_name, type_loc, is_public);
        break;
    }
}

func void Parser.parseFunctionType(Parser* p, u32 name, u32 loc, bool is_public) {
    p.consumeToken(); // func

    TypeRefHolder rtype;
    rtype.init();
    p.parseSingleTypeSpecifier(&rtype, true);

    DeclList params;
    params.init(4);

    bool is_variadic = p.parseFunctionParams(&params, is_public, false);
    Decl* f = p.builder.actOnFunctionTypeDecl(name, loc, is_public, &rtype, cast<VarDecl**>(params.getDecls()), params.size(), is_variadic);
    p.parseOptionalAttributes(); // (f)
    params.free();
    p.expectAndConsume(Kind.Semicolon);
}

func void Parser.parseStructType(Parser* p, bool is_struct, u32 name, u32 loc, bool is_public) {
    p.consumeToken();

    DeclList members;
    members.init(8);
    p.parseStructBlock(&members, is_public);
    StructTypeDecl* d = p.builder.actOnStructType(name, loc, is_public, is_struct, true, cast<VarDecl**>(members.getDecls()), members.size());
    members.free();

    p.parseOptionalAttributes(); // (d)
}

func void Parser.parseStructBlock(Parser* p, DeclList* members, bool is_public) {
    p.expectAndConsume(Kind.LBrace);

    while (1) {
        //Syntax:
        // struct_member ::= type_qualifier type_specifier.
        // struct_member ::= STRUCT <IDENTIFIER> LBRACE struct_block RBRACE.
        // struct_member ::= UNION <IDENTIFIER> LBRACE struct_block RBRACE.

        if (p.tok.kind == Kind.RBrace) break;
        if (p.tok.kind == Kind.KW_union || p.tok.kind == Kind.KW_struct) {
            bool is_struct = p.tok.kind == Kind.KW_struct;
            p.consumeToken();
            // name is optional
            u32 name = 0;
            u32 loc = 0;
            if (p.tok.kind == Kind.Identifier) {
                name = p.tok.text_idx;
                loc = p.tok.loc;
                p.consumeToken();
            }

            DeclList sub_members;
            sub_members.init(4);

            p.parseStructBlock(&sub_members, is_public);
            StructTypeDecl* member = p.builder.actOnStructType(name, loc, is_public, is_struct, false, cast<VarDecl**>(sub_members.getDecls()), sub_members.size());
            sub_members.free();
            members.add(member.asDecl());
        } else {
            TypeRefHolder ref;
            ref.init();
            p.parseTypeSpecifier(&ref, true);

            u32 name = 0;
            SrcLoc loc;
            if (p.tok.kind == Kind.Colon) { // anonymous bit-field
                loc = p.tok.loc;
            } else {
                p.expectIdentifier();
                name = p.tok.text_idx;
                loc = p.tok.loc;
                p.consumeToken();
            }

            Expr* bitfield = nil;
            if (p.tok.kind == Kind.Colon) {
                p.consumeToken();
                bitfield = p.parseExpr();
            }
            VarDecl* member = p.builder.actOnStructMember(name, loc, is_public, &ref, bitfield);
            members.add(member.asDecl());

            p.expectAndConsume(Kind.Semicolon);
        }
    }

    p.expectAndConsume(Kind.RBrace);
}

func void Parser.parseEnumType(Parser* p, u32 name, u32 loc, bool is_public) {
    p.consumeToken();

    // TODO lookup table (is_type?)
    switch (p.tok.kind) {
    case KW_char:
        break;
    case KW_f32: fallthrough;
    case KW_f64:
        p.error("enum type must be an integer");
        break;
    case KW_i8:  fallthrough;
    case KW_i16: fallthrough;
    case KW_i32: fallthrough;
    case KW_i64: fallthrough;
    case KW_isize:
        break;
    case KW_reg8:  fallthrough;
    case KW_reg16: fallthrough;
    case KW_reg32: fallthrough;
    case KW_reg64:
        p.error("enum type must be an integer");
        break;
    case KW_u8:  fallthrough;
    case KW_u16: fallthrough;
    case KW_u32: fallthrough;
    case KW_u64: fallthrough;
    case KW_usize:
        break;
    case KW_void:
        p.error("enum type must be an integer");
        break;
    default:
        p.error("expected enum type");
        break;
    }
    QualType qt = p.builder.actOnBuiltinType(tokKindToBuiltinKind(p.tok.kind));
    p.consumeToken();

    p.expectAndConsume(Kind.LBrace);

    bool is_incr = false;

    DeclList constants;
    constants.init(16);

    if (p.tok.kind == Kind.Plus) {
        is_incr = true;
        p.consumeToken();
        p.error("TODO incremental enum");
    } else {

        while (p.tok.kind == Kind.Identifier) {
            u32 const_name = p.tok.text_idx;
            SrcLoc const_loc = p.tok.loc;
            p.consumeToken();

            Expr* init_expr = nil;
            if (p.tok.kind == Kind.Equal) {
                p.consumeToken();
                init_expr = p.parseExpr(); // p.parseConstantExpr()
            }

            EnumConstantDecl* constant = p.builder.actOnEnumConstant(const_name, const_loc, is_public, qt, init_expr);
            constants.add(constant.asDecl());

            if (p.tok.kind != Kind.Comma) break;
            p.consumeToken();
        }
    }

    p.expectAndConsume(Kind.RBrace);

    p.builder.actOnEnumType(name, loc, is_public, is_incr, qt, cast<EnumConstantDecl**>(constants.getDecls()), constants.size());

    constants.free();
}

func void Parser.parseAliasType(Parser* p, u32 name, u32 loc, bool is_public) {
    TypeRefHolder ref;
    ref.init();
    p.parseTypeSpecifier(&ref, true);

    Decl* d = p.builder.actOnAliasType(name, loc, is_public, &ref);
    p.parseOptionalAttributes();
    p.expectAndConsume(Kind.Semicolon);
}

